BACKUP ~bg2fixpack/backup~ // location to store files for uninstall purposes
SUPPORT ~https://www.gibberlings3.net/forum/74-bg2-fixpack-general-discussion/~

ALWAYS

  ACTION_IF !VARIABLE_IS_SET tob_game THEN BEGIN

    OUTER_SET ee_game = 1
    OUTER_SET tob_game = 1
    OUTER_SET tobex_game = 0
  
    ACTION_IF GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ THEN BEGIN
  
      OUTER_SET ee_game = 0
  
      ACTION_IF !FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN
        OUTER_SET tob_game = 0
      END
  
      ACTION_IF FILE_EXISTS ~tobex_ini/tobexcore.ini~ THEN BEGIN
        OUTER_SET tobex_game = 1
      END

    END

  END

END

VERSION ~v13~

README ~bg2fixpack/languages/%LANGUAGE%/readme-bg2fixpack.html~ ~bg2fixpack/readme-bg2fixpack.html~

LANGUAGE ~English~                                ~english~ ~bg2fixpack/languages/english/setup.tra~
LANGUAGE ~Polski (by yarpen)~                     ~polski~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/polski/setup.tra~
LANGUAGE ~Espanol (by Immortality and Clan DLAN)~ ~spanish~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/spanish/setup.tra~
LANGUAGE ~Deutsch (by Leonardo Watson)~           ~german~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/german/setup.tra~
LANGUAGE ~Francais (by Anomaly)~                  ~french~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/french/setup.tra~
LANGUAGE ~Korean (by Web2Air)~                    ~korean~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/korean/setup.tra~
LANGUAGE ~Italian (by Andrea C.)~                 ~italian~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/italian/setup.tra~
LANGUAGE ~Russian (by Fess)~                      ~russian~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/russian/setup.tra~
LANGUAGE ~Japanese (by Taro)~                    ~japanese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/japanese/setup.tra~
LANGUAGE ~Traditional Chinese (by good0593)~     ~tchinese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/tchinese/setup.tra~
LANGUAGE ~Simplified Chinese (by good0593)~      ~schinese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/schinese/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Begin core component                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0 DESIGNATED 0 // Core Fixes
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~druidab.bcs~ @26 // in case BD or Fixpack already installed
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// disable broken stuff from key file
ACTION_FOR_EACH file IN iplot01k.itm iplot04g.itm iplot04h.itm iplot04i.itm xr2400.are xr2600.are BEGIN
  DISABLE_FROM_KEY "%file%"
END

INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions
INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches.tpa~ // batch patches, mainly immunity stuff

// os x "can't save" bug
ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~ THEN BEGIN
<<<<<<<< fix-temp
#!/bin/sh
test -f temp && (rm -f temp && mkdir temp)
>>>>>>>>
  COPY ~fix-temp~ ~fix-temp~
  AT_NOW ~sh fix-temp~
END

/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

// first column is strref to be updated, second column is tra reference (e.g. 150 uses @150 from the tra file)
// item speed and weight fixes now done as part of the item fixes
COPY ~bg2fixpack/lib/strings.tpa~ ~bg2fixpack/lib/strings_core.tpa~
ACTION_CLEAR_ARRAY cd_string_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_string_fixes BEGIN

   4330 => 150 // [IMOEN20] sound fix
   4432 => 168 // soa dust mephit familiar item description
   4433 => 170 // soa fairy dragon familiar item description
   4434 => 162 // soa ferret familiar item description
   4435 => 164 // soa imp familiar item description
   4436 => 174 // soa quasit familiar item description
   4437 => 160 // soa pseudo-dragon familiar item description
   4438 => 166 // soa rabbit familiar item description
   5962 => 152 // mustard jelly sound resref fix
   6526 => 215 // flame tongue: descript missing thac0 bonuses, +3 bonuses are against cold-using not cold/fire
   7345 => 214 // kondar: descript not mentioning extra thac0 vs. shapeshifters
   8064 => 155 // globe of invulnerability descript (remove dispel magic vulnerability)
   8857 => 100 // Viconia's non-sequitir line
   8863 => 101 // Viconia's non-sequitir line
  11848 => 153 // wrong save v. death penalty for claw of kazgaroth
  12146 => 154 // minor globe of invulnerability descript (remove dispel magic vulnerability)
  14320 => 141 // symbol fear descript (1 round per 3 levels, not 2 rounds)
  17890 => 127 // [VALYGA01] sound restoration
  21957 => 210 // strength requirement, bala's axe
  25201 => 137 // Berserker enrage missing protection list
  25894 => 156 // teleport field has no saving throw
  26315 => 136 // spellstrike descript (protections removed list)
  32186 => 211 // enchantment, name of defender of easthaven
  32187 => 212 // thac0 bonus, damage in descript of defender of easthaven
  35168 => 132 // [HAERDA95] sound restoration
  36460 => 139 // flail +2 damage
  37155 => 133 // [JANJAN67] sound restoration
  39461 => 213 // damage, blade of roses
  39565 => 255 // ashen scales usable by druids
  39578 => 256 // shield of the lost usable by druids
  41385 => 172 // soa cat familiar item description
  45869 => 138 // Barbarian rage missing protection list
  45889 => 159 // find familiar spell descript
  46589 => 157 // prevent dupe CoC items; see also ar2300.bcs, sahkng01.bcs, sahkng01.dlg, sahpr2.dlg, sahpr4.cre, sahtreas.bcs
  46590 => 158 // prevent dupe CoC items; see also ar2300.bcs, sahkng01.bcs, sahkng01.dlg, sahpr2.dlg, sahpr4.cre, sahtreas.bcs
  58250 => 134 // [ELLESI02] sound restoration
  60724 => 112 // adalon soundset restoration
  60741 => 113 // adalon soundset restoration

END

ACTION_IF tob_game THEN BEGIN // tob strings

  // supplement above array with tob strings
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_string_fixes BEGIN

    72757 => 173 // tob cat familiar item description
    72760 => 169 // tob dust mephit familiar item description
    72762 => 171 // tob fairy dragon familiar item description
    72763 => 163 // tob ferret familiar item description
    72764 => 165 // tob imp familiar item description
    72765 => 161 // tob pseudo-dragon familiar item description
    72766 => 175 // tob quasit familiar item description
    72767 => 167 // tob rabbit familiar item description

  END

END

ACTION_PHP_EACH cd_string_fixes AS string => tra BEGIN

  ACTION_IF FILE_CONTAINS_EVALUATED (~bg2fixpack/languages/%LANGUAGE%/setup.tra~ ~^[ %TAB%]*@%tra%[ %TAB%]*=~) BEGIN // check string is in language's tra file

    COPY ~bg2fixpack/lib/strings_core.tpa~ ~bg2fixpack/lib/strings_core.tpa~ // build library of string sets
      REPLACE_TEXTUALLY ~\(//replace\)~ ~STRING_SET %string% @%tra% 
\1~

  END

END

REINCLUDE ~bg2fixpack/lib/strings_core.tpa~ // now execute newly built library

// shove name changes here since it covers items and spells
ACTION_CLEAR_ARRAY cd_new_names
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_new_names BEGIN

  ~alphonse.cre~ => 35884 // (name/tooltip mismatch, use name) alphonse - servant
  ~arnboy01.cre~ => 38998 // name from Apprentice Torturer Douglas to Apprentice Torturer Douglas
  ~b1-10m2.itm~  => 10966 // name from Skull to Attack
  ~b1-12.itm~    => 10966 // name from Skull to Attack
  ~b1-12m3.itm~  => 10966 // name from Skull to Attack
  ~b1-20m3.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~b1-20m4.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~b1-6.itm~     => 10966 // name from Skull to Attack
  ~b1-8.itm~     => 10966 // name from Skull to Attack
  ~b1-8m1.itm~   => 10966 // name from Skull to Attack
  ~b2-16.itm~    => 10966 // name from Skull to Attack
  ~b3-24.itm~    => 10966 // name from <Invalid Strref -1> to Attack
  ~b4-32.itm~    => 10966 // name from Skull to Attack
  ~b4-32m3.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~b4-32m4.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~basilg1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~basilg2.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~basilg3.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~basill1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~basill2.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~behobs01.cre~ => 10726 // (name/tooltip mismatch, use tooltip) beholder - observer
  ~behspe01.cre~ => 10834 // (name/tooltip mismatch, use tooltip) beholder - spectator
  ~carrio1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~catjag.itm~   => 10966 // name from Skull to Attack
  ~catlio.itm~   => 10966 // name from Skull to Attack
  ~dogwawp.itm~  => 10966 // name from Skull to Attack
  ~elairl.itm~   => 10966 // name from Skull to Attack
  ~elesah02.cre~ => 31070 // (name/tooltip mismatch, use name) sahuagin baronial guard - baronial guard
  ~ensw2h.itm~   =>  6694 // name from Battle Axe to Battle Axe +3
  ~etterc1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~etterc2.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~figspid.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~fireelel.itm~ => 10966 // name from Skull to Attack
  ~fireelem.itm~ => 10966 // name from Skull to Attack
  ~gendjn01.cre~ => 11433 // (name/tooltip mismatch, use tooltip) djinni - noble djinni
  ~genefn01.cre~ => 11438 // (name/tooltip mismatch, use tooltip) efreeti - noble efreeti
  ~genscim.itm~  => 10966 // name from Skull to Attack
  ~ghast1.itm~   => 10966 // name from <Invalid Strref -1> to Attack
  ~golcla.itm~   => 10966 // name from <Invalid Strref -1> to Attack
  ~golfle.itm~   => 10966 // name from Skull to Attack
  ~golsto.itm~   => 10966 // name from <Invalid Strref -1> to Attack
  ~helmpr.cre~   => 17221 // name from Sir Donalus to Sir Lothtyran (duplicate sir donalus'sss'ss')
  ~jellmu1.itm~  => 10966 // name from Broken Shield to Attack
  ~korcrazy.cre~ => 30509 // name from Cleric to Crazyface
  ~korpimg1.cre~ => 30490 // name from Household Guardsman to Pimlico Guard
  ~kptrol06.cre~ =>  2149 // (name/tooltip mismatch, use tooltip) troll - ice troll
  ~kuotoa01.cre~ =>  3093 // (name/tooltip mismatch, use name) kuo-toa warrior - kuo-toa
  ~lacedo.itm~   => 10966 // name from <Invalid Strref -1> to Attack
  ~lasmist.cre~  =>  6522 // (name/tooltip mismatch, use tooltip) djinni - gaseous form
  ~mage16c.cre~  => 15232 // name from Khollynnus Paac to Cowled Wizard (duplicate Paacs)
  ~mepice02.cre~ => 22553 // name from Radiant Mephit to Ice Mephit
  ~mistpo.itm~   => 10966 // name from Skull to Attack
  ~nishruu.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~obsfir01.cre~ => 11438 // (name/tooltip mismatch, use tooltip) efreeti - noble efreeti
  ~obssah03.cre~ => 31070 // (name/tooltip mismatch, use name) sahuagin baronial guard - baronial guard
  ~ogre1.itm~    => 10966 // name from <Invalid Strref -1> to Attack
  ~p1-4.itm~     => 10966 // name from Skull to Attack
  ~p1-8.itm~     => 10966 // name from Skull to Attack
  ~p1-8m1.itm~   => 10966 // name from Skull to Attack
  ~p3-12m4.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~pirpir06.cre~ => 13839 // (name/tooltip mismatch, use name) sailor - stanet
  ~ppbodbat.cre~ => 18112 // (name/tooltip mismatch, use tooltip) vampire - vampire bat
  ~ppdjinn2.cre~ => 11433 // (name/tooltip mismatch, use tooltip) djinni - noble djinni
  ~ppmind01.cre~ => 10983 // (name/tooltip mismatch, use tooltip) mind flayer - ulitharid
  ~rngbeh01.cre~ => 10653 // (name/tooltip mismatch, use tooltip) beholder - death tyrant
  ~rodmace.itm~  =>  7413 // name from Mace +2 to Mace +2
  ~rodspear.itm~ =>  7807 // name from Spear to Spear +3
  ~s1-10m4.itm~  => 10966 // name from Skull to Attack
  ~s1-8.itm~     => 10966 // name from Skull to Attack
  ~sahbar01.cre~ => 31070 // (name/tooltip mismatch, use name) sahuagin baronial guard - baronial guard
  ~sahextra.cre~ => 31086 // (name/tooltip mismatch, use name) sahuagin royal guard - royal guard
  ~sahgrd01.cre~ => 31086 // (name/tooltip mismatch, use name) sahuagin royal guard - royal guard
  ~sahgrd02.cre~ => 31086 // (name/tooltip mismatch, use name) sahuagin royal guard - royal guard
  ~sahgrd03.cre~ => 31086 // (name/tooltip mismatch, use name) sahuagin royal guard - royal guard
  ~sarbul04.cre~ => 68792 // name from gromnir soldier to il-khan soldier
  ~sarbul05.cre~ => 68792 // name from gromnir soldier to il-khan soldier
  ~shop03.cre~   => 23303 // name from Maheer to Maheer
  ~spidgi1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~spidph1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~spidsw1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~spin775.spl~  =>  5739 // name from <Invalid Strref -1> to Psionic Blast (using the Psionic Blast ability should display a feedback string in the combat log)
  ~stalkesu.itm~ => 10966 // name from Fist to Attack
  ~tanomist.cre~ =>  6522 // (name/tooltip mismatch, use tooltip) vampire - gaseous form
  ~trolfr01.cre~ => 11605 // (name/tooltip mismatch, use tooltip) troll - freshwater troll
  ~trolfr02.cre~ => 11605 // (name/tooltip mismatch, use tooltip) troll - freshwater troll
  ~trolsn01.cre~ => 10981 // (name/tooltip mismatch, use tooltip) troll - snow troll
  ~trolsn02.cre~ => 10981 // (name/tooltip mismatch, use tooltip) troll - snow troll
  ~udelder.cre~  => 10901 // (name/tooltip mismatch, use tooltip) beholder - elder orb
  ~valemist.cre~ =>  6522 // (name/tooltip mismatch, use tooltip) vampire - gaseous form
  ~valran01.cre~ =>  2190 // name from Ranger to Derrick
  ~vampbat.cre~  => 18112 // (name/tooltip mismatch, use tooltip) vampire - vampire bat
  ~wolfwi1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~wolfwi2.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~wraith1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~wyvern1.itm~  => 10966 // name from <Invalid Strref -1> to Attack
  ~wyvern2.itm~  => 10966 // name from <Invalid Strref -1> to Attack

END

ACTION_PHP_EACH cd_new_names AS file => string BEGIN

  COPY_EXISTING ~%file%~ ~override~
    WRITE_LONG 0x08 string
    WRITE_LONG 0x0c string
    IF_EXISTS

END

/////                                                  \\\\\
///// ids fixes                                        \\\\\
/////                                                  \\\\\

// add ToB scripting actions/triggers to SoA; done as a library in case other mods want to nab it
INCLUDE ~bg2fixpack/lib/tob2soa.tph~

// Fix the BashDoor() entry in ACTION.IDS (Wounded_Lion)
COPY_EXISTING ~action.ids~ ~override~
  REPLACE_TEXTUALLY ~BashDoor(0:Object)~ ~BashDoor(O:Object)~
  BUT_ONLY

// Fix the LeaveAreaLUAPanicEntry() entry in ACTSLEEP.IDS (Lu_ and aVENGER)
COPY_EXISTING ~actsleep.ids~ ~override~
  PATCH_IF NOT FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~LeaveAreaLUAPanicEntry(S\:Area\*\,S\:Entry\*\,P\:Point\*\,I\:Face\*)~) BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH ~351 LeaveAreaLUAPanicEntry(S:Area*,S:Entry*,P:Point*,~ ~351 LeaveAreaLUAPanicEntry(S:Area*,S:Entry*,P:Point*,I:Face*)~
  END
  BUT_ONLY

// imprisoned summon fix: see cdwi917a.spl, cdwi910.eff, cdwi917a.eff, spin580.spl, spin626.spl, spin788.spl, spwi910.spl, spwi917.spl
APPEND ~gender.ids~ ~66 IMPRISONED_SUMMONED~ UNLESS ~^66[ %TAB%]~

// fix kit.ids; also done as an external library so other mods can grab it
INCLUDE ~bg2fixpack/lib/kit_ids_fixer.tpa~

// projectl.ids changes; projectiles need to be added early to make variable available for spell/item patching
// new projectile to correct detect evil range; see spcl212.spl, spin120.spl, sppr104.spl, spwi202.spl for additional changes
ADD_PROJECTILE ~bg2fixpack/pro/cddetevl.pro~

// clone beholder ray and assign it to beholder lightning bolt spells
// this allows it to be reflected by cloak of reflection without also reflecting all beholder projectiles
COPY_EXISTING ~spbehbla.pro~ ~override/cdbehbla.pro~
ADD_PROJECTILE ~override/cdbehbla.pro~

// eliminates duplicate SHADOW entries
COPY_EXISTING ~race.ids~ ~override~
  REPLACE_TEXTUALLY ~^149[ %TAB]+SHADOW\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~149 STATUE\1~
  BUT_ONLY

// add ettin racial entry; needed to give Crom ability to insta-kill ettins
APPEND ~race.ids~ ~199 ETTIN~ UNLESS ~^199[ %TAB]+ETTIN[ %TAB%%LNL%%MNL%%WNL%]~

// added snares for scripting purposes, fix broken entry
COPY_EXISTING ~spell.ids~ ~override~
  REPLACE_TEXTUALLY ~PSIONIC[ %TAB]+_SUPERIOR_INVISIBILITY~ ~PSIONIC_SUPERIOR_INVISIBILITY~ // 3544
APPEND ~spell.ids~ ~1718 CLERIC_SYMBOL_STUN~      UNLESS ~^1718[ %TAB]+CLERIC_SYMBOL_STUN[ %TAB%%LNL%%MNL%%WNL%]~
APPEND ~spell.ids~ ~1719 CLERIC_SYMBOL_DEATH~     UNLESS ~^1719[ %TAB]+CLERIC_SYMBOL_DEATH[ %TAB%%LNL%%MNL%%WNL%]~
APPEND ~spell.ids~ ~4321 BERSERKER_ENRAGE~        UNLESS ~^4321[ %TAB]+BERSERKER_ENRAGE[ %TAB%%LNL%%MNL%%WNL%]~
APPEND ~spell.ids~ ~4412 THIEF_SET_SNARE~         UNLESS ~^4412[ %TAB]+THIEF_SET_SNARE[ %TAB%%LNL%%MNL%%WNL%]~
APPEND ~spell.ids~ ~4414 THIEF_SET_SPECIAL_SNARE~ UNLESS ~^4414[ %TAB]+THIEF_SET_SPECIAL_SNARE[ %TAB%%LNL%%MNL%%WNL%]~

// new state needed to fix Dead() triggers that don't use DVs
// covers STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH
APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~^0x00000FC0[ %TAB]+STATE_REALLY_DEAD[ %TAB%%LNL%%MNL%%WNL%]~
// new state as shortcut for unavailable for dialogue; covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH,
// STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING
APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~^0x80101FEF[ %TAB]+CD_STATE_NOTVALID[ %TAB%%LNL%%MNL%%WNL%]~

// force WeiDU to reload altered IDS files
CLEAR_IDS_MAP

/////                                                  \\\\\
///// misc 2da fixes                                   \\\\\
/////                                                  \\\\\

// chicken sound fixes
COPY_EXISTING ~achk.2da~ ~override~
  SET_2DA_ENTRY 12 1 1 0
  SET_2DA_ENTRY 14 1 1 0
  BUT_ONLY

// allows multi-class thieves to be LN and LE
COPY_EXISTING ~alignmnt.2da~ ~override~
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 13 2 ~1~ // ft
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 13 3 ~1~
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 14 2 ~1~ // fmt
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 14 3 ~1~
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 15 2 ~1~ // mt
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 15 3 ~1~
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 17 2 ~1~ // ct
  SET_2DA_ENTRY_LATER ~ALIGNMNT~ 17 3 ~1~
  SET_2DA_ENTRIES_NOW ~ALIGNMNT~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

// blade pp fix; see cdblpp1-5.spl
COPY_EXISTING ~clabba02.2da~ ~override~
  SET_2DA_ENTRY_LATER ~blade~ 8 1 ~AP_CDBLPP1~
  FOR (col = 2 ; col < 15 ; col = col + 2) BEGIN
    SET_2DA_ENTRY_LATER ~blade~ 8 col ~AP_CDBLPP3~
  END
  FOR (col = 3 ; col < 16 ; col = col + 2) BEGIN
    SET_2DA_ENTRY_LATER ~blade~ 8 col ~AP_CDBLPP2~
  END
  FOR (col = 16 ; col < 37 ; col = col + 2) BEGIN
    SET_2DA_ENTRY_LATER ~blade~ 8 col ~AP_CDBLPP4~
  END
  FOR (col = 17 ; col < 37 ; col = col + 2) BEGIN
    SET_2DA_ENTRY_LATER ~blade~ 8 col ~AP_CDBLPP5~
  END
  SET_2DA_ENTRIES_NOW ~blade~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

// fix skald's lack of thac0 bonus
COPY_EXISTING ~clabba04.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]AP_SPCL\)5\(41[ %TAB%]\)~ ~\11\2~ // Replaces Skald's +1 damage with Swashbuckler's +1 thac0/damage.
  UNLESS ~[ %TAB%]AP_SPCL141[ %TAB%]~
  BUT_ONLY

// the progression of the kensai abilities spcl141 and spcl143 are both irregular; the table is also one cell short in the first row
COPY_EXISTING ~clabfi04.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]AP_SPCL141[ %TAB%]+\*\*\*\*[ %TAB%]+\)\(AP_SPCL141[ %TAB%]\)~
                    ~\1 **** \2~            //Fix the broken progression for SPCL141 by adding the missing cell
  REPLACE_TEXTUALLY ~[ %TAB%]AP_SPCL143[ %TAB%]~ ~ **** ~                             //Remove all instances of SPCL143 and start from scratch
  COUNT_2DA_COLS cols
  READ_2DA_ENTRIES_NOW "#rclabfi04" cols
  FOR (i = 0; i < cols; i += 4) BEGIN                              //Add abilities every 4 levels
    PATCH_IF i = 0 BEGIN SET i = 1 END
    FOR (j = 0; j < "#rclabfi04"; ++j) BEGIN
      READ_2DA_ENTRY_FORMER "#rclabfi04" j i entry
      PATCH_IF "%entry%" STRING_EQUAL "****" BEGIN
        SET_2DA_ENTRY_LATER "#sclabfi04" j i AP_SPCL143
        j = "#rclabfi04"
      END
    END
    PATCH_IF i = 1 BEGIN SET i = 0 END
  END
  SET_2DA_ENTRIES_NOW "#sclabfi04" cols
  PRETTY_PRINT_2DA
  BUT_ONLY

// fallen paladin/ranger has extra entries in the ABILITY1 line
COPY_EXISTING ~clabfi05.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(ABILITY1 +[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+\).*$~ ~\1~
  BUT_ONLY

// Fix Inquisitors not getting charm immunity at level 1 (Dakk)
COPY_EXISTING ~clabpa03.2da~ ~override~ // Inquisitor ability table
  SET_2DA_ENTRY  3 2 3 ~****~
  SET_2DA_ENTRY 10 1 3 ~AP_SPCL233b~
  PRETTY_PRINT_2DA
  BUT_ONLY

// in SoA, two swashbuckler ability columns run together
COPY_EXISTING ~clabth04.2da~ ~override~
  REPLACE_TEXTUALLY ~\(AP_SPCL[14]41\)\(\*+\|GA_SPCL412\)~ ~\1 \2~
  BUT_ONLY

// in ToB archers and stalkers not allowed to dual-class; adding restriction to SoA games as well
COPY_EXISTING ~dualclas.2da~ ~override~
  SET_2DA_ENTRY 32 2 7 ~0~ // archer (feralan)
  SET_2DA_ENTRY 33 2 7 ~0~ // stalker
  BUT_ONLY

// Imoen gets her groove back. And her banter file.
ACTION_IF tob_game THEN BEGIN

  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY

END ELSE BEGIN

  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ UNLESS ~[ %TAB%]BIMOEN2[ %TAB%%LNL%%MNL%%WNL%]~ // fixes SoA version


END

// maze not lasting as long as specified in descript; patching intmod.2da for int < 9
COPY_EXISTING ~intmod.2da~ ~override~
  SET dice = 20
  FOR (index = 3 ; index < 12 ; ++index) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 4 dice
    PATCH_IF (index = 5) BEGIN SET dice = 10 END ELSE
    PATCH_IF (index = 8) BEGIN SET dice = 5 END
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

// Mithral Field Plate Armor +2 and Missile Attraction +2 armors can no longer be worn together with rings and amulets of protection (Wisp)
APPEND ~itemexcl.2da~ ~bruenpla 1~ UNLESS ~^bruenpla[ %TAB%]~
APPEND ~itemexcl.2da~ ~leat06   1~ UNLESS ~^leat06[ %TAB%]~

// removes incorrect NWN reference in load hints
COPY_EXISTING ~loadhint.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)40954\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\134572\2~
  BUT_ONLY

// Fix the incorrect lore bonus for 15 INT/WIS (Ascension 64 and Hurricane)
COPY_EXISTING ~lorebon.2da~ ~override~
  SET_2DA_ENTRY 15 1 2 ~3~
  BUT_ONLY

// sound fixes for monsters
COPY_EXISTING ~mcar.2da~ ~override/mcwl.2da~
  REPLACE_TEXTUALLY ~^Mcar.+$~ ~%DEST_RES% /Carrion Crawler/~

COPY_EXISTING ~mdji.2da~ ~override/mdjl.2da~
  REPLACE_TEXTUALLY ~^Mdji.+$~ ~%DEST_RES% /djinni w legs/~

COPY_EXISTING ~mgo1.2da~ ~override/mgo3.2da~
  REPLACE_TEXTUALLY ~^MGO1.+$~ ~%DEST_RES% /goblin elite w axe/~

COPY_EXISTING ~mgo2.2da~ ~override/mgo4.2da~
  REPLACE_TEXTUALLY ~^MGO2.+$~ ~%DEST_RES% /goblin elite w bow/~

COPY_EXISTING ~mli2.2da~ ~override/mli3.2da~
  REPLACE_TEXTUALLY ~^MLI2.+$~ ~%DEST_RES% /lizard man caster 3/~

COPY_EXISTING ~mmyc.2da~ ~override/mmy2.2da~
  REPLACE_TEXTUALLY ~^MMYC.+$~ ~%DEST_RES% /myconid,blue/~

COPY_EXISTING ~mor2.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)orcbw07\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1orcbw07a\2~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)orcbw08\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1orcbw07b\2~
  BUT_ONLY

COPY_EXISTING ~mor2.2da~ ~override/mor4.2da~
  REPLACE_TEXTUALLY ~^MOR2.+$~  ~%DEST_RES% /orc ranged 4/~

COPY_EXISTING ~msal.2da~ ~override/msa2.2da~
  REPLACE_TEXTUALLY ~^MSAL.+$~ ~%DEST_RES% /salamander,frost/~

// sound fix
COPY_EXISTING ~achk.2da~ ~override/mshr.2da~ // It's easier to just build these from the ground up
  SET_2DA_ENTRY_LATER ~mshr~  0 0 ~MSHR /shrieker/~
  SET_2DA_ENTRY_LATER ~mshr~  3 1 ~SHRIE04B NOSOUND NOSOUND~
  SET_2DA_ENTRY_LATER ~mshr~  4 1 ~2 0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 11 1 ~SHRIE07A SHRIE07B~
  SET_2DA_ENTRY_LATER ~mshr~ 12 1 ~0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 13 1 ~SHRIE09A SHRIE09B~
  SET_2DA_ENTRY_LATER ~mshr~ 14 1 ~0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 25 1 ~SHRIE03A NOSOUND NOSOUND~
  SET_2DA_ENTRY_LATER ~mshr~ 26 1 ~2 0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 27 1 ~SHRIE03B NOSOUND NOSOUND~
  SET_2DA_ENTRY_LATER ~mshr~ 28 1 ~2 0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 29 1 ~SHRIE04A NOSOUND NOSOUND~
  SET_2DA_ENTRY_LATER ~mshr~ 30 1 ~2 0 0~
  SET_2DA_ENTRY_LATER ~mshr~ 39 1 ~***~ // Doesn't really need a battle cry, since it doesn't ever attack
  SET_2DA_ENTRIES_NOW ~mshr~ 1

COPY_EXISTING ~mska.2da~ ~override/mskt.2da~
  REPLACE_TEXTUALLY ~^MSKA.+$~ ~%DEST_RES% /skeleton armored no helmet/~

// small spider soundset fixes
COPY_EXISTING ~msps.2da~ ~override~
  REPLACE_TEXTUALLY ~^Selection[ %TAB%]+smspid03~ ~Selection                       smspid01~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)smspid03\([ %TAB%]+\)smspid04\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1smspid01\2smspid02\3~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)smspid08\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1smspid06\2~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)smspid09\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1smspid07\2~
  BUT_ONLY

COPY_EXISTING ~mwfm.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)pseudo0\([0-9][ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1pseud0\2~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)pseudo0\([0-9][ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1pseud0\2~
  BUT_ONLY

COPY_EXISTING ~mair.2da~ ~override/mwwe.2da~
  REPLACE_TEXTUALLY ~^Mair.+$~ ~%DEST_RES% /water weird~

COPY_EXISTING ~myu2.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)yanel\([78]a[ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1yanel0\2~
  REPLACE_TEXTUALLY ~\([ %TAB%]\)yanel\([78]b[ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1yanel0\2~ // b's were being missed since the space inbetween was being matched in the a replacements
  BUT_ONLY

// gives bards a fourth level 6 spell at high levels, SoA-only
ACTION_IF !tob_game THEN BEGIN

  COPY_EXISTING ~mxsplbrd.2da~ ~override~
    COUNT_2DA_ROWS ~1~ "rowcount"
    FOR (row = rowcount ; row > (rowcount - 19) ; row = row - 1) BEGIN
      SET_2DA_ENTRY_LATER ~MXSPLBRD~ "%row%" 6 ~4~
    END
    SET_2DA_ENTRIES_NOW ~MXSPLBRD~ 1
    BUT_ONLY

END

// NPCs joining level 25+ PCs in SoA would be at their minimum level, not max
COPY_EXISTING ~npclevel.2da~ ~override~
  COUNT_2DA_COLS col_num
  COUNT_2DA_ROWS (col_num - 1) row_num
  READ_2DA_ENTRY 0 (col_num - 2) (col_num - 1) level    // read last enry of top line as level
  READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
  SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
  FOR (index = level ; index < 40 ; ++index) BEGIN
    SET new = index + 1
    FOR (index2 = 1 ; index2 < row_num ; ++index2) BEGIN // extend all other rows with previous column's value
      READ_2DA_ENTRY index2 (col_num - 1) (col_num) val
      SET_2DA_ENTRY  index2 (col_num - 1) (col_num) ~%val% %val%~
    END
    SET_2DA_ENTRY 0 (col_num - 1) (col_num) ~%index% %new%~ // extend level row
    SET col_num += 1
  END
  REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY

// consistent raise dead prices
COPY_EXISTING ~raisdead.2da~ ~override~
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 31 1 ~12500~ // level 29
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 32 1 ~13000~ // level 30
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 34 1 ~14000~ // level 32
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 36 1 ~15000~ // level 34
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 38 1 ~16000~ // level 36
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 40 1 ~17000~ // level 38
  SET_2DA_ENTRY_LATER ~RAISDEAD~ 42 1 ~18000~ // level 40
  SET_2DA_ENTRIES_NOW ~RAISDEAD~ 1
  BUT_ONLY

// fixes to high level rogue saves
COPY_EXISTING ~saverog.2da~ ~override~
  FOR (col = 21 ; col < 41 ; ++col) BEGIN
    SET_2DA_ENTRY_LATER ~SAVEROG~ 3 "%col%" ~8~
    SET_2DA_ENTRY_LATER ~SAVEROG~ 4 "%col%" ~4~
    SET_2DA_ENTRY_LATER ~SAVEROG~ 5 "%col%" ~7~
    SET_2DA_ENTRY_LATER ~SAVEROG~ 6 "%col%" ~11~
    SET_2DA_ENTRY_LATER ~SAVEROG~ 7 "%col%" ~5~
  END
  SET_2DA_ENTRIES_NOW ~SAVEROG~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

// fixes to high level mage saves
COPY_EXISTING ~savewiz.2da~ ~override~
  FOR (col = 21 ; col < 41 ; ++col) BEGIN
    SET_2DA_ENTRY_LATER ~SAVEWIZ~ 3 "%col%" ~8~
    SET_2DA_ENTRY_LATER ~SAVEWIZ~ 4 "%col%" ~3~
    SET_2DA_ENTRY_LATER ~SAVEWIZ~ 5 "%col%" ~5~
    SET_2DA_ENTRY_LATER ~SAVEWIZ~ 6 "%col%" ~7~
    SET_2DA_ENTRY_LATER ~SAVEWIZ~ 7 "%col%" ~4~
  END
  SET_2DA_ENTRIES_NOW ~SAVEWIZ~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

//build skilldex down to 1
OUTER_FOR (index = 8 ; index > 0 ; --index) BEGIN

  OUTER_SET clone = index + 1

  COPY_EXISTING ~skilldex.2da~ ~override~
    REPLACE_TEXTUALLY ~^%clone%\([ %TAB%].+$\)~
~%index%\1
%clone%\1~
    UNLESS ~^%index%[ %TAB%]~
    BUT_ONLY

END

// Fix the incorrect thieving skill racial bonus for elves, half-elves and halflings (Hurricane and aVENGER)
COPY_EXISTING ~skillrac.2da~ ~override~
  SET_2DA_ENTRY 2 4 8 ~15~ // Elf Move Silently correction (was 20)
  SET_2DA_ENTRY 4 4 8 ~10~ // Half-Elf Move Silently correction (was 15)
  SET_2DA_ENTRY 5 4 8 ~20~ // Halfling Move Silently correction (was 25)
  BUT_ONLY

// Fix the incorrect Move Silently entry for Ranger level 2 (Ascension 64 and Hurricane)
COPY_EXISTING ~skillrng.2da~ ~override~
  SET_2DA_ENTRY 2 1 2 ~21~
  BUT_ONLY

// echo-y Promenade fix
COPY_EXISTING ~sndresrf.2da~ ~override~
  REPLACE_TEXTUALLY ~^AR0700[ %TAB%]+STONE0[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
  BUT_ONLY

// spell shield is hardcoded to display icon #73 which is currently protection from magical energy; need to swap
// see also states.bam, states2.bam, spwi606.spl
COPY_EXISTING ~statdesc.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(73[%TAB% ]+\)[0-9]+~ ~\126228~
  REPLACE_TEXTUALLY ~^\(123[%TAB% ]+\)[0-9]+~ ~\18286~
  BUT_ONLY

// thieves, bards, mage-thieves not getting final thac0 bump at level 21
COPY_EXISTING ~thac0.2da~ ~override~
  SET_2DA_ENTRY  6 21 1 ~10~
  SET_2DA_ENTRY  7 21 1 ~10~
  SET_2DA_ENTRY 15 21 1 ~10~
  BUT_ONLY

// adds dupe rod of lordly might, book of infinite spells
COPY_EXISTING ~tooltip.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(rods02\|misc3a\)\([ %TAB%]+.+\)$~ ~\1\2
\1a\2~
  BUT_ONLY

COPY_EXISTING ~weapprof.2da~ ~override~
  SET_2DA_ENTRY_LATER ~weapprof~ 16 41 ~2~ // 1 > 2 stars for swashbucklers in katanas
  SET_2DA_ENTRY_LATER ~weapprof~ 16 51 ~1~ // 0 > 1 star for monk in katanas
  SET_2DA_ENTRY_LATER ~weapprof~ 20 20 ~2~ // 3 > 2 stars for f/m/c in clubs
  SET_2DA_ENTRY_LATER ~weapprof~ 20 21 ~2~ // 3 > 2 stars for c/r in clubs
  SET_2DA_ENTRY_LATER ~weapprof~ 26 41 ~1~ // 2 > 1 star for swashbucklers in xbows
  SET_2DA_ENTRY_LATER ~weapprof~ 28 41 ~1~ // 2 > 1 star for swashbucklers in shortbows
  SET_2DA_ENTRY_LATER ~weapprof~ 29 32 ~0~ // 5 > 0 stars for kensai in darts
  SET_2DA_ENTRY_LATER ~weapprof~ 29 33 ~0~ // 2 > 0 stars for cavalier in darts
  SET_2DA_ENTRY_LATER ~weapprof~ 30 41 ~1~ // 2 > 1 star for swashbucklers in slings
  SET_2DA_ENTRY_LATER ~weapprof~ 31 19 ~2~ // 1 > 2 stars for f/d in 2 handed
  SET_2DA_ENTRY_LATER ~weapprof~ 31 20 ~2~ // 1 > 2 stars for f/m/c in 2 handed
  SET_2DA_ENTRY_LATER ~weapprof~ 31 21 ~2~ // 1 > 2 stars for c/r in 2 handed
  SET_2DA_ENTRY_LATER ~weapprof~ 31 41 ~1~ // 2 > 1 stars for swashbucklers in 2 handed
  SET_2DA_ENTRY_LATER ~weapprof~ 32 41 ~1~ // 2 > 1 stars for swashbucklers in sword & shield
  SET_2DA_ENTRY_LATER ~weapprof~ 33 41 ~1~ // 2 > 1 stars for swashbucklers in 1-handed style
  SET_2DA_ENTRY_LATER ~weapprof~ 34 19 ~3~ // 1 > 3 stars for f/d in 2 wpn style
  SET_2DA_ENTRY_LATER ~weapprof~ 34 20 ~3~ // 2 > 3 stars for f/m/c in 2 wpn style
  SET_2DA_ENTRY_LATER ~weapprof~ 34 21 ~3~ // 2 > 3 stars for c/r in 2 wpn style
  COUNT_2DA_ROWS ~1~ "rowcount"
  FOR (row = rowcount ; row > (rowcount - 20) ; row = row - 1) BEGIN // covers last 19 rows (n/a as actual profs)
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%"  5 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%"  8 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%"  9 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 11 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 12 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 14 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 15 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 20 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 21 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 30 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 31 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 32 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 33 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 34 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 35 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 36 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 37 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 38 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 42 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 43 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 44 ~0~
    SET_2DA_ENTRY_LATER ~weapprof~ "%row%" 52 ~0~
  END
  SET_2DA_ENTRIES_NOW ~weapprof~ 1
  PRETTY_PRINT_2DA
  BUT_ONLY

ACTION_IF tob_game THEN BEGIN // ToB-only stuff check

  // invalid resource ref used for bard kits
  COPY_EXISTING ~25stweap.2da~ ~override~
    REPLACE_TEXTUALLY ~\([ %TAB%]\)LEATH14\([ %TAB%]\)~ ~\1LEAT14 \2~
    BUT_ONLY

  // part of the multi-holy symbol exploit fix. Also needed: CDHLYSYM.spl, CDHLYSY2.spl, CDHLYSYM.itm, and holysym.baf.
  COPY_EXISTING ~clabpr01.2da~ ~override~
                ~clabpr02.2da~ ~override~
                ~clabpr03.2da~ ~override~
                ~clabpr04.2da~ ~override~
    REPLACE_TEXTUALLY ~\([ %TAB%]\)AP_SPCl93[1234]\([ %TAB%]\)~ ~\1AP_CDHLYSYM\2~
    BUT_ONLY

  APPEND ~loadhint.2da~ ~75          72818~ UNLESS ~^75[ %TAB%]~

  // removes incorrect NWN reference in load hints
  COPY_EXISTING ~loadh25.2da~ ~override~
    REPLACE_TEXTUALLY ~\([ %TAB%]\)40954\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\134572\2~
    BUT_ONLY

  // gives f/m/c and f/m/t unique HLAs; tables created below
  COPY_EXISTING ~luabbr.2da~ ~override~
    SET_2DA_ENTRY 14 1 1 ~FMT~
    SET_2DA_ENTRY 19 1 1 ~FMC~
    BUT_ONLY

  // Multiclass Thieves' Multiple Assassination Abilities Fix part 1
  COPY_EXISTING ~luba1.2da~ ~override~
                ~luba2.2da~ ~override~
                ~luba3.2da~ ~override~
                ~luct0.2da~ ~override~
                ~lumt0.2da~ ~override~
                ~luth2.2da~ ~override~
                ~luth3.2da~ ~override~
    SET_2DA_ENTRY 9 7 1 ~*~
    SET_2DA_ENTRY 9 8 1 ~GA_SPCL916~
    BUT_ONLY

  // Multiclass Thieves' Multiple Assassination Abilities Fix part 2
  COPY_EXISTING ~luft0.2da~ ~override~
    SET_2DA_ENTRY 19 7 1 ~*~
    SET_2DA_ENTRY 19 8 1 ~GA_SPCL916~
    BUT_ONLY

  // creates f/m/c HLA table
  COPY_EXISTING ~lufc0.2da~ ~override/lufmc.2da~
    FOR (ROW = 22; ROW < 25; ROW = ROW + 1) BEGIN
      SET_2DA_ENTRY_LATER ~LUFMC~ ROW 4 ~1~
      SET_2DA_ENTRY_LATER ~LUFMC~ ROW 5 ~99~
      SET_2DA_ENTRY_LATER ~LUFMC~ ROW 6 ~1~
    END
    SET_2DA_ENTRY_LATER ~LUFMC~ 22 1 ~AP_SPCL928~
    SET_2DA_ENTRY_LATER ~LUFMC~ 23 1 ~AP_SPCL929~
    SET_2DA_ENTRY_LATER ~LUFMC~ 23 7 ~AP_SPCL928~
    SET_2DA_ENTRY_LATER ~LUFMC~ 24 1 ~AP_SPCL930~
    SET_2DA_ENTRY_LATER ~LUFMC~ 24 7 ~AP_SPCL929~
    SET_2DA_ENTRIES_NOW ~LUFMC~ 1
    PRETTY_PRINT_2DA
    BUT_ONLY

  // creates f/m/t HLA table
  COPY_EXISTING ~luft0.2da~ ~override/lufmt.2da~
    FOR (ROW = 23; ROW < 26; ROW = ROW + 1) BEGIN
      SET_2DA_ENTRY_LATER ~LUFMT~ ROW 4 ~1~
      SET_2DA_ENTRY_LATER ~LUFMT~ ROW 5 ~99~
      SET_2DA_ENTRY_LATER ~LUFMT~ ROW 6 ~1~
    END
    SET_2DA_ENTRY_LATER ~LUFMT~ 23 1 ~AP_SPCL928~
    SET_2DA_ENTRY_LATER ~LUFMT~ 24 1 ~AP_SPCL929~
    SET_2DA_ENTRY_LATER ~LUFMT~ 24 7 ~AP_SPCL928~
    SET_2DA_ENTRY_LATER ~LUFMT~ 25 1 ~AP_SPCL930~
    SET_2DA_ENTRY_LATER ~LUFMT~ 25 7 ~AP_SPCL929~
    SET_2DA_ENTRIES_NOW ~LUFMT~ 1
    PRETTY_PRINT_2DA
    BUT_ONLY

  //fixes Gromnir XP bug
  COPY_EXISTING ~xplist.2da~ ~override~
    SET_2DA_ENTRY 67 38 39 ~-1 -1 -1 -1~
    PRETTY_PRINT_2DA
    BUT_ONLY
END

/////                                                  \\\\\
///// mass compile/copy actions actions                \\\\\
/////                                                  \\\\\

COMPILE ~bg2fixpack/compile/soa-dlg.d~    // all dialogue fixes
        ~bg2fixpack/compile/ar0512.baf~   // blanks area script for helm temple in bridge ditrict; had been using old durlag tower script
        ~bg2fixpack/compile/bhcrypt.baf~  // crypt king horror
        ~bg2fixpack/compile/cdkpdog.baf~  // keep dogs non-hostile, see kpdog1-4.cre
        ~bg2fixpack/compile/cut41i.baf~   // spellhold cutscene loop; starts dialogue after ending cutscene
        ~bg2fixpack/compile/cut41j.baf~   // spellhold cutscene loop; starts dialogue after ending cutscene
        ~bg2fixpack/compile/druidad.baf~  // makes druid messenger destroyself if PC is great druid; see druidad.cre
        ~bg2fixpack/compile/rdog.baf~     // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rdwarf.baf~   // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/retter.baf~   // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rgibbler.baf~ // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rhalflin.baf~ // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rhobgoba.baf~ // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rhobgobf.baf~ // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rkobold.baf~  // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rogre.baf~    // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rsiren.baf~   // corrupted scrpt; compile new one with uncorrupted bits
        ~bg2fixpack/compile/rsirine.baf~  // corrupted scrpt; compile new one with uncorrupted bits
COPY_EXISTING ~ar0512.bcs~ ~override/ar1700.bcs~ // small teeth pass also using old BG script
              ~ar0512.bcs~ ~override/ar1800.bcs~ // north forest also using old BG script

// list these individually for reference instead of a bulk copy
COPY ~bg2fixpack/copy/amb_e09b.wav~ ~override~ // missing ambient sound files from BG
     ~bg2fixpack/copy/amb_m09a.wav~ ~override~ // missing ambient sound files from BG
     ~bg2fixpack/copy/amb_m09b.wav~ ~override~ // missing ambient sound files from BG
     ~bg2fixpack/copy/ar0512.mos~   ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar0512.tis~   ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar0512.wed~   ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar0512ht.bmp~ ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar0512lm.bmp~ ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar0512sr.bmp~ ~override~ // bridge district temple of helm green water fix
     ~bg2fixpack/copy/ar1404.mos~   ~override~ // temple ruins daytime map
     ~bg2fixpack/copy/ar1900n.wed~  ~override~ // fixed nighttime wed; see ar1900.are
     ~bg2fixpack/copy/ax1h10a.eff~  ~override~ // 1d6+4 extra damage vs. undead for Azuredge (slashing)
     ~bg2fixpack/copy/ax1h10b.eff~  ~override~ // 1d6+4 extra damage vs. undead for Azuredge (missile)
     ~bg2fixpack/copy/cdblpp1.spl~  ~override~ // -12 pickpocket score adjustment spell for blade fix; see clabba02.2da
     ~bg2fixpack/copy/cdblpp2.spl~  ~override~ // -2 pickpocket score adjustment spell for blade fix; see clabba02.2da
     ~bg2fixpack/copy/cdblpp3.spl~  ~override~ // -3 pickpocket score adjustment spell for blade fix; see clabba02.2da
     ~bg2fixpack/copy/cdblpp4.spl~  ~override~ // +2 pickpocket score adjustment spell for blade fix; see clabba02.2da
     ~bg2fixpack/copy/cdblpp5.spl~  ~override~ // +3 pickpocket score adjustment spell for blade fix; see clabba02.2da
     ~bg2fixpack/copy/cdcmelem.eff~ ~override~ // ring of foo control charm fix (see ring27-9.itm)
     ~bg2fixpack/copy/cddetevl.spl~ ~override~ // detect evil 'shell spell' for get proper MR checking; see spcl212.spl, spin120.spl, spin696.spl, sppr104.spl, spwi202.spl for additional changes
     ~bg2fixpack/copy/cdelfcm0.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm1.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm2.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm3.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm4.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm5.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm6.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfcm7.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfsl0.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfsl1.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfsl2.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdelfsl3.eff~ ~override~ // elf/halfelf sleep/charm resistance
     ~bg2fixpack/copy/cdfampsd.itm~ ~override~ // SoA pseudo-dragon attack item
     ~bg2fixpack/copy/cdlv3na.spl~  ~override~ // removes 3rd level holy/unholy cleric spells per alignment
     ~bg2fixpack/copy/cdlv7na.spl~  ~override~ // removes 7th level holy/unholy cleric spells per alignment
     ~bg2fixpack/copy/cdrmv313.eff~ ~override~ // removes holy smite
     ~bg2fixpack/copy/cdrmv314.eff~ ~override~ // removes unholy blight
     ~bg2fixpack/copy/cdrmv710.eff~ ~override~ // removes holy word
     ~bg2fixpack/copy/cdrmv715.eff~ ~override~ // removes unholy word
     ~bg2fixpack/copy/cdspja00.eff~ ~override~ // str attribute penalies for harper's call
     ~bg2fixpack/copy/cdspja01.eff~ ~override~ // dex attribute penalies for harper's call
     ~bg2fixpack/copy/cdspja02.eff~ ~override~ // con attribute penalies for harper's call
     ~bg2fixpack/copy/cdspja03.eff~ ~override~ // int attribute penalies for harper's call
     ~bg2fixpack/copy/cdspja04.eff~ ~override~ // wis attribute penalies for harper's call
     ~bg2fixpack/copy/cdspja05.eff~ ~override~ // chr attribute penalies for harper's call
     ~bg2fixpack/copy/cdwi910.eff~  ~override~ // imprisoned summon fix: see gender.ids, cdwi917a.spl, cdwi917a.eff, spin580.spl, spin626.spl, spin788.spl, spwi910.spl, spwi917.spl
     ~bg2fixpack/copy/cdwi917a.eff~ ~override~ // imprisoned summon fix: see gender.ids, cdwi910.eff, cdwi917a.spl, spin580.spl, spin626.spl, spin788.spl, spwi910.spl, spwi917.spl
     ~bg2fixpack/copy/cdwi917a.spl~ ~override~ // imprisoned summon fix: see gender.ids, cdwi910.eff, cdwi917a.eff, spin580.spl, spin626.spl, spin788.spl, spwi910.spl, spwi917.spl
     ~bg2fixpack/copy/daystar2.eff~ ~override~ // for daystar fixes (see sw1h31.itm, daystar1.eff)
     ~bg2fixpack/copy/famcat01.wav~ ~override~ // cat familiar sounds from ToB to SoA
     ~bg2fixpack/copy/flpr717a.spl~ ~override~ // Creeping Doom's panic effect does not work (see also sppr717.spl) panic for 1 round, portrait icon, display string; save vs. spell at -2; power 0
     ~bg2fixpack/copy/icetr01.wav~  ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr02.wav~  ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr03a.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr03b.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr04a.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr04b.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr07a.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr07b.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr08a.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr08b.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr09a.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/icetr09b.wav~ ~override~ // missing sounds for small trolls, imported from IWD
     ~bg2fixpack/copy/iscrl5a.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5b.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5c.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5d.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5e.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5f.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5g.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5h.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5i.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5j.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5k.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5l.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5m.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5n.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5o.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5p.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/iscrl5q.bam~  ~override~ // fixed spell scroll bam
     ~bg2fixpack/copy/mdog_mo.bmp~  ~override~ // fixed moon dog avatar
     ~bg2fixpack/copy/misc_01b.wav~ ~override~ // missing sound files
     ~bg2fixpack/copy/misc_01c.wav~ ~override~ // missing sound files
     ~bg2fixpack/copy/misc_03a.wav~ ~override~ // missing sound files
     ~bg2fixpack/copy/misc_04a.wav~ ~override~ // missing sound files
     ~bg2fixpack/copy/misc_06b.wav~ ~override~ // missing sound files
     ~bg2fixpack/copy/montr10.wav~  ~override~ // restore missing sound from bg; for ilyich
     ~bg2fixpack/copy/pre_p06.wav~  ~override~ // missing sound files
//     ~bg2fixpack/copy/spcl415a.spl~ ~override/spcl415c.spl~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
     ~bg2fixpack/copy/spcl415a.spl~ ~override~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
//     ~bg2fixpack/copy/spcl415b.spl~ ~override~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
     ~bg2fixpack/copy/sppr203d.spl~ ~override~ // shell spell for chant, see sppr203.spl
     ~bg2fixpack/copy/sppr203e.spl~ ~override~ // shell spell for chant, see sppr203.spl
     ~bg2fixpack/copy/spwi413a.spl~ ~override/spwi413c.spl~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
     ~bg2fixpack/copy/spwi413a.spl~ ~override~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
     ~bg2fixpack/copy/spwi413b.spl~ ~override~ // otiluke spell shells; see also spwi413.spl, spwi413a-c.spl, spcl415.spl, spcl415a-c.spl
     ~bg2fixpack/copy/states.bam~   ~override~ // spell shield is hardcoded to display icon #73 which is currently protection from magical energy; need to swap - see also statdesc.2da, spwi606.spl
     ~bg2fixpack/copy/states2.bam~  ~override~ // spell shield is hardcoded to display icon #73 which is currently protection from magical energy; need to swap - see also statdesc.2da, spwi606.spl
     ~bg2fixpack/copy/worldmap.wmp~ ~override~ // fixed soa worldmap (see also ar1700.are, ar1800.are, ar2500.are, ar2600.are)

// these missing sound files are language-specific (BG demon knight power words, e.g. "Die!"); currently only English available
COPY ~bg2fixpack/languages/%LANGUAGE%/shael_07.wav~ ~override~ // missing sound files
     ~bg2fixpack/languages/%LANGUAGE%/shael_08.wav~ ~override~ // missing sound files
     ~bg2fixpack/languages/%LANGUAGE%/shael_09.wav~ ~override~ // missing sound files
     ~bg2fixpack/languages/%LANGUAGE%/shael_10.wav~ ~override~ // missing sound files
  IF_EXISTS

ACTION_IF tob_game THEN BEGIN // additional copy & compile for ToB

  COMPILE ~bg2fixpack/compile-tob/tob-dlg.d~            // all ToB dialogue fixes
  COMPILE ~bg2fixpack/compile-tob/wk-ntrjex.d~          // add WK interjections to SoA
  COPY    ~bg2fixpack/copy-tob/cdhlysy2.spl~ ~override~ // dupe holy symbol fixes, Also needed: baldur.bcs, baldur25.bcs, and clabpr02-04 changes.
          ~bg2fixpack/copy-tob/cdhlysym.spl~ ~override~ // dupe holy symbol fixes, Also needed: baldur.bcs, baldur25.bcs, and clabpr02-04 changes.
          ~bg2fixpack/copy-tob/cdhlysym.itm~ ~override~ // dupe holy symbol fixes, Also needed: baldur.bcs, baldur25.bcs, and clabpr02-04 changes.
          ~bg2fixpack/copy-tob/worldmap.wmp~ ~override~ // fixed soa worldmap (see also ar1700.are, ar1800.are, ar2500.are, ar2600.are); tob version is better due to cleaner drizzt fixes

  // celestial weapons turn opaque when attacking
  COPY_EXISTING ~masgg2bw.bam~ ~override/maslg2bw.bam~
                ~masgg2s1.bam~ ~override/maslg2s1.bam~
                ~msogg2bw.bam~ ~override/msolg2bw.bam~
                ~msogg2s1.bam~ ~override/msolg2s1.bam~

  // travel time fix for wk > abazigal (return trip is 9)
  COPY_EXISTING ~worldm25.wmp~ ~override~
    READ_LONG 0x0c mos_off
    READ_LONG (mos_off + 0x20) area_num
    READ_LONG (mos_off + 0x24) area_off
    READ_LONG (mos_off + 0x28) link_off
    FOR (index = 0 ; index < area_num ; ++index) BEGIN
      READ_ASCII (area_off + 0x08 + (index * 0xf0)) area
      PATCH_IF ("%area%" STRING_COMPARE_CASE "ar3000" = 0) BEGIN // wk
        SET wk = index
      END ELSE
      PATCH_IF ("%area%" STRING_COMPARE_CASE "ar6000" = 0) BEGIN // abazigal
        SET abazigal = index
      END
    END
    // read links
    FOR (index2 = 0 ; index2 < 4 ; ++index2) BEGIN
      READ_LONG (area_off + 0x50 + (index2 * 0x08) + (wk * 0xf0)) link_idx
      READ_LONG (area_off + 0x54 + (index2 * 0x08) + (wk * 0xf0)) link_num
      FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN
        READ_LONG (link_off +        ((link_idx + index3) * 0xd8)) target
        PATCH_IF (target = abazigal) BEGIN
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 9 // travel time
        END
      END
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

// these three show as corrupted by NI if done via d patch... believe it's empty DO ~~ actions. recompile fixes all issues.
COPY_EXISTING ~glanma.dlg~ ~override~
              ~ramazi.dlg~ ~override~
              ~thalan.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN END

// makes Delcia's guard's dialogue pause the game to prevent Delcia's interruption
COPY_EXISTING ~kpsold01.dlg~ ~override~
  WRITE_LONG 0x30 0
  BUT_ONLY

/////                                                  \\\\\
///// player AI script fixes                           \\\\\
/////                                                  \\\\\

// please don't cast chaotic commands on enemies
COPY ~scripts/cleric1.bs~ ~scripts/cleric1.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(CLERIC_CHAOTIC_COMMANDS)~ ~False()~
  END
  BUT_ONLY

// targets for the misc heals are poorly defined, can be mistargeted
COPY ~scripts/cleric2.bs~ ~scripts/cleric2.bs~
     ~scripts/cleric3.bs~ ~scripts/cleric3.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(HPPercentLT(LastSeenBy(Myself),10)\)~ ~See([PC]) \1~
    REPLACE_TEXTUALLY ~\(HPPercentLT(Myself,90)[%TAB% %LNL%%MNL%%WNL%]+HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(MostDamagedOf(Myself),CLERIC_CURE_LIGHT_WOUNDS)~
    ~\1 Spell(Myself,CLERIC_CURE_LIGHT_WOUNDS)~
    REPLACE_TEXTUALLY ~MostDamagedOf(Myself)~ ~MostDamagedOf()~
  END
  BUT_ONLY

// check to see if outside is broken
COPY ~scripts/cleric3.bs~ ~scripts/cleric3.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AreaCheck("UTDOOR)")~ ~AreaType(OUTDOOR)~
  END
  BUT_ONLY

// As a single-class fighter the PC should not be able to Hide in Shadows
COPY_EXISTING   ~scripts/fighter2.bs~ ~scripts~ // ranger ranged
                ~scripts/thief1.bs~   ~scripts~ // thief aggressive
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!StateCheck(Myself,STATE_INVISIBLE)~ ~!StateCheck(Myself,STATE_INVISIBLE) OR(3) Class(Myself,THIEF_ALL) Class(Myself,RANGER_ALL) Class(Myself,MONK)~
  END
  BUT_ONLY

// check if target dead broken
COPY ~scripts/fighter4.bs~ ~scripts/fighter4.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Dead("astAttackerOf(LastSeenBy(Myself)))")~ ~!StateCheck(LastAttackerOf(LastSeenBy(Myself)),STATE_REALLY_DEAD)~
  END
  BUT_ONLY

// please don't cast ray of enfeeblement on yourself
COPY ~scripts/mage3.bs~ ~scripts/mage3.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Spell(Myself,WIZARD_RAY_OF_ENFEEBLEMENT)~ ~Spell(LastSeenBy(Myself),WIZARD_RAY_OF_ENFEEBLEMENT)~
  END
  BUT_ONLY

COPY_EXISTING ~scripts/thief2.bs~ ~scripts~ // thief defensive
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!StateCheck(Myself,STATE_INVISIBLE)~ ~!StateCheck(Myself,STATE_INVISIBLE) OR(3) Class(Myself,THIEF_ALL) Class(Myself,RANGER_ALL) Class(Myself,MONK)~
    REPLACE_TEXTUALLY ~HPPercentLT(Myself,50)~ ~HPPercentLT(Myself,50) !StateCheck(Myself,STATE_INVISIBLE) OR(3) Class(Myself,THIEF_ALL) Class(Myself,RANGER_ALL) Class(Myself,MONK)~
  END
  BUT_ONLY

COPY_EXISTING ~scripts/thief3.bs~ ~scripts~ // thief adventurer
              ~scripts/thief4.bs~ ~scripts~ // thief scout
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!ModalState(DETECTTRAPS)~ ~!ModalState(DETECTTRAPS) OR(2) Class(Myself,THIEF_ALL) Class(Myself,MONK)~
  END
  BUT_ONLY

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

// actual compile errors first
// override corrupt biffed scripts with blanks
EXTEND_BOTTOM ~ar2812.bcs~   ~bg2fixpack/compile/ar0512.baf~

// fix compile error
COPY_EXISTING ~IDIOT01.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(31)~ ~OR(30)~
  END
  BUT_ONLY

// fix compile errors with bad numbers in OR blocks
COPY_EXISTING ~VICG.BCS~  ~override~
              ~VICG1.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(4)~ ~OR(2)~
  END
  BUT_ONLY

// big, major script patch 0/1456
COPY_EXISTING ~abazdrag.bcs~ ~override~ // !Dead("astSeenBy())"), !Dead("yself)")
              ~aeriex.bcs~   ~override~ // !Dead("yself)")
              ~aesgar.bcs~   ~override~ // !Dead("astSeenBy())"), !Dead("yself)")
              ~alarm25.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~amlich01.bcs~ ~override~ // !Dead("yself)")
              ~amlich02.bcs~ ~override~ // !Dead("yself)")
              ~anomx.bcs~    ~override~ // !Dead("astSeenBy(Myself))")
              ~apprenti.bcs~ ~override~ // !Dead("yself)"), CreateVisualEffectObject("ANCALL",Myself)
              ~ar0329.bcs~   ~override~ // Dead("aran02"), ambients not present but left intact
              ~ar1202.bcs~   ~override~ // Dead("firkraag"), "HOBARC02" fixed in cre file
              ~ar2500.bcs~   ~override~ // TakeItemReplace("DWDUST","DWCSW1H01",Player1)
              ~ar3021.bcs~   ~override~ // AddXP2DA("Plot06e")
              ~ar4000.bcs~   ~override~ // TakeItemReplace("DWDUST","DWCSW1H01",Player1)
              ~asylum.bcs~   ~override~ // CreateVisualEffectObject("ppireni1","SPDIMNDR")
              ~balmonk.bcs~  ~override~ // !Dead("astHeardBy(Myself))")
              ~beheld01.bcs~ ~override~ // !Dead("yself)")
              ~carch20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~carch30b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cassa20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cassa20c.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cauldron.bcs~ ~override~ // PlaySound("EFF_38")
              ~cbarb20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cbard12a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cbers20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cespen.bcs~   ~override~ // ApplySpell("runrun",0)
              ~cfigh20a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cfigh20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cfigh30b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cfmag20b.bcs~ ~override~ // !Dead("yself)")
              ~chaldie.bcs~  ~override~ // !Dead("yself)")
              ~chalpc01.bcs~ ~override~ // !Dead("astHeardBy(Myself))")
              ~ckens20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~ckens30b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~ckmag20a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~ckmag20b.bcs~ ~override~ // !Dead("yself)")
              ~ckthi20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cmacl20a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cmage20a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cmage20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cmonk20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cthie20a.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cthie20b.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~cut01.bcs~    ~override~ // ActionOverride("Imoen",SetDialog("None"))
              ~cut215a.bcs~  ~override~ // ActionOverride("gorsuc01",MoveBetweenAreasEffect("AR3015","SPDOOR",[1639.718],14))
              ~cut216a.bcs~  ~override~ // MoveToObject("ammonk")
              ~cut35b.bcs~   ~override~ // ActionOverride("Jaheria",Face(4))
              ~cut49e.bcs~   ~override~ // PlaySound("ISC_03A")
              ~cut57c.bcs~   ~override~ // CreateVisualEffectObject("SPOWWRD",Player1)
              ~cutd1.bcs~    ~override~ // ActionOverride("Imoen",SetDialog("None"))
              ~dadrow5.bcs~  ~override~ // !Dead("yself)")
              ~daqdrow.bcs~  ~override~ // !Dead("yself)")
              ~ddguard7.bcs~ ~override~ // CreateVisualEffectObject("yself","SPFLESHS")
              ~deck622.bcs~  ~override~ // Dead("layer1)") through Dead("layer6)")
              ~degard.bcs~   ~override~ // !Dead("PC])")
              ~degard2.bcs~  ~override~ // !Dead("yself)")
              ~draconis.bcs~ ~override~ // !Dead("yself)")
              ~dragbrow.bcs~ ~override~ // ApplySpell("runrun",0), !Dead("astSeenBy())")
              ~draggre2.bcs~ ~override~ // !Dead("astSeenBy())")
              ~draggree.bcs~ ~override~ // !Dead("astSeenBy())")
              ~dragsilv.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~druidx.bcs~   ~override~ // AreaCheck("UTDOOR)")
              ~dwvith.bcs~   ~override~ // !Dead("astSeenBy(Myself))")
              ~edwin.bcs~    ~override~ // !Dead("PC])")
              ~eltan.bcs~    ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~enforam.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~finsol04.bcs~ ~override~ // CreateCreatureObjectEffect("DEMOSUM1","SPPLOYMP",Myself), same with DEMSUC01 and TELALU1
              ~firlch01.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~fswheel.bcs~  ~override~ // AddXP2DA("Plot05B")
              ~gorcamb2.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~gorcamb3.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~gorcamb4.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~gorcamb5.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~gorcamb6.bcs~ ~override~ // !Dead("astAttackerOf("), !Dead("astSeenBy(Myself))")
              ~gordeckf.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~gorgoli.bcs~  ~override~ // AddXP2DA("PLOT05C")
              ~gorlic01.bcs~ ~override~ // !Dead("yself)")
              ~gorsal.bcs~   ~override~ // !Dead("astSeenBy())")
              ~gorstam.bcs~  ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~gp115.bcs~    ~override~ // !Dead("astSeenBy(Myself))")
              ~gp116.bcs~    ~override~ // !Dead("astSeenBy(Myself))")
              ~gparcher.bcs~ ~override~ // !Dead("astHeardBy(Myself))")
              ~gphealer.bcs~ ~override~ // !Dead("astHeardBy(Myself))"), !Dead("astHeardBy())")
              ~gpkensai.bcs~ ~override~ // !Dead("astHeardBy(Myself))")
              ~gpmage1.bcs~  ~override~ // !Dead("astHeardBy(Myself))")
              ~gpmage2.bcs~  ~override~ // !Dead("astHeardBy(Myself))")
              ~gpmerc.bcs~   ~override~ // !Dead("astHeardBy(Myself))")
              ~gpshout.bcs~  ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~gpthief.bcs~  ~override~ // !Dead("astHeardBy(Myself))")
              ~grpsht01.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~hellgen.bcs~  ~override~ // !Dead("yself)")
              ~hellslay.bcs~ ~override~ // !Dead("yself)")
              ~illasera.bcs~ ~override~ // !Dead("yself)")
              ~itglobes.bcs~ ~override~ // AddXP2DA("PLOT05C")
              ~jatermin.bcs~ ~override~ // !Dead("yself)")
              ~kaysmg03.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~kaysmg04.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~keldornx.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~kpfight.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~kproen02.bcs~ ~override~ // !Dead("yself)")
              ~kuoarc20.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~kuowar20.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~lehtinan.bcs~ ~override~ // !Dead("yself)")
              ~mage10a.bcs~  ~override~ // Spell(Myself,WIZARD_LIGHTNING_BOLT)
              ~mage10d.bcs~  ~override~ // Spell(Myself,WIZARD_LIGHTNING_BOLT)
              ~mage12b.bcs~  ~override~ // !Dead("yself)")
              ~mage12d.bcs~  ~override~ // !Dead("yself)")
              ~mage12e.bcs~  ~override~ // !Dead("yself)")
              ~mage14a.bcs~  ~override~ // !Dead("yself)")
              ~mage14b.bcs~  ~override~ // !Dead("yself)")
              ~mage14c.bcs~  ~override~ // !Dead("yself)")
              ~mage14d.bcs~  ~override~ // !Dead("yself)")
              ~mage14m.bcs~  ~override~ // !Dead("yself)")
              ~mage14t.bcs~  ~override~ // !Dead("yself)")
              ~mage16a.bcs~  ~override~ // !Dead("yself)")
              ~mage16b.bcs~  ~override~ // !Dead("yself)")
              ~mage16c.bcs~  ~override~ // !Dead("yself)")
              ~mage16m.bcs~  ~override~ // !Dead("yself)")
              ~mage18a.bcs~  ~override~ // !Dead("yself)")
              ~mage18b.bcs~  ~override~ // !Dead("yself)")
              ~mage18c.bcs~  ~override~ // !Dead("yself)")
              ~mage18d.bcs~  ~override~ // !Dead("yself)")
              ~mage18e.bcs~  ~override~ // !Dead("yself)")
              ~mage18y.bcs~  ~override~ // !Dead("yself)")
              ~mage20a.bcs~  ~override~ // !Dead("yself)")
              ~mage20b.bcs~  ~override~ // !Dead("yself)")
              ~mage20c.bcs~  ~override~ // !Dead("yself)")
              ~magehigh.bcs~ ~override~ // !Dead("astHeardBy(Myself))")
              ~mazzy.bcs~    ~override~ // Dead("gorf03") fixed elsewhere; See("Player1"), !StateCheck("Player1",STATE_SLEEPING)
              ~mindal01.bcs~ ~override~ // !Dead("yself)")
              ~minscx.bcs~   ~override~ // AreaCheck("UTDOOR)")
              ~minvsed.bcs~  ~override~ // AreaCheck("UTDOOR)")
              ~movie03a.bcs~ ~override~ // ActionOverride("Player2",Rest()) through ActionOverride("Player6",Rest())
              ~movie03b.bcs~ ~override~ // ActionOverride("Player2",Rest()) through ActionOverride("Player6",Rest())
              ~neva.bcs~     ~override~ // Spell(Myself,WIZARD_LIGHTNING_BOLT)
              ~npcdru1.bcs~  ~override~ // AreaCheck("UTDOOR)")
              ~ppcrus1.bcs~  ~override~ // Dead("astTrigger)"), !Dead("astTrigger)")
              ~pwarden.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~sarbul01.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sarbul02.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sarbul03.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sardw01.bcs~  ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sardw02.bcs~  ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sardw03.bcs~  ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sendark.bcs~  ~override~ // !Dead("yself)")
              ~sengua04.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~sengua05.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~shadel.bcs~   ~override~ // !Dead("yself)")
              ~spwnrak.bcs~  ~override~ // CreateCreature("dogre01",[-1.-1],0)
              ~sumtan01.bcs~ ~override~ // !Dead("astSeenBy(Myself))")
              ~teltief3.bcs~ ~override~ // !Dead("astHeardBy(Myself))")
              ~telwrai.bcs~  ~override~ // !Dead("yself)")
              ~tempv01.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~temsup.bcs~   ~override~ // !Dead("PC])")
              ~tief3.bcs~    ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
              ~udmaster.bcs~ ~override~ // !Dead("yself)")
              ~udtrain.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~und5509.bcs~  ~override~ // CreateVisualEffect("SPGFLSH",[737.568])
              ~useitem.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~valvsed.bcs~  ~override~ // AreaCheck("UTDOOR)")
              ~valygx.bcs~   ~override~ // AreaCheck("UTDOOR)")
              ~vicx.bcs~     ~override~ // !Dead("astSeenBy(Myself))")
              ~xeiwin.bcs~   ~override~ // !Dead("astHeardBy(Myself))")
              ~yagalt.bcs~   ~override~ // CreateVisualEffect("SPMAGICH", x2
              ~yagart.bcs~   ~override~ // CreateVisualEffect("SPMAGICH", x2
              ~yochlol.bcs~  ~override~ // !Dead("yself)")
              ~yscara.bcs~   ~override~ // !Dead("yself)")
              ~ysgp01.bcs~   ~override~ // !Dead("yself)")
              ~ysgp02.bcs~   ~override~ // !Dead("yself)")
              ~ysgp03.bcs~   ~override~ // !Dead("yself)")
              ~ysgp04.bcs~   ~override~ // !Dead("yself)")
              ~ysgrunt.bcs~  ~override~ // !Dead("astSeenBy(Myself))")
              ~zilmag01.bcs~ ~override~ // !Dead("yself)"), !Dead("astSeenBy(Myself))")
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY %Dead("yself)")% %StateCheck(Myself,STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %Dead("astSeenBy(\(Myself\)?))")% %StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %Dead("astHeardBy(\(Myself\)?))")% %StateCheck(LastHeardBy(Myself),STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %Dead("astTrigger)")% %StateCheck(LastTrigger,STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %Dead("PC\])")% %StateCheck([PC],STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %AreaCheck("UTDOOR)")% %AreaType(OUTDOOR)%
    REPLACE_TEXTUALLY %CreateVisualEffectObject("yself","SPFLESHS")% %CreateVisualEffectObject("SPFLESHS",Myself)%
    REPLACE_TEXTUALLY %TakeItemReplace("DWDUST","DWCSW1H01"% %TakeItemReplace("DWDUST","DWSW1H01"%
    REPLACE_TEXTUALLY %"EFF_38"% %"EFF_M38"%
    REPLACE_TEXTUALLY %"ISC_03A"% %"MISC_03A"%
    REPLACE_TEXTUALLY %CreateVisualEffectObject("ANCALL",Myself)% %CreateVisualEffectObject("AMCALL",Myself)%
    REPLACE_TEXTUALLY %CreateVisualEffectObject("SPOWWRD",Player1)% %CreateVisualEffectObject("SPPOWWRD",Player1)%
    REPLACE_TEXTUALLY %CreateVisualEffectObject("ppireni1","SPDIMNDR")% %CreateVisualEffectObject("SPDIMNDR","ppireni1")%
    REPLACE_TEXTUALLY %ActionOverride("Imoen",SetDialog\(ue\)?("None"))% %ActionOverride("Imoen",SetDialog(""))%
    REPLACE_TEXTUALLY %Dead("firkraag")% %Dead("firkra02")%
    REPLACE_TEXTUALLY %Dead("aran02")% %Dead("aran")%
    REPLACE_TEXTUALLY %"Jaheria"% %"Jaheira"%
    REPLACE_TEXTUALLY %See("Player1")% %See(Player1)%
    REPLACE_TEXTUALLY %ActionOverride("Player\([2-6]\)",Rest())% %ActionOverride(Player\1,Rest())%
    REPLACE_TEXTUALLY %StateCheck("Player1",STATE_SLEEPING)% %StateCheck(Player1,STATE_SLEEPING)%
    REPLACE_TEXTUALLY %CreateCreature("dogre01",\[-1\.-1\],0)% %CreateCreature("ogre01",[-1.-1],0)%
    REPLACE_TEXTUALLY %Spell(Myself,WIZARD_LIGHTNING_BOLT)% %Spell(NearestEnemyOf(Myself),WIZARD_LIGHTNING_BOLT)%
    REPLACE_TEXTUALLY %Dead("layer\([1-6]\))")% %StateCheck(Player\1,STATE_REALLY_DEAD)%
    REPLACE_TEXTUALLY %ApplySpell("runrun",0)% %ApplySpellRES("runrun",Myself)%
    REPLACE_TEXTUALLY %AddXP2DA("PLOT05C")% %AddXP2DA("PLOT5C")%
    REPLACE_TEXTUALLY %AddXP2DA("PLOT05B")% %AddXP2DA("PLOT5B")%
    REPLACE_TEXTUALLY %AddXP2DA("PLOT06E")% %AddXP2DA("PLOT6E")%
    REPLACE_TEXTUALLY %CreateCreatureObjectEffect("DEMOSUM1","SPPLOYMP",Myself)% %CreateCreatureObjectEffect("DEMOSUM1","SPPOLYMP",Myself)%
    REPLACE_TEXTUALLY %CreateCreatureObjectEffect("DEMSUC01","SPPLOYMP",Myself)% %CreateCreatureObjectEffect("DEMSUC01","SPPOLYMP",Myself)%
    REPLACE_TEXTUALLY %CreateCreatureObjectEffect("TELALU1","SPPLOYMP",Myself)% %CreateCreatureObjectEffect("TELALU1","SPPOLYMP",Myself)%
    REPLACE_TEXTUALLY %MoveBetweenAreasEffect("AR3015","SPDOOR"% %MoveBetweenAreasEffect("AR3015","SPDIMNDR"%
    REPLACE_TEXTUALLY %CreateVisualEffect("SPMAGICH"% %CreateVisualEffect("ICMAGICH"%
    REPLACE_TEXTUALLY %MoveToObject("ammonk")% %MoveToObject("ammonk01")%
    REPLACE_TEXTUALLY %CreateVisualEffect("SPGFLSH"% %CreateVisualEffect("SPGFLSH1"%
  END
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// eou romance fixes                                \\\\\
/////                                                  \\\\\

COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(SetGlobal("AnomenIsNotKnight","GLOBAL",1)\)~ ~\1 ChangeAlignment("Anomen",CHAOTIC_NEUTRAL)~
    REPLACE_TEXTUALLY ~"TALKEDTOCOR","GLOBAL"~ ~"TALKEDCOR","GLOBAL"~
    APPEND_FILE ~bg2fixpack/baf/anomen.baf~ // was extend_top'd, but no reason to
  END

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~IncrementGlobal("LoveTalk","LOCALS",1)[ %TAB%%LNL%%MNL%%WNL%]+\(SetGlobal("DerminSpawn","GLOBAL",5)[ %TAB%%LNL%%MNL%%WNL%]+SetGlobalTimer("DerminAppear","GLOBAL",\)TWO_DAYS)~
      ~\117280)~ // purge increment global, change timer length
    REPLACE_TEXTUALLY ~[ %TAB%]SetGlobalTimer("TerminselAppear","GLOBAL",FIVE_DAYS)~
                      ~ RealSetGlobalTimer("TerminselAppear","GLOBAL",3600)~ // originally 36000 seems to be more than random.
  END

COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("LoveTalk","LOCALS",71)~ ~False()~ // not a regular scheduled talk; only after handmaiden/yochlol encounter
  END

// If ToB installed, patch ToB BCS files:
ACTION_IF tob_game THEN BEGIN

  COPY_EXISTING ~anom25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(Global("AnomenSummoned","GLOBAL",1)\)~ ~\1 !Global("AnomenIsKnight","GLOBAL",1)~
    END
  EXTEND_TOP ~anom25.bcs~ ~bg2fixpack/baf/anom25.baf~

  COPY_EXISTING ~jahe25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(2)~ ~!Global("JaheiraRomanceActive","GLOBAL",0) !Global("JaheiraRomanceActive","GLOBAL",3) OR(2)~
    END

END

/////                                                  \\\\\
///// missing IDS entries                              \\\\\
/////                                                  \\\\\

// notes
// unused in BG2: andris, ch1cut01, ch1cut02, daitel, deathkni, hgwiz01, pheoarch, semaj
// SpellCast(0) is needed for CW summoning in ar0020, ar0300, ar0400, ar0500, ar0700, ar0900, ar1000
// SpellCastOnMe([ANYONE],0) is needed for nishru01; nishruus heal themselves when magic is used against them
// cdtamoko error is from cloning gpkensai
// specifics errors safe to ignore (?)
// SpellCast([PC],0) for ttxan is appropriate for tutorial

// alter missing areatype check to match banter trigger
COPY_EXISTING ~aerie.bcs~   ~override~
              ~anomen.bcs~  ~override~
              ~minsc.bcs~   ~override~
              ~nalia.bcs~   ~override~
              ~yoshimo.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~AreaType(0)~ ~AreaType(OUTDOOR)~ // for Minsc Umar Hills reminder, Nalia-Edwina banter
 END
 BUT_ONLY

// many priest scripts have a failed CLERIC_HEAL call
COPY_EXISTING ~aeriex.bcs~   ~override~
              ~anomx.bcs~    ~override~
              ~anvskel.bcs~  ~override~
              ~pries18a.bcs~ ~override~
              ~pries18b.bcs~ ~override~
              ~pries18c.bcs~ ~override~
              ~pries18d.bcs~ ~override~
              ~saerkx.bcs~   ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Spell(Myself,0)~ ~Spell(Myself,CLERIC_HEAL)~ // trigger checks for this spell
 END
 BUT_ONLY

// aerie's very straightforward--both missing Spell calls check for a spell in memory in the trigger
COPY_EXISTING ~aeriex.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)~ ~Spell(LastSeenBy(Myself),CLERIC_ANIMAL_SUMMONING_3)~ // trigger checks for this spell
 END
 BUT_ONLY

// recurring issue--going to enemy, but checks that allegiance is either neutral, goodbutblue, or ?
COPY_EXISTING ~gparcher.bcs~ ~override~
              ~gpshout.bcs~  ~override~
              ~trgrd02.bcs~  ~override~
              ~trgrd03.bcs~  ~override~
              ~trgrdr01.bcs~ ~override~
              ~trgrdr02.bcs~ ~override~
              ~trgrdr03.bcs~ ~override~
              ~trgrdr04.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,EVILBUTBLUE)~ // third allegiance check?
 END
 BUT_ONLY

// anomen's very straightforward--missing Spell calls check for a spell in memory in the trigger
COPY_EXISTING ~anomx.bcs~   ~override~
              ~anvskel.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)[%TAB% %LNL%%MNL%%WNL%]+AttackOneRound(LastSeenBy(Myself))~
                     ~Spell(LastSeenBy(Myself),CLERIC_SLAY_LIVING) AttackOneRound(LastSeenBy(Myself))~ // trigger checks for this spell
   REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)[%TAB% %LNL%%MNL%%WNL%]+Continue()~ 
                     ~Spell(LastSeenBy(Myself),CLERIC_ANIMAL_SUMMONING_3) Continue()~ // trigger checks for this spell
 END
 BUT_ONLY

// pairing MI with trigger
COPY_EXISTING ~clone1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY 
      ~HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+Spell(Myself,WIZARD_MIRROR_IMAGE)\)~
      ~!StateCheck(Myself,STATE_MIRRORIMAGE) HaveSpell(WIZARD_MIRROR_IMAGE)\1~ // Spell action associated with this block casts WIZARD_MIRROR_IMAGE, also add check to make sure MI isn't active
  END
  BUT_ONLY

// end of CI Escape cutscene, everyone leaving
COPY_EXISTING ~cut01g.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ForceSpell(Myself,0)~
      ~ForceSpell(Myself,DRYAD_TELEPORT)~   // cast by CSCowl6 and 8
    REPLACE_TEXTUALLY 
      ~ForceSpell("CSImoen",DRYAD_TELEPORT)~ 
      ~ForceSpell("CSImoen",DRYAD_TELEPORT) ForceSpell(Myself,DRYAD_TELEPORT)~   // cast by CSCowl4 on Imoen, added to causes effects for self as well
    REPLACE_TEXTUALLY
      ~ForceSpell("CSIren",0)~
      ~ForceSpell("CSIren",DRYAD_TELEPORT) ForceSpell(Myself,DRYAD_TELEPORT)~ // cast by CSCowl7 on Irenicus, also makes CSCowl7 go away
  END
  BUT_ONLY

// should attack Jon's minions until minions dead, then turn on party
COPY_EXISTING ~ishthf01.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,GOODBUTBLUE)~
 END
 BUT_ONLY

// missing spell is preceded by stringhead
COPY_EXISTING ~jatermin.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~ApplySpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)~ // preceded by string
 END
 BUT_ONLY

// action dictates trigger
COPY_EXISTING ~jatermin.bcs~ ~override~
              ~mage20a.bcs~  ~override~
              ~mage20c.bcs~  ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_MAZE)~ // associated action casts this spell
 END
 BUT_ONLY

// should attack Jon's minions until minions dead, then turn on party
COPY_EXISTING ~jonthief.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,GOODBUTBLUE)~
 END
 BUT_ONLY

// mage10d checks for buffing spells; remove fear appears to fit the gap between bless and sanctuary
COPY_EXISTING ~mage10d.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~SpellCastPriest(\[PC\],0)~ ~SpellCastPriest([PC],CLERIC_REMOVE_FEAR)~ // triggers are for party buffs
 END
 BUT_ONLY

// missing spell trigger is followed by casting spell
COPY_EXISTING ~mage16m.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_STONE_SKIN)~ // associated action casts this spell
 END
 BUT_ONLY

// missing spell is preceded by stringhead
COPY_EXISTING ~mage8d.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_CHROMATIC_ORB)~ // associated action casts this spell
 END
 BUT_ONLY

// surakw1-4 identical except for surakw2's spell trigger
COPY_EXISTING ~surakw2.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(IncrementGlobal("Fight","LOCALS",1)[%TAB% %LNL%%MNL%%WNL%]+\)ForceSpell(Myself,0)~
        ~\1 ForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)~ // from surakw4
    REPLACE_TEXTUALLY ~\(StateCheck(Myself,STATE_SILENCED)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)ForceSpell(Myself,0)~
        ~\1 ForceSpell(Myself,WIZARD_VOCALIZE)~ // if silenced - vocalize?
    REPLACE_TEXTUALLY ~\(!See(LastSeenBy(Myself))[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)ForceSpell(Myself,0)~
        ~\1 ForceSpell(Myself,WIZARD_TRUE_SIGHT)~ // if can detect but not see - true sight?
    REPLACE_TEXTUALLY
      ~ApplySpell(Myself,0)\([%TAB% %LNL%%MNL%%WNL%]+DisplayString(Myself,39968)[%TAB% %LNL%%MNL%%WNL%]+\)ApplySpell(Myself,0)~
      ~ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY) \1 ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)~ // two spell spell trigger
 END
 BUT_ONLY

// surakw1-4 identical except for surakw1's spell trigger
COPY_EXISTING ~surakw3.bcs~  ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~ApplySpell(Myself,0)\([%TAB% %LNL%%MNL%%WNL%]+DisplayString(Myself,39968)[%TAB% %LNL%%MNL%%WNL%]+\)ApplySpell(Myself,0)\([%TAB% %LNL%%MNL%%WNL%]+\)ApplySpell(Myself,0)~
       ~ApplySpell(Myself,WIZARD_STONE_SKIN) \1 ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY) \2 ApplySpell(Myself,WIZARD_MISLEAD)~ // from surakw4
   REPLACE_TEXTUALLY ~\(IncrementGlobal("Fight","LOCALS",1)[%TAB% %LNL%%MNL%%WNL%]+\)ForceSpell(Myself,0)~
       ~\1 ForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)~ // from surakw4
 END
 BUT_ONLY

// surakw1-4 identical except for surakw1's spell trigger
COPY_EXISTING ~surakw4.bcs~  ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~ApplySpell(Myself,0)\([%TAB% %LNL%%MNL%%WNL%]+DisplayString(Myself,39968)[%TAB% %LNL%%MNL%%WNL%]+\)ApplySpell(Myself,0)\([%TAB% %LNL%%MNL%%WNL%]+\)ApplySpell(Myself,0)~
       ~ApplySpell(Myself,WIZARD_STONE_SKIN) \1 ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY) \2 ApplySpell(Myself,WIZARD_MISLEAD)~ // three spell spell trigger
 END
 BUT_ONLY

// viccy's combat script for keldorn fight
COPY_EXISTING ~vicvskel.bcs~  ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Spell(Myself,0)~ ~Spell(Myself,CLERIC_HEAL)~
   REPLACE_TEXTUALLY ~\(HPGT("Keldorn",20)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
     ~\1 Spell(LastSeenBy(Myself),CLERIC_SLAY_LIVING)~
   REPLACE_TEXTUALLY ~\(HaveSpell(CLERIC_ANIMAL_SUMMONING_3)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
     ~\1 Spell(LastSeenBy(Myself),CLERIC_ANIMAL_SUMMONING_3)~
 END
 BUT_ONLY

// viccy's generic combat script
COPY_EXISTING ~vicx.bcs~  ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Spell(Myself,0)~ ~Spell(Myself,CLERIC_HEAL)~
   REPLACE_TEXTUALLY ~\(HPGT(NearestEnemyOf(Myself),20)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
     ~\1 Spell(LastSeenBy(Myself),CLERIC_SLAY_LIVING)~
   REPLACE_TEXTUALLY ~\(HaveSpell(CLERIC_ANIMAL_SUMMONING_3)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
     ~\1 Spell(LastSeenBy(Myself),CLERIC_ANIMAL_SUMMONING_3)~
 END
 BUT_ONLY

ACTION_IF tob_game THEN BEGIN // tob crap

  // many dispel magic actions have two missing CheckStatGT triggers in an OR() block
  COPY_EXISTING ~abazdrag.bcs~ ~override~
                ~amlich02.bcs~ ~override~
                ~dragbrow.bcs~ ~override~
                ~finsol04.bcs~ ~override~
                ~gorgua02.bcs~ ~override~
                ~meliss01.bcs~ ~override~
                ~meliss02.bcs~ ~override~
                ~meliss03.bcs~ ~override~
                ~planet.bcs~   ~override~
                ~senbattl.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~CheckStatGT(LastSeenBy(Myself),0,0)[%TAB% %LNL%%MNL%%WNL%]+CheckStatGT(LastSeenBy(Myself),0,0)~
                       ~CheckStatGT(LastSeenBy(Myself),0,IMPROVEDHASTE) CheckStatGT(LastSeenBy(Myself),0,STONESKINS)~
                       // triggers for a dispel magic
   END
   BUT_ONLY
  
  // recurring issue--going to enemy, but checks that allegiance is either neutral, goodbutblue, or ?
  COPY_EXISTING ~amduel1.bcs~  ~override~
                ~amduel2.bcs~  ~override~
                ~balmonk.bcs~  ~override~
                ~chalpc01.bcs~ ~override~
                ~gorbat.bcs~   ~override~
                ~gorbat1.bcs~  ~override~
                ~gortan.bcs~   ~override~
                ~gortan1.bcs~  ~override~
                ~gphealer.bcs~ ~override~
                ~gpkensai.bcs~ ~override~
                ~gpmage1.bcs~  ~override~
                ~gpmage2.bcs~  ~override~
                ~grpsht01.bcs~ ~override~
                ~sarbul01.bcs~ ~override~
                ~sarbul02.bcs~ ~override~
                ~sarbul03.bcs~ ~override~
                ~sardw01.bcs~  ~override~
                ~sardw02.bcs~  ~override~
                ~sardw03.bcs~  ~override~
                ~sarrein.bcs~  ~override~
                ~teltief3.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,EVILBUTBLUE)~ // third allegiance check?
   END
   BUT_ONLY

  COPY_EXISTING ~amlich02.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~ForceSpell(Myself,0)~ ~ForceSpell(Myself,WIZARD_STONE_SKIN)~ // trigger, locals checks for stoneskins
   END
   BUT_ONLY

  // all other cres are hasted with identical blocks to this one
  COPY_EXISTING ~cut207c.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~ApplySpell(Myself,CUTSCENE_HASTE)~ // all other creatures hasted in similar blocks
   END
   BUT_ONLY

  // displays 'protection from energy' string without, uh, casting it
  COPY_EXISTING ~dlich01.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~ApplySpell(Myself,WIZARD_PROTECTION_FROM_ENERGY)~ // action immediately above is displaystring 'protection from energy'
   END
   BUT_ONLY
  
  // saladrex's dispel block is missing a stat check and valid stat.ids refs
  COPY_EXISTING ~gorsal.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~OR(8)\([%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_HASTED)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_BLESS)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_BLUR)[%TAB% %LNL%%MNL%%WNL%]+StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)[%TAB% %LNL%%MNL%%WNL%]+\)CheckStatGT(LastSeenBy(Myself),0,0)\([%TAB% %LNL%%MNL%%WNL%]+THEN\)~
                       ~OR(9) \1 CheckStatGT(LastSeenBy(Myself),0,IMPROVEDHASTE) CheckStatGT(LastSeenBy(Myself),0,STONESKINS) \2~
   END
   BUT_ONLY
  
  // gphealer should run away and heal themselves, but missing actual casting; two more unknown issues
  COPY_EXISTING ~gphealer.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY
       ~HaveSpell(\([A-Z_]+\))\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+RunAwayFrom(NearestEnemyOf(Myself),30)[%TAB% %LNL%%MNL%%WNL%]+\)Spell(Myself,0)~ 
       ~HaveSpell(\1) \2 Spell(Myself,\1)~ //
   END
   BUT_ONLY
  
  // missing a priest spell in their resist fear trigger
  COPY_EXISTING ~gpmage1.bcs~  ~override~
                ~magehigh.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~SpellCastPriest(\[GOODCUTOFF\],0)~ ~SpellCastPriest([GOODCUTOFF],CLERIC_CLOAK_OF_FEAR)~ // only missing divine 'fear' spell
   END
   BUT_ONLY

  // trigger defines spell
  COPY_EXISTING ~senbattl.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
     REPLACE_TEXTUALLY ~Spell(Myself,0)~ ~Spell(Myself,WIZARD_STONE_SKIN)~ // associated trigger checks this spell
   END
   BUT_ONLY

END

/////                                                  \\\\\
///// other scripting fixes                            \\\\\
/////                                                  \\\\\

// Romance interests can comment on PhaereInnuendo in chapter 6 if they were not in the party during chapter 5 (the bigg) (see aerie.bcs, aeriej.dlg, jaheira.bcs, jaheiraj.dlg viconia.bcs, viconij.dlg)
COPY_EXISTING ~aerie.bcs~   ~override~
              ~jaheira.bcs~ ~override~
              ~viconia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!Range("Phaere",6)\)~ ~\1 Global("Chapter","GLOBAL",5)~
  END
  BUT_ONLY

// stop (kuo-toan) archers and air elementals from charging across areas to engage party
EXTEND_TOP ~airele01.bcs~ ~bg2fixpack/baf/gparcher.baf~
EXTEND_TOP ~gparcher.bcs~ ~bg2fixpack/baf/gparcher.baf~

// norh rep trap uses same name for timer and variable; see fixes to knight.dlg
COPY_EXISTING ~amntrp01.bcs~ ~override~
              ~amntrp02.bcs~ ~override~
              ~amntrp03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!GlobalTimerNotExpired("MostNobleOrder","GLOBAL")~ ~!GlobalTimerNotExpired("CDMostNobleOrder","GLOBAL")~
  END
  BUT_ONLY

// Puts the variable setting in front of the dialogue triggering so that Anomen's complaint about delaying Garren kid's quest gets triggered
// also add triggers to stop infinite whining if anomen is !good
COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(StartDialogu?e?NoSet(Player1)[%TAB% %LNL%%MNL%%WNL%]+\)\(SetGlobal("ddAnomenWhine","LOCALS",1)\)~
      ~\2 \1~
    REPLACE_TEXTUALLY ~Global("ddAnomenWhine","LOCALS",0)~ ~Global("ddAnomenWhine","LOCALS",0) !Dead("garkid01") !Dead("garkid02")~
  END
  BUT_ONLY

// flydian shouldn't hang around asking folks to save trademeet after it's already been saved
EXTEND_BOTTOM ~ar0020.bcs~ ~bg2fixpack/baf/ar0020.baf~

// UE not appearing bug
COPY_EXISTING ~ar0202.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("UnseeingEye","GLOBAL",1)~ ~Global("UnseeingEye","GLOBAL",1) Global("CDSpawnTheEyeOnlyOnce","GLOBAL",0)~
    REPLACE_TEXTUALLY ~CreateCreature("BHEYE",\[2429\.1914\],0)~ ~CreateCreature("BHEYE",[2429.1914],0) SetGlobal("CDSpawnTheEyeOnlyOnce","GLOBAL",1)~
  END

// instead of a wait, change to timer for new block
COPY_EXISTING ~ar0205.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("BHEYE",\[2528\.1897\],12)~ ~~
    REPLACE_TEXTUALLY ~Wait(~ ~SetGlobalTimer("CDUnseeingEyeAppears","AR0205",~
    APPEND_FILE ~bg2fixpack/baf/ar0205.baf~
  END

// fixing transition calls
COPY_EXISTING ~ar0300.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(TriggerActivation("Tran0329\)\(",[A-Z]+)\)~ ~\1a\2 \1b\2~
    REPLACE_TEXTUALLY ~Tran0303a~ ~Tran0303~
    PATCH_IF tob_game THEN BEGIN // RemoveMapNote is ToB-only
      REPLACE_TEXTUALLY ~\(AddMapNote(\[3078\.2434\],4636)\)~ ~RemoveMapNote([3078.2434],48359) \1~ // remove old map note instead of laying new one one top of it
    END
  END
  BUT_ONLY

// Arledrian goes hostile but stands there due to area script bug
COPY_EXISTING ~ar0312.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("ArledHostile","GLOBAL",1)~ ~SetGlobal("ArledHostile","AR0312",1)~
  END
  BUT_ONLY

// allows resting in thieves guild stronghold
EXTEND_BOTTOM ~ar0321.bcs~ ~bg2fixpack/baf/ar032x.baf~
EXTEND_BOTTOM ~ar0322.bcs~ ~bg2fixpack/baf/ar032x.baf~
EXTEND_BOTTOM ~ar0323.bcs~ ~bg2fixpack/baf/ar032x.baf~
EXTEND_BOTTOM ~ar0324.bcs~ ~bg2fixpack/baf/ar032x.baf~

// multiple stringheads for thieves return fix; not paying thieves guild quota fix; see also baldur.bcs, joster.bcs, shthlt01.dlg
COPY_EXISTING ~ar0322.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("NotifyThiefHead","GLOBAL",0)~ ~~
    REPLACE_TEXTUALLY ~CreateCreature("SHTH05",\[691\.292\],2)~
      ~SetGlobal("CDJoster","AR0322",1) CreateCreature("SHTH05",[691.292],2)~
  END
  BUT_ONLY

// welther can spawn infinitely, Shagbag's always-true block disables half the area script
COPY_EXISTING ~ar0400.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("ElgeaGone","GLOBAL",0)~
                      ~Global("ElgeaGone","GLOBAL",0)
                       Global("ElgeaFree","GLOBAL",0)~
    REPLACE_TEXTUALLY ~\(GlobalTimerExpired("FindShagbag","GLOBAL")[ %TAB%%LNL%%MNL%%WNL%]+\)\(THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+\)\(ActionOverride("korshag",DestroySelf())\)~
      ~\1 Global("CDShagbagHogsScript","AR0400",0) \2 SetGlobal("CDShagbagHogsScript","AR0400",1) \3~
  END

// stops Yoshimo's block from interrupting everything else in the area
COPY_EXISTING ~ar0406.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("WorkingForBodhi","GLOBAL",1)~ ~Global("WorkingForBodhi","GLOBAL",1) Exists("Yoshimo") InMyArea("Yoshimo")~
  END
  BUT_ONLY

// Teos/Planar Sphere fix
EXTEND_TOP ~ar0411.bcs~ ~bg2fixpack/baf/ar0411.baf~

// Tyrianna fix, part 1 (second is plgirl01.cre patch)
COPY_EXISTING ~ar0415.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Wait(5)[ %TAB%%LNL%%MNL%%WNL%]+StartCutSceneMode()~ ~StartCutSceneMode() Wait(3)~ // move wait after cutscene and shorten it
    REPLACE_TEXTUALLY ~Wait(2)~ ~StartCutSceneMode() Wait(2)~ // wrap wait/spawns in cutscene mode
    REPLACE_TEXTUALLY ~\(CreateCreature("HURGIS[FR]",\[528\.353\],7)\)~ ~\1 EndCutSceneMode()~ // wrap wait/spawns in cutscene mode
  END
  BUT_ONLY

// saerk's doors should lock and unlock together
COPY_EXISTING ~ar0500.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Unlock("DOOR0504")~ ~Unlock("DOOR0504") Unlock("DOOR0505b")~
    REPLACE_TEXTUALLY ~\bLock("DOOR0504")~ ~CloseDoor("DOOR0504") CloseDoor("DOOR0505b") Lock("DOOR0504") Lock("DOOR0505b")~
  END
  BUT_ONLY

// area objects can't have LOCALS variables
// bard playhouse issues--transition can be left open
COPY_EXISTING ~ar0522.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"SamuelAttacks","LOCALS"~ ~"SamuelAttacks","AR0522"~
    REPLACE_TEXTUALLY ~GlobalTimerExpired("MeetHiggin6a","GLOBAL")~
                      ~GlobalTimerExpired("MeetHiggin6a","GLOBAL") Global("BardPlot1","GLOBAL",40)~
  END
  BUT_ONLY

// bard playhouse issues--jenna not going to proper spot, meck sometimes not appearing upon completion
COPY_EXISTING ~ar0523.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride("bdact03",MoveToPoint(\[1301\.453\]))~
                      ~ActionOverride("bdact03",MoveToPoint([1301.181]))~
    REPLACE_TEXTUALLY ~OR(2)[ %TAB%%LNL%%MNL%%WNL%]+\(Global("BardPlot1","GLOBAL",30)\)~ ~OR(3) \1 Global("BardPlot1","GLOBAL",25)~
  END
  BUT_ONLY

// Waukeen's Promenade script loop fix (Nythrun)
COPY_EXISTING ~ar0700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\([ %TAB%]GlobalTimerExpired("Bystander","GLOBAL")\)~ ~\1 OR(2) Exists("bystand1") Exists("bystand2")~
  END
  BUT_ONLY

// ar0701 shouldn't be able to trigger in ar0205, but it does
COPY_EXISTING ~ar0701.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~PartyHasItem("MISC5Z")~ 
      ~PartyHasItem("MISC5Z") InMyArea(Player1)~
  END
  BUT_ONLY

// death of mekrath not clearing journal entries; see mekrat.dlg
EXTEND_BOTTOM ~ar0705.bcs~ ~bg2fixpack/baf/ar0705.baf~

// random Garrick stuff can prevent Bodhi's appearance
COPY_EXISTING ~ar0800.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("GarrickSpeak","GLOBAL",1)~
                      ~False()~
    APPEND_FILE ~bg2fixpack/baf/ar0800.baf~
  END

// bodhi broken if saved during her wait()-laden spawn; unstakable vampire fix
// Unlock the main door to Bodhi's lair if the player sides with her in Chapter 2 (aVENGER)
COPY_EXISTING ~ar0801.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("LassalVampires","GLOBAL",3)~ ~~
    REPLACE_TEXTUALLY ~CreateCreatureDoor("bodhi2",\[480\.1338\],14)~
      ~SetGlobal("LassalVampires","GLOBAL",3) CreateCreatureDoor("bodhi2",[480.1338],14)~
    REPLACE_TEXTUALLY EXACT_MATCH ~SetGlobal("SpawnBodhiFriends","AR0801",1)~ ~SetGlobal("SpawnBodhiFriends","AR0801",1) Unlock("DOOR03")~
    APPEND_FILE ~bg2fixpack/baf/ar0801.baf~
  END
  BUT_ONLY

// allow cleric-rangers to acquire temple stronghold; see bharval.dlg, borinall.dlg, scsain.dlg, travin.dlg
COPY_EXISTING ~ar0900.bcs~ ~override~
              ~ar0902.bcs~ ~override~
              ~arval.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(5)\([%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+\)\(Alignment(Player1,MASK_GOOD)\)~
                      ~OR(6) \1 Class(Player1,CLERIC_RANGER) \2~
  END
  BUT_ONLY

// can still rest in lathander temple after stripped due to var scope issue
// telwyn HoG exploit fix, pt 2/2 (see sctelwyn.d)
COPY_EXISTING ~ar0901.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"AR0902"~ ~"AR0901"~
    APPEND_FILE ~bg2fixpack/baf/ar0901.baf~
  END
  BUT_ONLY

// allows resting in paladin stronghold
EXTEND_BOTTOM ~ar0903.bcs~ ~bg2fixpack/baf/ar0903.baf~

// Temple of Talos script can loop interminably if <CHARNAME> murders too wantonly (Nythrun and Wisp)
COPY_EXISTING ~ar0904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~[ %TAB%]Dead("talmiss")~  ~  Dead("talmiss")  Exists("talmiss2") !Dead("talmiss2")~
    REPLACE_TEXTUALLY ~[ %TAB%]Dead("talmiss2")~ ~  Dead("talmiss2") Exists("talmiss")  !Dead("talmiss")~
  END
BUT_ONLY
UNLESS ~16465 0 1 0 0 "talmiss[2]?" "" OB~

// terrece should only spawn if you speak to corneil, not just enter his area
COPY_EXISTING ~ar1000.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("TerreceSpawn","GLOBAL",1)~
                      ~Global("TerreceSpawn","GLOBAL",1) Global("TalkedToCorneil","GLOBAL",1)~
  END
  BUT_ONLY

// ar1002 should check tolgerias' alternate DV; Tolgerias only vanishes once (proposed by Jastey, coded by DavidW)
// Ketlaar Argrim and his bodyguards go away from the Council building if he's imprisoned (see ar1002.are)
COPY_EXISTING ~ar1002.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Dead("TOLGER")~
                      ~OR(2) Dead("TOLGER") Dead("TOLGER2") Exists("tolger")~
    APPEND_FILE ~bg2fixpack/baf/ar1002.baf~
  END
  BUT_ONLY

// no escape from TR
EXTEND_BOTTOM ~ar1008.bcs~ ~bg2fixpack/baf/ar1008.baf~

// paladin-firkraag journal entry not being set due to bad variable scope
COPY_EXISTING ~ar1202.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"AR1200"~ ~"AR1202"~
  END
  BUT_ONLY

// monster invincible items updated, need to update script to same (see trolgi01.cre)
COPY_EXISTING ~ar1300.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride("trolgi0\([12]\)",DestroyItem("MINHP1"))~
                      ~ActionOverride("trolgi0\1",DestroyItem("MINHP1")) ActionOverride("trolgi0\1",DestroyItem("MONHP1"))~
  END
  BUT_ONLY

// can't lure dead umber hulks; assigned kpumb01 DVs to make this work (see kpumb01.cre)
COPY_EXISTING ~ar1301.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("UmberHungry","AR1301",0)~ ~Global("UmberHungry","AR1301",0) NumDeadLT("KPUMB01",5)~
  END
  BUT_ONLY

// fixes Glacias charm issue; see also kpglai.dlg
COPY_EXISTING ~ar1303.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StateCheck("kpglai01",STATE_CHARMED)~ ~!Allegiance("kpglai01",ENEMY)~
    REPLACE_TEXTUALLY ~[^,]ApplySpell("kpglai01",WIZARD_TRUE_DISPEL_MAGIC)~
                      ~ClearAllActions() ActionOverride("kpglai01",ApplySpell("kpglai01",FORCE_DISPEL_MAGIC))~
    REPLACE_TEXTUALLY ~[^,]ChangeEnemyAlly("kpglai01",NEUTRAL)~
                      ~ActionOverride("kpglai01",ChangeEnemyAlly(Myself,NEUTRAL))~
  END

// spirit doesn't escape area due to bad DV call
COPY_EXISTING ~ar1400.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride("rspirit1"~ ~ActionOverride("rspirit01"~
  END
  BUT_ONLY

// enables container in shade lord dungeon; see shaava01.dlg
COPY_EXISTING ~ar1401.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY ~SetGlobal("Deactivate","AR1401",1)~
    ~SetGlobal("Deactivate","AR1401",1)
     ContainerEnable("CONTAINER2",FALSE)~
  END
  BUT_ONLY

// more shadow fiends don't spawn due to DV typo
COPY_EXISTING ~ar1404.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"shdafi01"~ ~"shadfi01"~
  END
  BUT_ONLY

// The Spellhold Asylum door should be unlocked and opened once (aVENGER)
COPY_EXISTING ~ar1500.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY EXACT_MATCH ~GlobalGT("AsylumPlot","GLOBAL",55)~ ~GlobalGT("AsylumPlot","GLOBAL",55) Global("RR#Door1501","AR1500",0)~
  REPLACE_TEXTUALLY EXACT_MATCH ~Unlock("Door1501")~ ~SetGlobal("RR#Door1501","AR1500",1) Unlock("Door1501")~
END
BUT_ONLY

// asylum script bugs
COPY_EXISTING ~ar1515.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("ppattackedJon","GLOBAL",1)~
                      ~Global("ppattackedJon","GLOBAL",1)
                       Global("WackoArmy","GLOBAL",0)~
    REPLACE_TEXTUALLY ~Global("PPdeshSend","GLOBAL",0)~
                      ~OR(2)
                         Global("PPdeshSend","GLOBAL",0)
                         Global("PirateRefused","GLOBAL",1)~
  END
  UNLESS ~PirateRefused~

// Brynnlaw initial vamp fight should check for non-existence not dead (revised by Ardanis)
// saemon brynnlaw -> sahuagin city sequence errors (8 changes)
// see ppsaem3.dlg (x2), ppsailor.dlg, ar1600.bcs, cut41q.bcs, cut41zf.bcs, cut41zg.bcs, ppsaem2.bcs
// add extra trigger so this won't fire twice
COPY_EXISTING ~ar1600.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY ~Dead("ppvalen")[ %TAB%%LNL%%MNL%%WNL]+Dead("ppparis")[ %TAB%%LNL%%MNL%%WNL]+Dead("ppdel")~
    ~Global("ThiefGroup","GLOBAL",1) !Exists("ppvalen") !Exists("ppparis") !Exists("ppdel")~ // only trigger if the party has sided with the Shadow Thieves
    REPLACE_TEXTUALLY ~Dead("ppright")~  ~GlobalLT("AsylumPlot","GLOBAL",78) Dead("ppright")~ // saemon
  END
  BUT_ONLY

// multiple rerdes due to bad var scope
COPY_EXISTING ~ar1613.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"SpawnCaptain","AR1613"~ ~"SpawnCaptain","GLOBAL"~
  END
  BUT_ONLY

// Jenia will wait until PC officially hero of trademmet
COPY_EXISTING ~ar2000.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY ~Global("JeniaSpawn","AR2000",0)~
    ~Global("JeniaSpawn","AR2000",0)
     Global("loganjob2","GLOBAL",2)~
  END
  BUT_ONLY

// militia wizard breaking up fight
COPY_EXISTING ~ar2010.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(CreateCreature("trfued11",\[252\.725\],12)\)~ ~\1 SetGlobalTimer("CDtrfued11","AR2010",6)~
    APPEND_FILE ~bg2fixpack/baf/ar2010.baf~
  END
  BUT_ONLY

// aerie's banter meant for player1 as it mentions missing soul
COPY_EXISTING ~ar2100.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("AerieInUnderdark","AR2100",0)\)~ ~\1 IsValidForPartyDialogue(Player1)~
    REPLACE_TEXTUALLY ~ActionOverride("Aerie",StartDialog\(ue\)?NoSet(\[PC\]))~ ~ActionOverride("Aerie",StartDialogueNoSet(Player1))~
  END
  BUT_ONLY

// deirex killed during cutscene fix 1/2; see jarlich.cre
EXTEND_BOTTOM ~ar2207.bcs~ ~bg2fixpack/baf/ar2207.baf~

// prevent dupe CoC items; see also sahkng01.bcs, sahkng01.dlg, sahpr2.dlg, sahpr4.cre, sahtreas.bcs, string sets
EXTEND_BOTTOM ~ar2300.bcs~   ~bg2fixpack/baf/ar2300.baf~

// alignment change if evil path in hell trials; other fixes in teardoor.bcs and spin747.spl, spin749-51.spl, spin753.spl, apin755.spl
EXTEND_BOTTOM ~ar2900.bcs~ ~bg2fixpack/baf/ar2900.baf~

// script changes to ensure paladins/rangers fall in Hell tests; also closing selfish exploits
EXTEND_BOTTOM ~ar2901.bcs~ ~bg2fixpack/baf/ar2901.baf~
EXTEND_BOTTOM ~ar2902.bcs~ ~bg2fixpack/baf/ar2902.baf~
EXTEND_BOTTOM ~ar2903.bcs~ ~bg2fixpack/baf/ar2903.baf~
COPY_EXISTING ~ar2904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("PaladinGone","AR2904"~ ~Global("AbyssPaladinGone","GLOBAL"~ // brings fall block in line with others
    REPLACE_TEXTUALLY ~Global("RangerGone","AR2904"~  ~Global("AbyssRangerGone","GLOBAL"~
    REPLACE_TEXTUALLY ~Class(Player1,RANGER_ALL)~  ~Class(Player1,RANGER_ALL) !Class(Player1,CLERIC_RANGER)~
    REPLACE_TEXTUALLY ~\(OpenState("DOOR03",TRUE)\)~ ~\1 Global("OpenedDoor1","AR2904",1)~ // forces the penalties/bonuses to apply sequentially
    REPLACE_TEXTUALLY ~\(OpenState("DOOR04",TRUE)\)~ ~\1 Global("OpenedDoor2","AR2904",1)~
    REPLACE_TEXTUALLY ~\(OpenState("DOOR05",TRUE)\)~ ~\1 Global("OpenedDoor3","AR2904",1)~
    REPLACE_TEXTUALLY ~\(OpenState("DOOR06",TRUE)\)~ ~\1 Global("OpenedDoor4","AR2904",1)~
  END
  BUT_ONLY
EXTEND_BOTTOM ~ar2905.bcs~ ~bg2fixpack/baf/ar2905.baf~

// Viccy can romance half-orcs; multi-brus fixes
// drow item exploits closed
// not paying thieves guild quota fix; see also ar0322.bcs, joster.bcs, shthlt01.dlg
COPY_EXISTING ~baldur.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Race(Player1,0)~ ~Race(Player1,HALFORC)~
    REPLACE_TEXTUALLY ~Global("SpawnBrus","GLOBAL",1)~ 
      ~Global("SpawnBrus","GLOBAL",1) Global("CHAPTER","GLOBAL",2) !AreaCheck("AR0800") !AreaCheck("AR2000") CombatCounter(0)~
    REPLACE_TEXTUALLY ~CreateCreatureObjectOffScreen("BRUS3",Player1,0,0,0)~
      ~CreateCreatureObjectOffScreen("BRUS3",Player1,0,0,0) SetGlobal("SPAWNBRUS","GLOBAL",2)~
    REPLACE_TEXTUALLY ~[ %TAB%]Global("JosterLeave","GLOBAL",2)~ ~ !Global("JosterLeave","GLOBAL",1)~
    APPEND_FILE ~bg2fixpack/baf/baldur.baf~
  END

// brannel not commenting due to unassigned script (see shthdr01.cre), script errors
COPY_EXISTING ~brannel.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("LathanPlot1","GLOBAL",1)~ ~Global("LathanPlot","GLOBAL",1)~
    REPLACE_TEXTUALLY ~Global("LathanPlot2","GLOBAL",1)~ ~Global("LathanPlot","GLOBAL",2)~
    REPLACE_TEXTUALLY ~StartDialog\(ue\)?NoSet(Myself)~  ~StartDialogueNoSet(Player1)~
  END

// ch 6 allies check wrong drizzt DV
COPY_EXISTING ~c6arkan.bcs~ ~override~
              ~c6eric.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~("c6drizz")~ ~("c6drizz2")~
  END
  BUT_ONLY

// remove minhp1 for a moment to allow bodhi's str decrease to take effect
COPY_EXISTING ~c6bweak.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ApplySpell(Myself,C6BODHI_WEAKNESS)~
                      ~TakeItemReplace("monhp1","minhp1",Myself) ApplySpell(Myself,C6BODHI_WEAKNESS) TakeItemReplace("minhp1","monhp1",Myself)~
    REPLACE_TEXTUALLY ~DestroyItem("VAMPREG3")~ ~DestroyItem("VAMPREG2")~
  END
  BUT_ONLY

// Killing war elves while escaping Underdark lets you kill the surface elves
COPY_EXISTING ~c6elf.bcs~   ~override~
              ~c6elhan.bcs~ ~override~
              ~c6extra.bcs~ ~override~
              ~c6gener.bcs~ ~override~
              ~warsage.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("GoodElfKill","GLOBAL",0)~ ~GlobalLT("GoodElfKill","GLOBAL",2)~
  END
  BUT_ONLY

// valen's premature death prevents Del changing from bat form and attacking
COPY_EXISTING ~c6valen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpellDead(Myself,VALEN_MIST_FORM_CHANGE)~
      ~SetGlobal("ValenFight","AR0808",1) ReallyForceSpellDead(Myself,VALEN_MIST_FORM_CHANGE)~
  END
  BUT_ONLY

// cadril's script using wrong DVs for Cyrando and Irlana
COPY_EXISTING ~cadril.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"tcyrando"~ ~"cyrando"~
    REPLACE_TEXTUALLY ~"tirlana"~ ~"irlana"~
  END
  BUT_ONLY

// Cernd goes hostile if Grove poisoned
EXTEND_BOTTOM ~cernd.bcs~ ~bg2fixpack/baf/cernd.baf~

// trolls giving double XP if slain by instant-death spell
COPY_EXISTING ~dgtrol01.bcs~ ~override~ // troll, druid grove
              ~firamb03.bcs~ ~override~ // fire troll
              ~rogtro01.bcs~ ~override~ // roger's sea troll
              ~trolde01.bcs~ ~override~ // desert troll
              ~trolfr01.bcs~ ~override~ // freshwater troll
              ~trolgi01.bcs~ ~override~ // giant troll
              ~trolic01.bcs~ ~override~ // ice troll
              ~troll01.bcs~  ~override~ // generic troll
              ~trollsm2.bcs~ ~override~ // small troll - lacking timers and such
              ~trolsi01.bcs~ ~override~ // spirit troll
              ~trolsn01.bcs~ ~override~ // snow troll
              ~trolsp01.bcs~ ~override~ // spectral troll
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT(Myself,\([0-9]+\))~ ~!StateCheck(Myself,STATE_REALLY_DEAD) HPLT(Myself,\1)~
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "trolfr01" = 0) BEGIN // extra fix for freshwater troll
      REPLACE_TEXTUALLY ~"TROLGI02"~ ~"trolfr02"~ // freshwater trolls becoming giant trolls when knocked down
    END
  END
  BUT_ONLY

// new troll scripts to transform to dead versions at low HP
COPY_EXISTING ~troll01.bcs~  ~override/cdtroll1.bcs~
              ~trolsn01.bcs~ ~override/obsice11.bcs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ChangeAnimationNoEffect("TROLSN02")~ ~ChangeAnimationNoEffect("obsice11")~
    REPLACE_TEXTUALLY ~ChangeAnimationNoEffect("TROLL02")~  ~ChangeAnimationNoEffect("cdtroll2")~
  END

// new troll scripts to transform from dead to alive after time expires
COPY_EXISTING ~troll02.bcs~  ~override/cdtroll2.bcs~
              ~trolsn02.bcs~ ~override/obsice01.bcs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,TROLL_CHANGE)~      ~ReallyForceSpellRES("cdtroll1",Myself)~
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,TROLL_SNOW_CHANGE)~ ~ReallyForceSpellRES("obsice01",Myself)~
  END

// cromwell cutscene; removes multi-forged item bug
COPY_EXISTING ~cromwell.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CutSceneId("wsmith01")~ ~CutSceneId("wsmith01") ClearAllActions()~
  END
  BUT_ONLY

// removes call to nonexistent cre file
COPY_EXISTING ~cut31q.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("bdcoun04",\[592\.825\],10)~ ~~
  END
  BUT_ONLY

// rest commands not executed due to DayNight flakiness
COPY_EXISTING ~cut41c.bcs~ ~override~
              ~cut41d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]Rest()\)~ ~ Wait(1) \1~
  END
  BUT_ONLY

// fix cutscene for departing Amn
COPY_EXISTING ~cut41d.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride("pparan2",DestroySelf())~
                      ~ActionOverride("aran",DestroySelf())~
  END
  BUT_ONLY

// part of desharik's xp exploit fix, see ppdesh in the d file
COPY_EXISTING ~cut41f.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY ~SetGlobal("EnteredArea1500","GLOBAL",1)~
    ~SetGlobal("EnteredArea1500","GLOBAL",1)
     AddXPObject(Player1,38500)
     AddXPObject(Player2,38500)
     AddXPObject(Player3,38500)
     AddXPObject(Player4,38500)
     AddXPObject(Player5,38500)
     AddXPObject(Player6,38500)~
  END
  BUT_ONLY

// saemon brynnlaw -> sahuagin city sequence errors (8 changes)
// see ppsaem3.dlg (x2), ppsailor.dlg, ar1600.bcs, cut41q.bcs, cut41zf.bcs, cut41zg.bcs, ppsaem2.bcs
// many changes--clear actions, make uniterruptable, reorder textscreen, move saemon dialogue call here from ppsaem2.bcs
COPY_EXISTING ~cut41q.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CutSceneId(Player1)~     ~CutSceneId(Player1) ClearAllActions() SetInterrupt(FALSE)~
    REPLACE_TEXTUALLY ~TextScreen("SCRTXT05")~  ~ActionOverride("ppsaem3",StartDialogueNoSet(Player1))~
    REPLACE_TEXTUALLY ~MultiPlayerSync()~       ~MultiPlayerSync() TextScreen("SCRTXT05")~
    REPLACE_TEXTUALLY ~Explore()~               ~Explore() SetInterrupt(TRUE)~
  END
  BUT_ONLY

// saemon brynnlaw -> sahuagin city sequence errors (8 changes)
// see ppsaem3.dlg (x2), ppsailor.dlg, ar1600.bcs, cut41q.bcs, cut41zf.bcs, cut41zg.bcs, ppsaem2.bcs
// clear actions, make uninterruptable
COPY_EXISTING ~cut41zf.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CutSceneId(Player1)~                  ~CutSceneId(Player1) ClearAllActions() SetInterrupt(FALSE)~
    REPLACE_TEXTUALLY ~SetGlobal("AttackedGith","GLOBAL",1)~ ~SetGlobal("AttackedGith","GLOBAL",1) SetInterrupt(TRUE)~
  END
  BUT_ONLY

// saemon brynnlaw -> sahuagin city sequence errors (8 changes)
// see ppsaem3.dlg (x2), ppsailor.dlg, ar1600.bcs, cut41q.bcs, cut41zf.bcs, cut41zg.bcs, ppsaem2.bcs
// add a bit more time for sahuagin attack
COPY_EXISTING ~cut41zg.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobalTimer("GithTimer1","GLOBAL",5)~ ~SetGlobalTimer("GithTimer1","GLOBAL",10)~
  END
  BUT_ONLY

// familiars and summons should be pushed back when hell door opens, see spin658.spl
COPY_EXISTING ~cut85a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride("HELLSPY4",ApplySpell(Player[1-6],HELL_BUFFET))~ ~~
    REPLACE_TEXTUALLY ~ActionOverride("hellspy4",ApplySpell(\[FAMILIAR\],HELL_BUFFET))~ ~ActionOverride("HELLSPY4",ForceSpell(Player1,HELL_BUFFET))~
  END
  BUT_ONLY

// ust natha scenery cutscene can occur offscreen, meaning player stands in dark for no apparent reason
COPY_EXISTING ~dadrow20.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~StartCutSceneMode()~
      ~StartCutSceneMode() CreateCreature("cutspy",[3864.273],0) MoveViewPoint([3864.273],INSTANT)~
    REPLACE_TEXTUALLY ~EscapeArea()~
      ~EscapeArea() ActionOverride("cutspy",DestroySelf()) MoveViewObject(Player1,INSTANT)~
  END
  BUT_ONLY

// make mask of king strohm a little more reliable
COPY_EXISTING ~ddguard7.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!HasItemEquiped("key20",\[PC\])~ ~!HasItemEquiped("key20",LastSeenBy(Myself))~
  END
  BUT_ONLY

// delon's script using wrong variable for talking to Lloyd; had EscapeArea() issues
// if spawned later for ogron/umar events, could spam original stringhead or disappear prematurely
// prevent 'offer' stringhead if spawned for ogron attack
COPY_EXISTING ~delon.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Num\(berOf\)?TimesTalkedTo(0)\)~ ~\1 Global("DelonSpawn","GLOBAL",0)~
    REPLACE_TEXTUALLY ~TalkedToMayor~ ~TalkedToLloyd~
    REPLACE_TEXTUALLY ~OR(4)[%TAB% %LNL%%MNL%%WNL%]+Global("Acceptance","GLOBAL",1)~ ~OR(3)~
    REPLACE_TEXTUALLY ~AreaCheck("AR1000")~ ~~
    REPLACE_TEXTUALLY ~GlobalTimerExpired("MinscDelon","GLOBAL")~ ~False()~
    APPEND_FILE ~bg2fixpack/baf/delon.baf~
  END
  BUT_ONLY

// wound end up doing nothing if it didn't spot a GOODCUTOFF or mage/cleric
COPY_EXISTING ~demglasu.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~See(\[ANYONE\])[%TAB% %LNL%%MNL%%WNL%]+Global("Prep","LOCALS",1)~  ~See([ANYONE])~
  END
  BUT_ONLY

// druids fighting unspawned trolls
COPY_EXISTING ~dgdru01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Detect(\[PC\])~ ~Range([PC],25)~
    REPLACE_TEXTUALLY ~See(\[PC\])\([ %TAB%%LNL%%MNL%%WNL%]+Global("SpawnDGTrolls","AR1900",0)\)~ ~Range([PC],25) \1~
  END
  BUT_ONLY

// All trolls should have their hit points set to 1 after getting knocked down (aVENGER)
COPY_EXISTING ~dgtrol02.bcs~ ~override~ // Druid Grove trolls that are fighting some adventurers
              ~firamb05.bcs~ ~override~ // the troll fighting the werewolf captain in Firkraag's lair
              ~troll03.bcs~  ~override~ // small trolls which emerge out of a bigger one on the first floor of de'Arnise Keep
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~PlayDead(\([0-9]+\))~ ~ApplySpell(Myself,TROLL_SETHP1) PlayDead(\1)~
  END
  BUT_ONLY

// restore Nith's attack script, pt 1: false() out stuff that'll break; see drshnl01.cre
COPY_EXISTING ~drshnl01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT(Myself,10)~ ~False()~ // gets new troll script to handle low HP transform in cre patch
  END
  BUT_ONLY

// new troll scripts to transform to dead versions at low HP
COPY_EXISTING ~troll01.bcs~ ~override/drshnl11.bcs~
              ~troll01.bcs~ ~override/eletro03.bcs~
              ~troll01.bcs~ ~override/firtrl02.bcs~
              ~troll01.bcs~ ~override/kptrol23.bcs~
              ~troll01.bcs~ ~override/pptroll2.bcs~
              ~troll01.bcs~ ~override/sutroll2.bcs~
              ~troll01.bcs~ ~override/torgal2.bcs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ChangeAnimationNoEffect("TROLL02")~ ~ChangeAnimationNoEffect("%DEST_RES%")~
  END

// duplicates functionality, but needed for ub compatibility
EXTEND_TOP ~drshnl01.bcs~ ~override/drshnl11.bcs~

// new troll scripts to transform from dead to alive after time expires
COPY_EXISTING ~troll02.bcs~ ~override/drshnl21.bcs~
              ~troll02.bcs~ ~override/eletro01.bcs~
              ~troll02.bcs~ ~override/firtrl01.bcs~
              ~troll02.bcs~ ~override/kptrol13.bcs~
              ~troll02.bcs~ ~override/pptroll1.bcs~
              ~troll02.bcs~ ~override/sutroll.bcs~
              ~troll02.bcs~ ~override/torgal3.bcs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,TROLL_CHANGE)~ ~ReallyForceSpellRES("%DEST_RES%",Myself)~
  END

// duergar mage has inconsistent target/attack blocks
COPY_EXISTING ~duemag01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~See(NearestEnemyOf(Myself))[ %TAB%%LNL%%MNL%%WNL%]*\(HaveSpell(WIZARD_AGANNAZAR_SCORCHER)\)~
      ~See(SecondNearestEnemyOf(Myself)) \1~
  END
  BUT_ONLY

// Drow etc. guarding the way out of the Underdark start to stutter if they are charmed after you have made an enemy of the Ust Natha (Wisp)
COPY_EXISTING ~dwgates1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Allegiance(Myself,ENEMY)~ ~!Allegiance(Myself,ENEMY) !StateCheck(Myself,STATE_CHARMED)~
  END
  BUT_ONLY
  UNLESS ~16439 8192 1 0 0 "" "" OB~ //UNLESS !StateCheck(Myself,STATE_CHARMED)

// edwin soundset issues for transformation
COPY_EXISTING ~edwin.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetPlayerSound(Myself,30716,SELECT_ACTION4)~ ~SetPlayerSound(Myself,30715,SELECT_ACTION4)~
    REPLACE_TEXTUALLY ~SetPlayerSound(Myself,30717,SELECT_ACTION5)~ ~SetPlayerSound(Myself,3984,SELECT_ACTION5)~
    REPLACE_TEXTUALLY ~SetPlayerSound(Myself,30718,SELECT_ACTION6)~ ~SetPlayerSound(Myself,3985,SELECT_ACTION6)~
    REPLACE_TEXTUALLY ~SetPlayerSound(Myself,30719,SELECT_ACTION7)~ ~SetPlayerSound(Myself,3986,SELECT_ACTION7)~
  END

// containers can summon guards days after they're open
COPY_EXISTING ~enforam.bcs~ ~override~
              ~tempv01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(2)[%TAB% %LNL%%MNL%%WNL%]+!See(\[NOTGOOD\])[%TAB% %LNL%%MNL%%WNL%]+Global("warn","LOCALS",1)~
      ~GlobalTimerExpired("CDGoAway","LOCALS")~
    REPLACE_TEXTUALLY ~Global("warn","LOCALS",0)~ ~Global("warn","LOCALS",0) GlobalTimerNotExpired("CDGoAway","LOCALS")~
    REPLACE_TEXTUALLY ~Wait(5)~ ~~
    REPLACE_TEXTUALLY ~See(\[NEUTRAL\])~ ~See([NEUTRAL.HUMANOID])~
    APPEND_FILE ~bg2fixpack/baf/enforam.baf~
  END
  BUT_ONLY

// script should check live and dead troll DV
COPY_EXISTING ~firamb01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([^!]\)Dead("firamb05")~ ~\1OR(2) Dead("firamb03") Dead("firamb05")~
  END

// add slight delay so Anomen can comment on dead Windspear knights before Garren
// double Garren Windspears
COPY_EXISTING ~garren.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalLT("DomainPaladinBattle","GLOBAL",4)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100\)[%TAB% %LNL%%MNL%%WNL%]+\(StartDialogu?e?NoSet(Player1)\)~
      ~\1 Wait(1) \2~
    APPEND_FILE ~bg2fixpack/baf/garren.baf~
  END
  BUT_ONLY

// animals shold not run away from druids, elves, and rangers
COPY_EXISTING ~genshy.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(3)~ ~~
  END
  BUT_ONLY

//Kruin can spawn again if you killed him before he could talk (Wisp)
COPY_EXISTING ~githspwn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!Exists("Kruin")~ ~!Exists("Kruin") !Dead("Kruin")~
  END
  UNLESS ~16465 0 1 0 0 "Kruin" "" OB~
  BUT_ONLY

// other gladiators should do something when Hendak does
COPY_EXISTING ~glad2782.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Dead("Hendak")~
      ~OR(2)
       Dead("Hendak")
       Allegiance("Hendak",255)~
    APPEND_FILE ~bg2fixpack/baf/glad2782.baf~
  END
  BUT_ONLY

// gorf the squisher fixes
COPY_EXISTING ~gorf.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("GoKillEm","LOCALS",2)~
                      ~SetGlobal("GoKillEm","LOCALS",2)
                      SetGlobal("GorfBystander","GLOBAL",1)~
  END
  BUT_ONLY

// move grae's minhp1 destruction to dialogue options where she dies; see soa-dlg.d for rest of changes
COPY_EXISTING ~grae.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DestroyItem("minhp1")~ ~~
  END
  BUT_ONLY

// script assigned to both container and area-trigger traps, but only working for area triggers
COPY_EXISTING ~gtspike.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(IsOverMe(\[GOODCUTOFF\])\)~ ~OR(2) \1 Opened([GOODCUTOFF])~
  END
  BUT_ONLY
  UNLESS ~16521 [0-9]+ 0 0 0 "" "" OB~

// don't interrupt lucette when she's kiling xzar
COPY_EXISTING ~harpass1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~FaceObject("Lyros")~ ~SetInterrupt(FALSE) DialogInterrupt(FALSE) FaceObject("Lyros")~
    REPLACE_TEXTUALLY ~StartDialog\(ue\)?NoSet(Player1)~ ~DialogInterrupt(TRUE) SetInterrupt(TRUE) StartDialogueNoSet(Player1)~
  END
  BUT_ONLY

// Hendak should not randomly end up in some other part of the Copper Coronet if he performs a jump to arrive at Lehtinan's spot (Wisp)
COPY_EXISTING ~hendak.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH "SetHomeLocation([32.120])" "SetHomeLocation([526.1193])"
    REPLACE_TEXTUALLY EXACT_MATCH "JumpToPoint([526.1193])" "SetHomeLocation([526.1193]) JumpToPoint([526.1193])"
  END
BUT_ONLY
UNLESS // True unless there already is a SetHomeLocation before JumpToPoint.
~AC
261OB
0 0 0 0 0 0 0 0 0 0 0 0 ""OB
OB
0 0 0 0 0 0 0 0 0 0 0 0 ""OB
OB
0 0 0 0 0 0 0 0 0 0 0 0 ""OB
0 526 1193 0 0"" "" AC
AC
48OB~

// lesser clay golems in CI can be killed twice
COPY_EXISTING ~igolfle1.bcs~ ~override~
              ~igolfle2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("Ellsime","AR0602",1)~ ~Global("Ellsime","AR0602",1) !StateCheck(Myself,STATE_REALLY_DEAD)~
  END
  BUT_ONLY

// ilyich actually wears his imported armor
EXTEND_BOTTOM ~ilyich.bcs~ ~bg2fixpack/baf/ilyich.baf~

// sahugin spectre should only kill one party member and then escape
COPY_EXISTING ~impches3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Global("ImpRiddle","GLOBAL",3)~ ~!Global("ImpRiddle","GLOBAL",3) Global("CDImpChest","AR2300",0)~
    REPLACE_TEXTUALLY ~Kill(LastTrigger)~ ~Kill(LastTrigger) SetGlobal("CDImpChest","AR2300",1)~
  END
  BUT_ONLY

// not paying thieves guild quota fix; see also ar0322.bcs, baldur.bcs, shthlt01.dlg
EXTEND_BOTTOM ~joster.bcs~ ~bg2fixpack/baf/joster.baf~

// script fixes for anarg-smuggler battle; see also kaypal03.bcs, kaysmg04.bcs
COPY_EXISTING ~kaypal02.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~(\[NEUTRAL\.0\.HUMAN\.THIEF\])~ ~([NOTGOOD.0.0.CLERIC])~
   REPLACE_TEXTUALLY ~(\[ENEMY\.0\.HUMAN\.THIEF\])~   ~([NOTGOOD.0.0.THIEF]) Name("kaysmg",LastSeenBy(Myself))~
   REPLACE_TEXTUALLY ~AttackReevaluate(NearestEnemyOf(Myself),15)~
                     ~AttackReevaluate(LastSeenBy(Myself),15)~
 END
 BUT_ONLY

// script fixes for anarg-smuggler battle; see also kaypal02.bcs, kaysmg04.bcs
COPY_EXISTING ~kaypal03.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~AttackReevaluate(NearestEnemyOf(Myself),30)~
                     ~AttackReevaluate(LastSeenBy(Myself),30)~
 END
 BUT_ONLY

// script fixes for anarg-smuggler battle; see also kaypal02.bcs, kaypal03.bcs
COPY_EXISTING ~kaysmg04.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(HPPercentLT("kaysmgl",50)[%TAB% %LNL%%MNL%%WNL%]+See("kaysmgl")\)~ ~\1 !Dead("kaysmgl")~
   REPLACE_TEXTUALLY ~HPPercentLT("kaysmg",30)[%TAB% %LNL%%MNL%%WNL%]+See("kaysmg")~ 
                     ~See([NOTGOOD.0.0.THIEF]) HPPercentLT(LastSeenBy(Myself),30) Name("kaysmg",LastSeenBy(Myself))~
 END
 BUT_ONLY
  
// hannah xp exploit; see also kftown02.cre
EXTEND_BOTTOM ~kftown02.bcs~ ~bg2fixpack/baf/kftown02.baf~

// Arkanis Gath should not spawn and kill the PC post-Bynnlaw
EXTEND_TOP ~killpc.bcs~ ~bg2fixpack/baf/killpc.baf~

// flail of ages forge should still work after acquiring stronghold; wasn't due to bad variable scope
// Assembling the Flail of Ages piece by piece should grant an XP reward (Wisp)
COPY_EXISTING ~kpforge.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"AR1302",~ ~,"GLOBAL",~
    REPLACE_TEXTUALLY ~GiveItemCreate("BLUN14",LastTrigger,0,0,0)[ %TAB%%LNL%%MNL%%WNL%]+END~ 
                      ~GiveItemCreate("BLUN14",LastTrigger,0,0,0) AddexperienceParty(22350) SetGlobal("ForgeUsed","GLOBAL",1) END~
  END
  BUT_ONLY

// should be able to steal at Lathander temple if invisible
COPY_EXISTING ~lathalrm.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(2)~ ~~
  END
  BUT_ONLY

// lavok dying too early because of this cheese script
// False()d rather than unassigned in case someone wants to use this for lavok02-specific scripting
COPY_EXISTING ~lavok02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!HasItem("MINHP1",Myself)~ ~False()~
    APPEND_FILE ~bg2fixpack/baf/lavok02.baf~
  END
  BUT_ONLY

// madeen should go away if Tolgerias dead--check for other DV
COPY_EXISTING ~madeen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(2)[ %TAB%%LNL%%MNL%%WNL%]+Dead("TOLGER2")~ ~OR(3) Dead("TOLGER") Dead("TOLGER2")~
  END
  BUT_ONLY

// mazzy only goes hostile if Valygar is hostile and not charmed
COPY_EXISTING ~mazzy.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Allegiance("Valygar",ENEMY)~
                      ~Allegiance("Valygar",ENEMY)
                      !StateCheck("Valygar",STATE_CHARMED)~
    REPLACE_TEXTUALLY ~OR(2)[ %LNL%%MNL%%WNL%]+Dead("gorf")[ %LNL%%MNL%%WNL%]+Dead("gorf03")~ ~Dead("gorf")~
  END
  BUT_ONLY

// Hostile and out of party Minsc and Valygar shouldn't heal party members (Nythrun)
COPY_EXISTING ~minscx.bcs~  ~override~
              ~valvsed.bcs~ ~override~
              ~valygx.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Spell(MostDamagedOf(Myself),CLERIC_CURE~ ~Spell(Myself,CLERIC_CURE~
  END
  BUT_ONLY  

// disables Gath spawn from mvally
COPY_EXISTING ~mvally.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(2)~
                      ~False()~
  END
  BUT_ONLY
EXTEND_TOP ~mvally.bcs~ ~bg2fixpack/baf/mvally.baf~

// disables Gath spawn from mvally2
COPY_EXISTING ~mvally2.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AttackedBy(\[GOODCUTOFF\],DEFAULT)~
                      ~False()~
  END
  BUT_ONLY
EXTEND_TOP ~mvally2.bcs~ ~bg2fixpack/baf/mvally.baf~

// tyrianna should disappear after conclusion of her quest
EXTEND_BOTTOM ~plgirl01.bcs~ ~bg2fixpack/baf/plgirl01.baf~

// asylum crushing trap hitting wrong folks
COPY_EXISTING ~ppcrus1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!StateCheck(LastTrigger,STATE_REALLY_DEAD)~ ~False()~
    REPLACE_TEXTUALLY ~StateCheck(LastTrigger,STATE_REALLY_DEAD)~ ~~
  END
  BUT_ONLY

// saemon brynnlaw -> sahuagin city sequence errors (8 changes)
// see ppsaem3.dlg (x2), ppsailor.dlg, ar1600.bcs, cut41q.bcs, cut41zf.bcs, cut41zg.bcs, ppsaem2.bcs
// false block for saemon initiating dialogue (moved to cut41q.bcs); add block to make saemon hostile if attacked
COPY_EXISTING ~ppsaem2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("SaemInit1607","GLOBAL",0)~ ~False()~
    APPEND_FILE ~bg2fixpack/baf/ppsaem2.baf~
  END
  BUT_ONLY

//reddeath doesn't work because it uses the wrong action (Wisp)
COPY_EXISTING ~reddeath.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ReallyForceSpell ReallyForceSpellDead
  END
UNLESS 240OB
BUT_ONLY

// emphatic manifestation can be killed by resting outside
EXTEND_TOP ~riftcr04.bcs~ ~bg2fixpack/baf/riftcr04.baf~

// fixes breaking bridge in UE quest
COPY_EXISTING ~riftg01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Dead("Riftcr0[23]")~ ~False()~
    REPLACE_TEXTUALLY ~!Dead("Riftcr01")~ ~Dead("Riftcr01")~
    REPLACE_TEXTUALLY ~!Exists("Riftcr01")~
    ~CombatCounter(0)
    OR(2)
       !Exists("Riftcr03")
       Dead("Riftcr03")
    OR(2)
       !Exists("Riftcr02")
       Dead("Riftcr02")
     OR(2)
       !Exists("Riftcr01")~
  END
  BUT_ONLY

// amalas' buddies should leave together if you kill him
COPY_EXISTING ~rufpal.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global(\("itFight","R0406P"\|"R0406PitFight",""\),3)~ ~Global("PitFight","AR0406",3)~
  END
  BUT_ONLY

// trigger misordering causing folks to flee from party, not enemies
COPY_EXISTING ~runenemy.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(See(\[ENEMY\])\)[%TAB% %LNL%%MNL%%WNL%]+\(!?Detect(\[PC\])\)~ ~\2 \1~
  END
  BUT_ONLY

// prevent dupe CoC items; see also ar2300.bcs, sahkng01.dlg, sahpr2.dlg, sahpr4.cre, sahtreas.bcs, string sets
EXTEND_BOTTOM ~sahkng01.bcs~ ~bg2fixpack/baf/sahkng01.baf~

// prevent dupe CoC items; see also ar2300.bcs, sahkng01.dlg, sahpr2.dlg, sahpr4.cre, sahkng01.bcs, string sets
COPY_EXISTING ~sahtreas.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("sahkngjob","GLOBAL",2)\)~ ~\1 OR(2) HasItem("rods02",Myself) HasItem("brac19",Myself)~
    APPEND_FILE ~bg2fixpack/baf/sahtreas.baf~
  END
  BUT_ONLY

// habib shouldn't spawn if dead
COPY_EXISTING ~slilmat.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("SpawnHabib","GLOBAL",5)~
                      ~Global("SpawnHabib","GLOBAL",5) !Dead("Habib")~
  END
  BUT_ONLY

// ring of djinni summoning should destroy ring if djinni dies
COPY_EXISTING ~sumdj01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT(Myself,5)~ ~OR(2) Die() HPLT(Myself,5)~
  END
  BUT_ONLY

// avatar of rilifane xp exploit fix, see also suavatar.dlg
COPY_EXISTING ~sustatue.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Contains("MISCB3",Myself)~
      ~Contains("MISCB3",Myself) Global("CDNoRepeat","AR2803",0)~
    REPLACE_TEXTUALLY ~TriggerActivation("Tran2800",FALSE)~
      ~SetGlobal("CDNoRepeat","AR2803",1) TriggerActivation("Tran2800",FALSE)~
  END
  BUT_ONLY

// alignment change if evil path in hell trials; other fixes in ar2900.bcs and spin747.spl, spin749-51.spl, spin753.spl, apin755.spl
COPY_EXISTING ~teardoor.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ApplySpell(Player1,HELL_[A-Z]+_EVIL)\)~ ~SetGlobal("DisplayAlignmentChange","AR2900",1) \1~
  END

// stops Terrece from initiating dialogue from outside
COPY_EXISTING ~terrece.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("TerreceSpawn","GLOBAL",2)~ ~Global("TerreceSpawn","GLOBAL",2) See([PC])~
  END
  BUT_ONLY

// multiple ring of the ram exploit fixes, see tolger.dlg
EXTEND_TOP ~tolger.bcs~ ~bg2fixpack/baf/tolger.baf~

// removes bug of losing rogue stone if reloaded
// The Twisted Rune entrance trigger in the Bridge District should only consume a single Rogue Stone (aVENGER)
COPY_EXISTING ~tran1008.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TakePartyItem("MISC45")~ ~~
    REPLACE_TEXTUALLY ~ActionOverride(Player1,LeaveAreaLUAPanic("AR1008","",\[495\.755\],10))~
                      ~ActionOverride(Player1,LeaveAreaLUAPanic("AR1008","",[495.755],10))
                      TakePartyItemNum("MISC45",1)~
  END
  BUT_ONLY
 
// trrakxx should have dialogue when attacked
COPY_EXISTING ~trrak01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AttackedBy(\[ANYONE\],DEFAULT)~ ~Global("attacked","LOCALS",1)~
  END
  BUT_ONLY
EXTEND_TOP ~trrak01.bcs~ ~bg2fixpack/baf/trrak01.baf~

// endless Ust Natha tavern combat music; see also ucounter.cre
COPY_EXISTING ~ucounter.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~SetGlobal("HaltMusicWithLesaonar","GLOBAL",0)~ ~SetGlobal("HaltMusicWithLesaonar","GLOBAL",2)~
    REPLACE_TEXTUALLY ~\(CombatCounterLT(50)[%TAB% %LNL%%MNL%%WNL%]+Global("HaltMusicWithLesaonar","GLOBAL",\)0)~ ~\12)~
    APPEND_FILE ~bg2fixpack/baf/ucounter.baf~
  END
  BUT_ONLY

// prevent journal entry for already-openeed UD illithid doors
COPY_EXISTING ~uddoor2.bcs~ ~override~
              ~uddoor3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("thrallDoorMes","AR2400",0)~  ~Global("thrallDoorMes","AR2400",0)  Global("thrallDoorOpen","AR2400",0)~
    REPLACE_TEXTUALLY ~Global("thrallDoorMes2","AR2400",0)~ ~Global("thrallDoorMes2","AR2400",0) Global("thrallDoorOpen2","AR2400",0)~
  END
  BUT_ONLY

// Ust Natha egg guards no longer see invisible folks
COPY_EXISTING ~udeggs.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GlobalTimerExpired("eggGuardDoesWarn","GLOBAL")~
                      ~GlobalTimerExpired("eggGuardDoesWarn","GLOBAL")
                      See([PC])~
  END
  BUT_ONLY

//Surface elves by the Underdark exit can bail out prematurely (Wisp)
COPY_EXISTING ~udelf1.bcs~ ~override~
              ~udelf2.bcs~ ~override~
              ~udelf3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY "\(See(\[PC\])[%LNL%%TAB% ]*CombatCounter(0)\)" "\1 !Detect([ENEMY])"
  END
BUT_ONLY
UNLESS ~16500 0 1 0 0 "" "" OB%LNL%255 0 0 0 0 0 0 0 0 0 0 0 ""OB~ //UNLESS !Detect([ENEMY])

// replace flaky Die() in underdark mind flayer script
COPY_EXISTING ~udmpswn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Die()~ ~StateCheck(Myself,STATE_REALLY_DEAD) Global("CDDieReplace","LOCALS",0)~
    REPLACE_TEXTUALLY ~\(SetGlobal("spawnIll","AR2400",0)\)~ ~\1 SetGlobal("CDDieReplace","LOCALS",1)~
  END
  BUT_ONLY
  
// dialogue breaks in phaere's recue if Sola is invalid for dialogue
EXTEND_TOP ~udphae01.bcs~ ~bg2fixpack/baf/udphae01.baf~

// uhleave5 and 6 should be moving between areas, opening doors, etc.
COPY_EXISTING ~uhleave5.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~MoveToPointNoInterrupt(\[805\.407\])~ ~MoveToPointNoInterrupt([805.407]) Unlock("DOOR02") OpenDoor("DOOR02")~
  END
  BUT_ONLY
EXTEND_TOP ~uhleave5.bcs~ ~bg2fixpack/baf/uhleave5.baf~

COPY_EXISTING ~uhleave6.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~MoveToPointNoInterrupt(\[709\.301\])~ ~MoveToPointNoInterrupt([709.301]) Unlock("DOOR02") OpenDoor("DOOR02")~
  END
  BUT_ONLY
EXTEND_TOP ~uhleave6.bcs~ ~bg2fixpack/baf/uhleave6.baf~

// trap triggers can't check LOCALS variables; display different string if umber hulks already dead
COPY_EXISTING ~umbpoly.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("UmbPoly","LOCALS",0)~ ~Global("UmbPoly","AR1301",0) NumDeadLT("KPUMB01",5)~
    REPLACE_TEXTUALLY ~"LOCALS"~ ~"AR1301"~
    APPEND_FILE ~bg2fixpack/baf/umbpoly.baf~
  END
  BUT_ONLY

// Valygar only goes hostile if mazzy is hostile and not charmed
// valygar goes hostile if something happens to hervo, but should leave the party first
// valygar drops his body if imprisoned
COPY_EXISTING ~valygar.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Allegiance("Mazzy",ENEMY)~
                      ~Allegiance("Mazzy",ENEMY)
                      !StateCheck("Mazzy",STATE_CHARMED)~
    REPLACE_TEXTUALLY ~SetGlobal("vgAttackTheParty","LOCALS",1)~ ~SetGlobal("vgAttackTheParty","LOCALS",1) LeaveParty()~
    REPLACE_TEXTUALLY ~\(Global("DropValygar","GLOBAL",0)\)~ ~Dead("Valygar") \1~
  END
  BUT_ONLY

// Valygar leaving party in Slums can cause cutscene hang
COPY_EXISTING ~valyorb.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~IsOverMe("Valygar")~
      ~IsOverMe("Valygar") InParty("Valygar") IsValidForPartyDialogue("Valygar") !StateCheck("Valygar",STATE_SLEEPING)~
  END
  BUT_ONLY

// prevents viekang's double dialogue
COPY_EXISTING ~viekang.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("Attacked","LOCALS",0)~ ~Global("Attacked","LOCALS",0) !Global("ViekangSeesPC","LOCALS",1)~
  END
  BUT_ONLY

// closes Yoshimo exploit of resurrecting him after Spellhold
// prevent Yoshimo telling you to see Renal when you already have
COPY_EXISTING ~yoshimo.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Kill(Myself)~
                      ~Kill(Myself)
                      LeaveParty()~
    REPLACE_TEXTUALLY ~Global("YoshimoMentionsRenal","LOCALS",0)~ ~Global("YoshimoMentionsRenal","LOCALS",0) Global("TalkedToRenal","GLOBAL",0)~
  END
  BUT_ONLY

// Yoshimo should not drop two hearts when slain by a petrification effect (Nythrun)
COPY_EXISTING ~yoshimox.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]Die()\)~ ~\1 !StateCheck(Myself,128)~
  END
BUT_ONLY

ACTION_IF !tob_game THEN BEGIN // SoA

  // no-repeat Drizzt, patched differently if ToB installed, thanks to SetEncounterProbability action
  COPY_EXISTING ~ar0043.bcs~ ~override~
  EXTEND_BOTTOM ~ar2601.bcs~ ~override/ar0043.bcs~
  
END ELSE BEGIN // ToB, much easier thanks to new action

  // no-repeat Drizzt
  COPY_EXISTING ~ar2601.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"DrizztEncounter","AR2601"~ ~"DrizztEncounter","GLOBAL"~
      REPLACE_TEXTUALLY ~Explore()~ ~Explore()
        SetEncounterProbability("AR2500","AR0020",0)
        SetEncounterProbability("AR2500","AR1300",0)
        SetEncounterProbability("AR2500","AR1304",0)
        SetEncounterProbability("AR2500","AR1700",0)~
    END
    BUT_ONLY

  // tries to remove override scripts for knights before killing them
  COPY_EXISTING ~ar3020.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ChangeAIScript("one",OVERRIDE))~ ~ChangeAIScript("",OVERRIDE))~
    END
    BUT_ONLY
  
  // removing call to nonexistent trigger
  COPY_EXISTING ~ar3021.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~TriggerActivation("OilDoor",FALSE)~ ~~
    END
    BUT_ONLY

  // part of the multi-holy symbol exploit fix. Also needed: CDHLYSYM.spl, CDHLYSY2.spl, CDHLYSYM.itm, and clabpr02-04 changes.
  EXTEND_TOP ~baldur.bcs~   ~bg2fixpack/baf/holysym.baf~
  EXTEND_TOP ~baldur25.bcs~ ~bg2fixpack/baf/holysym.baf~

  // cespy's handing out undercharged items
  COPY_EXISTING ~botsmith.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~GiveItemCreate("clck31",Player1,0,0,0)~ ~GiveItemCreate("clck31",Player1,1,1,0)~
      REPLACE_TEXTUALLY ~GiveItemCreate("sw1h69",Player1,0,0,0)~ ~GiveItemCreate("sw1h69",Player1,0,1,1)~
    END
    BUT_ONLY
  
  // trolls giving double XP if slain by instant-death spell
  COPY_EXISTING ~chaltrol.bcs~ ~override~ // giant troll, challenge
                ~hgtrl01.bcs~  ~override~ // fire troll
                ~trolic03.bcs~ ~override~ // blizzard troll
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~HPLT(Myself,\([0-9]+\))~ ~!StateCheck(Myself,STATE_REALLY_DEAD) HPLT(Myself,\1)~
    END
    BUT_ONLY

  COPY_EXISTING ~ctaltar.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"PortalOpen","GLOBAL"~ ~"PortalOpen","AR3001"~
    END
    BUT_ONLY

  COPY_EXISTING ~demnabsu.bcs~ ~override~
                ~dempitsu.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~See(\[ANYONE\])~ ~See([ANYONE]) CheckStatLT(LastSeenBy(Myself),1,169)~
    END
    BUT_ONLY

  // Hostile Planetars and Devas should not attempt to heal party members (Nythrun and aVENGER)
  ACTION_FOR_EACH script IN devaevil devagood plangood BEGIN

    COPY_EXISTING ~%script%.bcs~ override
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~HaveSpell(CLERIC_CURE_DISEASE)~ ~HaveSpell(CLERIC_CURE_DISEASE) Allegiance(Myself,GOODCUTOFF)~
        REPLACE_TEXTUALLY ~HaveSpell(CLERIC_DISPEL_MAGIC)~ ~HaveSpell(CLERIC_DISPEL_MAGIC) Allegiance(Myself,GOODCUTOFF)~
        REPLACE_TEXTUALLY ~HaveSpell(CLERIC_LESSER_RESTORATION)~ ~HaveSpell(CLERIC_LESSER_RESTORATION) Allegiance(Myself,GOODCUTOFF)~
        REPLACE_TEXTUALLY ~HaveSpell(CLERIC_NEUTRALIZE_POISON)~ ~HaveSpell(CLERIC_NEUTRALIZE_POISON) Allegiance(Myself,GOODCUTOFF)~
        REPLACE_TEXTUALLY ~HaveSpell(CLERIC_REMOVE_FEAR)~ ~HaveSpell(CLERIC_REMOVE_FEAR) Allegiance(Myself,GOODCUTOFF)~
        REPLACE_TEXTUALLY ~HPPercentLT(MostDamagedOf(Myself),25)~ ~HPPercentLT(MostDamagedOf(Myself),25) Allegiance(Myself,GOODCUTOFF)~
      END
    BUT_ONLY IF_EXISTS

  END

  // generic healer script casting bless long before combat
  COPY_EXISTING ~gphealer.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(HaveSpell(CLERIC_BLESS)\)~ ~\1 See(NearestEnemyOf(Myself))~
    END
    BUT_ONLY
  
  // prevents Tamoko from running away and making PP challenge unfinishable, see chtaz02.cre for other half
  COPY_EXISTING ~gpkensai.bcs~ ~override/cdtamoko.bcs~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!HasItem("POTN55",Myself)~ ~False()~
    END
    BUT_ONLY

  // if killed directly via spell, fire trolls should do groovy death animations
  EXTEND_TOP ~hgtrl01.bcs~ ~bg2fixpack/baf/hgtrl01.baf~

  // moves LOCALS var set to local script instead of ar4500
  EXTEND_TOP ~keld25.bcs~ ~bg2fixpack/baf/keld25.baf~

  // one of the dwarf insults setting var with wrong scope
  COPY_EXISTING ~sardw01.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"SaradushAmbientInsults","GLOBAL"~ ~"SaradushAmbientInsults","AR5000"~
    END
    BUT_ONLY

  // sarevok soundset fixes
  EXTEND_BOTTOM ~sarev25.bcs~ ~bg2fixpack/baf/sarev25.baf~

  // summoned juggernaut golem shouldn't die instantly if summoned by clone
  COPY_EXISTING ~tomegol4.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!CheckStatGT(Player1,0,STONESKINSGOLEM)~ ~!CheckStatGT(LastSummonerOf(Myself),0,STONESKINSGOLEM) !CheckStatGT(Player1,0,STONESKINSGOLEM)~
    END
    BUT_ONLY

  EXTEND_TOP ~wish01.bcs~ ~bg2fixpack/baf/wish01.baf~ // asc64 wish fixes; see wish25.dlg

  // Killing unrelated baatezu outside Ka'rashur's room should not turn him and his cohort in Watcher's Keep hostile (see also ar3005.are, ar3006.are, ar3009.are and ar6012.are) (Wisp)
  COPY_EXISTING ~yssumm2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~"gorbat2"~ ~"deriny01"~
    END

END

/////                                                  \\\\\
///// bulk area fixes                                  \\\\\
/////                                                  \\\\\

// adds new entrance points to wmp areas that don't have one already (second half of wmp fixes, see worldmap.wmp)
COPY_EXISTING ~ar1700.are~ ~override~
              ~ar1800.are~ ~override~
              ~ar2500.are~ ~override~
              ~ar2600.are~ ~override~
  PATCH_IF ("ar1700" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN
    SET "x_coord" = 3375
    SET "y_coord" = 142
  END ELSE
  PATCH_IF ("ar1800" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN
    SET "x_coord" = 1325
    SET "y_coord" = 56
  END ELSE
  PATCH_IF ("ar2500" STRING_COMPARE_CASE ~%SOURCE_RES%~ = 0) BEGIN
    SET "x_coord" = 404
    SET "y_coord" = 125
  END ELSE BEGIN // ar2600
    SET "x_coord" = 3856
    SET "y_coord" = 115
  END
  READ_LONG  0x68 ent_off
  WRITE_LONG 0x6c (THIS + 1) // entrance number
  INSERT_BYTES ent_off 0x68
    WRITE_ASCII (ent_off       ) ~CDExit~ // entrance name
    WRITE_SHORT (ent_off + 0x20) x_coord
    WRITE_SHORT (ent_off + 0x22) y_coord
  PATCH_FOR_EACH offset IN 0x54 0x5c 0x60 0x6c 0x70 0x78 0x7c 0x84 0x88 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 0xc4 BEGIN
    READ_LONG offset val
    PATCH_IF val >= ent_off BEGIN
      WRITE_LONG offset (THIS + 0x68)
    END
  END
  BUT_ONLY

// some areas have scripts that are not assigned to them
COPY_EXISTING ~ar0321.are~ ~override~
              ~ar0323.are~ ~override~
              ~ar0324.are~ ~override~
              ~ar0327.are~ ~override~
              ~ar0401.are~ ~override~
              ~ar1005.are~ ~override~
              ~ar1009.are~ ~override~ // has wrong script assigned; could cause cutscene hangs
              ~ar1904.are~ ~override~
              ~ar5004.are~ ~override~
  WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
  BUT_ONLY IF_EXISTS

// adds outdoor flag
COPY_EXISTING ~ar0041.are~ ~override~
              ~ar0042.are~ ~override~
              ~ar0044.are~ ~override~
              ~ar0045.are~ ~override~
              ~ar1607.are~ ~override~
              ~ar2806.are~ ~override~
  WRITE_BYTE 0x48 (THIS BOR BIT0)
  BUT_ONLY

// removes outdoor flag
COPY_EXISTING ~ar0321.are~ ~override~
              ~ar0322.are~ ~override~
              ~ar0323.are~ ~override~
              ~ar0600.are~ ~override~
  WRITE_BYTE 0x48 (THIS BAND `BIT0)
  BUT_ONLY

// removes city flag
COPY_EXISTING ~ar0303.are~ ~override~
              ~ar0305.are~ ~override~
              ~ar0307.are~ ~override~
              ~ar0308.are~ ~override~
              ~ar0309.are~ ~override~
              ~ar0311.are~ ~override~
              ~ar0312.are~ ~override~
              ~ar0315.are~ ~override~
              ~ar0316.are~ ~override~
              ~ar0317.are~ ~override~
              ~ar0406.are~ ~override~ //Copper Coronet
              ~ar0501.are~ ~override~
              ~ar0504.are~ ~override~
              ~ar0505.are~ ~override~
              ~ar0506.are~ ~override~
              ~ar0507.are~ ~override~
              ~ar0508.are~ ~override~
//              ~ar0509.are~ ~override~ //Five Flagons, ground floor
//              ~ar0510.are~ ~override~ //Five Flagons, theater
//              ~ar0511.are~ ~override~ //Five Flagons, second floor
//              ~ar0512.are~ ~override~
              ~ar0513.are~ ~override~ //Calbor's Inn, first floor
              ~ar0514.are~ ~override~ //Calbor's Inn, second floor
              ~ar0515.are~ ~override~ //Calbor's Inn, third floor
//              ~ar0522.are~ ~override~ //Five Flagons, ground floor (stronghold)
//              ~ar0523.are~ ~override~ //Five Flagons, theater (stronghold)
              ~ar0706.are~ ~override~
              ~ar0901.are~ ~override~ //Temple District, Helm temple
              ~ar0902.are~ ~override~ //Temple District, Lathander temple
              ~ar0904.are~ ~override~ //Temple District, Talos temple
              ~ar0905.are~ ~override~ //Temple District, Pimlico's Estate
              ~ar0906.are~ ~override~ //Temple District, guarded compound ground floor
              ~ar0907.are~ ~override~ //Temple District, guarded compound second floor
              ~ar5507.are~ ~override~ //Saradush, basement entrance to Gromnir's castle
  WRITE_BYTE 0x48 (THIS BAND `BIT3)
  BUT_ONLY IF_EXISTS

// adds dungeon flag
COPY_EXISTING ~ar0310.are~ ~override~
              ~ar0331.are~ ~override~
              ~ar0418.are~ ~override~
              ~ar0521.are~ ~override~
              ~ar0526.are~ ~override~
              ~ar0600.are~ ~override~
              ~ar0601.are~ ~override~
              ~ar0606.are~ ~override~
              ~ar0804.are~ ~override~
              ~ar1106.are~ ~override~
              ~ar1203.are~ ~override~
              ~ar1402.are~ ~override~
              ~ar1501.are~ ~override~
              ~ar1608.are~ ~override~
              ~ar1610.are~ ~override~
              ~ar2901.are~ ~override~
              ~ar2902.are~ ~override~
              ~ar2903.are~ ~override~
              ~ar2905.are~ ~override~
  WRITE_BYTE 0x48 (THIS BOR BIT5)
  BUT_ONLY

// removes dungeon flag
COPY_EXISTING ~ar1101.are~ ~override~
              ~ar2603.are~ ~override~
  WRITE_BYTE 0x48 (THIS BAND `BIT5)
  BUT_ONLY

// adds 'can rest' flag
COPY_EXISTING ~ar0522.are~ ~override~ // five flagons theater, stronghold version
              ~ar0905.are~ ~override~ // pimlico's estate
              ~ar0906.are~ ~override~ // guarded compound ground floor
              ~ar0907.are~ ~override~ // guarded compound second floor
  WRITE_BYTE 0x48 (THIS BOR BIT7)
  BUT_ONLY

// fixes several areas with detected traps
COPY_EXISTING ~ar0062.are~ ~override~
              ~ar0063.are~ ~override~
              ~ar0064.are~ ~override~
              ~ar0065.are~ ~override~
              ~ar0328.are~ ~override~
              ~ar0406.are~ ~override~
              ~ar0413.are~ ~override~
              ~ar0801.are~ ~override~
              ~ar0803.are~ ~override~
              ~ar0808.are~ ~override~
              ~ar1006.are~ ~override~
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    READ_SHORT (cont_off + 0x30 + (index * 0xc0)) trapped
    READ_SHORT (cont_off + 0x32 + (index * 0xc0)) detected
    PATCH_IF ((trapped = 1) AND (detected = 1)) BEGIN
      WRITE_SHORT (cont_off + 0x32 + (index * 0xc0)) 0 // no longer detected
    END
  END
  BUT_ONLY

// areas with identified items in containers
COPY_EXISTING ~ar0405.are~ ~override~
              ~ar0602.are~ ~override~
              ~ar1101.are~ ~override~
  READ_SHORT 0x76 item_num
  READ_LONG  0x78 item_off
  FOR (index = 0 ; index < item_num ; ++index) BEGIN
    READ_ASCII (item_off +        (0x14 * index)) item
    PATCH_IF (("%item%" STRING_COMPARE_CASE "arow02" = 0) OR // ar1101, container 1
              ("%item%" STRING_COMPARE_CASE "arow08" = 0) OR // ar0405, barrel 3
              ("%item%" STRING_COMPARE_CASE "sper02" = 0) OR // ar1101, container 2
              ("%item%" STRING_COMPARE_CASE "sw1h05" = 0)) BEGIN // ar0602, table 3
      WRITE_BYTE  (item_off + 0x10 + (0x14 * index)) (THIS BAND `BIT0)
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// other area fixes                                 \\\\\
/////                                                  \\\\\

COPY_EXISTING ~ar0015.are~ ~override~ // Baldur's Gate Estate (Tutorial starts here)
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0018 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0016.are~ ~override~ // Baldur's Gate Estate (Tutorial area)
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 trap_detect = 0 STR_VAR container_name = "Container 1" END // fixes traps in tut area
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 trap_detect = 0 STR_VAR container_name = "Container 2" END // fixes traps in tut area
  BUT_ONLY

COPY_EXISTING ~ar0018.are~ ~override~ // Baldur's Gate Estate Cellar (Tutorial area)
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 8 STR_VAR match_item = ttwand END // zero-charge item: wand of the heavens (tutorial version) (num 5) has assigned 0 to stack 1 charges 100 in header 1
  BUT_ONLY

// add map note for crooked crane at city gates
COPY_EXISTING ~ar0020.are~ ~override~
  ADD_MAP_NOTE #231 #517 ~gray~ #46216
  BUT_ONLY

COPY_EXISTING ~ar0021.are~ ~override~ // Crooked Crane 1st Floor
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0020 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0082 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0022.are~ ~override~ // Crooked Crane 2nd Floor
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0021 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0065.are~ ~override~ // Empty (Triggers, Doors... are there but no area)
  LPF ALTER_AREA_ITEM INT_VAR charge2 = 10 STR_VAR match_item = wand05 END // zero-charge item: wand of fire (num 3) has assigned 0 to stack 1 charges 50 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0082.are~ ~override~ // Lich Grave in Bridge District Inn
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0021 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0201.are~ ~override~ // Leading to ghoul village and beholder lair (Temple District)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = exit0206a END // fix orientation: ghouls under temple from halfling village, south-southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = exit0206b END // fix orientation: ghouls under temple from halfling village, south-southeast
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 10 STR_VAR container_name = "Skeleton 1" END // correct container icon to body
  BUT_ONLY

COPY_EXISTING ~ar0202.are~ ~override~ // UE quest, cult hideout
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = exit0205 END // fix orientation: beholder hideout from cult hideout (UE), south-southeast
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 2037 coord_y = 2242 container_type = 6 STR_VAR container_name = "Drawer 1" END // move to avoid actor collision, correct icon to shelf
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "SWERDOOR" END
  BUT_ONLY

COPY_EXISTING ~ar0204.are~ ~override~ // UE quest, temple city
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 9 STR_VAR entrance_name = exit0202 END // fix orientation: graveyard to bohdi's lair, north-northeast
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 8 STR_VAR container_name = "Container 1" END // correct container icon to nonvisible
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 3738 coord_y = 2515 container_type = 8 STR_VAR container_name = "Container 3" END // move to avoid actor collision, correct icon to nonvisible
  BUT_ONLY

COPY_EXISTING ~ar0206.are~ ~override~ // Ghoul village (Temple District)
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 8 STR_VAR container_name = "Bones 1" END // correct container icon to nonvisible
  BUT_ONLY

COPY_EXISTING ~ar0300.are~ ~override~ // docks
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = exit0308 END // fix orientation: harper hq to docks, east-southeast
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0301 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0302 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0303 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0304a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0304b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0308 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0308b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0308c END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0309 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0309b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0328 END // transition should be party required
  BUT_ONLY

COPY_EXISTING ~ar0302.are~ ~override~ // mae'var's guild entrance
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = exit0301 END // fix orientation: basement to entrance, mae'var, east
  BUT_ONLY

COPY_EXISTING ~ar0304.are~ ~override~ // mae'var's guild third floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0303 END // fix orientation: 2nd to 3rd floor, mae'var, northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = exi0300b END // fix orientation: 2nd to 3rd floor, mae'var (pc), east
  BUT_ONLY

COPY_EXISTING ~ar0305.are~ ~override~ // Shadow Thieves Guild Entrance
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0300 END // transition can not be used by npcs
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = dagg01 END // overcharged item: dagg01 (num 10) has assigned 3 to stack 1 charges 0 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw1h01 END // overcharged item: dagg01 (num 10) has assigned 3 to stack 1 charges 0 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0306.are~ ~override~ // Renal Bloodscalp's Hideout
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = TRAN0300 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0307.are~ ~override~ // Aran Linvail's Hideout
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran300a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran300b END // transition can not be used by npcs
  // animation glitches due to bad layering/flags
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0xff7e flag_background = 1 STR_VAR match_name = am307tt1 END // spiky wheel in torturer's room
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0                          STR_VAR match_name = am307ur1 END // spiky wheel you run across
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0xffcc                     STR_VAR match_name = am307ur3 END // another spiky wheel you run across
  BUT_ONLY

COPY_EXISTING ~ar0308.are~ ~override~ // Harper's 1st Floor
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tra0300b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tra0300c END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tra0309 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0300 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0311.are~ ~override~ // Living Room (Entry points outside and up) -- Gaelan?
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 10 STR_VAR match_item = ax1h04 END // zero-charge item: ax1h04 (num 1) has assigned 0 to stack 10 charges 1 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0312.are~ ~override~ // sleeping room
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exit0311 END // fix orientation: gaelan downstairs to upstairs, northeast
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 2 STR_VAR container_name = "Container 2" END // correct container icon to chest
  // animation glitches due to bad layering
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 170 match_y_coord = 310 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 180 match_y_coord = 304 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 426 match_y_coord = 129 height = 0x3a END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 438 match_y_coord = 121 height = 0x34 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 552 match_y_coord = 107 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 573 match_y_coord = 114 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 670 match_y_coord = 188 height = 0x2e END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 680 match_y_coord = 194 height = 0x38 END // wall sconce
  BUT_ONLY

COPY_EXISTING ~ar0313.are~ ~override~ // Sea Bounty Tavern 1st Floor
  LPF ALTER_AREA_ANIMATION INT_VAR x_coord = 334 y_coord = 377 flag_background = 1 flag_not_covered_wall = 1 STR_VAR match_name = am003xa END // // animation glitches due to bad flags: oven
  BUT_ONLY

COPY_EXISTING ~ar0317.are~ ~override~ // Sleeping Room (Favour for Edwin -- Mae'Var Plot)
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 10 charge2 = 10 STR_VAR match_item = wand05 END // zero-charge item: wand of fire (num 3) has assigned 0 to stack 1 charges 50 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0319.are~ ~override~ // Temple of Oghma
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 5 STR_VAR container_name = "Table 1" END // correct container icon to table
  BUT_ONLY

COPY_EXISTING ~ar0322.are~ ~override~ // mae'var's guild entrance (pc controlled)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = exit0321 END // fix orientation: basement to entrance, mae'var (pc), east
  BUT_ONLY

COPY_EXISTING ~ar0324.are~ ~override~ // mae'var's guild third floor (pc controlled)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0323 END // fix orientation: 2nd to 3rd floor, mae'var (pc), northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = exit0300b END // fix orientation: 2nd to 3rd floor, mae'var (pc), east
  BUT_ONLY

COPY_EXISTING ~ar0327.are~ ~override~ // Shadow Thief Entrance (sided with Bodhi)
  LPF ALTER_AREA_CONTAINER INT_VAR lock_difficulty = 90 STR_VAR container_name = "Container 11" END // make unpickable chest pickable
  BUT_ONLY

// makes doors actually require keys that they are listed as requiring
COPY_EXISTING ~ar0329.are~ ~override~ // Aran's Hideout (sided with Bodhi)
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "DOOR07" END // sets uses key bit
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "DOOR08" END // sets uses key bit
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 1 STR_VAR match_item = ring26 END // zero-charge item: ring of djinni summoning (num 7) has assigned 0 to stack 1 charges 1 in header 1
  BUT_ONLY

// ar0333.are entry/exit points are reversed; undoing the Escher architecture
COPY_EXISTING ~ar0333.are~ ~override~ // Barracks in the Docks
  READ_SHORT 0x5a info_num
  READ_LONG  0x5c info_off
  READ_LONG  0x68 enter_off
  READ_LONG  0x6c enter_num
  FOR (index = 0 ; index < info_num ; ++index) BEGIN // swap transition (exit) points
    READ_ASCII (info_off + 0x38 + (0xc4 * index)) area
    PATCH_IF ("ar0300" STRING_COMPARE_CASE "%area%" = 0) BEGIN
      WRITE_ASCII (info_off +        (0xc4 * index)) "Tran0332" #32 // transition point name
      WRITE_ASCII (info_off + 0x38 + (0xc4 * index)) "ar0332" #8    // destination area
    END ELSE
    PATCH_IF ("ar0332" STRING_COMPARE_CASE "%area%" = 0) BEGIN
      WRITE_ASCII (info_off +        (0xc4 * index)) "Tran0300" #32 // transition point name
      WRITE_ASCII (info_off + 0x38 + (0xc4 * index)) "ar0300" #8    // destination area
    END
  END
  FOR (index = 0 ; index < enter_num ; ++index) BEGIN // swap entry points
    READ_ASCII (enter_off +        (index * 0x68)) name
    PATCH_IF ("%name%" STRING_COMPARE_CASE "Exit0300" = 0) BEGIN
      WRITE_ASCII (enter_off +        (index * 0x68)) "Exit0332"
    END ELSE
    PATCH_IF ("%name%" STRING_COMPARE_CASE "Exit0332" = 0) BEGIN
      WRITE_ASCII (enter_off +        (index * 0x68)) "Exit0300"
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar0334.are~ ~override~ // Cromwell's Shop
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow01 END // overcharged: composite long bow
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow03 END // overcharged: long bow
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0 STR_VAR match_name = fpit1s END // height of forges
  BUT_ONLY

// animation glitches due to bad layering
COPY_EXISTING ~ar0335.are~ ~override~ // Sleeping Room
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 170 match_y_coord = 310 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 180 match_y_coord = 304 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 426 match_y_coord = 129 height = 0x3a END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 438 match_y_coord = 121 height = 0x34 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 552 match_y_coord = 107 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 573 match_y_coord = 114 height = 0x31 END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 670 match_y_coord = 188 height = 0x2e END // wall sconce
  LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 680 match_y_coord = 194 height = 0x38 END // wall sconce
  BUT_ONLY

COPY_EXISTING ~ar0400.are~ ~override~ // slums
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 5           STR_VAR container_name = "Table 8" END // correct container icon to table
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 1000 coord_y = 840 STR_VAR container_name = "Table 1" END // move to avoid actor collision
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  3 STR_VAR entrance_name = exit0402 END // fix orientation: jansen home to slums, west-southwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exi0406b END // fix orientation: CC to slums, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0405 END // fix orientation: slaver ship to slums, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0406 END // fix orientation: CC to slums, southeast
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0407 END // transition can not be used by npcs (despite name, goes to ar0311)
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0402 END // transition should be party required (two in area, one not flagged)
  BUT_ONLY

COPY_EXISTING ~ar0402.are~ ~override~ // jansen ground floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 5 STR_VAR entrance_name = Exit0400 END // from the slums, west-northwest
  BUT_ONLY

COPY_EXISTING ~ar0404.are~ ~override~ // sewers under CC
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0405 END // fix orientation: slaver ship to sewers, northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0406 END // fix orientation: CC to sewers, southeast
  BUT_ONLY

COPY_EXISTING ~ar0405.are~ ~override~ // slaver's ship
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exit0400 END // fix orientation: slums to slaver ship, northeast
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 40 STR_VAR match_item = bolt01 END // zero-charge item: bolt01 (num 36) has assigned 0 to stack 40 charges 1 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0406.are~ ~override~ // copper coronet
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exit0400 END // fix orientation: slums to CC, northeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exi0400b END // fix orientation: slums to CC, northeast
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 2 STR_VAR container_name = "Crate 1" END // correct container icon to chest
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0 STR_VAR match_name = fpit1s END // height of forges
  BUT_ONLY

COPY_EXISTING ~ar0407.are~ ~override~ // Prebek's House
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0400 END // transition can not be used by npcs (despite name, goes to ar0300)
  LPF ALTER_AREA_CONTAINER INT_VAR STR_VAR container_name = "Container 2" container_script = "ctmm" END // trapped container doesn't actually do anything; assign script
  BUT_ONLY

COPY_EXISTING ~ar0409.are~ ~override~ // ployer's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0400 END // fix orientation: slums to ployer's home, northwest
  BUT_ONLY

COPY_EXISTING ~ar0410.are~ ~override~ // Sphere: Navigator's room -- Lavok's Hideout
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0412b END // transition can not be used by npcs (despite name, goes to ar0411)
  BUT_ONLY

COPY_EXISTING ~ar0411.are~ ~override~ // Sphere: Entrance floor
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 10 STR_VAR container_name = "Body 1" END // correct container icon to body
  BUT_ONLY

COPY_EXISTING ~ar0413.are~ ~override~ // Sphere: Enginge Room
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0412a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0412b END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0414.are~ ~override~ // Sphere: Demon Plane
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Exit0414 END // transition can not be used by npcs (goes to ar0411)
  LPF ALTER_AREA_ANIMATION INT_VAR y_coord = 2794 STR_VAR match_name = am414ey2 END // fixes demon eye animation
  BUT_ONLY

COPY_EXISTING ~ar0415.are~ ~override~ // Living Room 1st Floor
  LPF ALTER_AREA_CONTAINER INT_VAR STR_VAR container_name = "Container 1" container_script = "CTALARM" END // correct trap script reference
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 flag_party_required = 1 STR_VAR region_name = Tran0400 END // transition can not be used by npcs, party required (despite name, goes to ar0300)
  BUT_ONLY

COPY_EXISTING ~ar0500.are~ ~override~ // bridge district
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0506b END // fix orientation: noble house to bridge dist, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0513 END // fix orientation: calbor's inn to bridge dist, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0514a END // fix orientation: inn at bridge district to bridge, north-northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 2 STR_VAR entrance_name = exit0506a END // fix orientation: noble home to bridge, southwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 2 STR_VAR entrance_name = exit0515 END // fix orientation: inn to bridge dist, southwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 2 STR_VAR entrance_name = exit0705 END // fix orientation: mekrath's lair to bridge, southwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 7 STR_VAR entrance_name = exit0514b END // fix orientation: inn at bridge district to bridge, southeast
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0505a END // transition should be party required
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0505b END // transition should be party required
  // fixes multiple Stanets
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  SET stanets = 0
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "BMTOWN4" = 0) BEGIN // leave first stanet alone
      PATCH_IF (stanets = 0) BEGIN
        SET stanets = 1
      END ELSE BEGIN
        WRITE_ASCII (actor_off +        (index * 0x110)) ~Commoner~ #32 // change others to generic commoner
        WRITE_ASCII (actor_off + 0x80 + (index * 0x110)) ~BMTOWN2~ #8
      END
    END
  END
  BUT_ONLY

// incorrect container icon
COPY_EXISTING ~ar0501.are~ ~override~ // Tanner's Hideout 1st Floor
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 11 STR_VAR container_name = "Container 1" END // correct container icon to barrel
  BUT_ONLY

COPY_EXISTING ~ar0502.are~ ~override~
  LPF ALTER_AREA_REGION STR_VAR region_name = "Fireball" door_script = "gtfb" END // ground traps won't trigger due to container scripts
  BUT_ONLY

// container traps won't trigger due to ground scripts
COPY_EXISTING ar0503.are override
  LPF ALTER_AREA_CONTAINER STR_VAR container_name = "Container 1" container_script = "ctfb" END
  BUT_ONLY

COPY_EXISTING ~ar0504.are~ ~override~ // Saerk's House 1st Floor
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0500 END // transition should be party required
  BUT_ONLY

COPY_EXISTING ~ar0505.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0500a END // transition should be party required
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran0500b END // transition should be party required
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow01 END // overcharged item: bow01 (num 19) has assigned 2 to stack 1 charges 0 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow03 END // overcharged item: bow01 (num 19) has assigned 2 to stack 1 charges 0 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow05 END // overcharged item: bow01 (num 19) has assigned 2 to stack 1 charges 0 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw1h01 END // overcharged item: bow01 (num 19) has assigned 2 to stack 1 charges 0 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0506.are~ ~override~ // Saerk's House 2nd Floor
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 7 STR_VAR container_name = "Cabinet 1" END // correct container icon to altar
  BUT_ONLY

COPY_EXISTING ~ar0509.are~ ~override~ // first floor five flagons
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0500 END // fix orientation: bridge to five flagons, northwest
  BUT_ONLY

COPY_EXISTING ~ar0510.are~ ~override~ // Five Flagons Inn Theater
  LPF ALTER_AREA_ANIMATION INT_VAR x_coord = 839 STR_VAR match_name = AM0510A END // adjusts x coordinate to fix portal animation
  BUT_ONLY

COPY_EXISTING ~ar0513.are~ ~override~ // calbor's inn
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 886 coord_y = 471 STR_VAR container_name = "Table 3" END // move to avoid actor collision
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = exit0514 END // fix orientation: calbor's inn, second floor to first, south-southeast
  BUT_ONLY

COPY_EXISTING ~ar0527.are~ ~override~ // House 1st Floor
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 10 STR_VAR match_item = wand03 END // zero-charge item: wand03 (num 0) has assigned 0 to stack 1 charges 100 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0529.are~ ~override~ // Neb's Hideout
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 8 trap_detect = 50 trap_remove = 90 STR_VAR container_name = "Container 6" END // correct container icon to nonvisible, make disarmable, not auto-detected
  LPF ALTER_AREA_ANIMATION INT_VAR x_coord = 334 y_coord = 377 flag_background = 1 flag_not_covered_wall = 1 STR_VAR match_name = am003xa END // oven
  BUT_ONLY

COPY_EXISTING ~ar0530.are~ ~override~ // Storehouse
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw2h01 END // overcharged item: sw2h01 (num 3) has assigned 2 to stack 1 charges 0 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0531.are~ ~override~ // commoner house
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0500 END // fix orientation: bridge to commoner home, northwest
  BUT_ONLY

COPY_EXISTING ~ar0601.are~ ~override~ // Air Plane in Irenicus's Dungeon
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0602 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0603.are~ ~override~ // Irenicus's Dungeon 2nd Floor
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Ice Mephit" script_override = "waitpc2" END // ice mephit has wrong script
  BUT_ONLY

COPY_EXISTING ~ar0605.are~ ~override~ // Circus Tent 2nd Floor
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = TRAN0606 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0700.are~ ~override~ // promenade
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 1 STR_VAR entrance_name = exit0709 END // fix orientation: den of seven vales to promenade, south-southwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0706 END // fix orientation: fletcher to promenade, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0707 END // fix orientation: enge's shop to promenade, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0708 END // fix orientation: cernd's home to promenade, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = exit0710 END // fix orientation: fennecia's home to promenade, southeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = exit0704 END // fix orientation: mithrest to promenade, south-southeast
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 3249 coord_y = 300 STR_VAR container_name = "Table 2" END // move to avoid actor collision
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 572 coord_y = 1163 STR_VAR container_name = "Table 7" END // move to avoid actor collision
// changes guard reference in ar0700 to not be the rubble-guarder type
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "AMNG1" = 0) BEGIN
      WRITE_ASCII (actor_off + 0x80 + (index * 0x110)) AMNG2 #8
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar0701.are~ ~override~ // The Sewers
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "Door02" END // mind flayer door uses up key
  BUT_ONLY

COPY_EXISTING ~ar0703.are~ ~override~ // temple of ilmater (promenade)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exit0700 END // fix orientation: ilmater temple from promenade, northeast
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Priest of Ilmater" cre_file = "wilmat" END // wrong creature reference in Waukeen Ilmater temple; see soa-dlg.d for changes to wilmat.dlg
  BUT_ONLY

COPY_EXISTING ~ar0705.are~ ~override~ // Mekrath's Hideout in the Sewers (Haer'Dalis Plot)
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 6 STR_VAR container_name = "Container 3" END // correct container icon to shelf
  BUT_ONLY

COPY_EXISTING ~ar0706.are~ ~override~ // Armourer/Fletcher
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0 STR_VAR match_name = fpit1s END // height of forges
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 10 STR_VAR match_item = ax1h04 END // zero-charge item: ax1h04 (num 1) has assigned 0 to stack 10 charges 1 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0707.are~ ~override~ // enge's shop
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0700 END // fix orientation: promenade to enge's shop, northwest
  BUT_ONLY

COPY_EXISTING ~ar0713.are~ ~override~ // Store or Inn at Promenade
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Peasant" script_override = "runenemy" END // indoor actor has outdoor script
  LPF ALTER_AREA_ANIMATION INT_VAR x_coord = 334 y_coord = 377 flag_background = 1 flag_not_covered_wall = 1 STR_VAR match_name = am003xa END // oven
  BUT_ONLY

COPY_EXISTING ~ar0800.are~ ~override~ // Graveyard
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0801a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0801b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0801c END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0801d END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar0801.are~ ~override~ // bodhi's area
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 7 STR_VAR entrance_name = exit0800d END // fix orientation: graveyard to bohdi's lair, north-northwest
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0800a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0800b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0800c END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0800d END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran0802 END // transition can not be used by npcs
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = misc6w END // overcharged item: misc6w (num 0) has assigned 3 to stack 1 charges 0 in header 1
  BUT_ONLY

// makes VVMADMAN only exist at night so he won't EscapeArea()
// shift guarded compund entrance in temple district
COPY_EXISTING ~ar0900.are~ ~override~ // Temple District
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 8 STR_VAR container_name = "Shelf 1" END // correct container icon to nonvisible
  LPF ALTER_AREA_ACTOR INT_VAR flag_time_0 = 1 flag_time_1 = 1 flag_time_2 = 1 flag_time_3 = 1 flag_time_4 = 1 flag_time_19 = 1 flag_time_20 = 1 flag_time_21 = 1 flag_time_22 = 1 flag_time_23 = 1 // present only at night
    flag_time_5 = 0 flag_time_6 = 0 flag_time_7 = 0 flag_time_8 = 0 flag_time_9 = 0 flag_time_10 = 0 flag_time_11 = 0 flag_time_12 = 0 flag_time_13 = 0 flag_time_14 = 0 flag_time_15 = 0 flag_time_16 = 0 flag_time_17 = 0 flag_time_18 = 0
    STR_VAR actor_name = "Shadow Thief" END
  // moving entrance
  SET delta_x = "-98"
  SET delta_y = 40
  READ_SHORT 0x5a trig_num
  READ_LONG  0x5c trig_off
  READ_LONG  0x68 ent_off
  READ_LONG  0x6c ent_num
  READ_LONG  0x7c vert_off
  FOR (index = 0 ; index < ent_num ; ++index) BEGIN
    READ_ASCII (ent_off +        (index * 0x68)) name
    PATCH_IF ("%name%" STRING_COMPARE_CASE "Exit0906" = 0) BEGIN
      WRITE_SHORT (ent_off + 0x20 + (index * 0x68)) (THIS + delta_x) // exit coordinates
      WRITE_SHORT (ent_off + 0x22 + (index * 0x68)) (THIS + delta_y)
      SET index = ent_num // kill loop
    END
  END
  FOR (index = 0 ; index < trig_num ; ++index) BEGIN
    READ_ASCII (trig_off +        (index * 0xc4)) name
    PATCH_IF ("%name%" STRING_COMPARE_CASE "Tran0906" = 0) BEGIN
      WRITE_SHORT (trig_off + 0x22 + (index * 0xc4)) (THIS + delta_x) // bounding box
      WRITE_SHORT (trig_off + 0x24 + (index * 0xc4)) (THIS + delta_y)
      WRITE_SHORT (trig_off + 0x26 + (index * 0xc4)) (THIS + delta_x)
      WRITE_SHORT (trig_off + 0x28 + (index * 0xc4)) (THIS + delta_y)
      READ_SHORT  (trig_off + 0x2a + (index * 0xc4)) vert_num
      READ_LONG   (trig_off + 0x2c + (index * 0xc4)) vert_idx
      WRITE_SHORT (trig_off + 0x70 + (index * 0xc4)) (THIS + delta_x) // launch point
      WRITE_SHORT (trig_off + 0x72 + (index * 0xc4)) (THIS + delta_y)
      SET index = trig_num // kill loop
    END
  END
  FOR (index = vert_idx ; index < (vert_idx + vert_num) ; ++index) BEGIN
    WRITE_SHORT (vert_off +        (index * 0x04))  (THIS + delta_x)
    WRITE_SHORT (vert_off + 0x02 + (index * 0x04))  (THIS + delta_y)
  END
  BUT_ONLY

COPY_EXISTING ~ar0901.are~ ~override~ // Temple of Helm
  LPF ALTER_AREA_ITEM STR_VAR match_item = "potn53" item = "potn08" END // changes Festule's potion to minor healing
  BUT_ONLY

COPY_EXISTING ~ar0902.are~ ~override~ // Temple of Lathander
  LPF ALTER_AREA_CONTAINER INT_VAR container_type = 8 STR_VAR container_name = "Shelf 7" END // correct container icon to nonvisible
  BUT_ONLY

COPY_EXISTING ~ar0903.are~ ~override~ // norh
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exi0900b END // fix orientation: temple district to norh, northeast
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0900 END // fix orientation: norh from temple district, northwest
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = dagg01 END // overcharged item: dagg01 (num 15) has assigned 4 to stack 1 charges 0 in header 1
  BUT_ONLY

COPY_EXISTING ~ar0905.are~ ~override~ // pimlico's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit0900 END // fix orientation: temple district to pimlico's, northwest
  BUT_ONLY

// changes guard references in ar1000 to not be the rubble-guarder type, enables one ambient, fixes multiple gftown02
COPY_EXISTING ~ar1000.are~ ~override~ // Government District
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  SET gftown02 = 0
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "AMNG1" = 0) BEGIN // guard fix
      WRITE_ASCII (actor_off + 0x80 + (index * 0x110)) AMNG2 #8
    END ELSE
    PATCH_IF (("%cre_file%" STRING_COMPARE_CASE "victown1" = 0) OR
              ("%cre_file%" STRING_COMPARE_CASE "victown4" = 0)) BEGIN
      WRITE_BYTE (actor_off + 0x28 + (index * 0x110)) (THIS BOR BIT3) // use area name as DV for scripting fixes
    END ELSE
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "gftown02" = 0) BEGIN // duplicate gftown02
      PATCH_IF gftown02 = 0 BEGIN
        SET gftown02 = 1
      END ELSE BEGIN
        WRITE_ASCII (actor_off + 0x80 + (index * 0x110)) gftown01 #8
      END
    END
  END
  READ_SHORT 0x82 amb_num // inactive ambient
  READ_LONG  0x84 amb_off
  FOR (index = 0 ; index < amb_num ; ++index) BEGIN
    READ_SHORT (amb_off + 0x20 + (index * 0xd4)) x_coord
    PATCH_IF (x_coord = 907) BEGIN // SS_Prisoners1
      WRITE_LONG (amb_off + 0x8c + (index * 0xd4)) 0x001fffc0 // time active from 0 to this
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar1001.are~ ~override~ // delryn's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1000 END // fix orientation: gov't to delryn's home, northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exit1000b END // fix orientation: gov't to delryn's home, northeast
  BUT_ONLY

// part of making Ketlaar's guards disappear if Ketlaar is killed (see ar1002.bcs)
COPY_EXISTING ~ar1002.are~ ~override~ // Council Building
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  SET toggle = 1
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "BODYG2" = 0) BEGIN
      WRITE_ASCIIE (actor_off +        (index * 0x110)) ~MGKETBG%toggle%~ #32 // new area name
      WRITE_BYTE   (actor_off + 0x28 + (index * 0x110)) (THIS BOR BIT3) // use area name as DV
      SET toggle += 1
    END ELSE
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "argrim" = 0) BEGIN
      WRITE_ASCII (actor_off +        (index * 0x110)) ~argrim~ #32 // new area name
      WRITE_BYTE  (actor_off + 0x28 + (index * 0x110)) (THIS BOR BIT3) // use area name as DV
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar1003.are~ ~override~ // keldorn's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1000 END // fix orientation: gov't to keldorn's home, northwest
  BUT_ONLY

COPY_EXISTING ~ar1004.are~ ~override~ // deril's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1000 END // fix orientation: gov't to deril's home, northwest
  BUT_ONLY

COPY_EXISTING ~ar1005.are~ ~override~ // prison
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1000 END // fix orientation: gov't to prison, northwest
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = exi1000b END // fix orientation: prison from gov't district, northeast
  BUT_ONLY

COPY_EXISTING ~ar1006.are~ ~override~ // Jysstev's Estate (Oisig Plot)
  LPF ALTER_AREA_CONTAINER INT_VAR lock_difficulty = 90 STR_VAR container_name = "Container 2" END // make unpickable chest pickable
  BUT_ONLY

COPY_EXISTING ~ar1009.are~ ~override~ // roenall's home
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1000 END // fix orientation: gov't to roenall's home, northwest
  BUT_ONLY

COPY_EXISTING ~ar1100.are~ ~override~ // Umar Hills
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1104 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1106.are~ ~override~ // Umar's Cave
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1100 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1200.are~ ~override~ // windspear hills
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = exitw END // fix orientation: windspear hills from wmp, east
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1201 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1202.are~ ~override~ // Firkaarg's Maze
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1201 END // transition can not be used by npcs
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 lock_difficulty = 100 STR_VAR door_name = "Door24" END // sets uses key bit, makes door unpickable
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 lock_difficulty = 100 STR_VAR door_name = "Door29" END // sets uses key bit, makes door unpickable
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = key07 END // overcharged item: key07 (num 4) has assigned 4 to stack 1 charges 0 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge2 = 1 STR_VAR match_item = bow10  END // zero-charge item: heartseeker +3
  LPF ALTER_AREA_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h32 END // zero-charge item: dragonslayer
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3h END // zero-charge item: horn of blasting (num 20) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_AREA_REGION STR_VAR region_name = "CloudkillTrap" door_script = "gtck" END // ground traps won't trigger due to container scripts
  LPF ALTER_AREA_REGION STR_VAR region_name = "LightningBoltTrap" door_script = "gtlb" END // ground traps won't trigger due to container scripts
  BUT_ONLY

COPY_EXISTING ~ar1300.are~ ~override~ // de'arnise keep exterior
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exitse END // fix orientation: de'arnise keep from wmp, northwest
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1302a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1302b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1302c END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1302.are~ ~override~ // De'Arnise Keep 1st Floor
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1300a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1300b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1300c END // transition can not be used by npcs
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 20 STR_VAR match_item = dart03 END // zero-charge item: darts of stunning (num 74) has assigned 0 to stack 40 charges 1 in header 1
  BUT_ONLY

COPY_EXISTING ~ar1303.are~ ~override~ // De'Arnise Keep 2nd Floor (Destroyed)
  LPF ALTER_AREA_ITEM STR_VAR match_item = "" item = "arow08" END // fixes blank item reference in Nalia's keep
  BUT_ONLY

COPY_EXISTING ~ar1304.are~ ~override~ // de'arnise keep exterior (pc)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exitse END // fix orientation: de'arnise keep (pc) from wmp, northwest
  BUT_ONLY

// door from ground floor of stronghold de'Arnise Keep is off, not attached to door
COPY_EXISTING ~ar1306.are~ ~override~ // De'Arnise Keep (Stronghold)
  READ_SHORT 0x5a info_num
  READ_LONG  0x5c info_off
  READ_LONG  0x7c vert_off
  READ_LONG  0xa4 door_num
  READ_LONG  0xa8 door_off
  FOR (index = 0 ; index < info_num ; ++index) BEGIN
    READ_ASCII (info_off +        (index * 0xc4)) info_name (32) NULL
    PATCH_IF ("%info_name%" STRING_COMPARE_CASE "Tran1304a" = 0) BEGIN
      // bounding box
      WRITE_SHORT (info_off + 0x22 + (index * 0xc4)) 442  // bounding: left
      WRITE_SHORT (info_off + 0x24 + (index * 0xc4)) 1799 // bounding: top
      WRITE_SHORT (info_off + 0x26 + (index * 0xc4)) 520  // bounding: right
      WRITE_SHORT (info_off + 0x28 + (index * 0xc4)) 1935 // bounding: bottom
      // vertices
      READ_SHORT  (info_off + 0x2c + (index * 0xc4)) vert_idx
      WRITE_SHORT (vert_off +        (0x04 * vert_idx)) 442  // x0
      WRITE_SHORT (vert_off + 0x02 + (0x04 * vert_idx)) 1799 // y0
      WRITE_SHORT (vert_off + 0x04 + (0x04 * vert_idx)) 442  // x1
      WRITE_SHORT (vert_off + 0x06 + (0x04 * vert_idx)) 1891 // y1
      WRITE_SHORT (vert_off + 0x08 + (0x04 * vert_idx)) 520  // x2
      WRITE_SHORT (vert_off + 0x0a + (0x04 * vert_idx)) 1935 // y2
      WRITE_SHORT (vert_off + 0x0c + (0x04 * vert_idx)) 520  // x3
      WRITE_SHORT (vert_off + 0x0e + (0x04 * vert_idx)) 1845 // y3
      WRITE_BYTE (info_off + 0x61 + (0xc4 * index)) (THIS BOR BIT3) // add 'connected to door' flag; door adjusted next
      SET index = info_num // kills loop
    END
  END
  FOR (index = 0 ; index < door_num ; ++index) BEGIN
    READ_ASCII (door_off +        (index * 0xc8)) door_name (32) NULL
    PATCH_IF ("%door_name%" STRING_COMPARE_CASE "secret07" = 0) BEGIN
      WRITE_ASCII (door_off + 0x9c + (index * 0xc8)) "Tran1304a" #24
      SET index = door_num // kills loop
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar1400.are~ ~override~ // Shadow Temple Land (restored, Ranger)
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1401 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1402 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1403 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1512.are~ ~override~ // Bodhi's Hunt Level 1
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Door1513 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Door1513a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Door1514 END // transition can not be used by npcs
  BUT_ONLY

// move imp to passable ground; can't use ALTER_AREA_ACTOR since there are multiple imps
COPY_EXISTING ~ar1513.are~ ~override~ // Bodhi's Hunt Level 2
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "imp01" = 0) BEGIN
      READ_SHORT(actor_off + 0x20 + (index * 0x110)) x_coord
      READ_SHORT(actor_off + 0x22 + (index * 0x110)) y_coord
      PATCH_IF ((x_coord = 1639) AND (y_coord = 1140)) BEGIN
        WRITE_SHORT(actor_off + 0x20 + (index * 0x110)) 1724 // current x
        WRITE_SHORT(actor_off + 0x22 + (index * 0x110)) 1164 // current y
        WRITE_SHORT(actor_off + 0x24 + (index * 0x110)) 1724 // destination x
        WRITE_SHORT(actor_off + 0x26 + (index * 0x110)) 1164 // destination y
        SET index = actor_num // kill loop
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar1514.are~ ~override~ // Bodhi's Hunt Level 3 (1st Slayer Change Happens here)
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Door1507 END // transition can not be used by npcs
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 40 STR_VAR match_item = arow05 END // zero-charge item: arow05 (num 53) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_AREA_ITEM INT_VAR charge1 = 40 STR_VAR match_item = bolt04 END // zero-charge item
  BUT_ONLY

COPY_EXISTING ~ar1515.are~ ~override~ // Spellhold (Irenicus' Living Room)
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1500 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar1601.are~ ~override~ // Desharik's Estate
  LPF ALTER_AREA_ANIMATION INT_VAR flag_static = 0 STR_VAR match_name = smoke END // oven smoke plume, remove show in fog of war flag
  BUT_ONLY

COPY_EXISTING ~ar1603.are~ ~override~ // Brynnlaw's Shop
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0 STR_VAR match_name = fpit1s END // height of forges
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  READ_LONG  0x7c vert_off
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    READ_SHORT (cont_off + 0x20 + (index * 0xc0)) x_coord // container overlay corrections, container 2
    PATCH_IF (x_coord = 270) BEGIN
      WRITE_SHORT (cont_off + 0x38 + (index * 0xc0)) 243 // bounding box: left
      WRITE_SHORT (cont_off + 0x3a + (index * 0xc0)) 414 // bounding box: top
      WRITE_SHORT (cont_off + 0x3c + (index * 0xc0)) 292 // bounding box: right
      READ_SHORT  (cont_off + 0x50 + (index * 0xc0)) vert_idx
      WRITE_SHORT (vert_off +        ((vert_idx + 0) * 0x04)) 292 // x0
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 0) * 0x04)) 442 // y0
      WRITE_SHORT (vert_off +        ((vert_idx + 1) * 0x04)) 286 // x1
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 1) * 0x04)) 414 // y1
      WRITE_SHORT (vert_off +        ((vert_idx + 2) * 0x04)) 243 // x2
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 2) * 0x04)) 458 // y2
      WRITE_SHORT (vert_off +        ((vert_idx + 3) * 0x04)) 252 // x3
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 3) * 0x04)) 489 // y3
    END
  END
  BUT_ONLY

// Fix the incorrect orientation when entering Cayia's house in Brynnlaw (CamDawg)
COPY_EXISTING ~ar1606.are~ ~override~
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 6 STR_VAR entrance_name = exit1600 END // fix orientation: house of desharik's lover from brynnlaw, northwest
  BUT_ONLY

COPY_EXISTING ~ar1608.are~ ~override~ // Cellar of Brothel
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "DOOR05" END // sets uses key bit
  BUT_ONLY

// changes another door to use its key
COPY_EXISTING ~ar1611.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "Secret 1" END // sets uses key bit
  BUT_ONLY

// Unreferenced night tilesets (Nythrun), see also ar1900n.wed
COPY_EXISTING ~ar1900.are~ ~override~ // Brothel
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran1902 END // transition can not be used by npcs
  WRITE_SHORT  0x48 THIS | BIT6 // add 'extended night' flag
  BUT_ONLY

// no battle music for adratha's cabin
COPY_EXISTING ~ar1902.are~ ~override~
  READ_LONG 0xbc song_off
  WRITE_LONG (song_off + 0x0c) 54 // battle music, bm1
  BUT_ONLY

//moves troll to passable ground; can't use ALTER_AREA_ACTOR since there are multiple trolls
COPY_EXISTING ~ar1904.are~ ~override~ // Troll Hill
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "TROLL01" = 0) BEGIN
      READ_SHORT(actor_off + 0x20 + (index * 0x110)) x_coord
      READ_SHORT(actor_off + 0x22 + (index * 0x110)) y_coord
      PATCH_IF ((x_coord = 1492) AND (y_coord = 1172)) BEGIN
        WRITE_SHORT(actor_off + 0x20 + (index * 0x110)) 1273 // current x
        WRITE_SHORT(actor_off + 0x22 + (index * 0x110)) 1180 // current y
        WRITE_SHORT(actor_off + 0x24 + (index * 0x110)) 1273 // destination x
        WRITE_SHORT(actor_off + 0x26 + (index * 0x110)) 1180 // destination y
        SET index = actor_num // kill loop
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar2000.are~ ~override~ // Trademeet
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2008 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2008b END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2013 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran2002 END // transition should be party required
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran2002b END // transition should be party required
  LPF ALTER_AREA_DOOR INT_VAR flag_uses_key = 1 STR_VAR door_name = "DOOR01" END // sets uses key bit
  READ_SHORT 0x82 amb_num // inactive ambient
  READ_LONG  0x84 amb_off
  FOR (index = 0 ; index < amb_num ; ++index) BEGIN
    READ_SHORT (amb_off + 0x20 + (index * 0xd4)) x_coord
    READ_SHORT (amb_off + 0x22 + (index * 0xd4)) y_coord
    PATCH_IF ((x_coord = 35) AND (y_coord = 59)) BEGIN // SS-Dogs Day 1
      WRITE_LONG (amb_off + 0x8c + (index * 0xd4)) 0x001fffc0 // time active from 0 to this
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar2001.are~ ~override~ // The Smithy
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0 STR_VAR match_name = fpit1s END // height of forges
  BUT_ONLY

// removes spurious trap flag
COPY_EXISTING ~ar2002.are~ ~override~ // Fentan's House
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 trap_detect = 0 trap_remove = 0 STR_VAR container_name = "Container 1" END // removes spurious trap flag
  BUT_ONLY

COPY_EXISTING ~ar2008.are~ ~override~ // Temple of Waukeen
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2000 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2000b END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar2100.are~ ~override~ // Underdark
  READ_LONG 0xbc song_off
  WRITE_LONG (song_off + 0x04) 31 // corrects night music in underdark (was mx2000, trademeet)
  READ_LONG  0xc4 note_off // Correct the position of the Western Tunnels map note in the Underdark (aVENGER)
  READ_LONG  0xc8 note_num
    FOR (index = 0; index < note_num; ++index) BEGIN // cycle through automap notes
      READ_SHORT (note_off +        (index * 0x34)) x_coord
      READ_SHORT (note_off + 0x02 + (index * 0x34)) y_coord
      PATCH_IF ((x_coord = 335) AND  (y_coord = 1569)) BEGIN // Western Tunnels map note
       WRITE_SHORT (note_off +        (index * 0x34)) 250    // Coordinate X (250)
       WRITE_SHORT (note_off + 0x02 + (index * 0x34)) 1830   // Coordinate Y (1830)
      END
    END  
BUT_ONLY

COPY_EXISTING ~ar2101.are~ ~override~ // underdark beholder lair
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 8 STR_VAR entrance_name = Exit2100 END // fix orientation: underdark to beholder lair, north
  BUT_ONLY

COPY_EXISTING ~ar2200.are~ ~override~ // Ust Natha
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2206 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2207 END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2210a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2210b END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar2206.are~ ~override~ // Qilue's House in Ust Natha
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2200 END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar2207.are~ ~override~ // Deirex's Tower in Ust Natha
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 flag_party_required = 1 STR_VAR region_name = Tran2200 END // transition can not be used by npcs, party required
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 1 STR_VAR container_name = "Shelf 1" END // trap has everything except trapped? = yes
  LPF ALTER_AREA_CONTAINER INT_VAR trap_detect = 50 trap_remove = 90 STR_VAR container_name = "Shelf 3" END // trap required 0% for detection, but couldn't be removed
  BUT_ONLY

COPY_EXISTING ~ar2210.are~ ~override~ // Deirex's Cave
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2200a END // transition can not be used by npcs
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2200b END // transition can not be used by npcs
  BUT_ONLY

COPY_EXISTING ~ar2300.are~ ~override~ // Sahuagin City
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0xffef match_x_coord = 3590 match_y_coord = 2171 END // water spout
  LPF ALTER_AREA_ANIMATION INT_VAR height = 0xffd3 match_x_coord = 4116 match_y_coord = 1641 END // water spout
  BUT_ONLY

COPY_EXISTING ~ar2400.are~ ~override~ // Mind Flayers in Underdark
  LPF ALTER_AREA_ITEM INT_VAR charge2 = 0 STR_VAR match_item = misc3o END // overcharged item: misc3o (num 4) has assigned 3 to max 0 in header 2 (methlid's harp, no secondary charges)
  READ_LONG 0xc0 rest_off              // cleans up junk in the rest spawn block
  SAY         (rest_off + 0x40) #10134 // random, mismatched string
  WRITE_ASCII (rest_off + 0x70) ~~ #40 // blanks five junky cre fields
  BUT_ONLY

COPY_EXISTING ~ar2401.are~ ~override~ // war elves in ar2401 go hostile with drow shouts
  READ_LONG  0x54 actor_off
  READ_SHORT 0x58 actor_num
  FOR (index = 0 ; index < actor_num ; ++index) BEGIN
    READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF ("%cre_file%" STRING_COMPARE_REGEXP "udelf0[1-5]" = 0) BEGIN
      WRITE_ASCII (actor_off + 0x60 + (index * 0x110)) ~waitpc~ #8 // overrides class script (prevents gpshout in cre files from working)
      PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "udelf02" = 0) BEGIN
        WRITE_ASCII (actor_off + 0x58 + (index * 0x110)) ~wtarsgt~ #8 // also overrides general script (prevents gparcher in cre files from working)
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar2600.are~ ~override~ // Tethyr Wood
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2603 END // transition can not be used by npcs, no party required
  BUT_ONLY

COPY_EXISTING ~ar2603.are~ ~override~ // Coran's Cabin
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 0 STR_VAR region_name = Tran2600 END // removes party required flag for ar2603 > ar2600 transition
  BUT_ONLY

COPY_EXISTING ~ar2800.are~ ~override~ // Suldanesselar
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran2807 END // transition can not be used by npcs
  BUT_ONLY

// Unreferenced night tilesets (Nythrun)
COPY_EXISTING ~ar2807.are~ ~override~
  WRITE_SHORT  0x48 THIS | BIT6 // add 'extended night' flag
  BUT_ONLY

COPY_EXISTING ~ar2902.are~ ~override~ // Pride (Abyss)
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran2900 END // transition should be party required
  BUT_ONLY

ACTION_IF tob_game THEN BEGIN // ToB

  // watcher's keep exterior doors off by a few pixels, allowing players to bypass closed doors
  COPY_EXISTING ~ar3000.are~ ~override~ // Watcher's Keep
    READ_SHORT 0x5a info_num
    READ_LONG  0x5c info_off
    READ_LONG  0x7c vert_off
    READ_LONG  0xa4 door_num
    READ_LONG  0xa8 door_off
    FOR (index = 0 ; index < info_num ; ++index) BEGIN
      READ_ASCII ("%info_off%" + 0x7c + (0xC4 * index)) script
      PATCH_IF ("TP3001A" STRING_COMPARE_CASE "%script%" = 0) BEGIN // door to level one, altar
        WRITE_SHORT (info_off + 0x24 + (0xC4 * index)) 406  // bounding box top
        READ_LONG   (info_off + 0x2c + (0xC4 * index)) vert_idx
        WRITE_SHORT (vert_off +        ((vert_idx + 3) * 0x04)) 1040 // vertex
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 3) * 0x04)) 406  // vertex
      END ELSE
      PATCH_IF ("TP3017B" STRING_COMPARE_CASE "%script%" = 0) BEGIN // door to carston and the machine
        READ_LONG   (info_off + 0x2c + (0xC4 * index)) vert_idx
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 2) * 0x04)) 1774 // vertex
      END ELSE
      PATCH_IF ("TP3019B" STRING_COMPARE_CASE "%script%" = 0) BEGIN // door to helmite level -- last seals
        WRITE_SHORT (info_off + 0x24 + (0xC4 * index)) 1596 // bounding box
        READ_LONG   (info_off + 0x2c + (0xC4 * index)) vert_idx
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 1) * 0x04)) 1569 // vertex
      END ELSE
      PATCH_IF ("TP3020" STRING_COMPARE_CASE "%script%" = 0) BEGIN // door to imprisoned one
        WRITE_SHORT (info_off + 0x22 + (0xC4 * index)) 429  // bounding box
        WRITE_SHORT (info_off + 0x24 + (0xC4 * index)) 1899 // bounding box
        WRITE_SHORT (info_off + 0x26 + (0xC4 * index)) 489  // bounding box
        WRITE_SHORT (info_off + 0x28 + (0xC4 * index)) 2039 // bounding box
        READ_LONG   (info_off + 0x2c + (0xC4 * index)) vert_idx
        WRITE_SHORT (vert_off +        ((vert_idx    ) * 0x04)) 429  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx    ) * 0x04)) 2003 // vertex
        WRITE_SHORT (vert_off +        ((vert_idx + 1) * 0x04)) 448  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 1) * 0x04)) 1899 // vertex
        WRITE_SHORT (vert_off +        ((vert_idx + 2) * 0x04)) 489  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 2) * 0x04)) 1931 // vertex
        WRITE_SHORT (vert_off +        ((vert_idx + 3) * 0x04)) 483  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 3) * 0x04)) 2039 // vertex
      END
    END
    FOR (index = 0 ; index < door_num ; ++index) BEGIN
      READ_ASCII (door_off +        (index * 0xc8)) doorname
      PATCH_IF ("%doorname%" STRING_COMPARE_CASE "DOOR02" = 0) BEGIN // door to imprisoned one
        WRITE_SHORT (door_off + 0x44 + (index * 0xc8)) 508 // bounding box
        READ_LONG   (door_off + 0x34 + (index * 0xc8)) cl_vert_idx // closed vertex index
        WRITE_SHORT (vert_off +        ((cl_vert_idx + 1) * 0x04)) 445  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((cl_vert_idx + 1) * 0x04)) 1896 // vertex
        WRITE_SHORT (vert_off +        ((cl_vert_idx + 2) * 0x04)) 508  // vertex
        WRITE_SHORT (vert_off +        ((cl_vert_idx + 3) * 0x04)) 495  // vertex
        WRITE_SHORT (vert_off + 0x02 + ((cl_vert_idx + 3) * 0x04)) 2048 // vertex
      END
    END
    BUT_ONLY
  
  // corrects ambients, animation glitches due to bad flag
  COPY_EXISTING ~ar3001.are~ ~override~ // Watcher's Keep -- Altar level
    LPF ALTER_AREA_ANIMATION INT_VAR flag_static = 1 STR_VAR match_name = am3001f END // glowing globes, add show in fog of war flag
    READ_SHORT 0x82 amb_num
    READ_LONG  0x84 amb_off
    FOR (index = 0 ; index < amb_num ; ++index) BEGIN
      READ_SHORT (amb_off + 0x20 + (index * 0xd4)) x_coord
      PATCH_IF (x_coord = 1406) BEGIN // ghostly single shots
        WRITE_SHORT (amb_off + 0x80 + (index * 0xd4)) 10 // ambient was only using 1 out of 10 listed sounds
      END
    END
    BUT_ONLY
  
  COPY_EXISTING ~ar3004.are~ ~override~ // Watcher's Keep (maze)
    READ_LONG 0xc0 rest_off
    WRITE_ASCII (rest_off + 0x60) ~DGLAB01~ #8 // broken cre reference
    BUT_ONLY
  
    // Killing unrelated baatezu outside Ka'rashur's room should not turn him and his cohort in Watcher's Keep hostile (see also yssum2.bcs, ar6012.are) (Wisp)
  COPY_EXISTING ~ar3005.are~ ~override~ // Watcher's Keep -- tieflings
                ~ar3006.are~ ~override~ // Watcher's Keep -- Succubus
                ~ar3009.are~ ~override~ // Watcher's Keep -- Wild Magic ?
    READ_LONG 0xc0 rest_off
    FOR (index = 0 ; index < 10 ; ++index) BEGIN
      READ_ASCII (rest_off + 0x48 + (index * 0x08)) cre_file
      PATCH_IF ("%cre_file%" STRING_EQUAL_CASE gorbat5) BEGIN
        WRITE_ASCII (rest_off + 0x48 + (index * 0x08)) dbonef01 #8
      END
    END
    BUT_ONLY
  
  COPY_EXISTING ~ar3010.are~ ~override~ // Watcher's Keep -- Tanar'ri -- Obelisk
    READ_LONG 0xc0 rest_off
    WRITE_ASCII (rest_off + 0x58) ~DERINY01~ // broken cre reference
    BUT_ONLY
  
  COPY_EXISTING ~ar3016.are~ ~override~ // Watcher's Keep -- Chromatic Demon; Elementalist Level
    LPF ALTER_AREA_ITEM INT_VAR charge2 = 5 STR_VAR match_item = wand18 END // overcharged: wand of spell striking, second ability has five max charges
    LPF ALTER_AREA_ITEM INT_VAR charge2 = 0 charge3 = 0 STR_VAR match_item = wand19 END // no secondary/tertiary abilities
    BUT_ONLY
  
  COPY_EXISTING ~ar3021.are~ ~override~ // Watcher's Keep -- Ilithids
    LPF ALTER_AREA_ITEM INT_VAR charge2 = 0 charge3 = 0 STR_VAR match_item = wand19 END // overcharged: no secondary/tertiary abilities
    BUT_ONLY
  
  COPY_EXISTING ~ar3022.are~ ~override~ // Watcher's Keep -- Anti-Paladins
    LPF ALTER_AREA_ITEM INT_VAR charge2 = 5 STR_VAR match_item = wand18 END // overcharged: wand of spell striking, second ability has five max charges
    BUT_ONLY

  COPY_EXISTING ~ar5000.are~ ~override~ // Saradush
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5002 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5001.are~ ~override~ // Gromnir 1st Level
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5000 END // transition can not be used by npcs
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5002 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5006.are~ ~override~ // Saradush Prison
    READ_SHORT 0x5a info_num
    READ_LONG  0x5c info_off
    FOR (index = 0; index < info_num ; ++index) BEGIN // dupe SpiritAppear1 info points; rename one
      READ_SHORT (info_off + 0x24 + (0xc4 * index )) bound_top
      PATCH_IF (bound_top = 603) BEGIN
        WRITE_ASCII (info_off + (0xC4 * index )) SpiritAppear2 #32
        SET index = info_num // kill loop
      END
    END
    BUT_ONLY
  
  COPY_EXISTING ~ar5007.are~ ~override~ // Basement Entrance to Gromnir's Hideout
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5006 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5013.are~ ~override~ // Sewers beneath Saradush
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5000a END // transition can not be used by npcs
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5000b END // transition can not be used by npcs
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5000c END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5015.are~ ~override~ // Saradush Militia Entrance
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5016 END // transition can not be used by npcs
    LPF ALTER_AREA_DOOR INT_VAR flag_secret = 0 STR_VAR door_name = "Secret 1" END // removes secret flag; despite name not secret
    BUT_ONLY
  
  COPY_EXISTING ~ar5200.are~ ~override~ // Fire Giant Entrance Area
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5201 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5201.are~ ~override~
    LPF ALTER_AREA_REGION STR_VAR region_name = "Inc Cloud Trap" door_script= "gtfb" END // ground traps won't trigger due to container scripts
    BUT_ONLY
  
  COPY_EXISTING ~ar5204.are~ ~override~
    LPF ALTER_AREA_CONTAINER STR_VAR container_name = "Bed" container_script = "ctfb" END // container traps won't trigger due to ground scripts; see also ct028.bcs
    BUT_ONLY
  
  
  COPY_EXISTING ~ar5502.are~ ~override~ // Kerrick the Smith's House
    LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw1h05 END // overcharged item: sw1h05 (num 5) has assigned 2 to stack 1 charges 0 in header 1
    BUT_ONLY

  COPY_EXISTING ~ar5503.are~ ~override~ // Chyil's House
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5500 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5504.are~ ~override~ // Smuggler Cave
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5500 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar5507.are~ ~override~ // Empty Home
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran5500 END // transition can not be used by npcs
    BUT_ONLY
  
  // Killing unrelated baatezu outside Ka'rashur's room should not turn him and his cohort in Watcher's Keep hostile (see also yssum2.bcs, ar3005.are, ar3006.are, ar3009.are) (Wisp)
  COPY_EXISTING ~ar6012.are~ ~override~ // Pool Home -- breath under water
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Bone Fiend" cre_file = "dbonef01" END
    BUT_ONLY
  
  COPY_EXISTING ~ar6101.are~ ~override~ // Main Entrance Sendai's Lair
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran6100 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar6107.are~ ~override~ // Sendai's Lair -- Mithykyl
    LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 STR_VAR region_name = Tran6108 END // transition can not be used by npcs
    BUT_ONLY
  
  COPY_EXISTING ~ar6300.are~ ~override~ // Oasis in Tethyr
    LPF ALTER_AREA_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = plot16a END // overcharged item: plot16a (num 6) has assigned 2 to stack 1 charges 0 in header 1
    BUT_ONLY
  
END

/////                                                  \\\\\
///// bulk creature file fixes                         \\\\\
/////                                                  \\\\\

// alignment fixes
// read in array of changes from external file to make it easier for players to make their own changes here
INCLUDE ~bg2fixpack/lib/alignment_changes.tpa~

ACTION_PHP_EACH cd_bulk_cre_changes_align AS cre_file => n_align BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x27b n_align
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_race BEGIN

  AEWERE2  => 122 // Fighter, from HUMAN to LYCANTHROPE
  AEWERE3  => 122 // Fighter, from HUMAN to LYCANTHROPE
  AEWERE4  => 122 // Fighter, from HUMAN to LYCANTHROPE
  AEWERE5  => 122 // Fighter, from HUMAN to LYCANTHROPE
  AEWERE6  => 122 // Fighter, from HUMAN to LYCANTHROPE
  ALUFIE01 =>   2 // Alu-Fiend, from DWARF to ELF
  AR18FIG  => 143 // Mercenary, from OGRE to ORC
  ARNMAN07 =>   2 // Prisoner, from HUMAN to ELF
  BANSHE01 => 134 // Banshee, from NO_RACE to WRAITH
  BBEGG2   =>   5 // Beggar, from HUMAN to HALFLING
  BDTURM03 =>   1 // Turmish Sorceress, from ELF to HUMAN
  BEAST    =>   3 // Beastmaster, from HUMAN to HALF_ELF
  BODFGT01 => 133 // Grimwarder, from NO_RACE to SPECTRE
  BODFGT02 => 133 // Grimward Archer, from NO_RACE to SPECTRE
  BOUNHA   =>   2 // Bounty Hunter, from HUMAN to ELF
  BOUNHA04 => 154 // Bounty Hunter, from HUMAN to YUANTI
  CEDELICH => 150 // Deril Lich, from HUMAN to LICH
  CEGLUT   => 113 // Glut, from GOLEM to OGRE
  CHEVIL09 =>   2 // Jon Irenicus, from HUMAN to ELF
  CHGOOD01 =>   5 // Commoner, from HUMAN to HALFLING
  CLCOTI01 =>   1 // Cotirso, from HALF_ELF to HUMAN
  COPAMB03 =>   2 // Gladiator, from HUMAN to ELF
  COPLION  => 137 // Tabitha, from WOLF to CAT
  COWENF1  =>   1 // Cowled Enforcer, from ELF to HUMAN
  COWENF3  =>   1 // Cowled Enforcer, from ELF to HUMAN
  COWENF4  =>   1 // Cowled Enforcer, from ELF to HUMAN
  CSIREN   =>   2 // Mage, from HUMAN to ELF
  CSJON    =>   2 // Mage, from HUMAN to ELF
  CUJON    =>   2 // Jon Irenicus, from HUMAN to ELF
  CUJON2   =>   2 // Jon Irenicus, from HUMAN to ELF
  CUJON3   =>   2 // Jon Irenicus, from HUMAN to ELF
  CUJON4   =>   2 // Jon Irenicus, from HUMAN to ELF
  D1MOTHDW =>   4 // Alianna, from HUMAN to DWARF
  D1MOTHEL =>   2 // Alianna, from HUMAN to ELF
  D1MOTHHA =>   5 // Alianna, from HUMAN to HALFLING
  DAABOL   => 255 // Aboleth, from HUMAN to NO_RACE
  DEMFIG02 =>   2 // Valeria, from HUMAN to ELF
  DISRUP01 =>   5 // Wizard Slayer, from HUMAN to HALFLING
  DKHALID  =>   3 // Khalid, from HUMAN to HALF_ELF
  DKHALID2 =>   3 // Khalid, from HUMAN to HALF_ELF
  DPJON01  =>   2 // Jon Irenicus, from HUMAN to ELF
  DPJON02  =>   2 // Jon Irenicus, from HUMAN to ELF
  DREAM2   =>   2 // Jon Irenicus, from HUMAN to ELF
  DRIREN   =>   2 // Jon Irenicus, from HUMAN to ELF
  DRJON    =>   2 // Jon Irenicus, from HUMAN to ELF
  DRSHSP01 => 133 // Spirit of the Grove, from HUMAN to SPECTRE
  DRUFF2   =>   4 // Ruffian, from HALFORC to DWARF
  DUEMAG01 =>   4 // Mage, from HUMAN to DWARF
  DUEMAG02 =>   4 // Mage, from HUMAN to DWARF
  ELEARB01 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB02 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB03 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB04 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB05 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB06 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB07 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB08 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB09 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB10 => 133 // Apparition, from HUMAN to SPECTRE
  ELEARB11 => 133 // Apparition, from HUMAN to SPECTRE
  ELEPUZ01 => 133 // Apparition, from HUMAN to SPECTRE
  ELEPUZ02 => 133 // Apparition, from HUMAN to SPECTRE
  ELEPUZ03 => 133 // Apparition, from HUMAN to SPECTRE
  ELEPUZ04 => 133 // Apparition, from HUMAN to SPECTRE
  ESCORT2  =>   5 // Kerstia, from HUMAN to HALFLING
  ESCORT2A =>   5 // Kerstia, from HUMAN to HALFLING
  EYEDED01 => 134 // Eye of the Dead, from NO_RACE to WRAITH
  EYEGOR01 => 102 // Gorgon Eye, from NO_RACE to BASILISK
  EYESEK01 =>   6 // Seeker, from NO_RACE to GNOME
  EYESNT01 =>   2 // Sentinel, from NO_RACE to ELF
  EYEVIG01 =>   1 // Vigilant, from NO_RACE to HUMAN
  FIRMAG01 =>   1 // Conster, from ELF to HUMAN
  FIRMON01 => 134 // Banshee, from NO_RACE to WRAITH
  FSGIBB   => 109 // Gibberling, from HUMAN to GIBBERLING
  FSGOBL   => 112 // Goblin, from HUMAN to KOBOLD
  FSMAGE01 =>   1 // Mage, from ELF to HUMAN
  FSMAGE02 =>   1 // Mage, from ELF to HUMAN
  FSMAGE03 =>   1 // Mage, from ELF to HUMAN
  GIAFIR01 => 142 // Fire Giant, from DWARF to GIANT
  GIBBER01 => 109 // Gibberling, from HUMAN to GIBBERLING
  GIBBERSU => 109 // Gibberling, from HUMAN to GIBBERLING
  GORMIND  => 124 // Ulitharid, from HUMAN to MIND_FLAYER
  GORSTA02 =>   2 // Statue, from HUMAN to ELF
  GORSTA04 =>   4 // Statue, from HUMAN to DWARF
  GORSTA09 =>   2 // Statue, from HUMAN to ELF
  GORSTA11 =>   2 // Statue, from HUMAN to ELF
  GORSTA12 => 113 // Statue, from ELEMENTAL to OGRE
  GROMG02  => 143 // Il-Khan Soldier, from HUMAN to ORC
  HAKSU    => 136 // Hakeashar, from OGRE to MIST
  HAMASU   => 120 // Hamadryad, from 0 to FAIRY
  HARPASS1 =>   2 // Lucette, from HUMAN to ELF
  HELLJON  =>   2 // Jon Irenicus, from HUMAN to ELF
  HELLJON2 =>   2 // Jon Irenicus, from HUMAN to ELF
  HGSKU01  => 150 // Flaming Skull, from NO_RACE to LICH
  HLARCH   =>   2 // Archer, from HUMAN to ELF
  HLLAYEN  =>   1 // Layene, from ELF to HUMAN
  HLMAFER  => 110 // Maferan, from NO_RACE to GNOLL
  HLOLAF   => 143 // Olaf Rassmusen, from NO_RACE to ORC
  HLSKULL  => 150 // Golden Skull, from HUMAN to LICH
  HOBELI01 => 111 // Hobgoblin Elite, from HUMAN to HOBGOBLIN
  HOBGOB01 => 111 // Gerg, from HUMAN to HOBGOBLIN
  HOBGOBSU => 111 // Hobgoblin Elite, from HUMAN to HOBGOBLIN
  HSPECTR1 =>   2 // Spectral Harpist, from NO_RACE to ELF
  HSPECTR2 =>   1 // Spectral Harpist, from NO_RACE to HUMAN
  HSPECTR3 =>   2 // Spectral Harpist, from NO_RACE to ELF
  ICMYC01  => 112 // Goblin, from NONE to MYCONID
  ICMYC02  => 112 // Goblin, from NONE to MYCONID
  ICGOB03  => 112 // Goblin, from HOBGOBLIN to KOBOLD
  ICGOB04  => 112 // Goblin Captain, from HOBGOBLIN to KOBOLD
  ICLIZ01  => 154 // Lizard Man, from NO_RACE to YUANTI
  ICLIZ02  => 154 // Gorkale, from NO_RACE to YUANTI
  JADE1    =>   2 // Lennah, from HUMAN to ELF
  JAGUARSU => 137 // Jaguar, from NO_RACE to CAT
  JAHEI1   =>   2 // Harper, from HUMAN to ELF
  JAREV1   =>   2 // Harper, from HUMAN to ELF
  JON1     =>   2 // Jon Irenicus, from HUMAN to ELF
  KAYSMG01 =>   3 // Rindus, from HUMAN to HALF_ELF
  KCHILD1  => 143 // Peasant, from OGRE to ORC
  KCHILD2  => 143 // Peasant, from OGRE to ORC
  KPTROL01 => 129 // Troll, from TROLL to TROLL
  KPTROL02 => 129 // Troll, from TROLL to TROLL
  KPTROL03 => 129 // Troll, from TROLL to TROLL
  KPTROL04 => 129 // Troll, from TROLL to TROLL
  KPTROL05 => 129 // Troll, from TROLL to TROLL
  KPTROL06 => 129 // Ice Troll, from TROLL to TROLL
  KSLAVE01 => 120 // Pleasure Slave, from HUMAN to FAIRY
  LIFE01   =>   2 // Life Support Creature, from HUMAN to ELF
  LIFE02   =>   2 // Life Support Creature, from HUMAN to ELF
  LIFE03   =>   2 // Life Support Creature, from HUMAN to ELF
  LIFE04   =>   2 // Life Support Creature, from HUMAN to ELF
  LIN      =>   3 // Lin, from GNOME to HALF_ELF
  MEPHSP1  => 255 // Mephit Portal, from HUMAN to NO_RACE
  MEPHSP2  => 255 // Mephit Portal, from HUMAN to NO_RACE
  MEPHSP3  => 255 // Mephit Portal, from HUMAN to NO_RACE
  MEPHSP4  => 255 // Mephit Portal, from HUMAN to NO_RACE
  MGAPPR01 =>   2 // Larz, from HUMAN to ELF
  MINDUL01 => 124 // Ulitharid, from HUMAN to MIND_FLAYER
  MOURNER6 =>   5 // Mourner, from HUMAN to HALFLING
  NCAT     => 137 // Cat, from NO_RACE to CAT
  NISHRUSU => 136 // Nishruu, from OGRE to MIST
  OTYUGH01 => 127 // Otyugh, from BASILISK to OTYUGH
  PETTIN   => 199 // Ettin, from HUMAN to ETTIN
  PLANET01 => 158 // Planetar, from DARKPLANATAR to PLANATAR
  PLSHFG01 => 199 // Ettin, from GIANT to ETTIN
  PLSHFG02 => 199 // Ettin, from GIANT to ETTIN
  PLYSALA  => 145 // Fire Salamander, from TROLL to ELEMENTAL
  PPDRA2   =>   2 // Dradeel, from HUMAN to ELF
  PPDRADEE =>   2 // Dradeel, from HUMAN to ELF
  PPIRENI1 =>   2 // Coordinator, from HUMAN to ELF
  PPIRENI2 =>   2 // Jon Irenicus, from HUMAN to ELF
  PPSUNA   =>   2 // Suna Seni, from HUMAN to ELF
  RABBIT   => 151 // Rabbit, from NO_RACE to RABBIT
  RABBIT01 => 151 // Rabbit, from NO_RACE to RABBIT
  RAKRUH01 => 128 // Ruhk, from NO_RACE to RAKSHASA
  RIELEV   =>   2 // Rielev, from HUMAN to ELF
  RIFTG03  =>   1 // Avatar, from NO_RACE to HUMAN
  RSPIRIT1 => 120 // Mairyn, from NO_RACE to FAIRY
  SAHANGU  => 131 // Anguiliian, from NO_RACE to SAHUAGIN
  SAHBEH03 =>   1 // Sziltar's Apparition, from NO_RACE to HUMAN
  SAHOTY01 => 199 // Ettin, from GIANT to ETTIN
  SAHSKEL  => 115 // Skeleton Warrior, from HUMAN to SKELETON
  SAHZOMB  => 148 // Sea Zombie, from NO_RACE to ZOMBIE
  SARBHA02 =>   5 // Oris Nimblefinger, from HUMAN to HALFLING
  SARBUL04 => 143 // Gromnir Soldier, from HALFORC to ORC
  SARDW01  =>   4 // Dwarf, from HUMAN to DWARF
  SARDW02  =>   4 // Dwarf, from HUMAN to DWARF
  SARHAL   =>   5 // Halfling Cultist, from HUMAN to HALFLING
  SENANI01 =>   2 // Moose, from HUMAN to ELF
  SENANI02 =>   2 // Deer, from HUMAN to ELF
  SENDRO01 =>   2 // Drow, from HUMAN to ELF
  SENDRO02 =>   2 // Drow, from HUMAN to ELF
  SENFOD01 =>   2 // Drow, from HUMAN to ELF
  SENFOD02 =>   2 // Drow, from HUMAN to ELF
  SENGUA01 =>   2 // Woodcutter, from HUMAN to ELF
  SENKEN01 =>   2 // Thelynn'ss, from HUMAN to ELF
  SENSTALK => 133 // Invisible Stalker, from HUMAN to SPECTRE
  SHANK    =>   3 // Shank, from HUMAN to HALF_ELF
  SKELWA02 => 115 // Skeleton Warrior, from HUMAN to SKELETON
  SKELWA03 => 115 // Skeleton Warrior, from HUMAN to SKELETON
  SKELWASU => 115 // Skeleton Warrior, from HUMAN to SKELETON
  SLCAT    => 137 // Missy, from NO_RACE to CAT
  SLEEPDW  =>   4 // Sleeping Dwarf, from HUMAN to DWARF
  SLIFIS01 => 119 // Fission Slime, from HUMAN to SLIME
  SLIFIS02 => 119 // Fission Slime, from HUMAN to SLIME
  SLVIC02  =>   1 // Tourist, from ELF to HUMAN
  SPIRLION => 137 // Spirit Lion, from NO_RACE to CAT
  STALKE   => 133 // Invisible Stalker, from HUMAN to SPECTRE
  STATUE01 => 201 // Magical Sword, from GNOLL to SWORD
  STATUE02 => 201 // Magical Sword, from GNOLL to SWORD
  STATUE03 => 201 // Magical Sword, from GNOLL to SWORD
  STATUE04 => 201 // Magical Sword, from GNOLL to SWORD
  STATUE05 => 201 // Magical Sword, from GNOLL to SWORD
  STATUE06 => 201 // Magical Sword, from GNOLL to SWORD
  SUELF10  =>   2 // Reirra, from HUMAN to ELF
  SUJON    =>   2 // Jon Irenicus, from HUMAN to ELF
  SUJON2   =>   2 // Jon Irenicus, from HUMAN to ELF
  SUNA     =>   2 // Suna Seni, from HUMAN to ELF
  SWORD01  => 201 // Magical Sword, from GNOLL to SWORD
  SWORD02  => 201 // Ras, from GNOLL to SWORD
  SWORD03  => 201 // Sword, from GNOLL to SWORD
  THRAXI   =>   2 // Thraxis Gall, from HUMAN to ELF
  TOBBAN02 => 143 // Orc Archer, from ELF to ORC
  TOBPAR05 =>   4 // Delga Deepdelver, from HUMAN to DWARF
  TOLMAG02 =>   2 // Mage, from HUMAN to ELF
  TRANIM03 => 137 // Panther, from WOLF to CAT
  TREVIL01 =>   5 // Lord Khellon Menold, from HUMAN to HALFLING
  TROLDE01 => 129 // Desert Troll, from TROLL to TROLL
  TROLDE02 => 129 // Desert Troll, from TROLL to TROLL
  TROLFR01 => 129 // Freshwater Troll, from TROLL to TROLL
  TROLFR02 => 129 // Freshwater Troll, from TROLL to TROLL
  TROLGI01 => 129 // Giant Troll, from TROLL to TROLL
  TROLGI02 => 129 // Giant Troll, from TROLL to TROLL
  TROLIC01 => 129 // Ice Troll, from TROLL to TROLL
  TROLIC02 => 129 // Ice Troll, from TROLL to TROLL
  TROLIC03 => 129 // Blizzard Troll, from TROLL to TROLL
  TROLIC04 => 129 // Blizzard Troll, from TROLL to TROLL
  TROLL01  => 129 // Troll, from TROLL to TROLL
  TROLL02  => 129 // Troll, from TROLL to TROLL
  TROLL03  => 129 // Troll, from TROLL to TROLL
  TROLLENS => 129 // Giant Troll, from TROLL to TROLL
  TROLLSM2 => 129 // Troll, from TROLL to TROLL
  TROLSI01 => 129 // Spirit Troll, from TROLL to TROLL
  TROLSI02 => 129 // Spirit Troll, from TROLL to TROLL
  TROLSN01 => 129 // Snow Troll, from GHOUL to TROLL
  TROLSN02 => 129 // Snow Troll, from GHOUL to TROLL
  TROLSP01 => 129 // Spectral Troll, from TROLL to TROLL
  TROLSP02 => 129 // Spectral Troll, from TROLL to TROLL
  TROLUO01 => 129 // Troll, from TROLL to TROLL
  TTGIBB   => 109 // Gibberling, from HUMAN to GIBBERLING
  UDSVIR07 =>   6 // Svirfneblin, from DWARF to GNOME
  UDSVIR08 =>   6 // Svirfneblin Leader, from DWARF to GNOME
  VALRAN01 =>   2 // Ranger, from HUMAN to ELF
  WELLYN   =>   5 // Wellyn, from HUMAN to HALFLING
  YAGASPIR => 142 // Yaga-Shura, from DWARF to GIANT
  ZOMBIE01 => 148 // Zombie, from SKELETON to ZOMBIE
  ZOMBSE01 => 148 // Sea Zombie, from NO_RACE to ZOMBIE

END

ACTION_PHP_EACH cd_bulk_cre_changes_race AS cre_file => n_race BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x272 n_race
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_class BEGIN

  CHICKDEF => 255 // Rabid Chicken, from MAGE to NO_CLASS
  CHICKE   => 255 // Chicken, from MAGE to NO_CLASS
  CHICKER  => 255 // Rabid Chicken, from MAGE to NO_CLASS
  COW      => 255 // Cow, from MAGE to NO_CLASS
  COWH     => 255 // Cow, from MAGE to NO_CLASS
  DEADCOW1 => 255 // Cow, from MAGE to NO_CLASS
  DEADCOW2 => 255 // Cow, from MAGE to NO_CLASS
  DEVAEVIL =>   3 // Fallen Deva, from NO_CLASS to CLERIC
  DEVAGOOD =>   3 // Deva, from NO_CLASS to CLERIC
  DEVAST01 =>   3 // Astral Deva, from FIGHTER to CLERIC
  DEVMON01 =>   3 // Monadic Deva, from FIGHTER to CLERIC
  ELEMCHAN => 186 // Chan, from ELEMENTAL_EARTH to ELEMENTAL_AIR
  fampsd25 => 149 // ToB pseudo dragon familiar, from mage to wyvern (matching SoA version)
  famqua25 => 179 // ToB quasit familiar, from mage to imp (matching SoA version)
  GORJELF2 => 151 // Ooze Puddle, from MAGE to MUSTARD_JELLY
  GORJELFU => 151 // Ooze Puddle, from MAGE to MUSTARD_JELLY
  GORJELGR => 154 // Green Slime, from MAGE to GREEN_SLIME
  GORSTALK => 255 // Guardian of Air, from MAGE to NO_CLASS
  ICYUAN01 =>   2 // Yuan-Ti, from LONG_SWORD to FIGHTER
  ICYUAN02 =>   2 // Greater Yuan-Ti, from LONG_SWORD to FIGHTER
  ICYUAN03 =>   1 // Yuan-Ti mage, from LONG_SWORD to MAGE
  ICYUAN04 =>   2 // Yuan-Ti Elite, from LONG_SWORD to FIGHTER
  ICYUAN05 =>   1 // Yuan-Ti  Prince, from LONG_SWORD to MAGE
  JELGRA01 => 153 // Gray Ooze, from MAGE to GREY_OOZE
  JELGRE01 => 154 // Green Slime, from MAGE to GREEN_SLIME
  KPSHAM01 =>   1 // Yuan-Ti mage, from LONG_SWORD to MAGE
  KPYUAN01 =>   2 // Greater Yuan-Ti, from OGRE to FIGHTER
  MEPHSP1  => 255 // Mephit Portal, from MAGE to NO_CLASS
  MEPHSP2  => 255 // Mephit Portal, from MAGE to NO_CLASS
  MEPHSP3  => 255 // Mephit Portal, from MAGE to NO_CLASS
  MEPHSP4  => 255 // Mephit Portal, from MAGE to NO_CLASS
  NCHICK   => 255 // Chicken, from MAGE to NO_CLASS
  NCOW     => 255 // Cow, from MAGE to NO_CLASS
  NWYVBAB  => 149 // Baby Wyvern, from MAGE to WYVERN
  OTYUGH01 => 165 // Otyugh, from BASILISK_GREATER to OTYUGH
  PBHUNT03 =>   1 // Bounty Hunter, from LONG_SWORD to MAGE
  PBHUNT04 =>   1 // Bounty Hunter, from LONG_SWORD to MAGE
  PETTIN   => 180 // Ettin, from MAGE to GIANT
  PLANET01 =>   1 // Planetar, from TANARI to MAGE
  PLANEVIL =>   1 // Fallen Planetar, from NO_CLASS to MAGE
  PLANGOOD =>   1 // Planetar, from NO_CLASS to MAGE
  PLANWISH =>   1 // Fallen Planetar, from NO_CLASS to MAGE
  PLYSALA  => 187 // Fire Salamander, from TROLL to ELEMENTAL_FIRE
  PLYWYVRN => 149 // Baby Wyvern, from MAGE to WYVERN
  PUDDEN01 => 151 // Dense Pudding, from MAGE to MUSTARD_JELLY
  PUDDEN02 => 151 // Dense Pudding, from MAGE to MUSTARD_JELLY
  SAHZOMB  => 198 // Sea Zombie, from MAGE to ZOMBIE_NORMAL
  SENSTALK => 255 // Invisible Stalker, from MAGE to NO_CLASS
  STALKE   => 255 // Invisible Stalker, from MAGE to NO_CLASS
  TROLDE01 => 167 // Desert Troll, from TROLL to TROLL
  TROLDE02 => 167 // Desert Troll, from TROLL to TROLL
  TROLFR01 => 167 // Freshwater Troll, from TROLL to TROLL
  TROLFR02 => 167 // Freshwater Troll, from TROLL to TROLL
  TROLGI01 => 167 // Giant Troll, from TROLL to TROLL
  TROLGI02 => 167 // Giant Troll, from TROLL to TROLL
  TROLIC01 => 167 // Ice Troll, from TROLL to TROLL
  TROLIC02 => 167 // Ice Troll, from TROLL to TROLL
  TROLIC03 => 167 // Blizzard Troll, from TROLL to TROLL
  TROLIC04 => 167 // Blizzard Troll, from TROLL to TROLL
  TROLL01  => 167 // Troll, from TROLL to TROLL
  TROLL02  => 167 // Troll, from TROLL to TROLL
  TROLL03  => 167 // Troll, from TROLL to TROLL
  TROLLENS => 167 // Giant Troll, from TROLL to TROLL
  TROLLSM2 => 167 // Troll, from TROLL to TROLL
  TROLSI01 => 167 // Spirit Troll, from TROLL to TROLL
  TROLSI02 => 167 // Spirit Troll, from TROLL to TROLL
  TROLSN01 => 167 // Snow Troll, from GHOUL_GHAST to TROLL
  TROLSN02 => 167 // Snow Troll, from GHOUL_GHAST to TROLL
  TROLSP01 => 167 // Spectral Troll, from TROLL to TROLL
  TROLSP02 => 167 // Spectral Troll, from TROLL to TROLL
  TROLUO01 => 167 // Troll, from TROLL to TROLL
  WYVBAB01 => 149 // Baby Wyvern, from MAGE to WYVERN
  WYVERN01 => 149 // Wyvern, from MAGE to WYVERN
  WYVGRE01 => 149 // Greater Wyvern, from MAGE to WYVERN

END

ACTION_PHP_EACH cd_bulk_cre_changes_class AS cre_file => n_class BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x273 n_class
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_general BEGIN

  BODTAN   =>   4 // Tanova, from HUMANOID to UNDEAD
  BOUNHA04 => 255 // Bounty Hunter, from HUMANOID to MONSTER
  CEDELICH =>   4 // Deril Lich, from HUMANOID to UNDEAD
  D1GHOST  =>   4 // Alianna, from HUMANOID to UNDEAD
  D1GORI   =>   4 // Gorion, from HUMANOID to UNDEAD
  D1MOTHDW =>   4 // Alianna, from HUMANOID to UNDEAD
  D1MOTHEL =>   4 // Alianna, from HUMANOID to UNDEAD
  D1MOTHHA =>   4 // Alianna, from HUMANOID to UNDEAD
  D1MOTHHU =>   4 // Alianna, from HUMANOID to UNDEAD
  D1SKEL   =>   4 // Gorion, from HUMANOID to UNDEAD
  DAABOL   => 255 // Aboleth, from HUMANOID to MONSTER
  DASPITRO => 255 // Spirit Troll, from HUMANOID to MONSTER
  DGTROL01 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  DGTROL02 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  DRAGBLAC => 255 // Nizidramanii'yt, from ANIMAL to MONSTER
  DRAGSHAD => 255 // Thaxll'ssillyia, from ANIMAL to MONSTER
  DRUEAR01 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELAIRL01 =>   5 // Lesser Air Elemental, from MONSTER to GIANTHUMANOID
  ELAIRSU1 =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  ELAIRSU2 =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  ELAIRSU3 =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  ELAIRSUW =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  ELEARPR  =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARPR2 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARPR3 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARSU2 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARSU3 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARSU4 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELEARSUW =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  ELFIRL01 =>   5 // Lesser Fire Elemental, from MONSTER to GIANTHUMANOID
  ELFIRSUW =>   5 // Fire Elemental, from MONSTER to GIANTHUMANOID
  EYEEGL01 =>   2 // Eagle Eye, from HUMANOID to ANIMAL
  FINSOL04 => 255 // Fallen Solar, from GIANTHUMANOID to MONSTER
  GENIE02  =>   5 // Genie, from MONSTER to GIANTHUMANOID
  GIAFIR01 =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID
  GLBEAR   => 255 // Troll, from ANIMAL to MONSTER
  GORAIR01 =>   5 // Lesser Air Elemental, from MONSTER to GIANTHUMANOID
  GORMIM01 => 255 // Killer Mimic, from ANIMAL to MONSTER
  GORMIM02 => 255 // Killer Mimic, from ANIMAL to MONSTER
  GORMIM03 => 255 // Killer Mimic, from ANIMAL to MONSTER
  GORMIM04 => 255 // Killer Mimic, from ANIMAL to MONSTER
  GORSTA12 =>   5 // Statue, from UNDEAD to GIANTHUMANOID
  HAKSU    => 255 // Hakeashar, from GIANTHUMANOID to MONSTER
  HDRAGRED => 255 // Dragon, from ANIMAL to MONSTER
  HDRAGSIL => 255 // Dragon, from ANIMAL to MONSTER
  HLSKULL  =>   4 // Golden Skull, from HUMANOID to UNDEAD
  ICSALCOL => 255 // Ice Salamander, from UNDEAD to MONSTER
  ICSALFIR => 255 // Salamander, from UNDEAD to MONSTER
  IGOLFLE3 =>   5 // Lesser Clay Golem, from HUMANOID to GIANTHUMANOID
  JUGMIM01 => 255 // Killer Mimic, from ANIMAL to MONSTER
  KCHILD1  =>   1 // Peasant, from GIANTHUMANOID to HUMANOID
  KCHILD2  =>   1 // Peasant, from GIANTHUMANOID to HUMANOID
  KPTROL01 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  KPTROL02 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  KPTROL03 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  KPTROL04 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  KPTROL05 =>   5 // Troll, from UNDEAD to GIANTHUMANOID
  KPTROL06 =>   5 // Ice Troll, from MONSTER to GIANTHUMANOID
  KUOARC20 =>   1 // Kuo-Toa Archer, from MONSTER to HUMANOID
  KUOCLE20 =>   1 // Kuo-Toa Captain, from MONSTER to HUMANOID
  LAUNE    =>   4 // Meredath, from HUMANOID to UNDEAD
  MDEARTH  =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  MDEARTH2 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  MEKEAR01 =>   5 // Lesser Earth Elemental, from MONSTER to GIANTHUMANOID
  MEPHSP1  => 255 // Mephit Portal, from HUMANOID to MONSTER
  MEPHSP2  => 255 // Mephit Portal, from HUMANOID to MONSTER
  MEPHSP3  => 255 // Mephit Portal, from HUMANOID to MONSTER
  MEPHSP4  => 255 // Mephit Portal, from HUMANOID to MONSTER
  MIMIC01  => 255 // Killer Mimic, from ANIMAL to MONSTER
  NISHRUSU => 255 // Nishruu, from GIANTHUMANOID to MONSTER
  OBSDEM04 => 255 // Maurezhi, from GIANTHUMANOID to MONSTER
  OBSGOL02 =>   5 // Deactivated Guardian Golem, from MONSTER to GIANTHUMANOID
  PLANEVIL => 255 // Fallen Planetar, from GIANTHUMANOID to MONSTER
  PLANGOOD =>   5 // Planetar, from MONSTER to GIANTHUMANOID
  PLANWISH => 255 // Fallen Planetar, from GIANTHUMANOID to MONSTER
  PLSHFG01 =>   5 // Ettin, from MONSTER to GIANTHUMANOID
  PLSHFG02 =>   5 // Ettin, from MONSTER to GIANTHUMANOID
  RIFTCR04 => 255 // Empathic Manifestation, from HUMANOID to MONSTER
  RSPIRIT1 =>   4 // Mairyn, from HUMANOID to UNDEAD
  SAHBEH02 =>   4 // Sziltar's Apparition, from HUMANOID to UNDEAD
  SAHCHF01 =>   1 // Sahuagin Chieftain, from MONSTER to HUMANOID
  SAHOTY01 =>   5 // Ettin, from MONSTER to GIANTHUMANOID
  SARFAKI2 =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID
  SARFAKIE =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID
  SDSHADFI =>   4 // Shadow Fiend, from HUMANOID to UNDEAD
  SDSHADOW =>   4 // Shadow, from HUMANOID to UNDEAD
  SHAGRL01 =>   1 // Amuana, from UNDEAD to HUMANOID
  SKELDED  =>   4 // <Invalid Strref -1>, from HUMANOID to UNDEAD
  STATUE01 => 101 // Magical Sword, from HUMANOID to WEAPON
  STATUE02 => 101 // Magical Sword, from HUMANOID to WEAPON
  STATUE03 => 101 // Magical Sword, from HUMANOID to WEAPON
  STATUE04 => 101 // Magical Sword, from HUMANOID to WEAPON
  STATUE05 => 101 // Magical Sword, from HUMANOID to WEAPON
  STATUE06 => 101 // Magical Sword, from HUMANOID to WEAPON
  SUELEW2  =>   5 // Greater Fire Elemental, from MONSTER to GIANTHUMANOID
  SWAAIR01 =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  SWAAIR02 =>   5 // Air Elemental, from MONSTER to GIANTHUMANOID
  SWAEAR01 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  SWAEAR02 =>   5 // Earth Elemental, from MONSTER to GIANTHUMANOID
  SWAFIR01 =>   5 // Fire Elemental, from GIANTHUMANOID to GIANTHUMANOID
  SWORD01  => 101 // Magical Sword, from HUMANOID to WEAPON
  SWORD02  => 101 // Ras, from HUMANOID to WEAPON
  SWORD03  => 101 // Sword, from HUMANOID to WEAPON
  TORGAL   =>   5 // TorGal, from MONSTER to GIANTHUMANOID
  TOWNC01  =>   1 // Town Crier, from GENERAL_ITEM to HUMANOID
  TROLDE01 =>   5 // Desert Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLDE02 =>   5 // Desert Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLFR01 =>   5 // Freshwater Troll, from MONSTER to GIANTHUMANOID
  TROLFR02 =>   5 // Freshwater Troll, from MONSTER to GIANTHUMANOID
  TROLGI01 =>   5 // Giant Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLGI02 =>   5 // Giant Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLIC01 =>   5 // Ice Troll, from MONSTER to GIANTHUMANOID
  TROLIC02 =>   5 // Ice Troll, from MONSTER to GIANTHUMANOID
  TROLIC03 =>   5 // Blizzard Troll, from MONSTER to GIANTHUMANOID
  TROLIC04 =>   5 // Blizzard Troll, from MONSTER to GIANTHUMANOID
  TROLL01  =>   5 // Troll, from MONSTER to GIANTHUMANOID
  TROLL02  =>   5 // Troll, from MONSTER to GIANTHUMANOID
  TROLL03  =>   5 // Troll, from MONSTER to GIANTHUMANOID
  TROLLENS =>   5 // Giant Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLLSM2 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  TROLSI01 =>   5 // Spirit Troll, from HUMANOID to GIANTHUMANOID
  TROLSI02 =>   5 // Spirit Troll, from HUMANOID to GIANTHUMANOID
  TROLSN01 =>   5 // Snow Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLSN02 =>   5 // Snow Troll, from GIANTHUMANOID to GIANTHUMANOID
  TROLSP01 =>   5 // Spectral Troll, from MONSTER to GIANTHUMANOID
  TROLSP02 =>   5 // Spectral Troll, from MONSTER to GIANTHUMANOID
  TROLUO01 =>   5 // Troll, from MONSTER to GIANTHUMANOID
  UHOGRE03 =>   5 // Minotaur, from MONSTER to GIANTHUMANOID
  WELLYN   =>   4 // Wellyn, from HUMANOID to UNDEAD
  YSCARA03 =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID
  YSGUAR01 =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID
  YSSOLD10 =>   5 // Fire Giant, from HUMANOID to GIANTHUMANOID

END

ACTION_PHP_EACH cd_bulk_cre_changes_general AS cre_file => n_general BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x271 n_general
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_sex BEGIN

  AMFEM01  =>   2 // Commoner, from MALE to FEMALE
  AMFEM02  =>   2 // Commoner, from MALE to FEMALE
  AMGIRL01 =>   2 // Girl, from MALE to FEMALE
  AMMONK08 =>   2 // Monk, from MALE to FEMALE
  AR18ARCH =>   2 // Archer, from MALE to FEMALE
  BAZLIZ01 =>   1 // Lizard Man Shaman, from FEMALE to MALE
  BAZLIZ02 =>   1 // Lizard Man Shaman, from FEMALE to MALE
  C6CATTI  =>   2 // Catti Brie, from FEMALE to FEMALE
  C6CATTI2 =>   2 // Catti Brie, from FEMALE to FEMALE
  C6CLER3  =>   2 // Cleric Of Talos, from MALE to FEMALE
  C6DEL    =>   2 // Del, from MALE to FEMALE
  C6GOFUS  =>   1 // Gofus, from FEMALE to MALE
  C6GUEN   =>   2 // Guenhwyvar, from MALE to FEMALE
  C6GUEN2  =>   2 // Guenhwyvar, from MALE to FEMALE
  C6KACH   =>   1 // Kachiko, from FEMALE to MALE
  CEFALD02 =>   2 // Faldorn, from MALE to FEMALE
  CHICKDEF =>   2 // Rabid Chicken, from MALE to FEMALE
  CHICKE   =>   2 // Chicken, from MALE to FEMALE
  CHICKER  =>   2 // Rabid Chicken, from MALE to FEMALE
  COW      =>   2 // Cow, from MALE to FEMALE
  COWENF1  =>   1 // Cowled Enforcer, from FEMALE to MALE
  CSCLERIC =>   1 // Dawnmaster Kreel, from FEMALE to MALE
  CSHECK3  =>   2 // Heckler, from MALE to FEMALE
  CSSUPP2  =>   2 // Peasant, from MALE to FEMALE
  DEADCOW1 =>   2 // Cow, from MALE to FEMALE
  DEADCOW2 =>   2 // Cow, from MALE to FEMALE
  DEER01   =>   2 // Deer, from MALE to FEMALE
  DERRICK  =>   1 // Derrick, from FEMALE to MALE
  DEVAEVIL =>   2 // Fallen Deva, from MALE to FEMALE
  DEVAST01 =>   2 // Astral Deva, from MALE to FEMALE
  DEVMON01 =>   2 // Monadic Deva, from MALE to FEMALE
  DPFEMALE =>   2 // Peasant, from MALE to FEMALE
  DPMON01  =>   1 // Mind Flayer, from FEMALE to MALE
  DPSTAT05 =>   2 // Petrified Woman, from MALE to FEMALE
  DROFOD02 =>   2 // Drow, from MALE to FEMALE
  DROFOD03 =>   1 // Drow, from FEMALE to MALE
  DROFOD04 =>   2 // Drow Priestess, from MALE to FEMALE
  DROW04   =>   2 // Drow Priestess, from MALE to FEMALE
  DSBODY01 =>   2 // Shadow Thief, from MALE to FEMALE
  ESCORT3  =>   1 // Jenthan, from FEMALE to MALE
  FANGEL01 =>   2 // Einhiris, from MALE to FEMALE
  FINSOL01 =>   2 // Solar, from MALE to FEMALE
  GARKID01 =>   2 // Iltha, from MALE to FEMALE
  GEMCH01  =>   2 // Chicken, from MALE to FEMALE
  GEMCH02  =>   2 // Chicken, from MALE to FEMALE
  GORSTA05 =>   2 // Statue, from MALE to FEMALE
  HLSHYR   =>   2 // Shyressa, from MALE to FEMALE
  HSPECTR1 =>   2 // Spectral Harpist, from MALE to FEMALE
  JADE1    =>   2 // Lennah, from MALE to FEMALE
  JAHEI1   =>   2 // Harper, from MALE to FEMALE
  JAREV1   =>   2 // Harper, from MALE to FEMALE
  KAYSMG01 =>   1 // Rindus, from FEMALE to MALE
  KPSOLD10 =>   1 // de'Arnise Guard, from FEMALE to MALE
  KUOARC20 =>   1 // Kuo-Toa Archer, from FEMALE to MALE
  MGKHOL01 =>   2 // Khollynnus Paac, from MALE to FEMALE
  NCHICK   =>   2 // Chicken, from MALE to FEMALE
  NCOW     =>   2 // Cow, from MALE to FEMALE
  NYMPHSU  =>   2 // Nymph, from MALE to FEMALE
  PALERN   =>   1 // Palern Flynn, from FEMALE to MALE
  PALKNI02 =>   1 // Ogre, from FEMALE to MALE
  PALKNI04 =>   1 // Ogre, from FEMALE to MALE
  PALKNI05 =>   1 // Baby Wyvern, from FEMALE to MALE
  PCAPT02  =>   1 // Prison Captain, from FEMALE to MALE
  PIRFSH02 =>   2 // Fishwife, from MALE to FEMALE
  PIRSAL02 =>   2 // Sailor, from MALE to FEMALE
  PLANET01 =>   2 // Planetar, from MALE to FEMALE
  PLANEVIL =>   2 // Fallen Planetar, from MALE to FEMALE
  PLANGOOD =>   2 // Planetar, from MALE to FEMALE
  PLANWISH =>   2 // Fallen Planetar, from MALE to FEMALE
  PPMAG01  =>   2 // Pirate Mage, from MALE to FEMALE
  SARBHA01 =>   1 // Alexander Ralisar, from FEMALE to MALE
  SENDAI7  =>   2 // Sendai, from FEMALE to FEMALE
  SLEEPFH  =>   2 // Sleeping Woman, from MALE to FEMALE
  SLVIC02  =>   1 // Tourist, from FEMALE to MALE
  SOLAR    =>   2 // Solar, from MALE to FEMALE
  SOLAR01  =>   2 // Solar, from MALE to FEMALE
  STATUE01 =>   3 // Magical Sword, from MALE to OTHER
  STATUE02 =>   3 // Magical Sword, from MALE to OTHER
  STATUE03 =>   3 // Magical Sword, from MALE to OTHER
  STATUE04 =>   3 // Magical Sword, from MALE to OTHER
  STATUE05 =>   3 // Magical Sword, from MALE to OTHER
  STATUE06 =>   3 // Magical Sword, from MALE to OTHER
  SUENDEL2 =>   2 // Elven Warrior, from MALE to FEMALE
  SUENDEL4 =>   2 // Elf Female, from MALE to FEMALE
  SUENDEL6 =>   2 // Elf, from MALE to FEMALE
  SUENDEL8 =>   2 // Elven Warrior, from MALE to FEMALE
  SWORD01  =>   3 // Magical Sword, from MALE to OTHER
  SWORD02  =>   3 // Ras, from MALE to OTHER
  SWORD03  =>   3 // Sword, from MALE to OTHER
  TOBPAR02 =>   2 // Berena Elkan, from MALE to FEMALE
  TOBPAR04 =>   1 // Karun the Black, from FEMALE to MALE
  TRFTOW04 =>   2 // Peasant, from MALE to FEMALE
  TRFUED02 =>   1 // Lurraxol Guard, from FEMALE to MALE
  TRSKIN01 =>   2 // Raissa, from MALE to FEMALE
  UDDOOR06 =>   2 // Drow Priestess, from MALE to FEMALE
  UDDROW35 =>   2 // Priestess of Lolth, from MALE to FEMALE
  VAMPAER  =>   2 // Aerie, from MALE to FEMALE
  VAMPJAH  =>   2 // Jaheira, from MALE to FEMALE
  VAMPVIC  =>   2 // Viconia, from MALE to FEMALE
  VVMADMAN =>   2 // Shadow Thief, from MALE to FEMALE
  WINNKEEP =>   1 // Pugney, from FEMALE to MALE
  WPWENCH1 =>   2 // Tavern Wench, from MALE to FEMALE
  YARMY01  =>   2 // Fighter, from MALE to FEMALE
  YARMY03  =>   1 // Mage, from FEMALE to MALE
  YSMAGE02 =>   2 // Yaga-Shura Mage, from MALE to FEMALE

END

ACTION_PHP_EACH cd_bulk_cre_changes_sex AS cre_file => n_sex BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x237 n_sex
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_gender BEGIN

  ABISRED1 =>   4 // Abishai, from MALE to NIETHER
  ABYDEM01 =>   4 // Tanar'ri, from MALE to NIETHER
  AMMONK08 =>   2 // Monk, from MALE to FEMALE
  AR18ARCH =>   2 // Archer, from MALE to FEMALE
  AR18SKEL =>   4 // Skeleton Warrior, from MALE to NIETHER
  BAZLIZ01 =>   1 // Lizard Man Shaman, from FEMALE to MALE
  BAZLIZ02 =>   1 // Lizard Man Shaman, from FEMALE to MALE
  BDOCK1   =>   1 // Dockhand Ogre, from NIETHER to MALE
  BEARBLSU =>   6 // Black Bear, from MALE to SUMMONED
  BEARGRSU =>   6 // Grizzly Bear, from MALE to SUMMONED
  BEHHIV01 =>   4 // Hive Mother, from FEMALE to NIETHER
  BHGHOUL2 =>   4 // Ghast, from MALE to NIETHER
  BHGHOUL4 =>   4 // Ghast, from MALE to NIETHER
  C6CATTI  =>   2 // Catti-Brie, from MALE to FEMALE
  C6CATTI2 =>   2 // Catti-Brie, from MALE to FEMALE
  C6CLER3  =>   2 // Cleric Of Talos, from MALE to FEMALE
  C6DEL    =>   2 // Del, from MALE to FEMALE
  C6GOFUS  =>   1 // Gofus, from FEMALE to MALE
  C6GUEN2  =>   2 // Guenhwyvar, from MALE to FEMALE
  C6KACH   =>   1 // Kachiko, from FEMALE to MALE
  CEFALD02 =>   2 // Faldorn, from MALE to FEMALE
  CHICKDEF =>   2 // Rabid Chicken, from MALE to FEMALE
  CHICKE   =>   2 // Chicken, from MALE to FEMALE
  CHICKER  =>   2 // Rabid Chicken, from MALE to FEMALE
  COPLION  =>   2 // Tabitha, from MALE to FEMALE
  COW      =>   2 // Cow, from MALE to FEMALE
  COWENF1  =>   1 // Cowled Enforcer, from FEMALE to MALE
  CSHECK1  =>   2 // Heckler, from MALE to FEMALE
  CSHECK3  =>   2 // Heckler, from MALE to FEMALE
  CSSUPP2  =>   2 // Peasant, from MALE to FEMALE
  DACEMIST =>   4 // Gaseous Form, from MALE to NIETHER
  DADROW10 =>   2 // Drow Torturer, from MALE to FEMALE
  DADROW15 =>   1 // Drow, from FEMALE to MALE
  DADROW2  =>   1 // Drow Servant, from FEMALE to MALE
  DADROW23 =>   2 // Drow, from MALE to FEMALE
  DADROW8  =>   1 // Drow Torturer, from FEMALE to MALE
  DADROW9  =>   1 // Drow Torturer, from FEMALE to MALE
  DAGHAUN1 =>   2 // Ghaunadaur Priest Leader, from MALE to FEMALE
  DAGHAUN2 =>   2 // Ghaunadaur Priest, from MALE to FEMALE
  DAGNOLL  =>   1 // Gnoll, from SUMMONED to MALE
  DAUMBER  =>   1 // Umber Hulk, from SUMMONED to MALE
  DEADCOW1 =>   2 // Cow, from MALE to FEMALE
  DEADCOW2 =>   2 // Cow, from MALE to FEMALE
  DEADDEM1 =>   4 // Cornugon, from MALE to NIETHER
  DEATHKNI =>   4 // Demon Knight, from MALE to NIETHER
  DECK615  =>   4 // Demon Knight, from MALE to NIETHER
  DEER01   =>   2 // Deer, from MALE to FEMALE
  DELMIST  =>   4 // Gaseous Form, from MALE to NIETHER
  DEMABI01 =>   4 // Abishai, from MALE to NIETHER
  DEMCOR01 =>   4 // Cornugon, from MALE to NIETHER
  DEMGLA01 =>   4 // Glabrezu, from MALE to NIETHER
  DEMILICH =>   4 // Demi-Lich, from MALE to NIETHER
  DEMNAB02 =>   4 // Nabassu, from SUMMONED_DEMON to NIETHER
  DEMOSUM1 =>   4 // Marilith, from FEMALE to NIETHER
  DEMOSUM2 =>   4 // Balor, from MALE to NIETHER
  DEMOSUM3 =>   4 // Glabrezu, from MALE to NIETHER
  DEMPIT01 =>   4 // Pit Fiend, from MALE to NIETHER
  DERRICK  =>   1 // Derrick, from FEMALE to MALE
  DEVAST01 =>   2 // Astral Deva, from NIETHER to FEMALE
  DEVMON01 =>   2 // Monadic Deva, from NIETHER to FEMALE
  DGLAB01  =>   4 // Glabrezu, from MALE to NIETHER
  DGTROL01 =>   1 // Troll, from NIETHER to MALE
  DGTROL02 =>   1 // Troll, from NIETHER to MALE
  DORKUS   =>   1 // Dorkus, from 0 to MALE
  DPDEM01  =>   4 // Demon, from FEMALE to NIETHER
  DPFEMALE =>   2 // Peasant, from MALE to FEMALE
  DPMON01  =>   1 // Mind Flayer, from FEMALE to MALE
  DPMON02  =>   4 // Lich, from MALE to NIETHER
  DPSTAT05 =>   2 // Petrified Woman, from MALE to FEMALE
  DROFOD02 =>   1 // Drow, from NIETHER to MALE
  DROFOD03 =>   1 // Drow, from FEMALE to MALE
  DROFOD04 =>   2 // Drow Priestess, from MALE to FEMALE
  DROW04   =>   2 // Drow Priestess, from MALE to FEMALE
  DSBODY01 =>   2 // Shadow Thief, from MALE to FEMALE
  ELEARB01 =>   4 // Apparition, from MALE to NIETHER
  ELEARB02 =>   4 // Apparition, from MALE to NIETHER
  ELEARB03 =>   4 // Apparition, from MALE to NIETHER
  ELEARB04 =>   4 // Apparition, from MALE to NIETHER
  ELEARB05 =>   4 // Apparition, from MALE to NIETHER
  ELEARB06 =>   4 // Apparition, from MALE to NIETHER
  ELEARB07 =>   4 // Apparition, from MALE to NIETHER
  ELEARB08 =>   4 // Apparition, from MALE to NIETHER
  ELEARB09 =>   4 // Apparition, from MALE to NIETHER
  ELEARB10 =>   4 // Apparition, from MALE to NIETHER
  ELEARB11 =>   4 // Apparition, from MALE to NIETHER
  ELEPUZ01 =>   4 // Apparition, from MALE to NIETHER
  ELEPUZ02 =>   4 // Apparition, from MALE to NIETHER
  ELEPUZ03 =>   4 // Apparition, from FEMALE to NIETHER
  ELEPUZ04 =>   4 // Apparition, from FEMALE to NIETHER
  ESCORT3  =>   1 // Jenthan, from FEMALE to MALE
  ETTERCSU =>   6 // Ettercap, from NIETHER to SUMMONED
  FANGEL01 =>   2 // Einhiris, from NIETHER to FEMALE
  FINSOL01 =>   2 // Solar, from NIETHER to FEMALE
  FIRLCH01 =>   4 // Fire Lich, from MALE to NIETHER
  FIRWRA01 =>   4 // Greater Wraith, from MALE to NIETHER
  FSSKEL   =>   4 // Skeleton, from MALE to NIETHER
  GEMCH01  =>   2 // Chicken, from MALE to FEMALE
  GEMCH02  =>   2 // Chicken, from MALE to FEMALE
  GHOGR01  =>   4 // Greater Ghoul, from MALE to NIETHER
  GITH05   =>   1 // Warlock, from FEMALE to MALE
  GITH06   =>   1 // Captain, from FEMALE to MALE
  GLBEAR   =>   4 // Troll, from MALE to NIETHER
  GORAPR   =>   1 // Apprentice, from FEMALE to MALE
  GORBAT1  =>   4 // Ka'rashur, from MALE to NIETHER
  GORBAT3  =>   4 // Cornugon, from MALE to NIETHER
  GORCAMB  =>   4 // Aesgareth, from MALE to NIETHER
  GORCHR   =>   4 // Chromatic Demon, from MALE to NIETHER
  GORGUA04 =>   1 // Guardian Spirit, from FEMALE to MALE
  GORGUA05 =>   1 // Guardian Spirit, from FEMALE to MALE
  GORMISTP =>   4 // Poison Mist, from SUMMONED to NIETHER
  GORSTA05 =>   2 // Statue, from MALE to FEMALE
  GORTAN1  =>   4 // Tahazzar, from MALE to NIETHER
  GORTAN4  =>   4 // Glabrezu, from MALE to NIETHER
  GORWOM05 =>   4 // Hive Mother, from FEMALE to NIETHER
  GRSKEL1  =>   4 // Skeleton Warrior, from MALE to NIETHER
  GRSKEL2  =>   4 // Skeleton Warrior, from MALE to NIETHER
  HELLFEAR =>   4 // Fear, from MALE to NIETHER
  HELLGREE =>   4 // Greed, from MALE to NIETHER
  HELLPRID =>   4 // Pride, from MALE to NIETHER
  HELLSELF =>   4 // Selfishness, from MALE to NIETHER
  HGMIS02  =>   4 // Swamp Horror, from MALE to NIETHER
  HGSKL01  =>   4 // Skeleton Cleric, from MALE to NIETHER
  HGSKL02  =>   4 // Skeleton Assassin, from MALE to NIETHER
  HGSKL03  =>   4 // Skeleton Mage, from FEMALE to NIETHER
  HGWRA01  =>   4 // Gorion, from MALE to NIETHER
  HLDEMI   =>   4 // Kangaxx the Demi-Lich, from MALE to NIETHER
  HLMAGE   =>   1 // Mage, from NIETHER to MALE
  HLSHYR   =>   2 // Shyressa, from MALE to FEMALE
  HLSKULL  =>   4 // Golden Skull, from MALE to NIETHER
  HSPECTR1 =>   2 // Spectral Harpist, from MALE to FEMALE
  ICFUNG02 =>   4 // Spore Colony, from MALE to NIETHER
  ICFUNGUS =>   4 // Spore Colony, from MALE to NIETHER
  ICMYC01  =>   4 // Myconid, from MALE to NIETHER
  ICMYC02  =>   4 // Myconid King, from MALE to NIETHER
  IGOLFLE3 =>   4 // Lesser Clay Golem, from MALE to NIETHER
  IGOLFLE4 =>   4 // Lesser Clay Golem, from MALE to NIETHER
  JADE1    =>   2 // Lennah, from MALE to FEMALE
  JAHEI1   =>   2 // Harper, from MALE to FEMALE
  JAMERONI =>   2 // Meronia, from MALE to FEMALE
  JAREV1   =>   2 // Harper, from MALE to FEMALE
  JONDAL   =>   1 // Jondalar, from 0 to MALE
  KAYSMG01 =>   1 // Rindus, from FEMALE to MALE
  KCHILD1  =>   1 // Peasant, from NIETHER to MALE
  KCHILD2  =>   1 // Peasant, from NIETHER to MALE
  KELZOMB  =>   4 // Zombie, from MALE to NIETHER
  KPROEN03 =>   1 // Roenall Guard, from NIETHER to MALE
  KPSOLD10 =>   1 // de'Arnise Guard, from FEMALE to MALE
  KPTROL01 =>   1 // Troll, from NIETHER to MALE
  KPTROL02 =>   1 // Troll, from NIETHER to MALE
  KPTROL03 =>   1 // Troll, from NIETHER to MALE
  KPTROL04 =>   1 // Troll, from NIETHER to MALE
  KPTROL05 =>   1 // Troll, from NIETHER to MALE
  KPTROL06 =>   1 // Ice Troll, from NIETHER to MALE
  KRUIN    =>   1 // Kruin, from FEMALE to MALE
  KSHADOW  =>   4 // Shadow, from MALE to NIETHER
  KUOARC20 =>   1 // Kuo-Toa Archer, from FEMALE to MALE
  LAVOK02  =>   1 // Lavok, from NIETHER to MALE
  LESTER   =>   4 // Uncle Lester, from MALE to NIETHER
  MGKHOL01 =>   2 // Khollynnus Paac, from MALE to FEMALE
  MISTHO01 =>   4 // Mist Horror, from MALE to NIETHER
  MISTWA01 =>   4 // Wandering Horror, from MALE to NIETHER
  NCHICK   =>   2 // Chicken, from MALE to FEMALE
  NCOW     =>   2 // Cow, from MALE to FEMALE
  NEVM2    =>   4 // Skeleton Warrior, from MALE to NIETHER
  OBSDEM01 =>   4 // Lea'liyl, from MALE to NIETHER
  OGRE01   =>   1 // Ogre, from NIETHER to MALE
  OGRHAL01 =>   1 // Half Ogre, from NIETHER to MALE
  OGRILL01 =>   1 // Ogrillon, from NIETHER to MALE
  PALERN   =>   1 // Palern Flynn, from FEMALE to MALE
  PALKNI02 =>   1 // Ogre, from FEMALE to MALE
  PALKNI03 =>   1 // Gnoll Elite, from FEMALE to MALE
  PALKNI04 =>   1 // Ogre, from FEMALE to MALE
  PALKNI05 =>   1 // Baby Wyvern, from FEMALE to MALE
  PCAPT02  =>   1 // Prison Captain, from FEMALE to MALE
  PETTIN   =>   1 // Ettin, from 0 to MALE
  PIRFSH02 =>   2 // Fishwife, from MALE to FEMALE
  PIRSAL02 =>   2 // Sailor, from MALE to FEMALE
  PLANET01 =>   2 // Planetar, from NIETHER to FEMALE
  PLANWISH =>   2 // Fallen Planetar, from NIETHER to FEMALE
  PLYOGRE  =>   1 // Ogre, from 0 to MALE
  PLYSALA  =>   1 // Fire Salamander, from NIETHER to MALE
  PMASTER  =>   4 // Master of Thralls, from MALE to NIETHER
  PPBODHI3 =>   2 // Bodhi, from MALE to FEMALE
  PPDEMON  =>   4 // Demon, from MALE to NIETHER
  PPMAG01  =>   2 // Pirate Mage, from MALE to FEMALE
  PPNAB01  =>   4 // Nabassu, from MALE to NIETHER
  PPNAB02  =>   4 // Nabassu, from MALE to NIETHER
  PPNAB03  =>   4 // Nabassu, from MALE to NIETHER
  PPVALEN  =>   2 // Valen, from MALE to FEMALE
  PWARDEN  =>   4 // Warden, from MALE to NIETHER
  REMAGE01 =>   2 // Slaver Wizard, from MALE to FEMALE
  RETHUG02 =>   1 // Mage, from FEMALE to MALE
  RIFTG03  =>   1 // Avatar, from NIETHER to MALE
  RNGSHA   =>   4 // Shadow, from MALE to NIETHER
  RNGSHA01 =>   4 // Shadow, from MALE to NIETHER
  RNGSHA02 =>   4 // Shadow Patrick, from MALE to NIETHER
  RNGSHA03 =>   4 // Shadow, from MALE to NIETHER
  RNGSHA04 =>   4 // Shadow Jailor, from MALE to NIETHER
  RNGSHA2D =>   4 // Shadow Patrick, from MALE to NIETHER
  RSKEL01  =>   4 // Skeleton, from MALE to NIETHER
  RSKEL03  =>   4 // Skeleton, from MALE to NIETHER
  RUMAR02  =>   4 // Nabassu, from MALE to NIETHER
  SAHBEH03 =>   1 // Sziltar's Apparition, from NIETHER to MALE
  SAHOTY01 =>   1 // Ettin, from NIETHER to MALE
  SARBHA01 =>   1 // Alexander Ralisar, from FEMALE to MALE
  SDSHADFI =>   4 // Shadow Fiend, from MALE to NIETHER
  SDSHADOW =>   4 // Shadow, from MALE to NIETHER
  SENDAI7  =>   2 // Sendai, from MALE to FEMALE
  SENDARK  =>   4 // Dark Master, from MALE to NIETHER
  SEWERM   =>   1 // Quallo, from FEMALE to MALE
  SEWSHA01 =>   4 // Shadow Fiend, from MALE to NIETHER
  SEWSHA02 =>   4 // Shadow Fiend, from MALE to NIETHER
  SEWSHA03 =>   4 // Shadow, from MALE to NIETHER
  SHADEL   =>   4 // Shade Lord, from FEMALE to NIETHER
  SHADELD  =>   4 // Shade Lord, from FEMALE to NIETHER
  SHADFI01 =>   4 // Shadow Fiend, from MALE to NIETHER
  SHADFI02 =>   4 // Devil Shade, from MALE to NIETHER
  SHADOW01 =>   4 // Shadow, from MALE to NIETHER
  SHAPE    =>   4 // Frennedan, from MALE to NIETHER
  SHAWOL01 =>   4 // Shade Wolf, from MALE to NIETHER
  SHAWOL02 =>   4 // Greater Shade Wolf, from MALE to NIETHER
  SHTH03   =>   1 // Kretor, from FEMALE to MALE
  SKELAR01 =>   4 // Skeleton Archer, from MALE to NIETHER
  SKELAR02 =>   4 // Skeleton Archer, from MALE to NIETHER
  SKELE2   =>   4 // Skeleton, from MALE to NIETHER
  SKELET01 =>   4 // Skeleton, from MALE to NIETHER
  SKELHP1  =>   4 // Skeleton, from MALE to NIETHER
  SLEEPFH  =>   2 // Sleeping Woman, from MALE to FEMALE
  SLIFIS01 =>   4 // Fission Slime, from MALE to NIETHER
  SLIFIS02 =>   4 // Fission Slime, from MALE to NIETHER
  SLVIC02  =>   1 // Tourist, from FEMALE to MALE
  SUENDEL2 =>   2 // Elven Warrior, from MALE to FEMALE
  SUENDEL4 =>   2 // Elf Female, from MALE to FEMALE
  SUENDEL6 =>   2 // Elf, from MALE to FEMALE
  SUENDEL8 =>   2 // Elven Warrior, from MALE to FEMALE
  SUMDJINN =>   6 // Djinni, from NIETHER to SUMMONED
  SWORD02  =>   6 // Ras, from ILLUSIONARY to SUMMONED
  SWORD03  =>   4 // Sword, from BOTH to NIETHER
  TANARI01 =>   4 // Tanar'ri, from MALE to NIETHER
  TANOMIST =>   4 // Gaseous Form, from MALE to NIETHER
  TELBAL1  =>   4 // Balor, from MALE to NIETHER
  TELCAM1  =>   4 // Cambion, from SUMMONED_DEMON to NIETHER
  TELCOR1  =>   4 // Cornugon, from MALE to NIETHER
  TELPIT1  =>   4 // Pit Fiend, from MALE to NIETHER
  TELPIT2  =>   4 // Pit Fiend, from MALE to NIETHER
  TELSLAV  =>   4 // Slave Wraith, from MALE to NIETHER
  TELTAN1  =>   4 // Glabrezu, from MALE to NIETHER
  TELTAN2  =>   4 // Glabrezu, from MALE to NIETHER
  TELWRAI  =>   4 // Demon Wraith, from MALE to NIETHER
  TOBBAN01 =>   1 // Sharik Nefal, from NIETHER to MALE
  TOBPAR02 =>   2 // Berena Elkan, from MALE to FEMALE
  TOBPAR04 =>   1 // Karun the Black, from FEMALE to MALE
  TOLGER   =>   1 // Tolgerias, from NIETHER to MALE
  TOLGER2  =>   1 // Tolgerias, from NIETHER to MALE
  TOWNC01  =>   1 // Town Crier, from 0 to MALE
  TRCUT06  =>   1 // Lord Skarmaen Alibakkar, from NIETHER to MALE
  TRFTOW04 =>   2 // Peasant, from MALE to FEMALE
  TRFUED02 =>   1 // Lurraxol Guard, from FEMALE to MALE
  TRFUED05 =>   1 // Lord Skarmaen Alibakkar, from NIETHER to MALE
  TROLDE01 =>   1 // Desert Troll, from NIETHER to MALE
  TROLDE02 =>   1 // Desert Troll, from NIETHER to MALE
  TROLFR01 =>   1 // Freshwater Troll, from NIETHER to MALE
  TROLFR02 =>   1 //   I much prefer the wooded regions, but one must be flexible in these times., from NIETHER to MALE
  TROLGI01 =>   1 // Giant Troll, from NIETHER to MALE
  TROLGI02 =>   1 // Giant Troll, from NIETHER to MALE
  TROLIC01 =>   1 // Ice Troll, from NIETHER to MALE
  TROLIC02 =>   1 // Ice Troll, from NIETHER to MALE
  TROLIC03 =>   1 // Blizzard Troll, from NIETHER to MALE
  TROLIC04 =>   1 // Blizzard Troll, from NIETHER to MALE
  TROLL01  =>   1 // Troll, from NIETHER to MALE
  TROLL02  =>   1 // Troll, from NIETHER to MALE
  TROLL03  =>   1 // Troll, from NIETHER to MALE
  TROLLENS =>   1 // Giant Troll, from NIETHER to MALE
  TROLLSM2 =>   1 // Troll, from NIETHER to MALE
  TROLSI01 =>   1 // Spirit Troll, from NIETHER to MALE
  TROLSI02 =>   1 // Spirit Troll, from NIETHER to MALE
  TROLSN01 =>   1 // Snow Troll, from NIETHER to MALE
  TROLSN02 =>   1 // Snow Troll, from NIETHER to MALE
  TROLSP01 =>   1 // Spectral Troll, from NIETHER to MALE
  TROLSP02 =>   1 // Spectral Troll, from NIETHER to MALE
  TROLUO01 =>   1 // Troll, from NIETHER to MALE
  TRSKIN01 =>   2 // Raissa, from MALE to FEMALE
  UDDEATH  =>   4 // Demon Knight, from MALE to NIETHER
  UDDEATH2 =>   4 // Demon Knight, from MALE to NIETHER
  UDDOOR06 =>   2 // Drow Priestess, from MALE to FEMALE
  UDDOOR07 =>   4 // Skeleton, from SUMMONED to NIETHER
  UDDROW35 =>   2 // Priestess of Lolth, from MALE to FEMALE
  UDNABA   =>   4 // Nabassu, from MALE to NIETHER
  UHCREAT  =>   4 // Shadow, from MALE to NIETHER
  VALEMIST =>   4 // Gaseous Form, from MALE to NIETHER
  VAMANC01 =>   2 // Vampire, from MALE to FEMALE
  VAMEMI01 =>   2 // Vampire, from MALE to FEMALE
  VAMMAT01 =>   2 // Vampire, from MALE to FEMALE
  VAMPAER  =>   2 // Aerie, from MALE to FEMALE
  VAMPAT01 =>   2 // Vampire, from MALE to FEMALE
  VAMPJAH  =>   2 // Jaheira, from MALE to FEMALE
  VAMPVIC  =>   2 // Viconia, from MALE to FEMALE
  VAMVER01 =>   2 // Vampire, from MALE to FEMALE
  VVBODHI  =>   2 // Bodhi, from MALE to FEMALE
  VVDEL    =>   2 // Del, from MALE to FEMALE
  VVPARIS  =>   2 // Parisa, from MALE to FEMALE
  VVSALIA  =>   2 // Salia, from MALE to FEMALE
  VVTANOV  =>   2 // Tanova, from MALE to FEMALE
  WINNKEEP =>   1 // Pugney, from FEMALE to MALE
  WISH02   =>   4 // Djinni, from SUMMONED to NIETHER
  WOLFZO01 =>   4 // Zombie Wolf, from MALE to NIETHER
  WORGSU   =>   6 // Worg, from MALE to SUMMONED
  WPWENCH1 =>   2 // Tavern Wench, from MALE to FEMALE
  WRAITH01 =>   4 // Wraith, from MALE to NIETHER
  WRASHI01 =>   4 // Shimmering Wraith, from MALE to NIETHER
  XVARTSU  =>   6 // Xvart Protector, from MALE to SUMMONED
  YARMY01  =>   2 // Fighter, from MALE to FEMALE
  YARMY03  =>   1 // Mage, from FEMALE to MALE

END

ACTION_PHP_EACH cd_bulk_cre_changes_gender AS cre_file => n_gender BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x275 n_gender
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_animate BEGIN // most are corrected demon animations from D0Tweak

  ABISRED1 => 57568 // Abishai, from TANARRI to IC_CORNUGONSKI
  ar18skel => 60176 // skeleton warrior, from skeleton to IC_SKELETONA
  clone1   => 32561 // Ellesime clone in Chateau Irenicus, CLERIC_FEMALE_ELF to ELLISIME
  DEMGLA01 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  DEMGLAB  => 57585 // Glabrezu, from TANARRI to IC_GLAB
  DEMGLAB2 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  DEMGLASU => 57585 // Glabrezu, from TANARRI to IC_GLAB
  DEMPIT01 => 32558 // Pit Fiend, from TANARRI to RAVER
  ENDDEM01 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  ENDDEM02 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  GORDEM   =>  4352 // Balor, from IC_CORNUGONSKI to TANARRI
  hlshyr   => 32546 // shyressa is a female vamp, but uses male vamp avatar, VAMPIRE to VAMPIRE_FEMALE
  JONDEM01 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  JONDEM02 =>  4352 // Balor, from RAVER to TANARRI
  JONDEM03 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  JONDEM04 =>  4352 // Balor, from RAVER to TANARRI
  JONDEM05 => 57585 // Glabrezu, from TANARRI to IC_GLAB
  ppdra2   => 25089 // dradeel is an elf but uses a human avatar, MAGE_MALE_HUMAN to MAGE_MALE_ELF
  ppdradee => 25089 // dradeel is an elf but uses a human avatar, MAGE_MALE_HUMAN to MAGE_MALE_ELF
  redrad01 => 25089 // dradeel is an elf but uses a human avatar, MAGE_MALE_HUMAN to MAGE_MALE_ELF
  cssupp2  => 51472 // mob peasant from the gaal cutscene in unseeing eye quest; has female voiced lines (sex/gender also changed), PEASANT_MAN to PEASANT_WOMAN

END

ACTION_PHP_EACH cd_bulk_cre_changes_animate AS cre_file => n_animate BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_LONG 0x28 n_animate
    BUT_ONLY IF_EXISTS

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_dv BEGIN // most are corrected to just use their file anme as DV

  alex     => NobleOrder // norh paladin from rep trap has wrong DV, script fixed below
  bodfgt01 => bodfgt01   // used in arnfgt06.bcs (Haz)
  bodfgt02 => bodfgt02   // used in arnfgt06.bcs (Haz)
  brus     => brus       // brus dv change
  brus2    => brus       // brus dv change
  cattig01 => cattig01   // trbattle.bcs
  cldad    => cldad      // clmom.dlg
  ffcrowd1 => ffcrowd1   // ffactor1.dlg, ffbiff01.dlg
  ffcrowd2 => ffcrowd2   // ffactor1.dlg, ffbiff01.dlg
  ffcrowd3 => ffcrowd3   // ffactor1.dlg, ffbiff01.dlg
  ffcrowd4 => ffcrowd4   // ffactor1.dlg, ffbiff01.dlg
  ffcrowd5 => ffcrowd5   // ffactor1.dlg, ffbiff01.dlg
  firamb04 => firamb01   // wolfwere capt should have same DV for transformed, non-transformed creature files
  gerhardt => gerhardt   // jan.bcs
  guard3   => guard3     // hendak.dlg
  hobarc02 => hobarc02   // for ar1202.bcs
  kpumb01  => kpumb01    // for ar1301.bcs
  mddust   => dustmep    //
  mourner4 => mourner4   // mourner3.dlg
  mugger3  => mugger3    // slvic01.dlg
  nobl4    => nobl4      // nobl4.dlg
  rogtro01 => rogtro02   // wrong DV for roger's sea troll (active); if killed instantly (rather than via fire/acid) roger won't know
  rskel01  => rskel01    // for ar1400.bcs
  spidgi01 => spidgi01   // trbattle.bcs
  swsfoll1 => swsfoll1   // swshaman.dlg
  swsfoll2 => swsfoll2   // swshaman.dlg
  swsfoll3 => swsfoll3   // swshaman.dlg
  swsfoll4 => swsfoll4   // swshaman.dlg
  swsfoll5 => swsfoll5   // swshaman.dlg
  swsfoll6 => swsfoll6   // swshaman.dlg
  toady    => toady      // pehllus.dlg
  tolger2  => tolger2    // added for various fixes - ar1002.bcs, madeen.bcs, mgteos.dlg, tolger.bcs
  tolmag01 => tolmag01   // tolger2.dlg
  uddoor02 => uddoor02   // cut44c.bcs
  vammat01 => vammat01   // ppbodhi4.dlg
  vamold01 => vamold01   // ppbodhi4.dlg
  wcust01  => wcust01    //
  wcust02  => wcust02    // wcust01.dlg
  wcust03  => wcust03    //
  wcust04  => wcust04    //
  wolfdi   => wolfdi     // trbattle.bcs

END


ACTION_PHP_EACH cd_bulk_cre_changes_dv AS cre_file => n_dv BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_ASCIIE 0x280 ~%n_dv%~ #32
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// a brazilian inventory fixes                      \\\\\
/////                                                  \\\\\

COPY_EXISTING ~abydem01.cre~ ~override~ // tanar'ri
              ~deaddem2.cre~ ~override~ // quasit
              ~dquas01.cre~  ~override~ // quasit
              ~gorchr.cre~   ~override~ // chromatic demon
              ~gortan2.cre~  ~override~ // succubus
              ~gortan5.cre~  ~override~ // quasit
              ~sumtan01.cre~ ~override~ // tanar'ri
              ~sumtan02.cre~ ~override~ // tanar'ri
              ~teltan1.cre~  ~override~ // glabrezu
              ~teltan2.cre~  ~override~ // glabrezu
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // ringdemn (demon power ring) shouldn't expire
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~aegnoll.cre~  ~override~ // gnoll
              ~gnleli01.cre~ ~override~ // gnoll elite
              ~gnlwar01.cre~ ~override~ // gnoll
              ~gnoll01.cre~  ~override~ // gnoll
              ~gnollhp1.cre~ ~override~ // gnoll
              ~icliz01.cre~  ~override~ // lizard man
              ~icliz02.cre~  ~override~ // gorkale
  LPF cd_equip_item STR_VAR item = "rndtre04" slot = "inventory" END // legit random treasure not assigned to slot
  BUT_ONLY

COPY_EXISTING ~aemess.cre~   ~override~ // messenger
              ~alex.cre~     ~override~ // sir beverus
              ~ammajira.cre~ ~override~ // majira
              ~amtcle01.cre~ ~override~ // cleric of lathander
              ~amtcler0.cre~ ~override~ // cleric of lathander
              ~antenos.cre~  ~override~ // antenos
              ~arled.cre~    ~override~ // arledrian
              ~bazeye01.cre~ ~override~ // iycanth the mad
              ~bguard1.cre~  ~override~ // guard
              ~boo.cre~      ~override~ // boo
              ~bounha02.cre~ ~override~ // bounty hunter
              ~bounha03.cre~ ~override~ // bounty hunter
              ~ch3mag01.cre~ ~override~ // shadow thief
              ~copamb01.cre~ ~override~ // gladiator
              ~copamb03.cre~ ~override~ // gladiator
              ~copamb04.cre~ ~override~ // gladiator
              ~dadrow11.cre~ ~override~ // drow torturer
              ~dadrow17.cre~ ~override~ // taso kala
              ~dadrow20.cre~ ~override~ // drow priestess
              ~dadrow4.cre~  ~override~ // drow priestess
              ~daqilue.cre~  ~override~ // qilue
              ~democler.cre~ ~override~ // cleric
              ~derrick.cre~  ~override~ // derrick
              ~drow04.cre~   ~override~ // drow priestess
              ~eyeegl01.cre~ ~override~ // eagle eye
              ~firorc02.cre~ ~override~ // derg the orc
              ~firorc03.cre~ ~override~ // flaylas the orc
              ~glad2.cre~    ~override~ // gladiator
              ~gorcamb2.cre~ ~override~ // tiefling
              ~gorcamb3.cre~ ~override~ // tiefling
              ~gorpri1.cre~  ~override~ // priest
              ~hgber01.cre~  ~override~ // berenn
              ~hgskl01.cre~  ~override~ // skeleton cleric
              ~hgskl03.cre~  ~override~ // skeleton mage
              ~hlassa.cre~   ~override~ // assassin
              ~hlketta.cre~  ~override~ // ketta
              ~hlketta2.cre~ ~override~ // ketta
              ~hlmage.cre~   ~override~ // mage
              ~hprelate.cre~ ~override~ // prelate wessalen
              ~jolus.cre~    ~override~ // sir jolus
              ~kendak.cre~   ~override~ // sir laren
              ~magetest.cre~ ~override~ // mage
              ~mvpries.cre~  ~override~ // priest of cyric
              ~pace.cre~     ~override~ // pace
              ~pettin.cre~   ~override~ // ettin
              ~pirmur09.cre~ ~override~ // golin
              ~ppaltk1.cre~  ~override~ // kobold shaman
              ~ppaltk2.cre~  ~override~ // kobold shaman
              ~ppguard3.cre~ ~override~ // guard
              ~ppsuna.cre~   ~override~ // suna seni
              ~reband05.cre~ ~override~ // bandit
              ~recler01.cre~ ~override~ // slaver
              ~recler02.cre~ ~override~ // cleric
              ~rethie01.cre~ ~override~ // salver
              ~ryan.cre~     ~override~ // sir paritin
              ~sahamb03.cre~ ~override~ // rebel
              ~sahamb06.cre~ ~override~ // sahuagin
              ~salvanas.cre~ ~override~ // salvanas
              ~sarcult.cre~  ~override~ // human cultist
              ~sarcult2.cre~ ~override~ // human cultist
              ~sarculto.cre~ ~override~ // orc cultist
              ~sarhal.cre~   ~override~ // halfling cultist
              ~sartem01.cre~ ~override~ // sister farielle
              ~sendai8.cre~  ~override~ // sendai
              ~senorc01.cre~ ~override~ // derro berserker
              ~senorc03.cre~ ~override~ // derro berserker
              ~senpri01.cre~ ~override~ // diaytha
              ~shugpr01.cre~ ~override~ // vigil knight
              ~snake01.cre~  ~override~ // snake
              ~snakeg01.cre~ ~override~ // giant snake
              ~suna.cre~     ~override~ // suna seni
              ~tobpar05.cre~ ~override~ // delga deepdelver
              ~trevil01.cre~ ~override~ // lord khellon menold
              ~trthf02.cre~  ~override~ // kich
              ~turn.cre~     ~override~ // cleric
              ~uddear02.cre~ ~override~ // uder mordin
              ~uddrow14.cre~ ~override~ // drow
              ~uddrow15.cre~ ~override~ // drow
              ~uddrow31.cre~ ~override~ // drow
              ~uddrow35.cre~ ~override~ // priestess of lloth
              ~udelf05.cre~  ~override~ // war elf
              ~udgirl.cre~   ~override~ // priestess of lloth
              ~udtrap05.cre~ ~override~ // gont of riatavin
              ~uhmer02.cre~  ~override~ // min minling
              ~vorsquir.cre~ ~override~ // vorpal squirrel
              ~wauksna.cre~  ~override~ // snake
              ~yaga02.cre~   ~override~ // lientenant mage
              ~yaga04.cre~   ~override~ // lientenant cleric
              ~yssold14.cre~ ~override~ // yaga-shura mage
              ~yssold16.cre~ ~override~ // cleric of talos
              ~zilmag01.cre~ ~override~ // black reaver
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~aerbod01.cre~ ~override~ // aerie
              ~aerie6.cre~   ~override~ // aerie
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = slng01 END // overcharged: sling
  BUT_ONLY

COPY_EXISTING ~aerie9.cre~   ~override~ // aerie
  LPF cd_equip_item STR_VAR item = "leat04" slot = "armor" END // legit studded leather armor not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bull01 END // change stack of 0 to stack of 20 bullets
  BUT_ONLY

COPY_EXISTING ~aerie10.cre~  ~override~ // aerie
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "leat04" slot = "armor" END // legit studded leather armor not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bull01 END // change stack of 0 to stack of 20 bullets
  BUT_ONLY

COPY_EXISTING ~aerie11.cre~  ~override~ // aerie
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bull01 END // change stack of 0 to stack of 20 bullets
  BUT_ONLY

COPY_EXISTING ~amarch01.cre~ ~override~ // captain erelon
  ADD_CRE_ITEM ~arow01~ #20 #0 #0 ~NONE~ ~QUIVER1 QUIVER2 QUIVER3~ // bow but no arrows
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = helm33 END // overcharged: gold horned helmet
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = bow20 END // undecharged: darkfire bow +4
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amarch02.cre~ ~override~ // mercenary
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = helm33 END // overcharged: gold horned helmet
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amcarras.cre~ ~override~ // carras
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 2 STR_VAR match_item = boot12 END // undercharged: gargoyle boots
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amlich01.cre~ ~override~ // vongoethe
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ring46" slot = "rings" END // move ring of anti-venom from weapon slot to ring
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~ammerc07.cre~ ~override~ // mercenary
  LPF cd_equip_item STR_VAR item = "rndtre02" slot = "inventory" END // legit random treasure not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~ammerc08.cre~ ~override~ // mercenary
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 30 STR_VAR match_item = arow11 END // stack of 0 > 30 arrows +2
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amwyvern.cre~ ~override~ // lucy
              ~nwyvbab.cre~  ~override~ // baby wyvern
              ~plywyvrn.cre~ ~override~ // baby wyvern
              ~wyvbab01.cre~ ~override~ // baby wyvern
  REMOVE_CRE_ITEM misc52 // delete unassigned wyvern head
  BUT_ONLY

COPY_EXISTING ~ankheg01.cre~ ~override~ // ankheg
  LPF cd_no_pickpocket STR_VAR item = misc12 END // move ankheg shell to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~anomen6.cre~  ~override~ // anomen
              ~anomen7.cre~  ~override~ // anomen
              ~anomen8.cre~  ~override~ // anomen
              ~anomen9.cre~  ~override~ // anomen
              ~anomen10.cre~ ~override~ // anomen
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "slng01" slot = "weapon" END // move sling from quickslot/inventory to weapon slot
  BUT_ONLY

COPY_EXISTING ~ar18dwaf.cre~ ~override~ // duergar
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 10 charge2 = 0 STR_VAR match_item = ax1h04 END // no secondary charges for throwing axes, make a stack of 10
  BUT_ONLY

COPY_EXISTING ~ar18prie.cre~ ~override~ // has better melee set as offhand
  REPLACE_CRE_ITEM shld03 #0 #0 #0 ~NONE~ SHIELD // move mace +1 to mainhand
  REPLACE_CRE_ITEM blun05 #0 #0 #0 ~NONE~ WEAPON1 EQUIP
  BUT_ONLY

COPY_EXISTING ~aran.cre~     ~override~ // aran linvail
              ~maevar.cre~   ~override~ // mae'var
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = helmnoan END // no-animation helmet shouldnt have charges
  BUT_ONLY

COPY_EXISTING ~aran02.cre~  ~override~ // aran linvail
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ring07" slot = "rings" END // equip ring of protection +2 from inventory
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = misc6w END // stakes don't need charges
  BUT_ONLY

COPY_EXISTING ~arngol01.cre~ ~override~ // golem
              ~csgolem.cre~  ~override~ // golem
              ~golem01.cre~  ~override~ // clay golem
              ~icbone01.cre~ ~override~ // bone golem
              ~igolem01.cre~ ~override~ // sewage golem
              ~igolem02.cre~ ~override~ // jailkeep golem
  ADD_CRE_ITEM ~golcla~ #0 #0 #0 ~NONE~ ~RRING LRING AMULET~ // add golem immunity items
  BUT_ONLY

COPY_EXISTING ~arntra01.cre~ ~override~ // trainee
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sh1h04" item = "sw1h04" END // resref typo (sh1h04)
  BUT_ONLY

COPY_EXISTING ~bamng01.cre~ ~override~ // amnish guard
              ~bamng02.cre~ ~override~ // amnish guard
              ~circg1.cre~  ~override~ // amnish soldier
  LPF cd_equip_weapon_specific STR_VAR item = "sw1h04" END // equip long sword; amnish guards shouldn't be using bows as they have no bow animation
  BUT_ONLY

COPY_EXISTING ~bazdra01.cre~ ~override~ // draconis
              ~bazdra02.cre~ ~override~ // draconis
  LPF cd_no_pickpocket STR_VAR item = bazplo02 END // move draconis' head to unpickpocketable slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bazdra03.cre~ ~override~ // fll'yissetat
              ~dragyell.cre~ ~override~ // yellow dragon
  REMOVE_CRE_ITEM dragyell // delete unassigned item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bazliz04.cre~ ~override~ // lizard man captain
              ~gorsta02.cre~ ~override~ // wk statue
              ~jarevia.cre~  ~override~ // reviane
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "halb03" item = "halb07" END // Suryris's Blade is unique; replacing all cre references with generic halb +2 - gorsta09.cre is done separately
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bdturm02.cre~ ~override~ // turmish thief
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 expiry = 0 STR_VAR match_item = bolt03 END // stack of 0 > 20 lightning bolts, also shouldn't expire
  BUT_ONLY

COPY_EXISTING ~bearbr01.cre~ ~override~ // brown bear
  REMOVE_CRE_ITEM b1-8 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~bearca01.cre~ ~override~ // cave bear
              ~vbearca.cre~  ~override~ // cave bear
  REMOVE_CRE_ITEM b1-10 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~bearpo01.cre~ ~override~ // mountain bear
  REMOVE_CRE_ITEM b1-12 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~beast.cre~    ~override~ // beastmaster
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 0 STR_VAR match_item = arow01 END // no secondary charges for arrows
  BUT_ONLY

COPY_EXISTING ~bhaal01.cre~  ~override~ // bhaal
              ~sarevok.cre~  ~override~ // sarevok
  LPF cd_equip_item STR_VAR item = "bolt06" slot = "quiver" END // move bolt +2 to quiver slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bhcrypt.cre~  ~override~ // crypt king
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 3 STR_VAR match_item = sw1h36 END // add three secondary charges for namarra's silence ability
  BUT_ONLY

COPY_EXISTING ~bhelm.cre~    ~override~ // guardian vottnar
  REMOVE_CRE_ITEM scrl25 // delete nonexistant item

COPY_EXISTING ~bhghoul5.cre~ ~override~ // zombies
  REMOVE_CRE_ITEM arrw01 bow03 // remove (broken ref) arrows, bow
  LPF cd_equip_weapon_specific STR_VAR item = b3-12 END  // equip melee generic; zombies shouldn't be using bows as they have no bow animation
  BUT_ONLY

COPY_EXISTING ~bodhi.cre~    ~override~ // bodhi
              ~bodhi2.cre~   ~override~ // bodhi
              ~c6bodhi.cre~  ~override~ // bodhi
              ~cubodhi.cre~  ~override~ // bodhi
  REMOVE_CRE_ITEM s2-16 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~bodhiamb.cre~ ~override~ // bodhi
  REMOVE_CRE_ITEM s2-16 vampreg2 // delete unassigned items
  BUT_ONLY

COPY_EXISTING ~bonebld.cre~  ~override~ // bone blade
              ~sword01.cre~  ~override~ // magical sword (mordenkainen)
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 50 STR_VAR match_item = ipsion END // max charges (50) of greenstone amulet copy; can't be dropped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~braiwa2.cre~  ~override~ // blind priest
              ~pries12c.cre~ ~override~ // zorl
  REMOVE_CRE_ITEM plat01 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~bshop01.cre~  ~override~ // cutpurse
              ~ffactor2.cre~ ~override~ // lunisia
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "dagg01" slot = "weapon" END // move dagger into weapon slot from quickslot
  BUT_ONLY

COPY_EXISTING ~c6bran.cre~   ~override~ // Branet Al-Thon
              ~c6catti2.cre~ ~override~ // catti-brie
              ~c6elf1.cre~   ~override~ // Elven Warrior
              ~c6fake.cre~   ~override~ // Elhan
              ~c6gener.cre~  ~override~ // General Sovalidaas
              ~c6kalden.cre~ ~override~ // Kalden
              ~c6levin.cre~  ~override~ // Levin Rayn
              ~c6nerit.cre~  ~override~ // Nerit
              ~c6will.cre~   ~override~ // William Reirrac
              ~cupris1.cre~  ~override~ // Elven Warrior
              ~cupris2.cre~  ~override~ // Elven Warrior
              ~e34.cre~      ~override~ // Coran
              ~suelf12.cre~  ~override~ // Captain Aduo'on
              ~suelf13.cre~  ~override~ // Elven Warrior
              ~suelleg1.cre~ ~override~ // Elven Warrior
              ~suendel1.cre~ ~override~ // Elven Warrior
              ~suendel2.cre~ ~override~ // Elven Warrior
              ~suendel3.cre~ ~override~ // Elf Male
              ~suendel4.cre~ ~override~ // Elf Female
              ~suendel5.cre~ ~override~ // Elf
              ~suendel6.cre~ ~override~ // Elf
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw1h06" item = "sw1h41" END // Varscona is unique; repacing all references except Ribald's with generic +2 sword
  BUT_ONLY

COPY_EXISTING ~c6catti.cre~  ~override~ // catti-brie
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw1h06" item = "sw1h41" END // Varscona is unique; repacing all references except Ribald's with generic +2 sword
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = ring05 END // ring of invisibility is once/day item
  BUT_ONLY

COPY_EXISTING ~c6kach2.cre~  ~override~ // Kachiko
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw1h06" item = "sw1h41" END // Varscona is unique; repacing all references except Ribald's with generic +2 sword
  LPF cd_equip_weapon END // has no weapon equipped
  BUT_ONLY

COPY_EXISTING ~c6lanf.cre~   ~override~ // lanfear
              ~garkid02.cre~ ~override~ // taar
              ~gibmut01.cre~ ~override~ // mutated gibberling (note: not cam) (also note: why are you reading notes?)
  LPF cd_equip_item STR_VAR item = "rndtre02" slot = "inventory" END // legit random treasure not assigned to slot
  BUT_ONLY

COPY_EXISTING ~c6regis.cre~  ~override~ // regis
              ~chang01.cre~  ~override~ // angelo
              ~chang02.cre~  ~override~ // angelo
              ~chgood08.cre~ ~override~ // aran linvail
              ~deshar.cre~   ~override~ // ferric ironblade
              ~sevpat05.cre~ ~override~ // brenna risling
              ~tanthf01.cre~ ~override~ // rune assassin
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = ring05 END // ring of invisibility is once/day item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~c6coran.cre~  ~override~ // coran
  LPF cd_equip_item STR_VAR item = "bow03" slot = "weapon" END // move long bow into weapons slot
  BUT_ONLY

COPY_EXISTING ~c6drizz.cre~  ~override~ // drizzt
              ~c6drizz2.cre~ ~override~ // drizzt
              ~c6drizz3.cre~ ~override~ // drizzt
              ~drizzt.cre~   ~override~ // drizzt
  REMOVE_CRE_ITEM boot01 s1-12 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~c6elven2.cre~ ~override~ // elven war guard
              ~c6elvenw.cre~ ~override~ // elven war guard
              ~dacemist.cre~ ~override~ // gaseous form
              ~delmist.cre~  ~override~ // gaseous form
              ~tanomist.cre~ ~override~ // vampire
              ~valemist.cre~ ~override~ // vampire
              ~warsage.cre~  ~override~ // elven war sage
  LPF cd_equip_item STR_VAR item = "minhp1" slot = "jewelry" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~c6god.cre~    ~override~ // elven god
  LPF cd_equip_item STR_VAR item = "plat14" slot = "armor" END // legit item not assigned to slot
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw1h06" item = "sw1h41" END // Varscona is unique; repacing all references except Ribald's with generic +2 sword
  BUT_ONLY

COPY_EXISTING ~c6guen.cre~   ~override~ // guenhwyvar (summons, but has no droppable items to flag)
              ~c6guen2.cre~  ~override~ // guenhwyvar
              ~catjag01.cre~ ~override~ // panther
              ~catjagsu.cre~ ~override~ // panther
              ~catjagwp.cre~ ~override~ // panther
              ~catpan01.cre~ ~override~ // panther
              ~cefald02.cre~ ~override~ // faldorn
              ~tranim03.cre~ ~override~ // panther
              ~trevil05.cre~ ~override~ // panther
              ~wolfdi01.cre~ ~override~ // dire wolf
  REMOVE_CRE_ITEM p1-8 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~c6kngt1.cre~  ~override~ // morning knight
              ~fshorde2.cre~ ~override~ // goblin
              ~icgob01.cre~  ~override~ // goblin
              ~icgob02.cre~  ~override~ // goblin
              ~kobarc01.cre~ ~override~ // kobold
              ~kobcap01.cre~ ~override~ // kobold captain
              ~latkni01.cre~ ~override~ // morning knight
              ~latkni02.cre~ ~override~ // morning knight
              ~ppguard1.cre~ ~override~ // kobold
              ~shabod01.cre~ ~override~ // wallag's body
              ~swsfoll4.cre~ ~override~ // kobold
              ~swsfoll5.cre~ ~override~ // kobold
              ~waukni01.cre~ ~override~ // waukeenar knight
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = bow01 END // composite long bow shouldn't have charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~catlio01.cre~ ~override~ // lion
              ~catliosu.cre~ ~override~ // lion (summons, but has no droppable items to flag)
              ~catliowp.cre~ ~override~ // joolon (summons, but has no droppable items to flag)
              ~hellho01.cre~ ~override~ // hell hound
              ~hellil01.cre~ ~override~ // hell hound
  REMOVE_CRE_ITEM p1-10 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~catlis01.cre~ ~override~ // spotted lion
  REMOVE_CRE_ITEM p1-12 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~chalcy02.cre~ ~override~ // favored of cyric
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 50 STR_VAR match_item = chalcy3 END // max charges (50); can't be dropped
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 =  1 STR_VAR match_item = ring34 END // one charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chalpc01.cre~ ~override~ // <charname>
              ~chalpc02.cre~ ~override~ // <charname>
              ~chalpc03.cre~ ~override~ // <charname>
              ~chalpc04.cre~ ~override~ // <charname>
              ~chalpc05.cre~ ~override~ // <charname>
              ~chalpc06.cre~ ~override~ // <charname>
              ~chalpc07.cre~ ~override~ // <charname>
              ~chalpc08.cre~ ~override~ // <charname>
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1             STR_VAR match_item = amul22 END // one charge
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 charge2 = 1 STR_VAR match_item = ring29 END // one primary, secondary charge
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1             STR_VAR match_item = ring34 END // one charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chalpc09.cre~ ~override~ // <charname>
              ~chalpc10.cre~ ~override~ // <charname>
              ~chalpc11.cre~ ~override~ // <charname>
              ~chalpc12.cre~ ~override~ // <charname>
              ~chalpc13.cre~ ~override~ // <charname>
              ~chalpc14.cre~ ~override~ // <charname>
              ~chalpc15.cre~ ~override~ // <charname>
              ~chalpc16.cre~ ~override~ // <charname>
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1             STR_VAR match_item = amul22 END // one charge
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 charge2 = 1 STR_VAR match_item = ring29 END // one primary, secondary charge
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1             STR_VAR match_item = ring34 END // one charge
  LPF ALTER_CREATURE_ITEM INT_VAR             charge2 = 3 STR_VAR match_item = sw2h10 END // three secondary charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chevil02.cre~ ~override~
              ~dopgre01.cre~ ~override~
              ~dopple01.cre~ ~override~
  REMOVE_CRE_ITEM s1-12 // delete unassigned item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chevil09.cre~ ~override~ // undercharged: ring34 (num 5) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR             charge2 = 3 charge3 = 1 STR_VAR match_item = staf11 END // one secondary, tertiary charge
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1                         STR_VAR match_item = ring34 END // one charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chgood09.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "potn55" slot = "quickslot" END // legit item not assigned to slot
  LPF cd_equip_weapon END
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chsam01.cre~  ~override~ // undercharged: staf12 (num 4) has assigned 0 to max 10 in header 2
              ~chsam02.cre~  ~override~ // undercharged: staf12 (num 4) has assigned 0 to max 10 in header 3
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 10 charge3 = 10 STR_VAR match_item = staf12 END // ten secondary, tertiary charges
  LPF cd_equip_weapon END
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chtaz01.cre~  ~override~ // undercharged: clck20 (num 3) has assigned 0 to stack 1 charges 50 in header 1
              ~pbhunt03.cre~ ~override~ // undercharged: clck20 (num 2) has assigned 0 to stack 1 charges 50 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 50 STR_VAR match_item = clck20 END // max charges (50)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~cltiva01.cre~ ~override~
  LPF cd_no_pickpocket STR_VAR item = misc7f END // Ti'Vael's head // move ankheg shell to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~cor.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "2hsw02" item = "sw2h02" END // resref typo
  BUT_ONLY

COPY_EXISTING ~corneil.cre~  ~override~
              ~dario.cre~    ~override~
              ~elders.cre~   ~override~
              ~jamage1.cre~  ~override~
              ~jamage2.cre~  ~override~
              ~vara.cre~     ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "staf01" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~cowled.cre~   ~override~
  LPF cd_equip_item STR_VAR item = "dart01" slot = "weapon" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "potn08" slot = "quickslot" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "scrl1g" slot = "quickslot" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "scrl1q" slot = "quickslot" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~csiren.cre~   ~override~ // staf01, rndtre04
              ~lavok01.cre~  ~override~ // staf01, rndtre04
              ~lavok02.cre~  ~override~ // staf01, rndtre04
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = staf01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = rndtre04 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~cultist1.cre~ ~override~
              ~excult1.cre~  ~override~
              ~excult2.cre~  ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "leath04" item = "leat04" END // resref typo (leath04)
  BUT_ONLY

COPY_EXISTING ~cuphaer.cre~  ~override~
              ~drow05.cre~   ~override~
              ~udchal.cre~   ~override~
              ~udphae02.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "dwcha01" item = "dwchan01" END // resref typo (dwcha01)
  BUT_ONLY

COPY_EXISTING ~d1ghost.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "ghost3" slot = "jewelry" END // legit item not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~dagnoll.cre~  ~override~
  REMOVE_CRE_ITEM rndtre04 // legit item not assigned to slot, but since it's a summon...
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = all END // it shouldn't drop anything anyway (halb01, rndtre02)
  BUT_ONLY

//Dawnmaster Kreel's amazing wardrobe change (Wisp), part 1/2
COPY_EXISTING ~dawnmas.cre~ ~override~
  REMOVE_CRE_ITEMS
  ADD_CRE_ITEM staf01 #0 #0 #0 none weapon1 EQUIP TWOHANDED
  ADD_CRE_ITEM chan01 #0 #0 #0 none armor
  ADD_CRE_ITEM rndtre02 #0 #0 #0 none inv7

COPY_EXISTING ~deck622.cre~  ~override~
  REMOVE_CRE_ITEM s3-8m3 // delete unassigned item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~deckass1.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordeck5.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordeck6.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordeck7.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordeck8.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordeck9.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~gordecka.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~hgskl02.cre~  ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~sargrd05.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~sargrd09.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~sarkis01.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~sarkis02.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~sarkis03.cre~ ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
              ~senani05.cre~ ~override~ // overcharged: leat02 (num 0) has assigned 5 to max 1 in header 1
              ~sendro05.cre~ ~override~ // overcharged: leat02 (num 0) has assigned 5 to max 1 in header 1
              ~yaga05.cre~   ~override~ // overcharged: leat02 (num 1) has assigned 5 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = leat02 END // no charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~demosum2.cre~ ~override~ // ringdemn, balor, immune3
              ~gortan1.cre~  ~override~ // ringdemn, balor, immune3
              ~melsum04.cre~ ~override~ // ringdemn, balor, immune3
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = balor END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = immune3 END // item shouldn't expire
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~demosum3.cre~ ~override~ // ringdemn, immune2
              ~dglab01.cre~  ~override~ // ringdemn, immune2
              ~gortan4.cre~  ~override~ // ringdemn, immune2
              ~melsum01.cre~ ~override~ // ringdemn, immune2
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = immune2 END // item shouldn't expire
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~demosum4.cre~ ~override~
              ~gorcamb.cre~  ~override~
              ~gorcamb6.cre~ ~override~
              ~gorcamb7.cre~ ~override~
              ~hgfel01.cre~  ~override~
              ~telimp1.cre~  ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "rngdemn" item = ~ringdemn~ END // resref typo (rngdemn)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~docsol01.cre~ ~override~ // overcharged: chan01 (num 0) has assigned 20 to max 1 in header 1
              ~docsol02.cre~ ~override~ // overcharged: chan01 (num 0) has assigned 20 to max 1 in header 1
              ~pirsea02.cre~ ~override~ // overcharged: chan01 (num 0) has assigned 20 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = chan01 END // no charges
  BUT_ONLY

COPY_EXISTING ~dragblac.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "arow07" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "arow11" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "bolt06" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "bull03" slot = "inventory" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "dart04" slot = "inventory" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~drush.cre~    ~override~ // number of creatures with dupe entries for rndtre05
              ~ogrema01.cre~ ~override~
              ~ogrmag01.cre~ ~override~
              ~plshom01.cre~ ~override~
  REMOVE_CRE_ITEM rndtre05 // delete offending item, then add back manually
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV6~
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV7~
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV8~
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV9~
  BUT_ONLY

COPY_EXISTING ~dshop01.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "sw1h07" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~duearc01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "ax1h01" slot = "weapon" END // legit item not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = ax1h01 END // no charges
  BUT_ONLY

COPY_EXISTING ~duegau01.cre~ ~override~ // bull01
              ~duergar1.cre~ ~override~ // bull01
              ~shth01.cre~   ~override~ // bull01
              ~shth011.cre~  ~override~ // bull01
              ~uddwarf.cre~  ~override~ // bull01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = bull01 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~duegau02.cre~ ~override~ // ax1h01, bull01
              ~duergar3.cre~ ~override~ // ax1h01, bull01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = bull01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ax1h01 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~duemag01.cre~ ~override~ // staf01, slng01, potn08, bull01
              ~duemag02.cre~ ~override~ // staf01, slng01, potn08, bull01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = staf01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = slng01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = potn08 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = bull01 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~e32.cre~      ~override~
  REMOVE_CRE_ITEM leat04 sw2h01// delete unassigned item (already has plate and 2h sword +1)
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "boot01" slot = "boots" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~e33.cre~      ~override~ // overcharged: sw1h10 (num 5) has assigned 10 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw1h10 END // no charges
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 25 STR_VAR match_item = amul01 END // undercharged: amul01 (num 6) has assigned 0 to stack 1 charges 25 in header 1
  BUT_ONLY

COPY_EXISTING ~edwin12.cre~ ~override~
              ~edwin13.cre~ ~override~
  ADD_CRE_ITEM ~bull01~ #20 #0 #0 ~NONE~ ~QUIVER1 QUIVER2 QUIVER3~ // sling but no bullets
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "slng01" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~elearb01.cre~ ~override~
              ~elearb02.cre~ ~override~
              ~elearb03.cre~ ~override~
              ~elearb04.cre~ ~override~
              ~elearb05.cre~ ~override~
              ~elearb06.cre~ ~override~
              ~elearb07.cre~ ~override~
              ~elearb08.cre~ ~override~
              ~elearb09.cre~ ~override~
              ~elearb10.cre~ ~override~
              ~elearb11.cre~ ~override~
              ~gorsta11.cre~ ~override~
              ~shupol01.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck16" slot = "armor" END // has armor in cloak/quickslots/inventory
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~elekob01.cre~ ~override~
              ~kobcom01.cre~ ~override~
              ~swsfoll1.cre~ ~override~
              ~swsfoll6.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "arow01" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "arow08" slot = "quiver" END // have ammo in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~elemimix.cre~ ~override~ // undercharged: halb10 (num 2) has assigned 0 to max 2 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 2 STR_VAR match_item = halb10 END // two secondary charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~eletro01.cre~ ~override~ // troll
  LPF cd_no_pickpocket STR_VAR item = misc9d END // move giant troll's head to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~famimp.cre~   ~override~
              ~famqua25.cre~ ~override~
              ~famquas.cre~  ~override~
              ~fsridd.cre~   ~override~
              ~golsan01.cre~ ~override~
              ~mumgre01.cre~ ~override~
              ~riftcr03.cre~ ~override~
              ~shape.cre~    ~override~
  REMOVE_CRE_ITEM immune1 // delete unassigned item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~firkra02.cre~ ~override~ // undercharged: sw2h10 (num 4) has assigned 1 to max 3 in header 2
  REMOVE_CRE_ITEM misc52 // delete unassigned wyvern head
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 3 STR_VAR match_item = sw2h10 END // three secondary charges
  BUT_ONLY

COPY_EXISTING ~firmag01.cre~ ~override~
  LPF cd_no_pickpocket STR_VAR item = key09 END // firkraag's prison key from conster
  BUT_ONLY

COPY_EXISTING ~firmon01.cre~ ~override~ // undercharged: sw1h32 (num 3) has assigned 0 to max 1 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h32 END // one secondary charge
  BUT_ONLY

COPY_EXISTING ~firmon02.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "bow01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~firwlf01.cre~ ~override~
              ~firwlf02.cre~ ~override~
              ~rngwlf01.cre~ ~override~
              ~rngwlf02.cre~ ~override~
              ~rngwlf03.cre~ ~override~
              ~rngwlf04.cre~ ~override~
              ~rngwlf05.cre~ ~override~
              ~weregr01.cre~ ~override~
              ~weregrdr.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "ringloup" item = "ringwolf" END // replaces ringloup as it makes creatures well-nigh invincible as using old BG immunity (unused in BG2)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~flyfgt03.cre~ ~override~ // undercharged: arow01 (num 0) has assigned 0 to stack 40 charges 1 in header 1
              ~kaysmg01.cre~ ~override~ // undercharged: arow01 (num 4) has assigned 0 to stack 40 charges 1 in header 1
              ~latkni02.cre~ ~override~ // undercharged: arow01 (num 3) has assigned 0 to stack 40 charges 1 in header 1
              ~uhogre04.cre~ ~override~ // undercharged: arow01 (num 3) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = arow01 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~flyfgt04.cre~ ~override~
              ~gmage14.cre~  ~override~
              ~jangit02.cre~ ~override~
              ~rumar01.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck10" slot = "armor" END // has armor in cloak/quickslots/inventory
  BUT_ONLY

COPY_EXISTING ~firarc01.cre~ ~override~ // Orc Archer
  REMOVE_CRE_ITEM shld05 // Creatures shouldn't have two-handed items equipped alongside shields (Nythrun and devSin)

COPY_EXISTING ~fsdragon.cre~ ~override~ // undercharged: sw1h70 (num 4) has assigned 0 to max 1 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h70 END // one secondary charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~fshorde6.cre~ ~override~ // overcharged: bow03 (num 3) has assigned 20 to max 1 in header 1
              ~gororc02.cre~ ~override~ // overcharged: bow03 (num 3) has assigned 20 to max 1 in header 1
              ~hobarc02.cre~ ~override~ // overcharged: bow03 (num 4) has assigned 20 to max 1 in header 1
              ~orc02.cre~    ~override~ // overcharged: bow03 (num 3) has assigned 20 to max 1 in header 1
              ~plshhg01.cre~ ~override~ // overcharged: bow03 (num 4) has assigned 20 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = bow03 END // no charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~garock.cre~ ~override~
              ~rock.cre~   ~override~
  LPF cd_equip_item STR_VAR item = "helm01" slot = "helmet" END // legit item not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~garrick.cre~  ~override~
              ~jameroni.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "xbow04" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~gemch02.cre~  ~override~
  LPF cd_no_pickpocket STR_VAR item = misc6z END // beljuril from chicken // move beljuril to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~genie01.cre~  ~override~
              ~genie02.cre~  ~override~
              ~genie03.cre~  ~override~
              ~genie04.cre~  ~override~
              ~ppdjinn.cre~  ~override~
              ~trgeni02.cre~ ~override~
              ~trgeni03.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "b3-18" item = "b3-18m3" END // resref typo (b3-18)
  BUT_ONLY

COPY_EXISTING ~genie1.cre~   ~override~
              ~idjinni.cre~  ~override~
              ~kgenie1.cre~  ~override~
              ~kgenie2.cre~  ~override~
              ~trgeni01.cre~ ~override~
  REMOVE_CRE_ITEM b3-18 // delete unassigned (and nonexistant) item
  BUT_ONLY

COPY_EXISTING ~gerhardt.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "blun02" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~ghogr01.cre~  ~override~
              ~imp01.cre~    ~override~
              ~impqua01.cre~ ~override~
              ~riftcr01.cre~ ~override~
              ~rumar03.cre~  ~override~
              ~sahimp01.cre~ ~override~
              ~sahimp02.cre~ ~override~
              ~sahimp03.cre~ ~override~
              ~sahimp04.cre~ ~override~
              ~sahimp05.cre~ ~override~
              ~sahimp06.cre~ ~override~
              ~sahimp07.cre~ ~override~
              ~udimp.cre~    ~override~
  LPF cd_equip_item STR_VAR item = "immune1" slot = "jewelry" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~gibbersu.cre~ ~override~
  REMOVE_CRE_ITEM rndtre02 // legit item not assigned to slot, but since it's a summon it shouldn't drop anything anyway
  BUT_ONLY

COPY_EXISTING ~gith04.cre~   ~override~
  LPF cd_equip_item INT_VAR gpuse = 1 STR_VAR item = "sw2h01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~gnollsu.cre~  ~override~ // helm01.itm, halb01.itm
              ~hobgobsu.cre~ ~override~ // Hobgoblin Elite (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~koboldsu.cre~ ~override~ // Kobold Commando (spwi309.spl -> spmon1.eff -> spwi309.spl
              ~sahangu.cre~  ~override~ // Anguiliian (ar2300.are -> sahambo6.cre -> sahambo6.bcs)
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = all END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~golice01.cre~   ~override~
  ADD_CRE_ITEM ~golstone~ #0 #0 #0 ~NONE~ ~AMULET~ // add golem immunity item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorbat1.cre~  ~override~ // ringdemn, immune3, reghp2r
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = reghp2r END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = immune3 END // item shouldn't expire
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorfirg.cre~ ~override~ // has helm in quickslot/inventory
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "helm31" slot = "helmet" END // legit item not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorgith2.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "sw2h01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorje.cre~  ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "cham01" item = "chan01" END // resref typo (cham01)
  BUT_ONLY

COPY_EXISTING ~gorkuo05.cre~ ~override~
              ~kuowhi01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "xbow01" slot = "weapon" END // legit item not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = xbow01 END // no charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorlic01.cre~ ~override~ // undercharged: slng08 (num 3) has assigned 0 to max 1 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = slng08 END // one secondary charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorsta09.cre~ ~override~
  LPF ALTER_CREATURE_ITEM INT_VAR flag_stolen = 0 STR_VAR match_item = "halb03" item = "halb07" END // removes stolen flag from item, swap to non-unique
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = arow01 END // stack of 20
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorsta10.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "umber1" item = "umber01" END // resref typo (umber1)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gortan3.cre~  ~override~ // ringdemn, dwplat01, dwsw1h01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = dwplat01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = dwsw1h01 END // item shouldn't expire
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorwom01.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "rdntre03" item = "rndtre03" END // resref typo (rdntre03)
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "rdntre04" item = "rndtre04" END // resref typo (rdntre04)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorwom02.cre~ ~override~ // undercharged: helm16 (num 6) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 charge2 = 1 charge3 = 1 STR_VAR match_item = helm16 END // one charge everywhere
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorwom03.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "brac21" slot = "gloves" END // has bracers in quickslot/inventory
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gromg05.cre~  ~override~
              ~gromg08.cre~  ~override~
              ~gromg13.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck14" slot = "armor" END // has armor in cloak/quickslots/inventory
  LPF cd_equip_weapon END // has no weapon selected
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~grvlch01.cre~ ~override~ // overcharged: rods05 (num 6) has assigned 5 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = rods05 END // no charges
  BUT_ONLY

COPY_EXISTING ~habib.cre~    ~override~ // undercharged: sw1h57 (num 1) has assigned 0 to stack 10 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 10 STR_VAR match_item = sw1h57 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~hamasu.cre~   ~override~
  LPF cd_equip_item STR_VAR item = "potn08" slot = "quickslot" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "potn20" slot = "quickslot" END // legit item not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = dagg01 END // summons shouldn't drop items
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = potn08 END // summons shouldn't drop items
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = potn20 END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~hellgen.cre~  ~override~
  LPF cd_no_pickpocket STR_VAR item = miscb7 END // move tears of bhaal to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~hgnya01.cre~  ~override~ // undercharged: ring03 (num 5) has assigned 0 to stack 1 charges 100 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = ring03 END // 20 charges
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~hldemi.cre~   ~override~ // undercharged: ring39 (num 1) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 charge2 = 3 STR_VAR match_item = ring39 END // 1 secondary, 3 tertiary charges
  BUT_ONLY

COPY_EXISTING ~hlkang.cre~   ~override~
  LPF cd_no_pickpocket STR_VAR item = ring39 END // ring of gaxx from kangaxx // move ring of gaxx to unpickpocketable slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 charge2 = 3 STR_VAR match_item = ring39 END // 1 secondary, 3 tertiary charges
  BUT_ONLY

COPY_EXISTING ~hlsion.cre~   ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck14" slot = "armor" END // has armor in cloak/quickslots/inventory
  BUT_ONLY

COPY_EXISTING ~hobcap01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "chan04" slot = "armor" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "rndtre03" slot = "inventory" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~icgob02.cre~  ~override~ // icgob02 has overlapping refs
  REMOVE_CRE_ITEM arow01 // delete offending item, then add back manually
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "arow05" slot = "quiver" END // legit item not assigned to slot
  ADD_CRE_ITEM arow01 #20 #0 #0 NONE ~QUIVER2~
  ADD_CRE_ITEM arow01 #20 #0 #0 NONE ~QUIVER3~
  ADD_CRE_ITEM arow01 #20 #0 #0 NONE ~QUIVER4~
  BUT_ONLY

COPY_EXISTING ~igolfle1.cre~ ~override~
              ~igolfle2.cre~ ~override~
              ~igolfle3.cre~ ~override~
              ~igolfle4.cre~ ~override~
  ADD_CRE_ITEM ~golcla~ #0 #0 #0 ~NONE~ ~LRING RRING AMULET~ // lesser clay golems not immune to backstab
  BUT_ONLY

COPY_EXISTING ~imoen.cre~    ~override~
  REMOVE_CRE_ITEM potn08 // change 3x1 healing potions (only one legitimately assigned) into one stack of 3, place in right slot
  ADD_CRE_ITEM potn08 #3 #0 #0 NONE ~QITEM1 QITEM2 QITEM3~
  LPF cd_equip_item STR_VAR item = "potn09" slot = "quickslot" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~impqua01.cre~ ~override~ // both have dupe ring ref in weapon slot instead of weapon
  REMOVE_CRE_ITEM reghp1 impqua // delete offending item, then add back manually
  ADD_CRE_ITEM reghp1 #0 #0 #0 NONE ~LRING~
  ADD_CRE_ITEM impqua #0 #0 #0 NONE ~WEAPON1~ EQUIP
  BUT_ONLY

COPY_EXISTING ~jaerto1.cre~  ~override~
              ~jaerto2.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "sw1h01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~jaga4.cre~    ~override~ // overcharged: sw1h08 (num 4) has assigned 2 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = sw1h08 END // no charges
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "xbow04" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~jan15.cre~    ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "xbow12" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~jarlich.cre~  ~override~ // non-helm in helm slot for non-legit critical protection
  LPF cd_no_pickpocket STR_VAR item = miscbg END // lich's tooth // move ankheg shell to unpickpocketable slot
  BUT_ONLY
  
COPY_EXISTING ~jaylos.cre~   ~override~ // undercharged: dagg05 (num 1) has assigned 0 to stack 10 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 10 STR_VAR match_item = dagg05 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~jondal.cre~   ~override~
              ~plfarm06.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "plat01" slot = "armor" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~jugmim01.cre~ ~override~ // killer mimic
  ADD_CRE_ITEM ~wallpass~ #0 #0 #0 ~NONE~ ~AMULET LRING RRING~ // mimics should actually be inside its chest
  BUT_ONLY

COPY_EXISTING ~kchild1.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "b1-2" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~keldor8.cre~  ~override~ // overcharged: helm01 (num 1) has assigned 20 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = helm01 END // no charges
  BUT_ONLY

COPY_EXISTING ~kobsla01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "arow01" slot = "quiver" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~korgan8.cre~  ~override~
              ~korgan9.cre~  ~override~
              ~korgan11.cre~ ~override~
              ~korgan12.cre~ ~override~
  REMOVE_CRE_ITEM ax1h02 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~kproen03.cre~ ~override~ // undercharged: wand07 (num 1) has assigned 0 to stack 1 charges 50 in header 1
              ~rumar01.cre~  ~override~ // undercharged: wand07 (num 4) has assigned 0 to stack 1 charges 50 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 8 STR_VAR match_item = wand07 END // typical num of charges
  BUT_ONLY

COPY_EXISTING ~kpsold06.cre~ ~override~ // has shield in armor slot, armor not assigned
  REMOVE_CRE_ITEM chan04 shld05 // delete offending item, then add back manually
  ADD_CRE_ITEM chan04 #0 #0 #0 NONE ~ARMOR~
  ADD_CRE_ITEM shld05 #0 #0 #0 NONE ~SHIELD~
  BUT_ONLY

COPY_EXISTING ~kuoarc20.cre~ ~override~ // undercharged: kuobolt3 (num 3) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = kuobolt3 END // stack of 20
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~kuocle20.cre~ ~override~ // both have dupe ring ref in weapon slot instead of weapon
  REMOVE_CRE_ITEM kuoring blun33 // delete offending item, then add back manually
  ADD_CRE_ITEM kuoring #0 #0 #0 NONE ~LRING~
  ADD_CRE_ITEM blun33 #0 #0 #0 NONE ~WEAPON1~ EQUIP
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~lacedo01.cre~ ~override~
  REMOVE_CRE_ITEM ghoul1 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~lacedo02.cre~ ~override~
  REMOVE_CRE_ITEM ghast1 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~laune.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "vamp01" item = "vamp1" END // resref typo (vamp01)
  BUT_ONLY

COPY_EXISTING ~lester.cre~   ~override~
  LPF cd_equip_item STR_VAR item = "mage06" slot = "jewelry" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~loveone5.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck15" slot = "armor" END // has armor in cloak/quickslots/inventory
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~nalia8.cre~   ~override~
              ~nalia10.cre~  ~override~
              ~nalia11.cre~  ~override~
              ~nalia13.cre~  ~override~
              ~nalia15.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "bow05" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~nalia13.cre~ ~override~ // nalia13 has a stack of 0 arrows and a 20-charge bow
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 =  0 STR_VAR match_item = bow05 END
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = arow01 END
  BUT_ONLY

COPY_EXISTING ~neb.cre~      ~override~ // non-helm in helm slot for non-legit critical protection
  LPF cd_no_pickpocket STR_VAR item = misc9h END // move neb's head to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~obsdem01.cre~ ~override~ // s1-10, ringdemn
              ~pmaster.cre~  ~override~ // s1-10, ringdemn
              ~ppdemon.cre~  ~override~ // s1-10, ringdemn
              ~tanari01.cre~ ~override~ // S1-10, ringdemn
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = ringdemn END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = s1-10 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~obsdem04.cre~ ~override~
  REMOVE_CRE_ITEM rods05 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~ogremasu.cre~ ~override~ // sw1h02.itm
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = sw1h02 END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~pcapt03.cre~  ~override~ // undercharged: bolt03 (num 3) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bolt01 END // stack of 20
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bolt03 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~pcapt04.cre~  ~override~ // undercharged: wand05 (num 1) has assigned 0 to stack 1 charges 50 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 10 charge2 = 10 STR_VAR match_item = wand05 END // ten primary, secondary charges
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY

COPY_EXISTING ~pirexe01.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "blunt06" item = "blun06" END // resref typo (blunt06)
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bolt01 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~pirmur02.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "sw1h07" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~pirmur07.cre~ ~override~ // undercharged: dart02 (num 0) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = dart02 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~pirsea01.cre~  ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "leath01" item = "leat01" END // resref typo (leath01)
  BUT_ONLY

COPY_EXISTING ~plfarm03.cre~ ~override~
              ~plmetr01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "shld03" slot = "shield" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~plmetg02.cre~ ~override~
  LPF cd_equip_item STR_VAR "item" = arow07 slot = "quiver" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~pparan2.cre~  ~override~ // overcharged: misc6w (num 7) has assigned 3 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = misc6w END // no charges
  BUT_ONLY

COPY_EXISTING ~ppbhaal.cre~  ~override~
  REMOVE_CRE_ITEM sw1h01 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~ppcowled.cre~ ~override~ // undercharged: misc3a (num 1) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3a END // one charge
  BUT_ONLY

COPY_EXISTING ~ppireni1.cre~ ~override~
              ~ppireni2.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "jonhp1" item = "minhp1" END // changes Asylum Irenicus to use minhp1 instead of jonhp1 (Baldurdash)
  BUT_ONLY

COPY_EXISTING ~ppjoye2.cre~  ~override~ // non-helm in helm slot for non-legit critical protection
  LPF cd_no_pickpocket STR_VAR item = ghost3 END // item w/ visual effects
  BUT_ONLY

COPY_EXISTING ~ppsail03.cre~ ~override~ // has armor in weapon slot, weapon not assigned (fixed later)
  REMOVE_CRE_ITEM leat01 sw1h20 // delete offending item, then add back manually
  ADD_CRE_ITEM sw1h20 #0 #0 #0 NONE ~WEAPON1~ EQUIP
  ADD_CRE_ITEM leat01 #0 #0 #0 NONE ~ARMOR~
  BUT_ONLY

COPY_EXISTING ~pptroll1.cre~ ~override~ // non-helm in helm slot for non-legit critical protection
              ~trolsi01.cre~ ~override~ // non-helm in helm slot for non-legit critical protection
              ~trolsi02.cre~ ~override~ // non-helm in helm slot for non-legit critical protection
              ~trolsp01.cre~ ~override~ // non-helm in helm slot for non-legit critical protection
              ~trolsp02.cre~ ~override~ // non-helm in helm slot for non-legit critical protection
  LPF cd_no_pickpocket STR_VAR item = illblur END // item w/ visual effects, move to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~pries18c.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "blun04" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~prisonk1.cre~ ~override~
  ADD_CRE_ITEM ~bow01~ #0 #0 #0 ~NONE~ ~WEAPON3 WEAPON4~ // arrows but no bow, don't assign as already has weapon equipped
  BUT_ONLY

COPY_EXISTING ~pwarden.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "sw1h35" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~refigh01.cre~ ~override~
  ADD_CRE_ITEM ~bow01~ #0 #0 #0 ~NONE~ ~WEAPON2 WEAPON3 WEAPON4~ // // arrows but no bow, but don't assign as already has weapon equipped
  BUT_ONLY

COPY_EXISTING ~resuna.cre~ ~override~
  REMOVE_CRE_ITEM sw1h01 // delete unassigned item
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h27 END // adds a charge to Arbane on Suna Seni
  BUT_ONLY

COPY_EXISTING ~rethug03.cre~  ~override~ // weapon assigned in two slots
  REMOVE_CRE_ITEM dagg02 // delete offending item, then add back manually
  ADD_CRE_ITEM dagg02 #0 #0 #0 NONE ~WEAPON1~ EQUIP
  BUT_ONLY

COPY_EXISTING ~ribald.cre~   ~override~ // ribald
//  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw1h06" item  = "sw1h41" END // Varscona is unique; repacing all references except Ribald's with generic +2 sword
  LPF cd_equip_weapon END
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "sw1h41" slot = "weapon" END // has armor in cloak/quickslots/inventory
  BUT_ONLY

COPY_EXISTING ~rielev.cre~ ~override~ // Rielev
  REMOVE_CRE_ITEM mage01 // has invis ring in weapon slot, removing

COPY_EXISTING ~rigen02.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck13" slot = "armor" END // has armor in cloak/quickslots/inventory
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY

COPY_EXISTING ~rngsta01.cre~ ~override~
  REMOVE_CRE_ITEMS // has only one item, a null reference
  BUT_ONLY

COPY_EXISTING ~rskel01.cre~  ~override~
              ~skele2.cre~   ~override~
              ~skelet01.cre~ ~override~
              ~skelhp1.cre~  ~override~
  REMOVE_CRE_ITEM misc50 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~rskel03.cre~  ~override~ // rskel03 has dupe entries for throwing axes
  REMOVE_CRE_ITEM ax1h04 // delete offending item, then add back manually
  ADD_CRE_ITEM ax1h04 #10 #0 #0 NONE ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ax1h04 #10 #0 #0 NONE ~WEAPON3~
  BUT_ONLY

COPY_EXISTING ~rumar01.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "staf02" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~saerk.cre~    ~override~ // overcharged: ring34 (num 8) has assigned 4 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = ring34 END // once/day item
  BUT_ONLY

COPY_EXISTING ~sahbeh04.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "potn32" slot = "quickslot" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~sahcpt02.cre~ ~override~
              ~sahramb1.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "sahbolt" slot = "quiver" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~sahextra.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "xbow01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~sahlace.cre~  ~override~
  REMOVE_CRE_ITEM ghoul1 // delete unassigned item
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = all END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~sahoty01.cre~ ~override~
  REMOVE_CRE_ITEM b2-8 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~sahpr4.cre~ ~override~ // prevent dupe CoC items; see also ar2300.bcs, sahkng01.bcs, sahkng01.dlg, sahpr2.dlg, sahtreas.bcs, string sets
  REMOVE_CRE_ITEM misc8q
  BUT_ONLY

COPY_EXISTING ~sahpri02.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "bolt01" slot = "quiver" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~sahrnt01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "bolt04" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "sahbolt" slot = "quiver" END // legit item not assigned to slot
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "sper01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~sahskel.cre~  ~override~ // Skeleton Warrior (ar2003.are -> sahambo3.cre -> sahambo3.bcs)
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = helm08 END // summons shouldn't drop items
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = rndtre05 END // summons shouldn't drop items
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = sw2h02 END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~sahspc01.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "ghost01" item = "ghost" END // resref typo (ghost01)
  BUT_ONLY

COPY_EXISTING ~sahuag01.cre~ ~override~ // overcharged: xbow01 (num 2) has assigned 20 to max 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = xbow01 END // no charges
  BUT_ONLY

COPY_EXISTING ~sarjai01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "sw1h05" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sarkis04.cre~ ~override~ // undercharged: figure01 (num 4) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 0 charge2 = 0 charge3 = 0 STR_VAR match_item = leat02 END // no charges
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = figure01 END // one charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sarmag01.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck15" slot = "armor" END // has armor in cloak/quickslots/inventory
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sarvie01.cre~ ~override~ // undercharged: ring20 (num 4) has assigned 0 to stack 1 charges 50 in header 1
              ~viekang.cre~  ~override~ // undercharged: ring20 (num 6) has assigned 0 to stack 1 charges 50 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 50 STR_VAR match_item = ring20 END // max charges (50)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~scyarryl.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "blun20" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~senani03.cre~ ~override~
              ~sendro03.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "bwbolt01" item = "bolt04" END // resref typo - nearest would would be drow bolts of sleep (dwbolt01), but creature is outdoors
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sendai.cre~   ~override~ // overcharged: wand19 (num 5) has assigned 1 to max 0 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 0 charge3 = 0 STR_VAR match_item = wand19 END // no secondary/tertiary charges
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sendai2.cre~  ~override~
  REMOVE_CRE_ITEM blun21 // delete unassigned item
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sendai3.cre~  ~override~ // undercharged: arow05 (num 3) has assigned 0 to stack 40 charges 1 in header 1
              ~shth03.cre~   ~override~ // undercharged: arow05 (num 3) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = arow05 END // stack of 20
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sewdue01.cre~ ~override~ // undercharged: fragsap (num 2) has assigned 0 to stack 1 charges 1 in header 1
              ~sewyag01.cre~ ~override~ // undercharged: fragsap (num 2) has assigned 0 to stack 1 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = fragsap END // one charge
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sewyag03.cre~  ~override~ // sewyag03 has dupe treasure reference
  REMOVE_CRE_ITEM rndtre05 // delete offending item, then add back manually
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV2~
  ADD_CRE_ITEM rndtre05 #0 #0 #0 NONE ~INV6~
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~shadeld.cre~  ~override~ // overcharged: clck25 (num 0) has assigned 1 to max 0 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 0 charge3 = 0 STR_VAR match_item = clck25 END // no secondary/tertiary charges
  BUT_ONLY

COPY_EXISTING ~shth01.cre~   ~override~ // undercharged: bull01 (num 2) has assigned 0 to stack 40 charges 1 in header 1
              ~shth011.cre~  ~override~ // undercharged: bull01 (num 2) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bull01 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~shth03.cre~   ~override~ // arow05
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = arow05 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~shugmg01.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck16" slot = "armor" END // has armor in cloak/quickslots/inventory
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~stein.cre~    ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "leat04" slot = "armor" END // has armor in cloak/quickslots/inventory
  BUT_ONLY

COPY_EXISTING ~surly.cre~    ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "hamm01" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~suziyaad.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "immune2" slot = "jewelry" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~swshaman.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "sw1h08" slot = "weapon" END // legit item not assigned to slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = misc5t END // undercharged: misc5t (num 1) has assigned 0 to max 1 in header 2
  BUT_ONLY

COPY_EXISTING ~tasloisu.cre~ ~override~
              ~xvartsu.cre~  ~override~
  REMOVE_CRE_ITEM rndtre02 // legit item not assigned to slot, but since it's a summon...
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = all END // it shouldn't drop anything anyway
  BUT_ONLY

COPY_EXISTING ~teltief3.cre~ ~override~ // undercharged: wand18 (num 4) has assigned 0 to max 5 in header 2
  LPF ALTER_CREATURE_ITEM INT_VAR charge2 = 5 expiry = 0 STR_VAR match_item = wand18 END // item shouldn't expire, lacks secondary charges
  LPF cd_equip_item STR_VAR item = "potn55" slot = "quickslot" END // legit item not assigned to slot
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~temsup.cre~   ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "blun03" slot = "weapon" END // have weapons in quickslot/inventory
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY

COPY_EXISTING ~tethto.cre~   ~override~
              ~tethto2.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "chan02" slot = "armor" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "shld04" slot = "shield" END // legit item not assigned to slot
  LPF cd_equip_item STR_VAR item = "blun05" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~thief5.cre~   ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "dagg01" slot = "weapon" END // have weapons in quickslot/inventory
  LPF cd_equip_weapon END
  BUT_ONLY

COPY_EXISTING ~tirthold.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "staff01" item = "staf01" END // resref typo (staff01)
  BUT_ONLY

COPY_EXISTING ~tolger.cre~  ~override~
              ~tolger2.cre~ ~override~
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = ring33 END // ring of the ram should have a charge
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = staf01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = rndtre04 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~tomegol4.cre~ ~override~ // juggernaut golem missing weapon and has wrong immunity
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "immune1" item = "immune2" END
  LPF cd_equip_item STR_VAR item = "goltome4" slot = "weapon" END // legit item not assigned to slot
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~trfued05.cre~ ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shld01" slot = "shield" END // has shield in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~trple01.cre~ ~override~ // Red Willy
  REMOVE_CRE_ITEM xbow01 bolt01 // Creatures shouldn't have two-handed items equipped alongside shields (Nythrun and devSin)

COPY_EXISTING ~trrak01.cre~  ~override~
  LPF cd_no_pickpocket STR_VAR item = misc8k END // move ihtafeer's head to unpickpocketable slot
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = amul22 END // undercharged: amul22 (num 1) has assigned 0 to stack 1 charges 1 in header 1
  BUT_ONLY

COPY_EXISTING ~uddeath2.cre~ ~override~
  REMOVE_CRE_ITEM sw2h01 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~udduer01.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "leat01" slot = "armor" END // legit item not assigned to slot
  LPF cd_equip_weapon END // has wrong/no weapon equipped
  BUT_ONLY

COPY_EXISTING ~udlesa.cre~   ~override~
  REMOVE_CRE_ITEM dwsw1h02 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~udprince.cre~ ~override~
  LPF cd_no_pickpocket STR_VAR item = misca7 END // sahuagin prince's blood to unpickpocketable slot
  BUT_ONLY

COPY_EXISTING ~uhman02.cre~  ~override~ // bolt01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 charge1 = 20 STR_VAR match_item = bolt01 END // stack of 20, shouldn't expire
  BUT_ONLY

COPY_EXISTING ~uhogre03.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "chan04" slot = "armor" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~uhogre04.cre~ ~override~ // arow01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = arow01 END // item shouldn't expire
  BUT_ONLY

COPY_EXISTING ~vakg02.cre~   ~override~ // xbow01
  LPF ALTER_CREATURE_ITEM INT_VAR expiry = 0 STR_VAR match_item = xbow01 END // item shouldn't expire
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = bolt01 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~valyg9.cre~   ~override~
              ~valyg11.cre~  ~override~
              ~valyg12.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "bow03" slot = "weapon" END // have weapons in quickslot/inventory
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "sper01" slot = "weapon" END // have weapons in quickslot/inventory
  BUT_ONLY

COPY_EXISTING ~vulfgt02.cre~ ~override~ // undercharged: dart01 (num 1) has assigned 0 to stack 40 charges 1 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 20 STR_VAR match_item = dart01 END // stack of 20
  BUT_ONLY

COPY_EXISTING ~vvamn1.cre~   ~override~
  REMOVE_CRE_ITEM chan01 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~vvshad1.cre~  ~override~
              ~vvshad4.cre~  ~override~
  LPF cd_equip_item STR_VAR item = "dagg01" slot = "weapon" END // legit item not assigned to slot
  BUT_ONLY

COPY_EXISTING ~weregrdr.cre~ ~override~
  REMOVE_CRE_ITEM s1-12m2 // delete unassigned item
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~wolfwisu.cre~ ~override~ // misc01.itm
              ~wolfwwsu.cre~ ~override~ // Winter Wolf (sppr602.spl -> spanim3.eff -> anisum03.2da)
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = misc01 END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~wraithsu.cre~ ~override~
  REMOVE_CRE_ITEM s1-8 // delete unassigned item
  BUT_ONLY

COPY_EXISTING ~wyvernsu.cre~ ~override~ // Wyvern (spwi619.spl -> wyvernsu.eff)
  LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 flag_undroppable = 1 STR_VAR match_item = misc52 END // summons shouldn't drop items
  BUT_ONLY

COPY_EXISTING ~xappren1.cre~ ~override~ // undercharged: wand03 (num 0) has assigned 0 to stack 1 charges 100 in header 1
  LPF ALTER_CREATURE_ITEM INT_VAR charge1 = 10 STR_VAR match_item = wand03 END // ten primary charges
  BUT_ONLY

COPY_EXISTING ~xgolem.cre~   ~override~
  ADD_CRE_ITEM ~golfle~ #0 #0 #0 ~NONE~ ~RRING~ // add golem immunity items
  BUT_ONLY

COPY_EXISTING ~yaga01.cre~   ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shld32" slot = "shield" END // has shield in quickslot/inventory
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~yoshi11.cre~ ~override~
  ADD_CRE_ITEM ~arow01~ #20 #0 #0 ~NONE~ ~QUIVER1~ // bow but no arrows
  ADD_CRE_ITEM ~arow01~ #20 #0 #0 ~NONE~ ~QUIVER2~
  ADD_CRE_ITEM ~arow01~ #20 #0 #0 ~NONE~ ~QUIVER3~
  BUT_ONLY

COPY_EXISTING ~ysfire02.cre~ ~override~
  REMOVE_CRE_ITEM giafir2 // delete unassigned item
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

// knows class- or alignment-restricted spell
COPY_EXISTING ~aerbod01.cre~ ~override~
  REMOVE_KNOWN_SPELL sppr105 sppr314
  BUT_ONLY

// aerie spellbook fix, should know flame blade
COPY_EXISTING ~aerie6.cre~  ~override~
              ~aerie7.cre~  ~override~
              ~aerie9.cre~  ~override~
              ~aerie10.cre~ ~override~
              ~aerie11.cre~ ~override~
              ~aerie12.cre~ ~override~
  ADD_KNOWN_SPELL ~sppr206~ #1 ~priest~
  BUT_ONLY IF_EXISTS

// aerie7 has an extra star; need to remove spurious club prof
COPY_EXISTING ~aerie7.cre~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 233 match_parameter2 = 115 END // nuke club prof
  BUT_ONLY
  
// norh paladin from rep trap has wrong DV, script
COPY_EXISTING ~alex.cre~ ~override~
  WRITE_ASCII 0x248 ~NOrder~ #8 // override script
  BUT_ONLY

// has typos in spell resrefs
COPY_EXISTING ~amcarras.cre~ ~override~
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    READ_LONG (offset       ) spell_off
    READ_LONG (offset + 0x04) spell_num
    FOR (index = 0 ; index < spell_num ; ++index) BEGIN
      READ_ASCII (spell_off +        (0x0c * index)) spell
      PATCH_IF ("%spell%" STRING_COMPARE_REGEXP "^spw[0-9][0-9][0-9]$" = 0) BEGIN
        READ_ASCII   (spell_off + 0x03 + (0x0c * index)) code (3) // just grab the three digit number
        WRITE_ASCIIE (spell_off +        (0x0c * index)) ~spwi%code%~
      END
    END
  END
  BUT_ONLY IF_EXISTS

// mem & known spell resref typos
COPY_EXISTING ~amelm01.cre~  ~override~
              ~jatermin.cre~ ~override~
  REMOVE_KNOWN_SPELL     sppr112 spwi122 spwi904 spwi906 // don't exist, no map
  REMOVE_MEMORIZED_SPELL sppr112 spwi122 spwi904 spwi906 // don't exist, no map
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN
    spwi121 => spwi125 // doesn't exist, map to spook
    spwi610 => spwi624 // doesn't exist, map to summon nishruu
    spwi706 => spwi719 // doesn't exist, map to summon hakeashar
    spwi709 => spwi718 // doesn't exist, map to summon djinni
    spwi801 => spwi802 // doesn't exist, map to spell deflection
    spwi806 => spwi816 // doesn't exist, map to symbol stun
    spwi814 => spwi817 // doesn't exist, map to symbol death
    spwi901 => spwi917 // doesn't exist, map to freedom
  END
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    LPF cd_spellbook_sub END
  END
  BUT_ONLY IF_EXISTS

// knows class- or alignment-restricted spell
COPY_EXISTING ~ammajira.cre~ ~override~
              ~chgood09.cre~ ~override~
  REMOVE_KNOWN_SPELL sppr506 sppr517 sppr605 sppr702 sppr717
  BUT_ONLY IF_EXISTS

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~ammonk05.cre~ ~override~
  WRITE_LONG 0xa4 61994 //Do not anger me.
  WRITE_LONG 0xb8 "-1"
  WRITE_LONG 0xc8 61995 //You shall pay for this, and pay dearly.
  WRITE_LONG 0xcc 61996 //Suffer!
  WRITE_LONG 0xec 61997
  WRITE_LONG 0xf0 61998
  WRITE_LONG 0x10c 61993 //As always, the pleasure is mine.
  BUT_ONLY IF_EXISTS

// eliminates non-STs from using ST soundset
COPY_EXISTING ~amsi.cre~     ~override~
              ~gpthief1.cre~ ~override~
  WRITE_LONG 0xcc 0xffffffff // was "No one crosses the Shadow Thieves... and lives!"
  BUT_ONLY

// anomen save fix
COPY_EXISTING ~anomen6.cre~ ~override~
  WRITE_BYTE 0x57 12 // save v breath
  BUT_ONLY IF_EXISTS

// anomen spellbook fix, remove fire seeds, profs, specifics, portrait
COPY_EXISTING ~anomen6.cre~  ~override~
              ~anomen7.cre~  ~override~
              ~anomen8.cre~  ~override~
              ~anomen9.cre~  ~override~
              ~anomen10.cre~ ~override~
              ~anomen12.cre~ ~override~
  READ_BYTE 0x235 level
  WRITE_BYTE 0x274 0 // anomen specifics fix
  ADD_KNOWN_SPELL ~sppr206~ #1 ~priest~ // flame blade
  ADD_KNOWN_SPELL ~sppr314~ #2 ~priest~ // unholy blight
  ADD_KNOWN_SPELL ~sppr414~ #3 ~priest~ // cause serious wounds
  PATCH_IF level > 10 BEGIN
    REMOVE_KNOWN_SPELL sppr606 sppr607 // fire seeds (shouldn't know), heal (memorized twice)
    ADD_KNOWN_SPELL ~sppr607~ #5 ~priest~ // restore heal
    CLEAR_ARRAY cd_spell_sub
    DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
      sppr606 => sppr607 // fire seeds to heal
    END
    PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
      LPF cd_spellbook_sub END
    END
  END
  WRITE_ASCII 0x3c ~NANOMENM~  // portrait fix for anomen9 in SoA
  // anomen short 4 stars from f > c dual (revised by Wisp)
  SET_BG2_PROFICIENCY 98 2     //spears
  SET_BG2_PROFICIENCY 102 1    //staffs
  SET_BG2_PROFICIENCY 112 2    //sword & shield
  SET_BG2_PROFICIENCY 97 (level < 12) ? 1 : 2 //war hammers (1 at low level two at high)
  BUT_ONLY IF_EXISTS

// remove invisible flag from visible creatures
COPY_EXISTING ~arnman11.cre~ ~override~
              ~arnwar03.cre~ ~override~
  WRITE_BYTE 0x20 (THIS BAND `BIT4) // remove the STATE_INVISIBILITY flag
  BUT_ONLY

COPY_EXISTING ~bazpat03.cre~ ~override~ // typo
  WRITE_ASCII 0x268 ~wtasight~ #8
  BUT_ONLY IF_EXISTS

// master summons fix: These are all player accessible (inventory patched in previous section)
COPY_EXISTING ~bearblsu.cre~ ~override~ // Black Bear (sppr501.spl -> spanim2.eff -> anisum02.2da)
              ~bearbrsu.cre~ ~override~ // Brown Bear (sppr501.spl -> spanim2.eff -> anisum02.2da)
              ~bearcasu.cre~ ~override~ // Cave Bear (sppr501.spl -> spanim2.eff -> anisum02.2da) (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~bearposu.cre~ ~override~ // Mountain Bear (sppr602.spl -> spanim3.eff -> anisum03.2da) (sppr604.spl -> spanim04.eff -> anisum04.2da)
              ~catliosu.cre~ ~override~ // Lion (sppr501.spl -> spanim2.eff -> anisum02.2da) (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~catliowp.cre~ ~override~ // Joolon (misc3d.itm -> figlion.eff)
              ~demglasu.cre~ ~override~ // Glabrezu (spwi807.spl -> spfiend.eff)
              ~djinnisu.cre~ ~override~ // Djinni (spwi718.spl -> spdj.eff)
              ~dogwasu.cre~  ~override~ // War Dog (sppr402.spl -> spani01.eff -> anisum01.2da)
              ~dogwisu.cre~  ~override~ // Rabid Dog (spwi309.spl -> spmon1.eff -> spwi309.spl)
              ~efreetsu.cre~ ~override~ // Efreeti (spwi717.spl -> spefreet.eff)
              ~ettercsu.cre~ ~override~ // Ettercap (spwi503.spl -> spmon3.eff -> monsum03.2da)
              ~ghastsu.cre~  ~override~ // Skeleton (sppr301.spl/spwi501.spl -> spandead.eff)
              ~gnollsu.cre~  ~override~ // Gnoll Elite (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~haksu.cre~    ~override~ // Hakeashar (spwi719.spl -> sphack.eff)
              ~hobgobsu.cre~ ~override~ // Hobgoblin Elite (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~koboldsu.cre~ ~override~ // Kobold Commando (spwi309.spl -> spmon1.eff -> spwi309.spl
              ~nishrusu.cre~ ~override~ // Nishruu (spwi624.spl -> spnish.eff)
              ~nymphsu.cre~  ~override~ // Nymph (sppr410.spl -> nymphsu.eff)
              ~ogregrsu.cre~ ~override~ // Ogre Berserker (spwi503.spl -> spmon3.eff -> monsum03.2da)
              ~rabbitsu.cre~ ~override~ // Rabbit (spwi919 -> wish02.cre -> spin735.spl -> rabbitsu.eff)
              ~servsu.cre~   ~override~ // Aerial Servant (sppr601.spl -> spserv.eff)
              ~skelwasu.cre~ ~override~ // Skeleton Warrior (sppr301.spl/spwi501 -> spandl15.eff)
              ~smoundsu.cre~ ~override~ // Shambling Mound (staf14.itm -> mound.eff)
              ~spidgisu.cre~ ~override~ // Giant Spider (spwi423.spl -> spidgisu.eff)
              ~spidphsu.cre~ ~override~ // Phase Spider (spwi423.spl -> spidphsu.eff)
              ~spidswsu.cre~ ~override~ // Sword Spider (spwi423.spl -> spidswsu.eff)
              ~stalke.cre~   ~override~ // Invisible Stalker (spwi601.spl -> spstalk.eff)
              ~sumdjinn.cre~ ~override~ // Djinni (ring26.itm -> sumdjinn.eff)
              ~sumelair.cre~ ~override~ // Lesser Air Elemental (staf15.itm -> staf15.eff)
              ~sumefree.cre~ ~override~ // Efreeti (misc3c.itm -> sumefr.eff)
              ~sumelear.cre~ ~override~ // Lesser Earth Elemental (staf16.itm -> staf16.eff)
              ~sumelfir.cre~ ~override~ // Lesser Fire Elemental (staf17.itm -> staf17.eff)
              ~tomegol1.cre~ ~override~ // Flesh Golem (tome01.itm -> tome01.eff)
              ~tomegol2.cre~ ~override~ // Clay Golem (tome02.itm -> tome02.eff)
              ~tomegol3.cre~ ~override~ // Stone Golem (tome03.itm -> tome03.eff
              ~tomegol4.cre~ ~override~ // Juggernaut Golem (tome04.itm -> tome04.eff)
              ~wish01.cre~   ~override~ // wish djinni - overwritten by wish02, but only in ToB games
              ~wish02.cre~   ~override~ // Djinni (spwi919.spl)
              ~wolfdisu.cre~ ~override~ // Dire Wolf (sppr402.spl -> spani01.eff -> anisum01.2da) (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~wolfwwsu.cre~ ~override~ // Winter Wolf (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~worgsu.cre~   ~override~ // Worg (spwi309.spl -> spmon1.eff -> spwi309.spl)
              ~wyvernsu.cre~ ~override~ // Wyvern (spwi619.spl -> wyvernsu.eff)
              // and these are enemy only but summoned in a familiar way
              ~c6guen.cre~   ~override~ // Guenhwyvar (c6drizz.cre -> c6drizz1.dlg)
              ~sahangu.cre~  ~override~ // Anguiliian (ar2300.are -> sahambo6.cre -> sahambo6.bcs)
              ~sahlace.cre~  ~override~ // Lacedon (ar2003.are -> sahambo3.cre -> sahambo3.bcs)
              ~sahskel.cre~  ~override~ // Skeleton Warrior (ar2003.are -> sahambo3.cre -> sahambo3.bcs)
              ~sahzomb.cre~  ~override~ // Sea Zombie (ar2003.are -> sahambo3.cre -> sahambo3.bcs)
              ~senstalk.cre~ ~override~ // Invisible Stalker (ar6108.bcs)
              ~telelfir.cre~ ~override~ // Greater Fire Elemental (ar3010.are -> teltan1.cre -> teltan1.bcs -> spin570.spl -> sutelfir.eff)
//              ~telicesa.cre~ ~override~ // Ice Salamander (ar3010.are -> teltan2.cre -> teltan2.bcs -> spin569.spl -> sutelice.eff) no changes needed (XP/gold 0, no droppable items)
              // These are unused but of obvious intent for UB authors - spend the day ironing your doilies and polishing your lawn instead
              /*
              ~basilgsu.cre~ ~override~ // Greater Basilisk (spwi8)
              ~basillsu.cre~ ~override~ // Lesser Basilisk (spwi610.spl)
              ~beargrsu.cre~ ~override~ // Grizzly Bear
              ~catjagsu.cre~ ~override~ // Panther
              ~ghastgsu.cre~ ~override~ // Greater Ghast
              ~gibbersu.cre~ ~override~ // Gibberling
              ~hamasu.cre~   ~override~ // Hamadryad
              ~jaguarsu.cre~ ~override~ // Jaguar
              ~jellmusu.cre~ ~override~ // Mustard Jelly (spwi610.spl)
              ~ogremasu.cre~ ~override~ // Ogre Mage (spwi610.spl)
              ~sumtan01.cre~ ~override~ // Tanar'ri
              ~sumtan02.cre~ ~override~ // Tanar'ri
              ~tasloisu.cre~ ~override~ // Tasloi Elite Trooper (spwi706.spl)
              ~wolfsu.cre~   ~override~ // Wolf
              ~wolfwisu.cre~ ~override~ // Winter Wolf (spwi610.spl)
              ~wraithsu.cre~ ~override~ // Wraith
              ~xvartsu.cre~  ~override~ // Xvart Protector
              */
  WRITE_LONG 0x001c 0x00  // gold, just hobgobsu.cre
  WRITE_BYTE 0x0240 0x00  // "Don't check morale"
  BUT_ONLY IF_EXISTS

// set 0 xp for player-accessible summons
COPY_EXISTING ~bearblsu.cre~ ~override~ // Black Bear (sppr501.spl -> spanim2.eff -> anisum02.2da)
              ~bearbrsu.cre~ ~override~ // Brown Bear (sppr501.spl -> spanim2.eff -> anisum02.2da)
              ~bearcasu.cre~ ~override~ // Cave Bear (sppr501.spl -> spanim2.eff -> anisum02.2da) (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~bearposu.cre~ ~override~ // Mountain Bear (sppr602.spl -> spanim3.eff -> anisum03.2da) (sppr604.spl -> spanim04.eff -> anisum04.2da)
              ~catliosu.cre~ ~override~ // Lion (sppr501.spl -> spanim2.eff -> anisum02.2da) (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~catliowp.cre~ ~override~ // Joolon (misc3d.itm -> figlion.eff)
              ~demglasu.cre~ ~override~ // Glabrezu (spwi807.spl -> spfiend.eff)
              ~djinnisu.cre~ ~override~ // Djinni (spwi718.spl -> spdj.eff)
              ~dogwasu.cre~  ~override~ // War Dog (sppr402.spl -> spani01.eff -> anisum01.2da)
              ~dogwisu.cre~  ~override~ // Rabid Dog (spwi309.spl -> spmon1.eff -> spwi309.spl)
              ~efreetsu.cre~ ~override~ // Efreeti (spwi717.spl -> spefreet.eff)
              ~ettercsu.cre~ ~override~ // Ettercap (spwi503.spl -> spmon3.eff -> monsum03.2da)
              ~ghastsu.cre~  ~override~ // Skeleton (sppr301.spl/spwi501.spl -> spandead.eff)
              ~gnollsu.cre~  ~override~ // Gnoll Elite (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~haksu.cre~    ~override~ // Hakeashar (spwi719.spl -> sphack.eff)
              ~hobgobsu.cre~ ~override~ // Hobgoblin Elite (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~koboldsu.cre~ ~override~ // Kobold Commando (spwi309.spl -> spmon1.eff -> spwi309.spl
              ~nishrusu.cre~ ~override~ // Nishruu (spwi624.spl -> spnish.eff)
              ~nymphsu.cre~  ~override~ // Nymph (sppr410.spl -> nymphsu.eff)
              ~ogregrsu.cre~ ~override~ // Ogre Berserker (spwi503.spl -> spmon3.eff -> monsum03.2da)
              ~rabbitsu.cre~ ~override~ // Rabbit (spwi919 -> wish02.cre -> spin735.spl -> rabbitsu.eff)
              ~servsu.cre~   ~override~ // Aerial Servant (sppr601.spl -> spserv.eff)
              ~skelwasu.cre~ ~override~ // Skeleton Warrior (sppr301.spl/spwi501 -> spandl15.eff)
              ~smoundsu.cre~ ~override~ // Shambling Mound (staf14.itm -> mound.eff)
              ~spidgisu.cre~ ~override~ // Giant Spider (spwi423.spl -> spidgisu.eff)
              ~spidphsu.cre~ ~override~ // Phase Spider (spwi423.spl -> spidphsu.eff)
              ~spidswsu.cre~ ~override~ // Sword Spider (spwi423.spl -> spidswsu.eff)
              ~stalke.cre~   ~override~ // Invisible Stalker (spwi601.spl -> spstalk.eff)
              ~sumdjinn.cre~ ~override~ // Djinni (ring26.itm -> sumdjinn.eff)
              ~sumelair.cre~ ~override~ // Lesser Air Elemental (staf15.itm -> staf15.eff)
              ~sumefree.cre~ ~override~ // Efreeti (misc3c.itm -> sumefr.eff)
              ~sumelear.cre~ ~override~ // Lesser Earth Elemental (staf16.itm -> staf16.eff)
              ~sumelfir.cre~ ~override~ // Lesser Fire Elemental (staf17.itm -> staf17.eff)
              ~tomegol1.cre~ ~override~ // Flesh Golem (tome01.itm -> tome01.eff)
              ~tomegol2.cre~ ~override~ // Clay Golem (tome02.itm -> tome02.eff)
              ~tomegol3.cre~ ~override~ // Stone Golem (tome03.itm -> tome03.eff
              ~tomegol4.cre~ ~override~ // Juggernaut Golem (tome04.itm -> tome04.eff)
              ~wish01.cre~   ~override~ // wish djinni - overwritten by wish02, but only in ToB games
              ~wish02.cre~   ~override~ // Djinni (spwi919.spl)
              ~wolfdisu.cre~ ~override~ // Dire Wolf (sppr402.spl -> spani01.eff -> anisum01.2da) (spwi407.spl -> spmon2.eff -> monsum02.2da)
              ~wolfwwsu.cre~ ~override~ // Winter Wolf (sppr602.spl -> spanim3.eff -> anisum03.2da)
              ~worgsu.cre~   ~override~ // Worg (spwi309.spl -> spmon1.eff -> spwi309.spl)
              ~wyvernsu.cre~ ~override~ // Wyvern (spwi619.spl -> wyvernsu.eff)
  WRITE_LONG 0x0014 0x00  // XP
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bonebld.cre~ ~override~
              ~sword01.cre~ ~override~
  WRITE_BYTE 0x23a 25 // lower int to allowed range
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~boy1.cre~     ~override~ //typo
              ~vvstand3.cre~ ~override~ //typo
  WRITE_ASCII 0x258 ~runenemy~ #8
  BUT_ONLY

// Lady Beth
COPY_EXISTING ~bystand2.cre~ ~override~
  WRITE_ASCII 0x250 ~~ #8 // blanks old bg script
  BUT_ONLY

// catti should not attack mages randomly
COPY_EXISTING ~c6catti.cre~ ~override~
  WRITE_ASCII 0x0250 ~~ #8
  BUT_ONLY

// added paladiny stats to c6eric; replaces harbinger with party-friendly version (see cdsw2h07.itm)
// both are also patched to LG alignment in the Oversight alignment section
COPY_EXISTING ~c6eric.cre~  ~override~
              ~c6eric3.cre~ ~override~
  WRITE_BYTE 0x238 15 // str
  WRITE_BYTE 0x23b 16 // wis
  WRITE_BYTE 0x23d 12 // con
  WRITE_BYTE 0x23e 18 // chr
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "sw2h07" item = "cdsw2h07" END // replaces harbinger with party-friendly version
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Males with female sounds
COPY_EXISTING ~c6nerit.cre~ ~override~
  WRITE_LONG 0xa4 61724 //I welcome you with outstretched hand.
  WRITE_LONG 0xb8 61732 //May the Gods protect me!
  WRITE_LONG 0xc8 61729 //Justice shall be swift and final!
  WRITE_LONG 0xec 61730
  WRITE_LONG 0xf0 61731
  WRITE_LONG 0x10c 61725 //I trust you are here in good faith.
  WRITE_LONG 0x110 61726 //May the Gods look upon you kindly.
  WRITE_LONG 0x114 61724 //I welcome you with outstretched hand.
  WRITE_LONG 0x118 61727 //All of the faithful are welcome, here.
  BUT_ONLY

// cernd spellbook fixes
COPY_EXISTING ~cernd10.cre~  ~override~
              ~cernd12.cre~  ~override~
              ~cernd13.cre~  ~override~
              ~cernd13b.cre~ ~override~
              ~cernd14.cre~  ~override~
  REMOVE_KNOWN_SPELL sppr318 sppr405 sppr507 // remove ZoSA, mental domination, champion's strength
  ADD_KNOWN_SPELL ~sppr317~ #2 ~priest~ // add cure disease
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
    sppr507 => sppr506 // iron skins for champion's strength
    sppr405 => sppr410 // call woodland beings for mental domination
    sppr318 => sppr317 // cure disease for ZoSA
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
    LPF cd_spellbook_sub END
  END
  BUT_ONLY IF_EXISTS

// cernd12 missing star in single weapon style
COPY_EXISTING ~cernd12.cre~  ~override~
  SET_BG2_PROFICIENCY 113 1
  BUT_ONLY

// highest level cernds missing their override script, removing BG profs
COPY_EXISTING ~cernd13.cre~  ~override~
              ~cernd13b.cre~ ~override~
  WRITE_BYTE  0x6e 0
  WRITE_BYTE  0x6f 0
  WRITE_BYTE  0x70 0
  WRITE_BYTE  0x75 0
  WRITE_ASCII 0x248 ~CERND~ #8
  BUT_ONLY

// ToB cernd also missing his override script
COPY_EXISTING ~cernd14.cre~ ~override~
  WRITE_ASCII 0x248 ~CERN25~ #16 // extra-long write to blank dupe class script
  WRITE_BYTE  0x6e 0
  WRITE_BYTE  0x6f 0
  WRITE_BYTE  0x70 0
  WRITE_BYTE  0x75 0
  BUT_ONLY IF_EXISTS

//Sahuagin with a gnoll soundset; strip him of all sounds, as that is how other Sahuagin roll (Wisp)
COPY_EXISTING ~chevil04.cre~ ~override~
  PATCH_FOR_EACH offset IN 0xc8 0xcc 0xec 0xf0 0x10c 0x110 BEGIN
    WRITE_LONG offset "-1"
  END
  BUT_ONLY IF_EXISTS

// prevents Tamoko from running away and making PP challenge unfinishable, see cdtamoko.bcs for other half
COPY_EXISTING ~chtaz02.cre~ ~override~
  WRITE_ASCII 0x268 ~cdtamoko~ #8
  BUT_ONLY IF_EXISTS

//Tabitha should be immune to charm (Wisp); extra effects fleshed out in immunity batches
COPY_EXISTING ~coplion.cre~ ~override~
  LPF ADD_CRE_EFFECT INT_VAR opcode = 101 parameter2 = 5 timing = 9 END
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Males with female sounds
COPY_EXISTING ~cowenf1.cre~ ~override~
  WRITE_LONG 0xa4 61862 //Declare yourself!
  WRITE_LONG 0xb8 61869 //Retreat!
  WRITE_LONG 0xc8 61866 //I'll test your mettle with cold steel!
  WRITE_LONG 0xec 61867
  WRITE_LONG 0xf0 61868
  WRITE_LONG 0x10c 61863 //Do not disturb my duties.
  WRITE_LONG 0x110 61864 //At ease, citizen.
  WRITE_LONG 0x114 61865 //I trust you have no hostile intentions.
  WRITE_LONG 0x118 61862 //Declare yourself!
  BUT_ONLY

COPY_EXISTING ~dadrow9.cre~ ~override~ // typo
  WRITE_ASCII 0x250 ~mage8a~ #8
  BUT_ONLY

COPY_EXISTING ~dadrow10.cre~ ~override~ //typo
  WRITE_ASCII 0x250 ~pries10b~ #8
  BUT_ONLY

COPY_EXISTING ~dadrow12.cre~ ~override~ //typo
  WRITE_ASCII 0x250 ~pries14t~ #8
  BUT_ONLY

COPY_EXISTING ~dadrow15.cre~ ~override~ //typo
  WRITE_ASCII 0x258 ~mage8a~ #8
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~daelf.cre~ ~override~
  WRITE_LONG 0xa4 11107 //Yeah?
  WRITE_LONG 0xec 11167
  WRITE_LONG 0xf0 11168
  WRITE_LONG 0x10c 11119 //Hello there.
  WRITE_LONG 0x110 11070 //Few of the fair folk concern themselves with the affairs of the state.
  WRITE_LONG 0x114 "-1"
  BUT_ONLY

COPY_EXISTING ~daghaun1.cre~ ~override~ //typo
  WRITE_ASCII 0x258 ~pries14t~ #8
  BUT_ONLY

//Dawnmaster Kreel's amazing wardrobe change (Wisp), part 2/2
COPY_EXISTING ~dawnmas.cre~ ~override~
  SET off = 0x2c
  PATCH_FOR_EACH colour IN 226 238 237 201 228 226 200 BEGIN //Values courtesy of cscleric.cre
    WRITE_BYTE off colour
    SET off += 1
  END

// lots o' bugs in ddguard series, easier to clone a friend
OUTER_SET num = 15
COPY_EXISTING ~ddguard1.cre~ ~override/ddguard2.cre~
              ~ddguard1.cre~ ~override/ddguard3.cre~
              ~ddguard1.cre~ ~override/ddguard4.cre~
              ~ddguard1.cre~ ~override/ddguard5.cre~
              ~ddguard1.cre~ ~override/ddguard6.cre~
  WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // dv
  WRITE_ASCIIE 0x2cc ~%DEST_RES%~  #8 // dialogue file
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "key14" item = EVAL ~key%num%~ END // key14 to key15 - 19
  SET num += 1
  BUT_ONLY

// illegal CON scores
COPY_EXISTING ~deadb01.cre~  ~override~
              ~deadb02.cre~  ~override~
              ~deadb03.cre~  ~override~
              ~firwlf01.cre~ ~override~
              ~firwlf02.cre~ ~override~
              ~rngwlf01.cre~ ~override~
              ~rngwlf02.cre~ ~override~
              ~rngwlf03.cre~ ~override~
              ~rngwlf04.cre~ ~override~
              ~rngwlf05.cre~ ~override~
  WRITE_BYTE 0x23d 25 // lower con to allowed range
  BUT_ONLY

COPY_EXISTING ~demgla01.cre~ ~override~ // has summoned script, so destroys allies in shade lord dungeon
              ~demnab02.cre~ ~override~ // script reference broken, changed so they'll use actual powers instead of just melee
  WRITE_ASCII 0x248 ~tanari~ #8
  BUT_ONLY

// The Glabrezus in the Ranger stronghold quest and inside the Suldanessalar temple don't give any XP when killed (aVENGER)
COPY_EXISTING ~demgla01.cre~   ~override~ // Ranger stronghold Glabrezu
              ~demglab2.cre~   ~override~ // Suldanessalar temple Glabrezu
  WRITE_LONG 0x14 12500 // XP value
  BUT_ONLY

COPY_EXISTING ~dempit01.cre~ ~override~ //assigned to dempit.cre
              ~telpit2.cre~  ~override~ //assigned to dempit.cre
  WRITE_ASCII 0x248 ~dempit~ #8 // typo, was using dempit01
  BUT_ONLY IF_EXISTS

// ferric runs away since he uses Yoshimo's combat script
COPY_EXISTING ~deshar.cre~ ~override~
  WRITE_ASCII 0x258 ~thief14t~ #8
  BUT_ONLY

// fallen devas using normal deva script
COPY_EXISTING ~devaevil.cre~ ~override~
  WRITE_ASCII 0x248 ~devaevil~ // override script
  BUT_ONLY IF_EXISTS

// fixes good deva's spells
// sets devas to lev 25 to match fallen devas and planetars
COPY_EXISTING ~devagood.cre~  ~override~
  WRITE_BYTE 0x234 25 // level
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ (2) // add two castings of cure critical wounds to make even with devaevil
  ADD_MEMORIZED_SPELL ~sppr725~ #6 ~priest~ (2) // add two castings of globe of blades to make even with devaevil
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~drow06.cre~   ~override~
              ~uddrow19.cre~ ~override~
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN
    spwi801 => spwi811 // doesn't exist, map to Symbol Fear
  END
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    LPF cd_spellbook_sub END
  END
  BUT_ONLY

// add monhp1 item to trolls to prevent death; assign script to force transformation to dead form at low HP
COPY_EXISTING ~drshnl01.cre~ ~override~
              ~eletro01.cre~ ~override~
              ~kptrol03.cre~ ~override~
              ~pptroll1.cre~ ~override~
              ~sutroll.cre~  ~override~
              ~torgal.cre~   ~override~
  ADD_CRE_ITEM ~monhp1~ #0 #0 #0 ~NONE~ ~AMULET~
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "kptrol03" = 0) BEGIN
    WRITE_ASCII 0x248 ~kptrol23~
  END ELSE
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "drshnl01" = 0) BEGIN
    WRITE_ASCII 0x248 ~drshnl11~
    WRITE_ASCII 0x258 ~drshnl01~ // restore Nith's attack script, pt 2: assign script; see drshnl01.bcs
  END ELSE
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "eletro01" = 0) BEGIN
    WRITE_ASCII 0x248 ~eletro03~
  END ELSE
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "pptroll1" = 0) BEGIN
    WRITE_ASCII 0x248 ~pptroll2~
  END ELSE
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "sutroll" = 0) BEGIN
    WRITE_ASCII 0x248 ~sutroll2~
  END ELSE
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "torgal" = 0) BEGIN
    WRITE_BYTE  0x234 16 // torgal's HD are exceedingly low, making her vulnerable to cloudkill
    WRITE_ASCII 0x258 ~torgal2~ #8 // race script
  END

// one-off change for Nilthiri to prevent repeating dialogue after regenerating
COPY_EXISTING ~drshnl01.cre~ ~override/drshnl21.cre~
  WRITE_ASCII 0x250 ~~ #8 // no approach and speak script
  WRITE_BYTE  0x270 255   // enemy
  WRITE_ASCII 0x2cc ~~ #8 // no dialogue file

// troll cook gets her own sequence to maintain her name
COPY_EXISTING ~firtrl01.cre~ ~override~
  WRITE_ASCII 0x248 ~firtrl02~ // override script
  BUT_ONLY

// create 'dead' or 'knocked down' versions of 'special' trolls (revised by Nythrun)
COPY_EXISTING ~drshnl01.cre~ ~override/drshnl11.cre~
              ~eletro01.cre~ ~override/eletro03.cre~
              ~firtrl01.cre~ ~override/firtrl02.cre~ // troll cook
              ~kptrol03.cre~ ~override/kptrol23.cre~
              ~pptroll1.cre~ ~override/pptroll2.cre~
              ~sutroll.cre~  ~override/sutroll2.cre~
              ~torgal.cre~   ~override/torgal2.cre~
  WRITE_SHORT    0x24    1 // current HP
  WRITE_SHORT    0x46   10 // natural AC
  WRITE_SHORT    0x48   10 // effective AC
  WRITE_BYTE     0x5a  100 // resist cold
  WRITE_BYTE     0x5b  100 // resist electricity
  WRITE_BYTE     0x5f  100 // resist magic cold
  WRITE_BYTE     0x60  100 // resist slashing
  WRITE_BYTE     0x61  100 // resist crushing
  WRITE_BYTE     0x62  100 // resist piercing
  WRITE_BYTE     0x63  100 // resist missile
  WRITE_BYTE     0x23c   9 // dexterity
  WRITE_BYTE     0x270 255 // enemy
  PATCH_IF          "%SOURCE_RES%" STRING_EQUAL_CASE kptrol03 BEGIN
    WRITE_ASCII  0x248 kptrol13 #40   // override script
  END ELSE PATCH_IF "%SOURCE_RES%" STRING_EQUAL_CASE drshnl01 BEGIN
    WRITE_ASCII  0x248 drshnl21 #40
  END ELSE PATCH_IF "%SOURCE_RES%" STRING_EQUAL_CASE torgal   BEGIN
    WRITE_ASCII  0x248 torgal3 #40
  END ELSE BEGIN
    WRITE_ASCIIE 0x248 ~%SOURCE_RES%~ #40 // new script
  END
  WRITE_ASCII    0x2cc ~~ #8          // blanks dialog file
  REMOVE_CRE_ITEM  minhp1 monhp1 trollreg trollspi
  REPLACE_CRE_ITEM trolldie #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ BELT

// makes druidad DestroySelf() if player is great druid; druidad.baf is compiled in the folder
COPY_EXISTING ~druidad.cre~ ~override~
  WRITE_ASCII 0x248 ~druidad~ #8 // override script
  WRITE_ASCII 0x250 ~initdlg~ #8 // class script
  BUT_ONLY

// edwin spell slot fix
// this was long and ugly, now it's just long; thanks ADD_KNOWN/MEMORIZED_SPELL!
// all of the xp > 120000 checks are basically for ToB edwin, who has a more diverse spellbook
COPY_EXISTING ~edwin7.cre~  ~override~
              ~edwin9.cre~  ~override~
              ~edwin11.cre~ ~override~
              ~edwin12.cre~ ~override~
              ~edwin13.cre~ ~override~
              ~edwin15.cre~ ~override~
  READ_LONG 0x018 xp
  READ_BYTE 0x234 level
  REMOVE_MEMORIZED_SPELLS // nuke all memorized
  ADD_MEMORIZED_SPELL ~spwi101~ #0 ~wizard~ (1) // grease
  ADD_MEMORIZED_SPELL ~spwi102~ #0 ~wizard~ (1) // armor
  ADD_MEMORIZED_SPELL ~spwi104~ #0 ~wizard~ (1) // charm person
  ADD_MEMORIZED_SPELL ~spwi112~ #0 ~wizard~ (2) // magic missile
  ADD_MEMORIZED_SPELL ~spwi119~ #0 ~wizard~ (1) // larloch's minor drain
  ADD_MEMORIZED_SPELL ~spwi205~ #1 ~wizard~ (1) // reflected image
  ADD_MEMORIZED_SPELL ~spwi211~ #1 ~wizard~ (1) // melf's acide arrow
  ADD_MEMORIZED_SPELL ~spwi212~ #1 ~wizard~ (1) // Mirror Image
  ADD_MEMORIZED_SPELL ~spwi215~ #1 ~wizard~ (1) // Web
  ADD_MEMORIZED_SPELL ~spwi220~ #1 ~wizard~ (1) // Power Word Sleep
  ADD_MEMORIZED_SPELL ~spwi302~ #2 ~wizard~ (1) // Remove Magic
  ADD_MEMORIZED_SPELL ~spwi303~ #2 ~wizard~ (1) // Flame Arrow
  ADD_MEMORIZED_SPELL ~spwi304~ #2 ~wizard~ (1) // Fireball
  ADD_MEMORIZED_SPELL ~spwi305~ #2 ~wizard~ (1) // Haste
  ADD_MEMORIZED_SPELL ~spwi309~ #2 ~wizard~ (1) // Monster Summoning I
  ADD_MEMORIZED_SPELL ~spwi314~ #2 ~wizard~ (1) // Vampiric Touch
  ADD_MEMORIZED_SPELL ~spwi403~ #3 ~wizard~ (1) // Fire Shield (Blue)
  ADD_MEMORIZED_SPELL ~spwi406~ #3 ~wizard~ (1) // Minor Globe of Invulneribility
  ADD_MEMORIZED_SPELL ~spwi407~ #3 ~wizard~ (1) // Monster Summoning II
  ADD_MEMORIZED_SPELL ~spwi416~ #3 ~wizard~ (1) // Polymorph Self
  ADD_MEMORIZED_SPELL ~spwi504~ #4 ~wizard~ (1) // Monster Summoning III
  ADD_MEMORIZED_SPELL ~spwi505~ #4 ~wizard~ (1) // Shadow Door
  ADD_MEMORIZED_SPELL ~spwi516~ #4 ~wizard~ (1) // Conjure Lesser Fire Elemental
  ADD_MEMORIZED_SPELL ~spwi522~ #4 ~wizard~ (1) // Minor Spell Turning
  PATCH_IF (xp > 1200000) BEGIN
    ADD_MEMORIZED_SPELL ~spwi120~ #0 ~wizard~ (1) // reflected image
    ADD_MEMORIZED_SPELL ~spwi201~ #1 ~wizard~ (1) // blur
    ADD_MEMORIZED_SPELL ~spwi408~ #3 ~wizard~ (1) // Stoneskin
  END ELSE BEGIN
    ADD_MEMORIZED_SPELL ~spwi119~ #0 ~wizard~ (1) // Larloch's Minor Drain
    ADD_MEMORIZED_SPELL ~spwi211~ #1 ~wizard~ (1) // melf's acid arrow
    ADD_MEMORIZED_SPELL ~spwi406~ #3 ~wizard~ (1) // Minor Globe of Invulneribility
  END
  PATCH_IF (level > 10) BEGIN
    ADD_MEMORIZED_SPELL ~spwi520~ #4 ~wizard~ (1) // Conjure Lesser Air Elemental
    ADD_MEMORIZED_SPELL ~spwi521~ #4 ~wizard~ (1) // Conjure Lesser Earth Elemental
    PATCH_IF (xp > 1200000) BEGIN
      ADD_MEMORIZED_SPELL ~spwi217~ #1 ~wizard~ (1) // Agannazar's Scorcher
      ADD_MEMORIZED_SPELL ~spwi308~ #2 ~wizard~ (1) // Lightning Bolt
      ADD_MEMORIZED_SPELL ~spwi405~ #3 ~wizard~ (1) // Improved Invisibility
    END ELSE BEGIN
      ADD_MEMORIZED_SPELL ~spwi205~ #1 ~wizard~ (1) // Horror
      ADD_MEMORIZED_SPELL ~spwi305~ #2 ~wizard~ (1) // Haste
      ADD_MEMORIZED_SPELL ~spwi407~ #3 ~wizard~ (1) // Monster Summoning II
    END
    PATCH_IF (level > 11) BEGIN
      ADD_MEMORIZED_SPELL ~spwi502~ #4 ~wizard~ (1) // Cloudkill
      ADD_MEMORIZED_SPELL ~spwi601~ #5 ~wizard~ (1) // Invisible Stalker
      ADD_MEMORIZED_SPELL ~spwi605~ #5 ~wizard~ (1) // Death Spell
      ADD_MEMORIZED_SPELL ~spwi612~ #5 ~wizard~ (1) // Power Word Silence
      ADD_MEMORIZED_SPELL ~spwi619~ #5 ~wizard~ (1) // Wyvern Call
      PATCH_IF (xp > 1200000) BEGIN
        ADD_MEMORIZED_SPELL ~spwi401~ #3 ~wizard~ (1) // Confusion
      END ELSE BEGIN
        ADD_MEMORIZED_SPELL ~spwi403~ #3 ~wizard~ (1) // Fire Shield (Blue)
      END
      PATCH_IF (level > 12) BEGIN
        ADD_MEMORIZED_SPELL ~spwi620~ #5 ~wizard~ (1) // Conjure Fire Elemental
        PATCH_IF (xp > 1200000) BEGIN
          ADD_MEMORIZED_SPELL ~spwi106~ #0 ~wizard~ (1) // armor
          ADD_MEMORIZED_SPELL ~spwi209~ #1 ~wizard~ (1) // Luck
          ADD_MEMORIZED_SPELL ~spwi307~ #2 ~wizard~ (1) // Invisibility 10' Radius
        END ELSE BEGIN
          ADD_MEMORIZED_SPELL ~spwi102~ #0 ~wizard~ (1) // armor
          ADD_MEMORIZED_SPELL ~spwi220~ #1 ~wizard~ (1) // Power Word Sleep
          ADD_MEMORIZED_SPELL ~spwi304~ #2 ~wizard~ (1) // Fireball
        END
      END
    END
  END
  // wrap up spells memorizable/currently memorizable
  READ_LONG 0x2a8 mem_info
  FOR (index = 7 ; index < 13 ; ++index) BEGIN // levels 1-6, arcane
    READ_SHORT (mem_info + 0x0c + (index * 0x10)) spells
    PATCH_IF spells > 1 BEGIN // skip if no spells known at this level
      WRITE_SHORT (mem_info + 0x02 + (index * 0x10)) (spells - 2) // reduce by two to account for Edwin's amulet
      WRITE_SHORT (mem_info + 0x04 + (index * 0x10)) (spells - 2)
    END
  END
  BUT_ONLY IF_EXISTS

// 'knocked down' troll can be killed by anything
COPY_EXISTING ~firamb05.cre~ ~override~
  WRITE_SHORT           0x24    1            // current HP
  WRITE_SHORT           0x46   10            // natural AC
  WRITE_SHORT           0x48   10            // effective AC
  WRITE_BYTE            0x5a  100            // resist cold
  WRITE_BYTE            0x5b  100            // resist electricity
  WRITE_BYTE            0x5f  100            // resist magic cold
  WRITE_BYTE            0x60  100            // resist slashing
  WRITE_BYTE            0x61  100            // resist crushing
  WRITE_BYTE            0x62  100            // resist piercing
  WRITE_BYTE            0x63  100            // resist missile
  WRITE_BYTE            0x23c   9            // dexterity
  WRITE_BYTE            0x270 255            // enemy
  BUT_ONLY

// lvl 6 trolls should be routed through a different knocked-down version
COPY_EXISTING ~kptrol01.cre~ ~override~
              ~kptrol02.cre~ ~override~
              ~kptrol04.cre~ ~override~
              ~kptrol05.cre~ ~override~
  WRITE_ASCII 0x248 ~cdtroll1~ // override script
  BUT_ONLY

// haer's elemental resistances and kit.ids fixes
COPY_EXISTING ~haer10.cre~ ~override~
              ~haer11.cre~ ~override~
              ~haer13.cre~ ~override~
              ~haer15.cre~ ~override~
              ~haer19.cre~ ~override~
  WRITE_BYTE 0x59 25 // fire (these w_b are fixes for haer's elemental resistances)
  WRITE_BYTE 0x5a 50 // cold
  WRITE_BYTE 0x5b 25 // electrcity
  WRITE_BYTE 0x5c 0  // acid
  WRITE_BYTE 0x5d 0  // magic
  WRITE_BYTE 0x5e 25 // magic fire
  WRITE_BYTE 0x5f 50 // magic cold
  WRITE_BYTE 0x60 15 // slashing
  WRITE_BYTE 0x61 15 // crushing
  WRITE_BYTE 0x62 15 // piercing
  WRITE_BYTE 0x63 15 // missile
  WRITE_BYTE 0x6a 90 //pickpocketing, overwritten below for haer10, 11, 13
  WRITE_LONG 0x244 0x400d0000 // kit value
  BUT_ONLY IF_EXISTS

// hd pickpocket fixes
COPY_EXISTING ~haer10.cre~ ~override~
  WRITE_BYTE 0x6a 65 //pickpocketing

COPY_EXISTING ~haer11.cre~ ~override~
  WRITE_BYTE 0x6a 70 //pickpocketing

COPY_EXISTING ~haer13.cre~ ~override~
  WRITE_BYTE 0x6a 80 //pickpocketing

// haer'dalis spell issues
COPY_EXISTING ~haer15.cre~ ~override~
  ADD_KNOWN_SPELL ~spwi106~ #0 ~wizard~
  ADD_KNOWN_SPELL ~spwi112~ #0 ~wizard~
  ADD_KNOWN_SPELL ~spwi113~ #0 ~wizard~
  ADD_KNOWN_SPELL ~spwi116~ #0 ~wizard~
  ADD_KNOWN_SPELL ~spwi201~ #1 ~wizard~
  ADD_KNOWN_SPELL ~spwi206~ #1 ~wizard~
  ADD_KNOWN_SPELL ~spwi209~ #1 ~wizard~
  ADD_KNOWN_SPELL ~spwi212~ #1 ~wizard~
  ADD_KNOWN_SPELL ~spwi303~ #2 ~wizard~
  ADD_KNOWN_SPELL ~spwi305~ #2 ~wizard~
  ADD_KNOWN_SPELL ~spwi312~ #2 ~wizard~
  ADD_KNOWN_SPELL ~spwi401~ #3 ~wizard~
  ADD_KNOWN_SPELL ~spwi406~ #3 ~wizard~
  ADD_KNOWN_SPELL ~spwi408~ #3 ~wizard~
  ADD_KNOWN_SPELL ~spwi508~ #4 ~wizard~
  ADD_KNOWN_SPELL ~spwi510~ #4 ~wizard~
  REMOVE_MEMORIZED_SPELLS // nuke all memorized
  ADD_MEMORIZED_SPELL ~spwi113~ #0 ~wizard~ (1) // Protection from Evil
  ADD_MEMORIZED_SPELL ~spwi112~ #0 ~wizard~ (1) // Magic Missile
  ADD_MEMORIZED_SPELL ~spwi106~ #0 ~wizard~ (1) // Blindness
  ADD_MEMORIZED_SPELL ~spwi209~ #1 ~wizard~ (1) // Luck
  ADD_MEMORIZED_SPELL ~spwi206~ #1 ~wizard~ (1) // Invisibility
  ADD_MEMORIZED_SPELL ~spwi201~ #1 ~wizard~ (1) // Blur
  ADD_MEMORIZED_SPELL ~spwi312~ #2 ~wizard~ (1) // Slow
  ADD_MEMORIZED_SPELL ~spwi305~ #2 ~wizard~ (1) // Haste
  ADD_MEMORIZED_SPELL ~spwi303~ #2 ~wizard~ (1) // Flame Arrow
  ADD_MEMORIZED_SPELL ~spwi408~ #3 ~wizard~ (1) // Stoneskin
  ADD_MEMORIZED_SPELL ~spwi406~ #3 ~wizard~ (1) // Minor Globe of Invulneribility
  ADD_MEMORIZED_SPELL ~spwi401~ #3 ~wizard~ (1) // Confusion
  ADD_MEMORIZED_SPELL ~spwi510~ #4 ~wizard~ (1) // Spell Immunity
  ADD_MEMORIZED_SPELL ~spwi508~ #4 ~wizard~ (1) // Chaos
  BUT_ONLY

// knows class- or alignment-restricted spell
COPY_EXISTING ~heartg3.cre~ ~override~
              ~heartg4.cre~ ~override~
              ~heartg5.cre~ ~override~
  REMOVE_KNOWN_SPELL sppr105 sppr314 sppr605 sppr606
  BUT_ONLY

// improved continuity fixes from Oversight; swap ogre sounds for david warner
COPY_EXISTING ~hellslay.cre~ ~override~
  SAY BATTLE_CRY1 #60501 // "You cannot hope to defeat me!" for "Me will crush you, crush you to goo!"
  SAY BATTLE_CRY2 #60502 // "Die! Die!!  All of you, DIE!!" for "Me will smash your face."
  SAY SELECT_COMMON1 #60500 // "You are but a gnat compared to my power." for "I will crush."
  SAY DAMAGE #60503 // irenic05 for ogre sound, ogree08
  SAY DYING #60504 // irenic06 for ogre sound, ogree09

COPY_EXISTING ~helmbyr.cre~  ~override~ // replaces nonexistent initpc
              ~latlara.cre~  ~override~
              ~talvilon.cre~ ~override~
  WRITE_ASCII 0x250 ~initmain~ #8
  BUT_ONLY

// trolls lacking the correct 1hp items
COPY_EXISTING ~hgtrl01.cre~  ~override~ // fire troll
              ~firtrl01.cre~ ~override~ // troll cook
              ~kptrol01.cre~ ~override~ // kptrol0[1-4] are trolls from de'Arnise keep
              ~kptrol02.cre~ ~override~
//              ~kptrol03.cre~ ~override~ handled in special batch above
              ~kptrol04.cre~ ~override~
              ~obsice01.cre~ ~override~ // snow troll from planar sphere
              ~trolgi01.cre~ ~override~ // generic giant troll
              ~trollens.cre~ ~override~ // giant troll
  REPLACE_CRE_ITEM ~monhp1~ #0 #0 #0 ~NONE~ ~AMULET~ // either replaces minhp1 or adds it new
  BUT_ONLY IF_EXISTS

// imoen's thieving, script, prof, and thac0 fixes
COPY_EXISTING ~imoen10.cre~  ~override~
              ~imoen15.cre~  ~override~
              ~imoen211.cre~ ~override~
              ~imoen213.cre~ ~override~
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "imoen10") BEGIN  // restore battlecry and correct Imoen's bio post-Spellhold
    SAY 0xd0  #11035 // "You're not gonna keep me here!" to "My blade will cut you down to size!"
    SAY 0x1cc @102   // removes references to horrors of current location
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "imoen213" = 0) BEGIN
      WRITE_ASCII 0x248 ~imoen2~ #8 // override script missing from imoen213
    END
  END
  WRITE_BYTE 0x68 15 // move silently +5
  WRITE_BYTE 0x69 85 // find/disarm traps +10
  WRITE_BYTE 0x6a 10 // pickpocketing +10
  PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "imoen10" = 0) OR
            ("%SOURCE_RES%" STRING_COMPARE_CASE "imoen211" = 0)) BEGIN // thac0 adjustment for low level versions
    WRITE_BYTE 0x52 17 // thac0
  END ELSE BEGIN                                                       // extra prof for high level versions
    SET_BG2_PROFICIENCY 113 1 // one pip for single weapon
  END
  SET_BG2_PROFICIENCY 91 1 // one pip for short sword at all levels
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~jael01.cre~   ~override~
              ~ppumb01.cre~  ~override~
              ~pries18b.cre~ ~override~
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN
    sppr714 => sppr706 // doesn't exist, map to symbol fear
  END
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    LPF cd_spellbook_sub END
  END
  BUT_ONLY

// assign galvarey a script that won't cause him to stutter (see soa-dlg.d for dialogue trigger change)
COPY_EXISTING ~jagalvar.cre~ ~override~
  WRITE_ASCII 0x250 ~initin15~ // class script
  BUT_ONLY

// jaheira thac0 and spellbook fixes... many
COPY_EXISTING ~jaheir7.cre~  ~override~
              ~jaheir8.cre~  ~override~
              ~jaheir11.cre~ ~override~
              ~jaheir12.cre~ ~override~
              ~jahei14.cre~  ~override~
  READ_BYTE 0x234 level1 // fighter level
  WRITE_BYTE 0x52 (21 - level1) // thac0
  REMOVE_KNOWN_SPELL sppr108 sppr405 sppr507 // remove fear, mental domination, champion's strength; cofirmed ZoSA is intended for Jaheira from the devs
  ADD_KNOWN_SPELL ~sppr105~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr317~ #2 ~priest~
  ADD_KNOWN_SPELL ~sppr318~ #2 ~priest~ // missing in jahei14
  ADD_KNOWN_SPELL ~sppr319~ #2 ~priest~
  ADD_KNOWN_SPELL ~sppr414~ #3 ~priest~
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN
    sppr108 => sppr105 // entangle for remove fear
    sppr405 => sppr414 // cause serious wounds for mental domination
    sppr507 => sppr505 // true seeing for champion's strength
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // mem
    LPF cd_spellbook_sub END
  END
  PATCH_IF (level1 >= 8) BEGIN // jaheir11, jaheir12, jahei14
    ADD_KNOWN_SPELL ~sppr505~ #4 ~priest~
    PATCH_IF ((level1 = 8) OR (level1 = 10)) BEGIN // jaheir11, jahei14
      ADD_KNOWN_SPELL ~sppr501~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr502~ #4 ~priest~
  //    ADD_KNOWN_SPELL ~sppr505~ #4 ~priest~ added above
      ADD_KNOWN_SPELL ~sppr506~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr508~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr509~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr510~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr514~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr516~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr517~ #4 ~priest~
      ADD_KNOWN_SPELL ~sppr602~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr604~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr605~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr606~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr607~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr608~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr610~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr611~ #5 ~priest~
      ADD_KNOWN_SPELL ~sppr613~ #5 ~priest~
      PATCH_IF (level1 = 10) BEGIN // jahei14
        ADD_KNOWN_SPELL ~sppr401~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr402~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr404~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr406~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr407~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr409~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr410~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr411~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr413~ #3 ~priest~
  //      ADD_KNOWN_SPELL ~sppr414~ #3 ~priest~ added above
        ADD_KNOWN_SPELL ~sppr415~ #3 ~priest~
        ADD_KNOWN_SPELL ~sppr416~ #3 ~priest~
      END
    END
  END
  BUT_ONLY IF_EXISTS

// fixes Jahs' missing prof star and inconsistent dex at higher levels
COPY_EXISTING ~jaheir12.cre~ ~override~
              ~jahei14.cre~  ~override~
  WRITE_BYTE 0x23c 17 // sets dex to 17
  SET_BG2_PROFICIENCY 95 2 // one > two pips for scimitar/waki/ninja-to
  BUT_ONLY IF_EXISTS

// Jan Jansen now properly receives his bonus spell per level for being a specialist mage (Nythrun)
COPY_EXISTING ~jan8.cre~  ~override~
              ~jan10.cre~ ~override~
              ~jan11.cre~ ~override~
              ~jan12.cre~ ~override~
              ~jan15.cre~ ~override~
  ADD_MEMORIZED_SPELL spwi118 #0 WIZARD
  ADD_MEMORIZED_SPELL spwi201 #1 WIZARD
  ADD_MEMORIZED_SPELL spwi302 #2 WIZARD
  PATCH_IF          ~%SOURCE_RES%~ STRING_EQUAL jan8  BEGIN
    ADD_MEMORIZED_SPELL spwi405 #3 WIZARD
  END ELSE PATCH_IF ~%SOURCE_RES%~ STRING_EQUAL jan10 BEGIN
    ADD_MEMORIZED_SPELL spwi405 #3 WIZARD
    ADD_MEMORIZED_SPELL spwi508 #4 WIZARD
  END ELSE BEGIN
    ADD_MEMORIZED_SPELL spwi401 #3 WIZARD
    ADD_MEMORIZED_SPELL spwi508 #4 WIZARD
  END
  BUT_ONLY IF_EXISTS

// jan's thieving skills incorrect
COPY_EXISTING ~jan10.cre~ ~override~
   WRITE_BYTE 0x45  0 // Hide in Shadows
   WRITE_BYTE 0x64 60 // Detect Illusions
   WRITE_BYTE 0x65 20 // Set Traps +20
   WRITE_BYTE 0x67 75 // Open Locks
   WRITE_BYTE 0x68  0 // Move Silently
   WRITE_BYTE 0x69 65 // Find Traps
   WRITE_BYTE 0x6a 45 // Pickpockets

COPY_EXISTING ~jan11.cre~ ~override~
   WRITE_BYTE 0x45  0 // Hide in Shadows
   WRITE_BYTE 0x64 70 // Detect Illusions +10
   WRITE_BYTE 0x65 25 // Set Traps +5
   WRITE_BYTE 0x67 75 // Open Locks
   WRITE_BYTE 0x68  0 // Move Silently
   WRITE_BYTE 0x69 75 // Find Traps +10
   WRITE_BYTE 0x6a 45 // Pickpockets

// jan12 and jan15 shortchanged one prof star, thieving skill fixes
COPY_EXISTING ~jan12.cre~  ~override~
              ~jan15.cre~  ~override~
  WRITE_BYTE 0x45  5 // Hide in Shadows
  WRITE_BYTE 0x64 76 // Detect Illusions +6
  WRITE_BYTE 0x65 34 // Set Traps +9
  WRITE_BYTE 0x67 78 // Open Locks +3
  WRITE_BYTE 0x68 0  // Move Silently
  WRITE_BYTE 0x69 77 // Find Traps +2
  WRITE_BYTE 0x6a 45 // Pickpockets
  SET_BG2_PROFICIENCY 106 1 // one pip for darts
  BUT_ONLY IF_EXISTS

// deirex killed during cutscene fix 2/2; see 2207.bcs
COPY_EXISTING ~jarlich.cre~ ~override~
  WRITE_BYTE 0x270 128 // make neutral
  BUT_ONLY

// keldorn's initial saves are too high
COPY_EXISTING ~keldor8.cre~  ~override~
              ~keldor9.cre~  ~override~
              ~keldor10.cre~ ~override~
              ~keldor12.cre~ ~override~
              ~keldor14.cre~ ~override~
  WRITE_LONG 0x244 0x40050000 // kit.ids correction
  FOR (index = 0x54 ; index < 0x59 ; ++index) BEGIN
    WRITE_BYTE  index (THIS - 2)
  END
  BUT_ONLY IF_EXISTS
  
//Keldorn proficiency fix
COPY_EXISTING ~keldor9.cre~ ~override~
  SET_BG2_PROFICIENCY 90 2 // two pips for long swords
  BUT_ONLY

// hannah xp exploit; see also kftown02.bcs
COPY_EXISTING ~kftown02.cre~ ~override~
  WRITE_ASCII 0x250 ~kftown02~ #8

// korgan's kit.ids fixes
COPY_EXISTING ~korgan8.cre~  ~override~
              ~korgan9.cre~  ~override~
              ~korgan11.cre~ ~override~
              ~korgan12.cre~ ~override~
              ~korgan15.cre~ ~override~
  WRITE_LONG 0x244 0x40010000
  BUT_ONLY IF_EXISTS

// keep dogs don't go hostile when oty attacked; see cdkpdog.bcs in mass compile
COPY_EXISTING ~kpdog01.cre~ ~override~
              ~kpdog02.cre~ ~override~
              ~kpdog03.cre~ ~override~
              ~kpdog04.cre~ ~override~
  WRITE_ASCII 0x248 ~cdkpdog~ #8
  BUT_ONLY

// new method for glacias charm issue; see ar1303.bcs and kpglai01.dlg - make him neutral but under a (very easily) dispellable 'change AI' effect
COPY_EXISTING ~kpglai01.cre~ ~override~
  WRITE_BYTE 0x270 128 // allegiance neutral
  LPF ADD_CRE_EFFECT INT_VAR opcode = 72 target = 1 parameter1 = 255 parameter2 = 0 timing = 0 duration = 360000
    resist_dispel = 1 casterlvl = 1 power = 1 school = 4 sectype = 11 END

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~kproen05.cre~ ~override~
  WRITE_LONG 0xa4 61872 //I serve with my blade.
  WRITE_LONG 0xb8 61877 //Back... I must fall back!
  WRITE_LONG 0xc8 61874 //Cross blades with me and die!
  WRITE_LONG 0xec 61875
  WRITE_LONG 0xf0 61876
  WRITE_LONG 0x10c 61870 //Speak quickly, citizen, I have little time for this.
  WRITE_LONG 0x110 61871 //Good day to you.
  WRITE_LONG 0x114 61872 //I serve with my blade.
  WRITE_LONG 0x118 61873 //Do nothing stupid and there'll be no problems.
  BUT_ONLY

// live/dead troll versions have mismatching XP
COPY_EXISTING ~kptrol01.cre~ ~override~
              ~kptrol02.cre~ ~override~
              ~kptrol03.cre~ ~override~
              ~kptrol04.cre~ ~override~
              ~rogtro01.cre~ ~override~
  WRITE_LONG 0x14 1400
  BUT_ONLY

// create revived version of kptrol03
COPY_EXISTING ~kptrol03.cre~ ~override/kptrol13.cre~

// Correct a minor issue in Mazzy's soundset (aVENGER)
COPY_EXISTING ~mazzy8.cre~  ~override~
              ~mazzy9.cre~  ~override~
              ~mazzy11.cre~ ~override~
              ~mazzy12.cre~ ~override~
              ~mazzy15.cre~ ~override~
  SAY EXISTANCE2 #29727
  SAY EXISTANCE3 #-1    // assign the Spell Failed sound to the proper soundslot
  BUT_ONLY IF_EXISTS

//Mazzy proficiency fix
COPY_EXISTING ~mazzy9.cre~  ~override~
  SET_BG2_PROFICIENCY 91 2 // pips for short swords
  BUT_ONLY

COPY_EXISTING ~mazzy12.cre~ ~override~
              ~mazzy15.cre~ ~override~
  SET_BG2_PROFICIENCY 91 3 // pips for short swords
  BUT_ONLY IF_EXISTS

// eliminates soundsets for these two mindflayers as no other ones have sounds, especially since they're hobgoblin sounds
COPY_EXISTING ~melsum06.cre~ ~override~
              ~mindal01.cre~ ~override~
  WRITE_LONG 0xc8  0xffffffff
  WRITE_LONG 0xcc  0xffffffff
  WRITE_LONG 0xec  0xffffffff
  WRITE_LONG 0xf0  0xffffffff
  WRITE_LONG 0x10c 0xffffffff
  WRITE_LONG 0x110 0xffffffff
  WRITE_LONG 0x1b8 0xffffffff
  BUT_ONLY IF_EXISTS

//one mephit portal has inconsistent dexterity
COPY_EXISTING ~mephsp1.cre~ ~override~
  WRITE_BYTE 0x23c 20 // dex
  BUT_ONLY

// just grab correct values for mepice02, obsice02
COPY_EXISTING ~mepice01.cre~ ~override~
  // rest of these reads are to fix obsice02, mepice02 below
  READ_ASCII 0x2c  color (7)        // entire coloring block
  READ_LONG  0x46  ac               // natural and effective AC
  READ_ASCII 0x59  resistances (11) // entire resistances block
  READ_BYTE  0x23c dex              // dexterity
  BUT_ONLY

COPY_EXISTING ~mepice01.cre~ ~override~ // ice mephit should have ice shard memorized, not magma ball
              ~mepice02.cre~ ~override~ // fixes other two ice mephits
              ~obsice02.cre~ ~override~
  // writes in correct values from mepice01
  WRITE_ASCIIE 0x2c  "%color%" #7        // entire coloring block
  WRITE_LONG   0x46  "%ac%"              // natural and effective AC
  WRITE_ASCIIE 0x59  "%resistances%" #11 // entire resistances block
  WRITE_BYTE   0x23c "%dex%"             // dexterity
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // correct known/memorized spells
    spin930 => spin936 // magma ball to ice shard
  END
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    LPF cd_spellbook_sub END
  END
  REPLACE_CRE_ITEM mepice #0 #0 #0 NONE ~WEAPON1~ EQUIP // corrects primary weapon for mepice02, obsice02
  BUT_ONLY

// fixes minsc spells
COPY_EXISTING ~minsc8.cre~  ~override~
              ~minsc9.cre~  ~override~
              ~minsc10.cre~ ~override~
              ~minsc12.cre~ ~override~
  READ_BYTE 0x234 level
  ADD_KNOWN_SPELL ~sppr101~ #0 ~priest~ // normal druid level 1 spells
  ADD_KNOWN_SPELL ~sppr103~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr104~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr105~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr110~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr111~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr113~ #0 ~priest~
  REMOVE_MEMORIZED_SPELLS
  ADD_MEMORIZED_SPELL ~spin117~ #0 ~innate~ (1) // berserk
  ADD_MEMORIZED_SPELL ~sppr104~ #0 ~priest~ (1) // Detect Evil
  PATCH_IF (level > 8) BEGIN
    ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ (1) // Cure Light Wounds
    PATCH_IF (level > 9) BEGIN
      ADD_KNOWN_SPELL ~sppr202~ #1 ~priest~ // normal druid level 2 spells
      ADD_KNOWN_SPELL ~sppr204~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr205~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr206~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr207~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr209~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr210~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr212~ #1 ~priest~
      ADD_MEMORIZED_SPELL ~sppr202~ #1 ~priest~ (1) // Barkskin
      PATCH_IF (level > 11) BEGIN
        ADD_KNOWN_SPELL ~sppr302~ #2 ~priest~ // normal druid level 3 spells
        ADD_KNOWN_SPELL ~sppr303~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr305~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr306~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr309~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr310~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr311~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr312~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr315~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr317~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr319~ #2 ~priest~
        ADD_MEMORIZED_SPELL ~sppr204~ #1 ~priest~ (1) // Charm Person or Mammal
        ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ (1) // Cure Medium Wounds
      END
    END
  END
  BUT_ONLY

// nalia short one prof star at all levels
COPY_EXISTING ~nalia8.cre~  ~override~
              ~nalia10.cre~ ~override~
              ~nalia11.cre~ ~override~
              ~nalia13.cre~ ~override~
              ~nalia15.cre~ ~override~
              ~nalia18.cre~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 match_parameter2 = 106 parameter2 = 107 silent = 1 END // if present, change dart prof to sling (nalia13, 15, 18)
  SET_BG2_PROFICIENCY 106 1 // add one pip for darts
  BUT_ONLY IF_EXISTS

// more heinous spell slot fixes; nalia8 and nalia9 have one extra fifth
// level spell, nalia13 one extra 6th level spell
COPY_EXISTING ~nalia8.cre~  ~override~
              ~nalia10.cre~ ~override~
              ~nalia13.cre~ ~override~
  READ_BYTE 0x234 level
  READ_LONG 0x2a8 slot_off
  REMOVE_MEMORIZED_SPELLS // nuke it from orbit
  ADD_MEMORIZED_SPELL ~spwi118~ #0 ~wizard~ (1) // Chromatic Orb
  ADD_MEMORIZED_SPELL ~spwi115~ #0 ~wizard~ (1) // Shocking Grasp
  ADD_MEMORIZED_SPELL ~spwi110~ #0 ~wizard~ (1) // Identify
  ADD_MEMORIZED_SPELL ~spwi105~ #0 ~wizard~ (1) // Color Spray
  ADD_MEMORIZED_SPELL ~spwi215~ #1 ~wizard~ (1) // Web
  ADD_MEMORIZED_SPELL ~spwi214~ #1 ~wizard~ (1) // Strength
  ADD_MEMORIZED_SPELL ~spwi212~ #1 ~wizard~ (1) // Mirror Image
  ADD_MEMORIZED_SPELL ~spwi312~ #2 ~wizard~ (1) // Slow
  ADD_MEMORIZED_SPELL ~spwi308~ #2 ~wizard~ (1) // Lightning Bolt
  ADD_MEMORIZED_SPELL ~spwi306~ #2 ~wizard~ (1) // Hold Person
  ADD_MEMORIZED_SPELL ~spwi416~ #3 ~wizard~ (1) // Polymorph Self
  ADD_MEMORIZED_SPELL ~spwi408~ #3 ~wizard~ (1) // Stoneskin
  ADD_MEMORIZED_SPELL ~spwi505~ #4 ~wizard~ (1) // Shadow Door
  PATCH_IF level > 10 BEGIN
    ADD_MEMORIZED_SPELL ~spwi211~ #1 ~wizard~ (1) // Melf's Acid Arrow
    ADD_MEMORIZED_SPELL ~spwi305~ #2 ~wizard~ (1) // Haste
    ADD_MEMORIZED_SPELL ~spwi403~ #3 ~wizard~ (1) // Fire Shield (Blue)
    ADD_MEMORIZED_SPELL ~spwi415~ #3 ~wizard~ (1) // Polymorph Other
    ADD_MEMORIZED_SPELL ~spwi508~ #4 ~wizard~ (1) // Chaos
    ADD_MEMORIZED_SPELL ~spwi506~ #4 ~wizard~ (1) // Domination
    ADD_MEMORIZED_SPELL ~spwi502~ #4 ~wizard~ (1) // Cloudkill
    WRITE_SHORT (slot_off + 0x02 + (12 * 0x10)) 0 // spells memorizable at level 6
    WRITE_SHORT (slot_off + 0x04 + (12 * 0x10)) 0 // current spells memorized at level 6
  END ELSE BEGIN
    WRITE_SHORT (slot_off + 0x02 + (11 * 0x10)) 1 // spells memorizable at level 5
    WRITE_SHORT (slot_off + 0x04 + (11 * 0x10)) 1 // current spells memorized at level 5
  END

// Nalia's saves
COPY_EXISTING ~nalia13.cre~ ~override~
              ~nalia15.cre~ ~override~
              ~nalia18.cre~ ~override~
  WRITE_BYTE 0x54 11 // save vs. death
  BUT_ONLY IF_EXISTS

// add monhp1 item to trolls to prevent death; assign script to force transformation to dead form at low HP
COPY_EXISTING ~obsice01.cre~ ~override~
  REPLACE_CRE_ITEM ~monhp1~ #0 #0 #0 ~NONE~ ~AMULET~
  WRITE_ASCII 0x260 ~obsice11~

// create 'dead' or 'knocked down' versions of 'special' trolls
COPY_EXISTING ~obsice01.cre~ ~override/obsice11.cre~
  WRITE_SHORT           0x24    1            // current HP
  WRITE_SHORT           0x46   10            // natural AC
  WRITE_SHORT           0x48   10            // effective AC
  WRITE_BYTE            0x5a  100            // resist cold
  WRITE_BYTE            0x5b  100            // resist electricity
  WRITE_BYTE            0x5f  100            // resist magic cold
  WRITE_BYTE            0x60  100            // resist slashing
  WRITE_BYTE            0x61  100            // resist crushing
  WRITE_BYTE            0x62  100            // resist piercing
  WRITE_BYTE            0x63  100            // resist missile
  WRITE_BYTE            0x23c   9            // dexterity
  WRITE_BYTE            0x270 255            // enemy
  WRITE_ASCIIE          0x248 ~%SOURCE_RES%~ #40 // new script, blanks other slots
  WRITE_ASCII           0x2cc ~~ #8          // blanks dialog file
  // item changes
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = trollreg END
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = trollspi END // just in case
  REPLACE_CRE_ITEM ~trolldie~ #0 #0 #0 ~NONE~ ~AMULET~ // either replaces monhp1 or adds it new

// planar-prison guard can not make the actors hostile by shouting (Wisp)
COPY_EXISTING ~pcapt03.cre~ ~override~
  WRITE_ASCII 0x248 gensht01 #8
  BUT_ONLY

// hostile planetars via wish would still cast spells to benefit the party; generic planetar just missing scripts
COPY_EXISTING ~planet01.cre~ ~override~ // unused?
              ~planwish.cre~ ~override~ // hostile planetar summoned via wish
  WRITE_ASCII 0x248 ~planet~   #8 // override script
  WRITE_ASCII 0x268 ~wtasight~ #8 // default script
  BUT_ONLY IF_EXISTS

// fixes evil planetar's class and spell list
COPY_EXISTING ~planevil.cre~ ~override~
  REMOVE_KNOWN_SPELL sppr307 // has dupe sppr307 entries
  ADD_KNOWN_SPELL ~sppr303~ #2 ~priest~ // add back plus missing dispel magic
  ADD_KNOWN_SPELL ~sppr307~ #2 ~priest~
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ (3) // add three memorized dispels to bring even with plangood
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN
    sppr710 => sppr715 // holy word to unholy word
  END
  PATCH_FOR_EACH offset IN 0x2a0 0x2b0 BEGIN // known, mem
    LPF cd_spellbook_sub END
  END
  BUT_ONLY IF_EXISTS

// fixes regen rate for good planetars
COPY_EXISTING ~plangood.cre~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 98 parameter1 = 4 END // 4 hp/sec, not 1/sec
  BUT_ONLY IF_EXISTS

// attacking squatters in paladin-baron quest causes normal townspeople to turn hostile
COPY_EXISTING ~plfarm04.cre~ ~override~
              ~plfarm05.cre~ ~override~
              ~plfarm06.cre~ ~override~
  WRITE_ASCII 0x248 ~gensht01~ // override script
  BUT_ONLY

// Tyrianna fix, part 2 (first is ar0415.bcs patch)
COPY_EXISTING ~plgirl01.cre~ ~override~
  WRITE_ASCII 0x248 ~~ #8
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~ppmag01.cre~ ~override~
  WRITE_LONG 0xa4 47345 //A woman on the high seas has t' be tougher than any mate.
  WRITE_LONG 0xec 51867
  WRITE_LONG 0xf0 51868
  WRITE_LONG 0x10c 47345 //A woman on the high seas has t' be tougher than any mate.
  WRITE_LONG 0x110 47347 //Ahhhh, don't look at me like that, or I'll gut ye where ye stand.
  WRITE_LONG 0x114 48338 //Grog.  Grog an' a bath.  Aye...
  WRITE_LONG 0x118 51494 //So I smells like fish.  Begone with ye.
  WRITE_LONG 0x1b8 47347 //Ahhhh, don't look at me like that, or I'll gut ye where ye stand.
  BUT_ONLY

// assigns amalas.bcs to Amalas as his override script
COPY_EXISTING ~ruffian.cre~ ~override~
  WRITE_ASCII 0x248 ~AMALAS~ #8
  BUT_ONLY

// fixes sahzomb flags
COPY_EXISTING ~sahzomb.cre~ ~override~
  WRITE_LONG 0x10 0 // every flag set; this clears them
  BUT_ONLY

// Orcs sound like orcs; blanks human sounds
COPY_EXISTING ~sarculto.cre~ ~override~
              ~sarorc01.cre~ ~override~
              ~saroro01.cre~ ~override~
              ~sarrein1.cre~ ~override~
              ~sartro01.cre~ ~override~
              ~sartro03.cre~ ~override~
  WRITE_LONG 0xa4 0xffffffff
  WRITE_LONG 0xc8 0xffffffff
  WRITE_LONG 0xec 0xffffffff
  WRITE_LONG 0xf0 0xffffffff
  WRITE_LONG 0x10c 0xffffffff
  WRITE_LONG 0x198 0xffffffff
  BUT_ONLY IF_EXISTS

// fixes to Sarevok's saves
COPY_EXISTING ~sarevok.cre~ ~override~
  WRITE_BYTE 0x57 4 // save vs. breath
  BUT_ONLY IF_EXISTS

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~sargrd06.cre~ ~override~
  WRITE_LONG 0xa4 4901 //Yer a handsome bunch, ain't ya?
  WRITE_LONG 0xec 12568
  WRITE_LONG 0xf0 12569
  WRITE_LONG 0x10c 4901 //Yer a handsome bunch, ain't ya?
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sarpro01.cre~ ~override~ // typo
  WRITE_ASCII 0x268 ~wtrunsgt~ #8
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sendro05.cre~ ~override~ //typo
  WRITE_ASCII 0x268 ~cassa20b~ #8
  BUT_ONLY IF_EXISTS

// Silent Sapper (uses same soundset as sewdue01)
COPY_EXISTING ~sewdue02.cre~ ~override~
  SAY 0xa4 #4940
  SAY 0xec #12584
  SAY 0xf0 #12585
  SAY 0x10c #4939
  SAY 0x110 #4939
  SAY 0x114 #4940
  BUT_ONLY IF_EXISTS

// 'glowing pool' creature from lilarcor quest has dispellable invisibility (via mage item); add it directly as creature effect
COPY_EXISTING ~sewsw.cre~  ~override~
  LPF ADD_CRE_EFFECT INT_VAR opcode = 20 timing = 1 END // permanent invis
  BUT_ONLY

// brannel not commenting due to unassigned script, script errors (see brannel.bcs)
COPY_EXISTING ~shthdr01.cre~ ~override~
  WRITE_ASCII 0x250 ~BRANNEL~ #8 // class script
  BUT_ONLY

// add sleeping flag to sleeping creatures
COPY_EXISTING ~sleepdw.cre~ ~override~   // sleeping dwarf
              ~sleepfh.cre~ ~override~   // sleeping woman
              ~sleepmh.cre~ ~override~   // sleeping man
  WRITE_BYTE 0x20 (THIS BOR BIT0) // add the STATE_SLEEPING flag
  BUT_ONLY

COPY_EXISTING ~slmage2.cre~   ~override~
  REMOVE_KNOWN_SPELL spwi422 // doesn't exist
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
    spwi422 => spwi408 // doesn't exist to Stoneskin
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
    LPF cd_spellbook_sub END
  END
  BUT_ONLY

COPY_EXISTING ~sppain.cre~  ~override~
              ~vakola.cre~ ~override~
  REMOVE_KNOWN_SPELL sppr112 // doesn't exist
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
    sppr112 => sppr106 // doesn't exist to magic stone
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
    LPF cd_spellbook_sub END
  END
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~suelf8.cre~ ~override~
  WRITE_LONG 0xa4 61856 //The Tree of Life!  'Tis the Tree that must be saved, before any of us!
  WRITE_LONG 0xec 61861
  WRITE_LONG 0xf0 61860
  WRITE_LONG 0x10c 61854 //This is the Exile's doing!
  WRITE_LONG 0x110 61855 //So... so many have died!  So many!
  WRITE_LONG 0x114 61856 //The Tree of Life!  'Tis the Tree that must be saved, before any of us!
  WRITE_LONG 0x118 61857 //Rillifane!  Rillifane save us, I beg you!
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Males with female sounds
COPY_EXISTING ~suelfw6.cre~ ~override~
  WRITE_LONG 0xa4 61850 //Monsters!  There are monsters everywhere in Suldanessellar!
  WRITE_LONG 0xec 61853
  WRITE_LONG 0xf0 61852
  WRITE_LONG 0x10c 61847 //Wh-what has happened to the queen?  She has been taken, hasn't she!
  WRITE_LONG 0x110 61848 //It is... it is the Exile!  The Exile has returned!
  WRITE_LONG 0x114 61851 //This cannot be happening!  It cannot!
  WRITE_LONG 0x118 61850 //Monsters!  There are monsters everywhere in Suldanessellar!
  BUT_ONLY

// adds many missing effects to Mordy swords (Baldurdash)
// extra confusion prots now handled by batch immunities
COPY_EXISTING ~sword01.cre~ ~override~
  PATCH_FOR_EACH immunity IN 135 134 128 109 106 80 78 76 74 45 39 24 5 BEGIN // polymorph, petrify, confusion, hold, break morale, deafness, disease, feeblemind, blindness, stun, sleep, panic, charm
    LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 19 parameter2 = immunity END // clone existing immunity to intelligence change
  END
  PATCH_FOR_EACH spell IN spin975 spin974 spin959 spin912 spin911 spin910 spin909 spin834 spin775 spin774 BEGIN // psionic spells
    LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 19 opcode = 206 parameter2 = "-1" STR_VAR resource = EVAL ~%spell%~ END // clone existing immunity to intelligence change
  END
  BUT_ONLY

// one-off change for Tor'gal to prevent repeating dialogue after regenerating
COPY_EXISTING ~torgal.cre~ ~override/torgal3.cre~
  WRITE_ASCII 0x248 ~~ #8 // no approach and speak script
  WRITE_BYTE  0x270 255   // enemy
  WRITE_ASCII 0x2cc ~~ #8 // no dialogue file

// makes Logan's cre files consistent
COPY_EXISTING ~celogan.cre~ ~override/trcut07.cre~
  WRITE_ASCII 0x248 ~~ #40 // dump all scripts
  WRITE_ASCII 0x280 ~trcut07~ #18
  WRITE_ASCII 0x2cc ~trcut07~ #8

// knocked-down trolls should match their live counterparts
COPY_EXISTING ~troll01.cre~ ~override/cdtroll1.cre~
              ~troll02.cre~ ~override/cdtroll2.cre~
  WRITE_BYTE   0x234 6 // level
  WRITE_ASCIIE 0x248 ~%DEST_RES%~ // override script

// knocked-down trolls should match their live counterparts
COPY_EXISTING ~troll02.cre~ ~override~
  WRITE_BYTE 0x234 8 // level
  BUT_ONLY

// Prevent Ice Trolls from losing their coloring upon death (aVENGER)
COPY_EXISTING ~trolic01.cre~  ~override~ // ice troll
              ~trolic02.cre~  ~override~ // ice troll
  LPF ALTER_EFFECT INT_VAR match_opcode = 51 target = 1 timing = 9 END // target self and timing perm after death for color opcode
  BUT_ONLY

// trollens should require fire/acid to be killed
COPY_EXISTING ~trollens.cre~ ~override~
  WRITE_ASCII 0x258 ~troll01~ #8 // transform to fire/acid dead form at low HP
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Males with female sounds
COPY_EXISTING ~ttser2.cre~ ~override~
  WRITE_LONG 0xa4 61690 //Care to dance?
  WRITE_LONG 0xb8 61687 //Another time, another place!
  WRITE_LONG 0xc8 61779 //Plant a blade in your innards, I will!
  WRITE_LONG 0xec 61780
  WRITE_LONG 0xf0 61781
  WRITE_LONG 0x10c 61774 //Prepare to be eviscerated, fool.
  WRITE_LONG 0x110 61690 //Care to dance?
  WRITE_LONG 0x114 61777 //This will be a slow and painful process.
  WRITE_LONG 0x118 61778 //You will suffer... oh, yes.
  BUT_ONLY
  
// endless ust natha combat music; see also ucounter.bcs
COPY_EXISTING ~ucounter.cre~ ~override~
  WRITE_BYTE 0x270 128 // enemy > neutral; enemy prevents saving and other stupidity
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~udelf05.cre~ ~override~
  WRITE_LONG 0xa4 11063 //Good day and hello to you.
  WRITE_LONG 0xec 11075
  WRITE_LONG 0xf0 11076
  WRITE_LONG 0x10c 11070 //Few of the fair folk concern themselves with the affairs of the state.
  WRITE_LONG 0x110 11065 //I much prefer the wooded regions, but one must be flexible in these times.
  WRITE_LONG 0x114 11063 //Good day and hello to you.
  WRITE_LONG 0x118 11070 //Few of the fair folk concern themselves with the affairs of the state.
  BUT_ONLY

// restoring Adalon's sound set
COPY_EXISTING ~udsilver.cre~ ~override~
  SAY 0x0d0 @103 // BATTLE_CRY3 ~You've made a fatal error in judgment!~ [SILVER19]
  SAY 0x0d4 @104 // BATTLE_CRY4 ~This is your end!~ [SILVER18]
  SAY 0x110 @109 // SELECT_COMMON2 ~You have come for a reason. Out with it.~ [SILVER14]
  SAY 0x114 @110 // SELECT_COMMON3 ~I would not have expected the likes of you.~ [SILVER12]
  SAY 0x118 @111 // SELECT_COMMON4 ~You are welcome in my sight.~ [SILVER11]
  BUT_ONLY

// valyg11 should be level 10 instead of 11, based off his XP
COPY_EXISTING ~valyg11.cre~ ~override~
  WRITE_SHORT 0x24  78 // current hp
  WRITE_SHORT 0x26  78 // max hp
  WRITE_BYTE  0x45  78 // hide in shadows
  WRITE_BYTE  0x52  11 // THAC0
  WRITE_BYTE  0x54   8 // save v death
  WRITE_BYTE  0x55  10 // save v wand
  WRITE_BYTE  0x56   9 // save v polymorph
  WRITE_BYTE  0x57   9 // save v breath
  WRITE_BYTE  0x58  11 // save v spell
  WRITE_BYTE  0x68  78 // move silently
  WRITE_BYTE  0x234 10 // level
  BUT_ONLY

// Valygar spell fixes
COPY_EXISTING ~valyg8.cre~  ~override~
              ~valyg9.cre~  ~override~
              ~valyg11.cre~ ~override~
              ~valyg12.cre~ ~override~
              ~valyg14.cre~ ~override~
  READ_BYTE 0x234 level
  ADD_KNOWN_SPELL ~sppr101~ #0 ~priest~ // normal druid level 1 spells
  ADD_KNOWN_SPELL ~sppr103~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr104~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr105~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr110~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr111~ #0 ~priest~
  ADD_KNOWN_SPELL ~sppr113~ #0 ~priest~
  REMOVE_MEMORIZED_SPELLS
  ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ (1) // Cure Light Wounds
  PATCH_IF (level > 8) BEGIN
    ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ (1) // doooooooooooooom
    PATCH_IF (level > 9) BEGIN
      ADD_KNOWN_SPELL ~sppr202~ #1 ~priest~ // normal druid level 2 spells
      ADD_KNOWN_SPELL ~sppr204~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr205~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr206~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr207~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr209~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr210~ #1 ~priest~
      ADD_KNOWN_SPELL ~sppr212~ #1 ~priest~
      ADD_MEMORIZED_SPELL ~sppr204~ #1 ~priest~ (1) // Charm Person or Mammal
      PATCH_IF (level > 11) BEGIN
        ADD_KNOWN_SPELL ~sppr302~ #2 ~priest~ // normal druid level 3 spells
        ADD_KNOWN_SPELL ~sppr303~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr305~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr306~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr309~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr310~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr311~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr312~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr315~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr317~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr319~ #2 ~priest~
        ADD_KNOWN_SPELL ~spra301~ #2 ~priest~ // plus standard stalker spells
        ADD_KNOWN_SPELL ~spra302~ #2 ~priest~
        ADD_KNOWN_SPELL ~spra303~ #2 ~priest~
        ADD_MEMORIZED_SPELL ~sppr202~ #1 ~priest~ (1) // Barkskin
        ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ (1) // Cure Medium Wounds
      END
    END
  END
  BUT_ONLY IF_EXISTS

// vic6 has incorrect class script
COPY_EXISTING ~viconi6.cre~ ~override~
  WRITE_ASCII 0x250 ~initrg10~ // all other vics use this
  BUT_ONLY

// Viconia proficiency fix, remove hfire seeds, add flame blade (holy smite confirmed to be included by the devs)
COPY_EXISTING ~viconi6.cre~  ~override~
              ~viconi8.cre~  ~override~
              ~viconi9.cre~  ~override~
              ~viconi11.cre~ ~override~
              ~viconi13.cre~ ~override~
              ~viconi16.cre~ ~override~
  READ_BYTE 0x234 level
  ADD_KNOWN_SPELL ~sppr206~ #1 ~priest~ // Flame Blade
  PATCH_IF level > 10 BEGIN
    REMOVE_KNOWN_SPELL sppr606 sppr607 // fire seeds (shouldn't know), heal (memorized twice)
    ADD_KNOWN_SPELL ~sppr607~ #5 ~priest~ // restore heal
  END
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
    sppr606 => sppr601 // fire seeds to aerial servant
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
    LPF cd_spellbook_sub END
  END
  SET_BG2_PROFICIENCY 112 1 // one pip in sword & shield
  PATCH_IF (level > 12) BEGIN
    SET_BG2_PROFICIENCY 100 1 // one pip in flail
  END
  BUT_ONLY IF_EXISTS

// fixes to Parisa's soundset, change from male vampire soundset to female
COPY_EXISTING ~vvparis.cre~ ~override~
  SAY INITIAL_MEETING  #61743 // Death awaits you... but not I.
  SAY UNHAPPY_BREAKING #61749 // I shall return, fool!
  SAY BATTLE_CRY1      #61745 // I shall feast on your heart!
  SAY BATTLE_CRY2      #61746 // Be honored, for you shall feed me!
  SAY DAMAGE           #61747
  SAY DYING            #61748
  SAY SELECT_COMMON1   #61741 // I am the predator that lives amongst you.
  SAY SELECT_COMMON2   #61742 // Let me have a taste, come.
  SAY SELECT_COMMON3   #61743 // Death awaits you... but not I.
  SAY SELECT_COMMON4   #61744
  SAY PICKED_POCKET    #61744

// A fair bunch of people are male with female sounds or vice versa (Wisp): Males with female sounds
COPY_EXISTING ~vvshad2.cre~ ~override~
  WRITE_LONG 0xa4 61885 //Explain your intent, and make it good!
  WRITE_LONG 0xb8 61890 //Must... return to the shadows!
  WRITE_LONG 0xc8 61886 //Beg for death and I'll make it quick!
  WRITE_LONG 0xcc 61887 //No one crosses the Shadow Thieves... and lives!
  WRITE_LONG 0xec 61888
  WRITE_LONG 0xf0 61889
  WRITE_LONG 0x10c 61882 //Good to see a like-minded friend-to-be.
  WRITE_LONG 0x110 61883 //Speak if ye will.
  WRITE_LONG 0x114 61884 //Is there something you seek?
  WRITE_LONG 0x118 61885 //Explain your intent, and make it good!
  BUT_ONLY

// wish01 and wish02 should be the same genie
COPY_EXISTING ~wish02.cre~ ~override/wish01.cre~
  WRITE_ASCII 0x280 ~wish01~ #18 // death var
  WRITE_ASCII 0x2cc ~wish~    #8 // dialogue
  BUT_ONLY IF_EXISTS

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~yaga02.cre~ ~override~
  WRITE_LONG 0xa4 61625 //We will be saved in the End.
  WRITE_LONG 0xb8 61630 //Aiiiieee!!  I have failed him!!
  WRITE_LONG 0xec 61628
  WRITE_LONG 0xf0 61629
  WRITE_LONG 0x10c 61625 //We will be saved in the End.
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~yarmy01.cre~ ~override~ // typo
  WRITE_ASCII 0x258 ~cfigh20b~ #8
  BUT_ONLY IF_EXISTS
  
// yoshimo has too many thieving points
COPY_EXISTING ~yoshi7.cre~  ~override~
              ~yoshi8.cre~  ~override~
              ~yoshi10.cre~ ~override~
  WRITE_BYTE 0x45 35 // hide in shadows -5
  WRITE_BYTE 0x68 40 // move silently -40
  BUT_ONLY
  
// yoshimo has too many thieving points
COPY_EXISTING ~yoshi11.cre~ ~override~
  WRITE_BYTE 0x45 40 // hide in shadows -15
  WRITE_BYTE 0x68 45 // move silently -35
  BUT_ONLY

// yoshimo has too many thieving points
COPY_EXISTING ~yoshi12.cre~ ~override~
  WRITE_BYTE 0x45 45 // hide in shadows -15
  WRITE_BYTE 0x65 15 // set traps -15
  WRITE_BYTE 0x68 50 // move silently -30
  BUT_ONLY

// A fair bunch of people are male with female sounds or vice versa (Wisp): Females with male sounds
COPY_EXISTING ~yssold16.cre~ ~override~
  WRITE_LONG 0xa4 61980 //You are welcome in my sight.
  WRITE_LONG 0xc8 61981 //This is your end!
  WRITE_LONG 0xcc 61982 //You've made a fatal error in judgement.
  WRITE_LONG 0xec 61985
  WRITE_LONG 0xf0 61986
  WRITE_LONG 0x10c 61981 //This is your end!
  WRITE_LONG 0x110 61982 //You've made a fatal error in judgement.
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// familiar fixes                                   \\\\\
/////                                                  \\\\\

// see also string fixes

// soa cat
COPY_EXISTING ~famcat.cre~   ~override~
  WRITE_BYTE  0x45 89 // hide in shadows
  WRITE_SHORT 0x46 4  // natural ac
  WRITE_SHORT 0x48 4  // effective ac
  WRITE_BYTE  0x68 89 // move silently
  // sound restorations
  SAY 0xa4 @126
  SAY 0xc8 @126
  SAY 0xec @126
  SAY 0xf0 @126
  SAY 0x010c @126
  SAY 0x0110 @126
  REMOVE_CRE_ITEM hastring // disables attacking, remove but...
  LPF ADD_CRE_EFFECT INT_VAR opcode = 16 timing = 1 END // manually restore haste effect from removed hastring (same haste effect as ToB cat)
  // add mage class so it can use blur
  READ_BYTE  0x234 level // copy level of first class...
  WRITE_BYTE 0x235 level // to second class
  WRITE_BYTE 0x273 13    // change class to m/t
  REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
  REMOVE_MEMORIZED_SPELL ~spwi201~
  ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
  ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
  BUT_ONLY

// soa dust mephit
COPY_EXISTING ~famdust.cre~   ~override~
  WRITE_SHORT 0x46 10 // natural ac
  WRITE_SHORT 0x48 10 // effective ac
  REMOVE_KNOWN_SPELL spwi201
  BUT_ONLY

// soa fairy dragon
COPY_EXISTING ~famfair.cre~   ~override~
  WRITE_SHORT 0x46 8 // natural ac
  WRITE_SHORT 0x48 8 // effective ac
  WRITE_BYTE  0x59 0 // resist fire
  WRITE_BYTE  0x5b 0 // resist electricity
  WRITE_BYTE  0x5e 0 // resist magic fire
  REMOVE_KNOWN_SPELL spwi201
  REPLACE_CRE_ITEM s1-2 #0 #0 #0 none WEAPON1 EQUIP
  BUT_ONLY

// soa ferret
COPY_EXISTING ~famfer.cre~ ~override~
  WRITE_BYTE  0x45 30 // hide in shadows
  WRITE_SHORT 0x46 4  // natural ac
  WRITE_SHORT 0x48 4  // effective ac
  WRITE_BYTE  0x68 30 // move silently
  WRITE_BYTE  0x69 15 // find/disarm traps
  WRITE_BYTE  0x6a 65 // pick pockets
  // add mage class so it can use blur
  READ_BYTE  0x234 level // copy level of first class...
  WRITE_BYTE 0x235 level // to second class
  WRITE_BYTE 0x273 13    // change class to m/t
  REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
  REMOVE_MEMORIZED_SPELL ~spwi201~
  ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
  ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
  BUT_ONLY

// soa imp
COPY_EXISTING ~famimp.cre~ ~override~
  WRITE_SHORT 0x46 6  // natural ac
  WRITE_SHORT 0x48 6  // effective ac
  REMOVE_KNOWN_SPELL spwi201
  BUT_ONLY

// soa pseudo-dragon
COPY_EXISTING ~fampsd.cre~   ~override~
  WRITE_SHORT 0x46  2 // AC
  WRITE_SHORT 0x48  2 // AC
  WRITE_BYTE  0x5d 50 // resist magic
  REPLACE_CRE_ITEM cdfampsd #0 #0 #0 none WEAPON1 EQUIP
  BUT_ONLY

// soa quasit
COPY_EXISTING ~famquas.cre~   ~override~
  WRITE_SHORT 0x46 6  // natural ac
  WRITE_SHORT 0x48 6  // effective ac
  REPLACE_CRE_ITEM s1-6 #0 #0 #0 none WEAPON1 EQUIP
  REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
  REMOVE_MEMORIZED_SPELL ~spwi201~
  ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
  ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
  BUT_ONLY

// soa rabbit
COPY_EXISTING ~famrab.cre~   ~override~
  WRITE_BYTE  0x45 20 // hide in shadows
  WRITE_SHORT 0x46 5  // natural ac
  WRITE_SHORT 0x48 5  // effective ac
  WRITE_BYTE  0x59 75 // resist fire
  WRITE_BYTE  0x5c 0  // resist acid
  WRITE_BYTE  0x5d 65 // resist magic
  WRITE_BYTE  0x5e 75 // resist magic fire
  WRITE_BYTE  0x68 20 // move silently
  WRITE_BYTE  0x69 45 // find/disarm traps
  WRITE_BYTE  0x6a 0  // pick pockets
  REPLACE_CRE_ITEM s1-2 #0 #0 #0 none WEAPON1 EQUIP
  REMOVE_CRE_ITEM rabring // disables thieving, remove
  // add mage class so it can use blur
  READ_BYTE  0x234 level // copy level of first class...
  WRITE_BYTE 0x235 level // to second class
  WRITE_BYTE 0x273 13    // change class to m/t
  REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
  REMOVE_MEMORIZED_SPELL ~spwi201~
  ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
  ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
  BUT_ONLY

ACTION_IF tob_game THEN BEGIN // tob familiars
    
  // tob cat
  COPY_EXISTING ~famcat25.cre~   ~override~
    WRITE_BYTE  0x45 89 // hide in shadows
    WRITE_SHORT 0x46 0  // natural ac
    WRITE_SHORT 0x48 0  // effective ac
    WRITE_BYTE  0x68 89 // move silently
    WRITE_BYTE  0x69 55 // find/disarm traps
    WRITE_BYTE  0x6a 50 // pick pockets
    // hastring deleted elsewhere
    // add mage class so it can use blur
    READ_BYTE  0x234 level // copy level of first class...
    WRITE_BYTE 0x235 level // to second class
    WRITE_BYTE 0x273 13    // change class to m/t
    REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
    REMOVE_MEMORIZED_SPELL ~spwi201~
    ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
    ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
    BUT_ONLY
  
  // tob dust mephit
  COPY_EXISTING ~famdus25.cre~ ~override~
    WRITE_SHORT 0x46 7  // natural ac
    WRITE_SHORT 0x48 7  // effective ac
    ADD_MEMORIZED_SPELL ~spwi224~ #1 ~wizard~     // glitterdust
    ADD_MEMORIZED_SPELL ~spin935~ #0 ~innate~ (2) // 2x glass dust
    BUT_ONLY

  // tob fairy dragon
  COPY_EXISTING ~famfai25.cre~   ~override~
    WRITE_SHORT 0x24 48 // current hp
    WRITE_SHORT 0x26 48 // max hp
    WRITE_SHORT 0x46 4  // natural ac
    WRITE_SHORT 0x48 4  // effective ac
    REPLACE_CRE_ITEM s1-2 #0 #0 #0 none WEAPON1 EQUIP
    BUT_ONLY

  // tob ferret
  COPY_EXISTING ~famfer25.cre~ ~override~
    WRITE_BYTE  0x45 50 // hide in shadows
    WRITE_SHORT 0x46 0  // natural ac
    WRITE_SHORT 0x48 0  // effective ac
    WRITE_BYTE  0x68 50 // move silently
    WRITE_BYTE  0x69 55 // find/disarm traps
    WRITE_BYTE  0x6a 85 // pick pockets
    WRITE_BYTE 0x238 15 // strength
    // add mage class so it can use blur
    READ_BYTE  0x234 level // copy level of first class...
    WRITE_BYTE 0x235 level // to second class
    WRITE_BYTE 0x273 13    // change class to m/t
    REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
    REMOVE_MEMORIZED_SPELL ~spwi201~
    ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
    ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
    BUT_ONLY

  // tob imp
  COPY_EXISTING ~famimp25.cre~ ~override~
    WRITE_SHORT 0x46 2  // natural ac
    WRITE_SHORT 0x48 2  // effective ac
    REMOVE_KNOWN_SPELL spwi201
    BUT_ONLY

  // tob pseudo-dragon
  COPY_EXISTING ~fampsd25.cre~   ~override~
    WRITE_SHORT 0x46  2 // AC
    WRITE_SHORT 0x48  2 // AC
    WRITE_BYTE  0x5d 50 // resist magic
    WRITE_BYTE 0x238 15 // strength
    BUT_ONLY
    
  // tob quasit
  COPY_EXISTING ~famqua25.cre~   ~override~
    WRITE_SHORT 0x46 2  // natural ac
    WRITE_SHORT 0x48 2  // effective ac
    ADD_KNOWN_SPELL ~spwi201~ #1 ~wizard~
    REMOVE_MEMORIZED_SPELL ~spwi201~  // blur set as lev 3 spell in known & memorized table
    ADD_MEMORIZED_SPELL ~spwi201~ #1 ~wizard~
    LPF DELETE_EFFECT INT_VAR match_opcode = 51 END // match SoA quasit coloring
    BUT_ONLY

  // tob rabbit
  COPY_EXISTING ~famrab25.cre~ ~override~
    WRITE_BYTE  0x45 50 // hide in shadows
    WRITE_SHORT 0x46 5  // natural ac
    WRITE_SHORT 0x48 5  // effective ac
    WRITE_BYTE  0x59 75 // resist fire
    WRITE_BYTE  0x5a 75 // resist cold
    WRITE_BYTE  0x5b 75 // resist electricity
    WRITE_BYTE  0x5e 75 // resist magic fire
    WRITE_BYTE  0x5f 75 // resist magic cold
    WRITE_BYTE  0x68 50 // move silently
    WRITE_BYTE  0x69 80 // find/disarm traps
    WRITE_BYTE  0x6a 0  // pick pockets
    // add mage class so it can use blur
    READ_BYTE  0x234 level // copy level of first class...
    WRITE_BYTE 0x235 level // to second class
    WRITE_BYTE 0x273 13    // change class to m/t
    REMOVE_KNOWN_SPELL     ~spwi201~ // blur set as lev 3 spell in known & memorized table
    REMOVE_MEMORIZED_SPELL ~spwi201~
    ADD_KNOWN_SPELL        ~spwi201~ #1 ~wizard~
    ADD_MEMORIZED_SPELL    ~spwi201~ #1 ~wizard~
    BUT_ONLY

END

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// bulk item file fixes                             \\\\\
/////                                                  \\\\\

// many items have effects not being applied due to bad timing modes
COPY_EXISTING ~aegis.itm~    ~override~ // color
              ~aegis2.itm~   ~override~ // color
              ~ax1h04.itm~   ~override~ // color
              ~ax1h05.itm~   ~override~ // color
              ~ax1h06.itm~   ~override~ // color
              ~ax1h08.itm~   ~override~ // color
              ~bazpatrg.itm~ ~override~ // immunity to teleport field
              ~blun01.itm~   ~override~ // color
              ~blun04.itm~   ~override~ // color
              ~blun06.itm~   ~override~ // color
              ~blun10.itm~   ~override~ // color
              ~blun23.itm~   ~override~ // color
              ~blun24.itm~   ~override~ // color
              ~blun31.itm~   ~override~ // color
              ~bodhi.itm~    ~override~ // protection from spell: nature's beauty
              ~bow02.itm~    ~override~ // color
              ~bow05.itm~    ~override~ // color
              ~bow08.itm~    ~override~ // color
              ~bow09.itm~    ~override~ // color
              ~bow10.itm~    ~override~ // color
              ~bow11.itm~    ~override~ // color
              ~bow14.itm~    ~override~ // color
              ~bow15.itm~    ~override~ // color
              ~bow16.itm~    ~override~ // color
              ~bow18.itm~    ~override~ // color
              ~bow19.itm~    ~override~ // color
              ~bow20.itm~    ~override~ // color
              ~bow21.itm~    ~override~ // color
              ~bow24.itm~    ~override~ // color
              ~bow26.itm~    ~override~ // color
              ~bow99.itm~    ~override~ // color
              ~bownon.itm~   ~override~ // color
              ~cattac1.itm~  ~override~ // color
              ~cattibow.itm~ ~override~ // color
              ~chalslay.itm~ ~override~ // protection from spell: lower resist
              ~chwraith.itm~ ~override~ // immunities to charm, sleep, hold creature, hold creature type
              ~clown.itm~    ~override~ // colorglow pulse
              ~dagg04.itm~   ~override~ // color
              ~dagg05.itm~   ~override~ // color
              ~dagg16.itm~   ~override~ // color
              ~dagg18.itm~   ~override~ // color
              ~daggshit.itm~ ~override~ // color
              ~demogorg.itm~ ~override~ // protection from spell: lower resist
              ~demsuc01.itm~ ~override~ // protection from spell: nature's beauty
              ~dwhalb01.itm~ ~override~ // color
              ~enmace.itm~   ~override~ // color
              ~enmorn.itm~   ~override~ // color
              ~ensw2h.itm~   ~override~ // color
              ~frosty.itm~   ~override~ // cold resist bonus
              ~fsspir.itm~   ~override~ // character color, immunity to teleport field
              ~ghost.itm~    ~override~ // slow
              ~ghost2.itm~   ~override~ // slow
              ~giafir.itm~   ~override~ // color
              ~giafir2.itm~  ~override~ // color
              ~giafir3.itm~  ~override~ // color
              ~giants01.itm~ ~override~ // color
              ~gorchr.itm~   ~override~ // immunity to teleport field, slay, kill target
              ~gorfirg.itm~  ~override~ // protection from nature's beauty, immunity to animations spmindat & spflayer
              ~gorjelfu.itm~ ~override~ // protection from nature's beauty, immunity to animations spmindat & spflayer
              ~gorjelgr.itm~ ~override~ // protection from nature's beauty, immunity to animations spmindat & spflayer
              ~gormisti.itm~ ~override~ // protection from nature's beauty, immunity to animations spmindat & spflayer
              ~gorwom1.itm~  ~override~ // protection from nature's beauty
              ~halb01.itm~   ~override~ // color
              ~halbrd01.itm~ ~override~ // color
              ~hamm01.itm~   ~override~ // color
//              ~helm14.itm~   ~override~ // remove fear, remove fear 2 effects - remove fear/reset morale should be instant/perm timing
              ~hgber01.itm~  ~override~ // color
              ~hgnymph.itm~  ~override~ // protection from nature's beauty
              ~hgwra01.itm~  ~override~ // protection from nature's beauty
              ~holdring.itm~ ~override~ // immunity to teleport field
              ~hslaywpn.itm~ ~override~ // regen
              ~hspectre.itm~ ~override~ // protection from deathsong, invis detection by script
              ~iax1h01.itm~  ~override~ // color
              ~iblun04.itm~  ~override~ // color
              ~ibow03.itm~   ~override~ // color
              ~ihamm01.itm~  ~override~ // color
              ~innoc2.itm~   ~override~ // protection from nature's beauty
              ~invulner.itm~ ~override~ // immunity to teleport field
              ~irongol.itm~  ~override~ // immunity to sleep, poison
              ~kuobow.itm~   ~override~ // color
              ~lich.itm~     ~override~ // resistance to cold & electricity, immunity to non-magic weapons and spell lev < 6
              ~mdog1.itm~    ~override~ // immunity to charm
              ~npbow.itm~    ~override~ // color
              ~objring.itm~  ~override~ // protection from nature's beauty, immunity to break morale
              ~plymstar.itm~ ~override~ // color
              ~reaver.itm~   ~override~ // color
              ~ring94.itm~   ~override~ // protection from nature's beauty
              ~ring95.itm~   ~override~ // protection from nature's beauty, immunity to animations spmindat & spflayer
              ~ring99.itm~   ~override~ // immunity to animations spmindat & spflayer
              ~ringwolf.itm~ ~override~ // regen
              ~seltest.itm~  ~override~ // use eff
              ~shalt01.itm~  ~override~ // protection from nature's beauty, immunity to break morale
              ~skelclub.itm~ ~override~ // color
              ~skelwasu.itm~ ~override~ // color
              ~slayerwp.itm~ ~override~ // current hp bonus
              ~slaysh01.itm~ ~override~ // protection from nature's beauty
              ~spectr.itm~   ~override~ // protection from nature's beauty
              ~stalker.itm~  ~override~ // protection from nature's beauty
              ~stonring.itm~ ~override~ // set item color
              ~stonskin.itm~ ~override~ // stoneskin effect
              ~surehp1.itm~  ~override~ // immunity to teleport field
              ~sw1h08.itm~   ~override~ // color
              ~sw1h14.itm~   ~override~ // color
              ~sw1h57.itm~   ~override~ // color
              ~sw1h99.itm~   ~override~ // color
              ~sw2h01.itm~   ~override~ // color
              ~sw2h01b.itm~  ~override~ // color
              ~sw2h02.itm~   ~override~ // color
              ~sw2h05.itm~   ~override~ // color
              ~sw2h14.itm~   ~override~ // imunity to spconfus animation
              ~telslav.itm~  ~override~ // protection from nature's beauty
              ~telslav2.itm~ ~override~ // immunity to charm, sleep
              ~ttsword2.itm~ ~override~ // color
              ~vamp.itm~     ~override~ // protection from nature's beauty
              ~vamp1.itm~    ~override~ // protection from nature's beauty
              ~vamp2.itm~    ~override~ // protection from nature's beauty
              ~vamp3.itm~    ~override~ // protection from nature's beauty
              ~wight.itm~    ~override~ // protection from nature's beauty
              ~wraith1.itm~  ~override~ // protection from nature's beauty
              ~xbow01.itm~   ~override~ // color
              ~xbow02.itm~   ~override~ // modify attacks per round
              ~xbow03.itm~   ~override~ // color
              ~xbow07.itm~   ~override~ // modify attacks per round
              ~xbow08.itm~   ~override~ // modify attacks per round
              ~xbow17.itm~   ~override~ // modify attacks per round
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 timing = 2 duration = 0 END // instant/while equipped for all global effects
  BUT_ONLY IF_EXISTS

// adds magical flag to a mess of undroppable items to prevent black dragon from taking them; other items lacking magical flag
COPY_EXISTING ~aeriebab.itm~ ~override~
              ~bearspir.itm~ ~override~
              ~bootdriz.itm~ ~override~
              ~bonefd.itm~   ~override~ // bone fiend attack
              ~clck18.itm~   ~override~
              ~dagg19.itm~   ~override~
              ~dax1h01.itm~  ~override~
              ~famcat.itm~   ~override~
              ~famcat25.itm~ ~override~
              ~famdus25.itm~ ~override~
              ~famdust.itm~  ~override~
              ~famfai25.itm~ ~override~
              ~famfair.itm~  ~override~
              ~famfer.itm~   ~override~
              ~famfer25.itm~ ~override~
              ~famimp.itm~   ~override~
              ~famimp25.itm~ ~override~
              ~fampsd.itm~   ~override~
              ~fampsd25.itm~ ~override~
              ~famqua25.itm~ ~override~
              ~famquas.itm~  ~override~
              ~famrab.itm~   ~override~
              ~famrab25.itm~ ~override~
              ~helmskwa.itm~ ~override~
              ~kuobow.itm~   ~override~
              ~kuosper.itm~  ~override~
              ~lionspir.itm~ ~override~
              ~misc4g.itm~   ~override~
              ~misc73.itm~   ~override~ // horn of kazgaroth
              ~misc84.itm~   ~override~
              ~plat98.itm~   ~override~
              ~plybass.itm~  ~override~
              ~plyfist.itm~  ~override~
              ~plymstar.itm~ ~override~
              ~plysala.itm~  ~override~
              ~plyspid.itm~  ~override~
              ~plywyvrn.itm~ ~override~
              ~scrlag.itm~   ~override~
              ~snakspir.itm~  ~override~
              ~sw1h47.itm~   ~override~
              ~wolfspir.itm~ ~override~
  WRITE_BYTE 0x18 (THIS BOR BIT6)
  BUT_ONLY IF_EXISTS

// fix power levels
COPY_EXISTING ~arow06.itm~   ~override~ // arrows of detonation - explosion has power 3
              ~arow08.itm~   ~override~ // arrows of fire - fire damage has power 6
              ~arow09.itm~   ~override~ // arrows of ice - cold damage has power 6
              ~aurstaf.itm~  ~override~ // aurumach rilmani's undroppable staff - slow effects have power 6
              ~ax1h07.itm~   ~override~ // bala's axe - miscast magic effects have power 3
              ~blun20.itm~   ~override~ // ardulia's flail - slow effects have power 3
              ~bolt03.itm~   ~override~ // bolt of lightning - electrical damage has power 6
              ~bolt05.itm~   ~override~ // bolt of polymorphing - polymorph effects are power 6
              ~bonedag.itm~  ~override~ // bone dagger attack item - slow effects have power 2, all other effects 0
              ~chalcy2.itm~  ~override~ // undroppable weapon of cyric's chosen - blindness effect has power 1
              ~dart03.itm~   ~override~ // dart of stunning - stun effects have power 8
              ~dart04.itm~   ~override~ // dart of wounding - poison damage has power 0, poison portrait icon has power 8
              ~dart05.itm~   ~override~ // asp's nest - poison damage has power 0, poison portrait icon has power 8
              ~dartmel.itm~  ~override~ // melissan's bone daggers - slow and disease effects have power 8
              ~globblu1.itm~ ~override~ // blue globe - cold damage at power 5
              ~globred1.itm~ ~override~ // red globe - fire damage at power 4
              ~gorcamb.itm~  ~override~ // aesgearth's undroppable weapon - stun at power 6, str/con drain at power 8
              ~hamm03.itm~   ~override~ // ashideena - electrical damage has power 8
              ~hamm05.itm~   ~override~ // borok's fist - electrical damage has power 8
              ~hgber01.itm~  ~override~ // berren's undroppable weapon - slow and dex penalties at power 4
              ~hgnya01.itm~  ~override~ // nyalee's undroppable weapon - slow, entangle effects at power 4
              ~icetrl.itm~   ~override~ // blizzard trolls' undroppable weapon - stun, dex penalty at power 6
              ~killsw01.itm~ ~override~ // arkanis gath's insta-kill sword for crossing shadow thieves - all elemental damages at power 8
              ~kuobolt3.itm~ ~override~ // high level kuo-toa bolts - web/entangle effects at power 2
              ~misc5c.itm~   ~override~ // rift device - fire damage has power 100, all other effects are at 0
              ~mistcd.itm~   ~override~ // attack of crimson deaths - visual effect has power 1
              ~mistho.itm~   ~override~ // attack of mist horrors, swamp horrors - horror effects are at power 4
              ~mistice.itm~  ~override~ // attack of ice mists - horror effects are at power 4
              ~mistva.itm~   ~override~ // attack of vampiric mists - level drain at power 1
              ~mistva2.itm~  ~override~ // attack of vampiric wraiths - level drain at power 1
              ~mistwa.itm~   ~override~ // attack of wandering horrors - stun at power 4
              ~mound.itm~    ~override~ // attack of shambling mounds - slow, ac penalty, extra damage, entangle at power 1, play sound at power 2
              ~nebdag.itm~   ~override~ // neb's dagger - slow effect at power 2, poison at 0
              ~potn26.itm~   ~override~ // potion of explosions
              ~potn27.itm~   ~override~ // potion of firebreath
              ~quasclaw.itm~ ~override~ // attack of quasit familiars - dex drain has power 4
              ~ravag01.itm~  ~override~ // attack of ravager - one effect (visual) has power 3, all others 0
              ~ravag02.itm~  ~override~ // attack of ravager - stun has power 6, all others 0
              ~revent1.itm~  ~override~ // attack of revenants - paralyzation effects at power 1
              ~ring20.itm~   ~override~ // ring of energy
              ~rods03.itm~   ~override~ // rod of resurrection
              ~scrl03.itm~   ~override~ // protection from acid
              ~scrl04.itm~   ~override~ // protection from cold
              ~scrl05.itm~   ~override~ // protection from electricity
              ~scrl06.itm~   ~override~ // protection from fire
              ~scrl08.itm~   ~override~ // protection from poison
              ~scrl09.itm~   ~override~ // protection from undead
              ~scrl15.itm~   ~override~ // protection from petrification
              ~secret02.itm~ ~override~ // pulse ammo - magical damage for easter egg item at power 8
              ~sendai.itm~   ~override~ // sendai's flail - slow has power 6, other effects such as str drain and poison use 0
              ~shille.itm~   ~override~ // shillelagh - visual effect has power 1
              ~spermel.itm~  ~override~ // mellisan's spear - slow and stun at power 8, save penalties at power 6
              ~staf14.itm~   ~override~ // staff of the woodlands
              ~sw1h06.itm~   ~override~ // varscona - cold damage at power 8
              ~sw1h58.itm~   ~override~ // short sword of mask - some entangle effects at power 1, 2
              ~sw1h59.itm~   ~override~ // short sword of mask - some entangle effects at power 1, 2
              ~telslav.itm~  ~override~ // attack of slave wraiths - slay and finger of death animation at power 7
              ~trolltor.itm~ ~override~ // attack of tor'gal - slow and str drain at power 3
              ~wand12.itm~   ~override~ // wand of wonder - all effects at power 0 except visuals at 8
              ~wolfwi1.itm~  ~override~ // attack of winter wolves - cold damage at power 1
              ~wolfwi2.itm~  ~override~ // attack of winter wolves - cold damage at power 1
              ~xbow15.itm~   ~override~ // firetooth - fire damage at power 6
              ~xbow16.itm~   ~override~ // firetooth - fire damage at power 6
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 0 END // should bypass spell protections
  BUT_ONLY IF_EXISTS

// weapon-speed fixes (fixes descripts and items)
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  // descript is right, item is wrong
  aegis    => 1 // aegis fang (descript is correct)
  aegis2   => 1 // aegis fang (descript is correct)
  ax1h07   => 7 // bala's axe (descript is correct)
  dart01   => 2 // dart (descript is correct)
  misc9q   => 5 // scimitar (descript is correct)
  sper03   => 6 // spear +3, backbiter (descript is correct)
  staf04   => 4 // quarterstaff (descript is correct)
  staf05   => 1 // staff of striking (descript is correct)
  stardart => 0 // dart +5 (descript doesn't have speed listed w/o GTU/L)
  sw1h26   => 2 // ilbratha +1 (descript is correct)
  sw2h03   => 10// cursed berserking sword +3 (descript is correct)

  // both item and descript are wrong
  blun22   => 1 // blackblood +3 (string #39490 and item need updates)
  blun23   => 2 // bone club +2, +3 vs. undead (string #39493 and item need updates)
  blun24   => 2 // gnasher +2 (string #39495 and item need updates)
  blun25   => 5 // imod (string #60877 and item need updates)
  blun26   => 1 // club of detonation +3 (string #66375 and item need updates)
  blun27   => 0 // club of detonation +5 (string #66376 and item need updates)
  bow25    => 4 // long bow +3 (string #71058 and item need updates) (string #71958 already fixed in strings for separate thac0 issue)
  dagg11   => 0 // boomerang dagger (string #39527 and item need updates)
  dagg12   => 0 // fire tooth +3 (string #39528 and item need updates)
  dwxbow01 => 2 // drow crossbow of speed (string #46706 and item need updates)
  hamm04   => 3 // hammer +1, +4 vs. giantkin (string #22263 and item need updates)
  hamm09   => 0 // crom faeyr (string #60863 and item need updates)
  misc4u   => 2 // embarl's dagger (string #34732 and item need updates)
  npbow    => 4 // bow of arvoreen (string #50827 and item need updates)
  slng08   => 2 // erinne sling +4 (string #66393 and item need updates)
  slng09   => 1 // erinne sling +5 (string #66394 and item need updates)
  sper12   => 1 // ixil's nail +6 (string #66474 and item need updates) 
  staf09   => 2 // staff of command (string #39625 and item need updates)
  staf12   => 2 // staff of power (string #39628 and item need updates)
  staf13   => 2  //staff of thunder and lightning (string #39629 and item need updates)
  sw1h24   => 4 // flame tongue (string #6526 and item need updates)
  sw1h28   => 0 // cutthroat +4 (string #39451 and item need updates)
  sw1h54   => 2 // equalizer (string #60866 and item need updates)
  sw2h14   => 7 // lilarcor (string #34090 and item need updates)
  waflail  => 4 // defender of easthaven (string #32187 and item need updates) (string #32187 already fixed in strings for separate thac0 issue)
  wasling  => 1 // sling of everard (string #32143 and item need updates)
  wastaff  => 1 // staff of arundel (string #32128 and item need updates)
  xbow03   => 5 // heavy crossbow of accuracy (string #7352 and item need updates)
  xbow06   => 4 // light crossbow of speed (string #7353 and item need updates)
  xbow13   => 1 // crossbow of afflication (string #39506 and item need updates)

  // item is right, descript is wrong
  blun14d  => 5 // flail of ages +2 (cold, fire) (strref #41389)
  blun14e  => 5 // flail of ages +2 (cold, acid) (strref #41620)
  blun14f  => 5 // flail of ages +2 (acid, fire) (strref #41621)
  blun35   => 3 // ice star +4 (strref #73912)
  bow11    => 5 // strong arm (strref #39511)
  bow17    => 5 // long bow +2 (strref #176)
  bow18    => 4 // short bow +2 (strref #199)
  halb04   => 6 // dragon's bane (strref #39531)
  misc5t   => 4 // shaman's staff of goodberries (strref #47131)
  npsw05   => 1 // entropy (strref #51947)
  npsw06   => 1 // chaos blade (strref #51949)
  sper10   => 2 // spear of withering (strref #2494)
  sper11   => 2 // ixil's nail +4 (strref #66372)
  staf10   => 3 // staff of curing (strref #39626)
  staf11   => 1 // staff of the magi (strref #39627)
  staf15   => 2 // staff of air (strref #39631)
  staf16   => 2 // staff of earth (strref #39632)
  staf17   => 2 // staff of fire (strref #39633)
  staf18   => 2 // staff +2 (strref #39634)
  staf23   => 2 // serpent shaft (strref #66369)
  sw1h52   => 2 // scimitar +3 water's edge (strref #51884)
  sw1h53   => 4 // sword of flame +1 (strref #4421)
  sw1h60   => 1 // angurvadal +4 (strref #66203)
  sw1h69   => 0 // spectral brand +5 (strref #66275)
  sw1h73   => 2 // long sword +3 (strref #70785)
  sw2h15   => 7 // silver sword (strref #48658)
  wa2halb  => 6 // harmonium halberd (strref #61593)

END

OUTER_SPRINT text_match @30

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 speed = fix END // anything that turns up in weapon slots (e.g. not magical abilities)
    LAUNCH_PATCH_MACRO cd_bulk_fix_item_strings
    BUT_ONLY IF_EXISTS

END

// Comprehensive batch of item-weight fixes (Wisp)
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

//  chan02   => 20 // chainmail +1 (identified strref is wrong so this would patch wrong string; fixed in individual fixes)
//  plymstar =>  0 // (zero-weight magical items) ogre's morningstar from polymorph self; uses generic morningstar description so fixed individually

  // weight in item is wrong
  bolt07   =>  0 // (zero-weight magical items) jan's flashers
  bow06    =>  2 // short bow +1
  brac15   =>  2 // bracers of defense ac 3
  brdflute =>  0 // (zero-weight magical items) bard flute
  clck13   =>  4 // traveller's robe
  dagg05   =>  0 // throwing dagger
  dwchan02 => 12 // drow adamantine chain +5
  dwshld01 =>  7 // drow shield +3
  enmace   =>  0 // (zero-weight magical items) enchanted weapon: mace
  enmorn   =>  0 // (zero-weight magical items) enchanted weapon: morningstar
  enstaff  =>  0 // (zero-weight magical items) enchanted weapon: staff
  ensw1h01 =>  0 // (zero-weight magical items) enchanted weapon: long sword
  ensw1h02 =>  0 // (zero-weight magical items) enchanted weapon: short sword
  ensw2h   =>  0 // (zero-weight magical items) enchanted weapon: battle axe
  melfmet  =>  0 // (zero-weight magical items) melf's minute meteors
  plysala  =>  0 // (zero-weight magical items) salamander's spear from polymorph self
  shille   =>  0 // (zero-weight magical items) shillegagh
  shld23   => 10 // fortress shield +3
  sorb     =>  0 // (zero-weight magical items) sol's searing orb
  sper06   =>  3 // spear +3
  staf04   =>  4 // quarterstaff
  staf08   =>  4 // martial staff +3
  staf10   =>  4 // staff of curing
  staf11   =>  4 // staff of the magi
  staf13   =>  4 // staff of thunder and lightning
  staf20   =>  3 // staff of rynn +4
  sw1h15   =>  4 // scimitar +3, frostbrand
  sw1h16   =>  4 // scimitar +5, defender
  sw1h23   =>  4 // scimitar +2, rashad's talon
  sw1h32   =>  3 // dragonslayer
  sw2h03   => 15 // cursed berserking sword +3
  sw2h12   => 10 // flame of the north
  sw2h15   => 15 // silver sword

  // weight in description is wrong
  bag06    =>  2 // potion case (strref #71131) (also used by other potion cases)
  blun03   => 13 // flail +1 (strref #19359)
  blun04   => 10 // generic mace (strref #6709)
  hlolth   =>  7 // handmaiden's mace (strref #54340)
  kuobolt  =>  0 // kuo-toa bolt (strref #57897) (also used by kuobolt2, kuobolt3)
  npstaf   =>  3 // staff of the high forest (strref #8505)
  plat19   => 40 // full plate mail +2 (strref #1768)
  shld27   =>  4 // saving grace (strref #39579)
  wa2shiel =>  4 // shield of balduran (strref #61580)
  
  // string and item are wrong
  sahbolt  => 0 // Paralytic Bolt (strref #57919)

END

OUTER_SPRINT text_match @31

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_LONG 0x4c fix // fixes item weight
    LAUNCH_PATCH_MACRO cd_bulk_fix_item_strings
    BUT_ONLY IF_EXISTS

END

// thac0 fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN
  arow08 => 0 // arrows of fire, only needs an item fix
  bow09  => 3 // ripper +2, also fixes string #39508
  bow13  => 5 // mana bow +4, also fixes string #39513
  bow17  => 3 // long bow +2, also fixes string #176
  bow22  => 5 // taralash +4, also fixes string #66388
  bow23  => 6 // taralash +5, also fixes string #66389
  bow25  => 4 // long bow +2, also fixes string #71058
END

OUTER_SPRINT text_match @32

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 to_hit = fix END // anything that turns up in weapon slots (e.g. not magical abilities)
    LAUNCH_PATCH_MACRO cd_bulk_fix_item_strings
    BUT_ONLY IF_EXISTS

END

// lore fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  bull06   =>  35 // bullet +4
  chan03   =>  65 // mail of the dead
  chan21   =>  75 // chain mail +3
  dwchan01 =>  75 // drow elven chain +3
  dwchan02 =>  75 // drow adamantine chain +5
  dwplat01 =>  75 // drow full plate +5
  halb03   =>  60 // suryris' blade
  leat03   =>  60 // protector of the second +2
  leat06   => 100 // missile attraction +2
  misc3a   =>  80 // book of infinite spells
  plat02   =>  40 // plate mail +1
  plat23   =>  75 // full plate mail +2
  rods02   =>  70 // rod of lordly might
  rods06   =>  85 // rod of reversal
  shld17   =>  10 // buckler +1
  slng03   =>  45 // sling +3
  staf06   =>  25 // staff mace
  staf07   =>  65 // staff spear +2
  sw1h06   =>  60 // long sword +2
  wand11   =>  50 // wand of the heavens

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_SHORT 0x42 fix
      BUT_ONLY IF_EXISTS

END

// item type fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  blun27   => 17 // Club of Detonation +5
  book05   =>  0 // Manual of Quickness of Action (type 0 allows quickslotting)
  book07   =>  0 // Tome of Leadership and Influence (type 0 allows quickslotting)
  book08   =>  0 // Tome of Understanding (type 0 allows quickslotting)
  book32   => 37 // History of Tethyr
  book94   => 37 // The Vampiricus Omnibus: Unabridged
  book95   => 37 // Dea Vampir Becomos
  book96   => 37 // Conjur Ota Servanta
  dagg10   => 16 // soultaker dagger
  misc73   =>  0 // horn of kazgaroth (type 0 allows quickslotting)
  scrla9   => 37 // waneev's note
  scrlaa   => 37 // waneev's note
  scrlab   => 37 // monkey balls
  scrlac   => 37 // amnish dragoon soup
  scrlad   => 37 // baldur's delight
  scrlaf   => 37 // ruby racks
  scrlag   => 37 // crom faeyr scroll
  scrlhp   => 37 // harper note
  scrlick  => 37 // illithid correspndence
  scrlmz   => 37 // note from Mazzy Fentan

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_SHORT 0x1c fix
      BUT_ONLY IF_EXISTS

END

// prof fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  // all of these prof 2 items are ammo; prof 0 lets you use them w/o non-prof penalty. The non-zero value keeps the prof type from the bow/sling/whatever
  aegis    =>  97 // aegis fang
  aegis2   =>  97 // aegis fang
  arow01   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows - arrows w/o shortbow prof
  arow02   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows +1 - arrows w/o shortbow prof
  arow03   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow of slaying - arrows w/o shortbow prof
  arow04   =>   2 // (ammo with 0 prof removes non-prof penalty)  acid arrows - arrows w/o shortbow prof
  arow05   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow of biting - arrows w/o shortbow prof
  arow06   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows of detonation  - arrows w/o shortbow prof
  arow07   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows of dispelling - arrows w/o shortbow prof
  arow08   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow of fire - arrows w/o shortbow prof
  arow09   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows of ice - arrows w/o shortbow prof
  arow10   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows of piercing - arrows w/o shortbow prof
  arow11   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow +2 - arrows w/o shortbow prof
  arow14   =>   2 // (ammo with 0 prof removes non-prof penalty)  poisoned arrow - arrows w/o shortbow prof
  arow15   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow +3 - arrows w/o shortbow prof
  arow1a   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrow +2 - arrows w/o shortbow prof
  bolt01   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt - bolts w/o xbow prof
  bolt02   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt +1 - bolts w/o xbow prof
  bolt03   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt of lightning - bolts w/o xbow prof
  bolt04   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt of biting  - bolts w/o xbow prof
  bolt05   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt of polymorphing - bolts w/o xbow prof
  bolt06   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt +2 - bolts w/o xbow prof
  bolt07   =>   2 // (ammo with 0 prof removes non-prof penalty)  flasher master bruiser mate - bolts w/o xbow prof
  bolt08   =>   2 // (ammo with 0 prof removes non-prof penalty)  blessed bolt - bolts w/o xbow prof
  bolt09   =>   2 // (ammo with 0 prof removes non-prof penalty)  bolt +3 - bolts w/o xbow prof
  bow06    => 105 // short bow +1
  bow07    => 104 // long bow of marksmanship
  bruenaxe =>  92 // battle axe +3
  bull01   =>   2 // (ammo with 0 prof removes non-prof penalty)  bullet - bullet w/o sling prof
  bull02   =>   2 // (ammo with 0 prof removes non-prof penalty)  bullet +1 - bullet w/o sling prof
  bull03   =>   2 // (ammo with 0 prof removes non-prof penalty)  bullet +2 - bullet w/o sling prof
  bull04   =>   2 // (ammo with 0 prof removes non-prof penalty)  sunstone bullet +1 - bullet w/o sling prof
  bull05   =>   2 // (ammo with 0 prof removes non-prof penalty)  bullet +3 - bullet w/o sling prof
  bull06   =>   2 // (ammo with 0 prof removes non-prof penalty)  bullet +4 - bullet w/o sling prof
  dwbolt01 =>   2 // (ammo with 0 prof removes non-prof penalty)  drow bolt of sleep - bolts w/o xbow prof
  dwbolt02 =>   2 // (ammo with 0 prof removes non-prof penalty)  drow bolt of stunning - bolts w/o xbow prof
  dwbolt03 =>   2 // (ammo with 0 prof removes non-prof penalty)  drow bolt +1 - bolts w/o xbow prof
  flam01   =>   2 // (ammo with 0 prof removes non-prof penalty)  flamethrower - bolts w/o xbow prof
  frag01   =>   2 // (ammo with 0 prof removes non-prof penalty)  frag grenade - bolts w/o xbow prof
  frosty   =>   2 // (ammo with 0 prof removes non-prof penalty)  dr. freeze death ray - bolts w/o xbow prof
  iarow01  =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows - arrows w/o shortbow prof
  kuobolt  =>   2 // (ammo with 0 prof removes non-prof penalty)  kuo-toa bolts - bolts w/o xbow prof
  kuobolt2 =>   2 // (ammo with 0 prof removes non-prof penalty)  kuo-toa bolts - bolts w/o xbow prof
  kuobolt3 =>   2 // (ammo with 0 prof removes non-prof penalty)  kuo-toa bolts - bolts w/o xbow prof
  laser    =>   2 // (ammo with 0 prof removes non-prof penalty)  laser - bolts w/o xbow prof
  light    =>   2 // (ammo with 0 prof removes non-prof penalty)  wand of magic missiles - bolts w/o xbow prof
  misc4q   =>  89 // ogre's sword
  misc4u   =>  96 // embarl's dagger
  misc5t   => 102 // shaman's staff
  misc75   =>  96 // dagger of venom
  puls01   =>   2 // (ammo with 0 prof removes non-prof penalty)  wand of magic missiles - bolts w/o xbow prof
  puls02   =>   2 // (ammo with 0 prof removes non-prof penalty)  wand of magic missiles - bolts w/o xbow prof
  quiv01   =>   2 // (ammo with 0 prof removes non-prof penalty)  arrows - arrows w/o shortbow prof
  quiver01 =>   2 // (ammo with 0 prof removes non-prof penalty)  quiver of plenty +1 - arrows w/o shortbow prof
  quiver02 =>   2 // (ammo with 0 prof removes non-prof penalty)  case of plenty +1 - bolts w/o xbow prof
  quiver03 =>   2 // (ammo with 0 prof removes non-prof penalty)  quiver of plenty +2 - arrows w/o shortbow prof
  quiver04 =>   2 // (ammo with 0 prof removes non-prof penalty)  case of plenty +2 - bolts w/o xbow prof
  quiver05 =>   2 // (ammo with 0 prof removes non-prof penalty)  bag of plenty +1 - bullet w/o sling prof
  quiver06 =>   2 // (ammo with 0 prof removes non-prof penalty)  bag of plenty +2 - bullet w/o sling prof
  sahbolt  =>   2 // (ammo with 0 prof removes non-prof penalty)  paralytic bolt - bolts w/o xbow prof
  secret02 =>   2 // (ammo with 0 prof removes non-prof penalty)  pulse ammunition - bolts w/o xbow prof
  secret03 =>   2 // (ammo with 0 prof removes non-prof penalty)  frag grenade - bolts w/o xbow prof
  secret04 =>   2 // (ammo with 0 prof removes non-prof penalty)  scorcher ammunition - bolts w/o xbow prof

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_BYTE 0x31 fix
      BUT_ONLY IF_EXISTS

END

// enchantment fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  arow04   => 1 // acid arrows
  bearspir => 3 // weapon of totemic druid summon
  bolt07   => 1 // Flasher Master Bruiser Mate
  bow06    => 1 // short bow +1
  bruenaxe => 3 // battle axe +3
  bull04   => 1 // sunstone bullet
  dagg19   => 1 // dagger of <CHARNAME>
  dart05   => 1 // asp's nest
  kuobolt2 => 2 // kuo-toa bolt
  lionspir => 3 // weapon of totemic druid summon
  misc75   => 2 // dagger of venom
  p3-12m4  => 4 // by Bioware naming convention, this item should be 4 enchantment
  snakspir => 3 // weapon of totemic druid summon
  staf10   => 1 // staff of curing
  sw1h03   => 3 // kondar
  sw1h31   => 4 // daystar
  wolfspir => 3 // weapon of totemic druid summon

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_LONG  0x60 fix
      BUT_ONLY IF_EXISTS

END

//items with dispellable on-equip effects (Wisp)
COPY_EXISTING ~aurstaf.itm~  ~override~ // dispellable protection from projectile: bullet
              ~dragring.itm~ ~override~ // dispellable protection from projectile: arrowhvy, axe, bolt, bullet, dagger, dart
              ~elemchan.itm~ ~override~ // dispellable opcode 232, renewing globe of blades ability
              ~elemyanc.itm~ ~override~ // ditto
              ~impinvis.itm~ ~override~ // dispellable invisibility (creatures should not be visible, ever)
              ~jonhp1.itm~   ~override~ // dispellable protection from fiends
              ~kuoring.itm~  ~override~ // dispellable immunity to slow
              ~lich.itm~     ~override~ // dispellable protection from fiends
              ~mage05.itm~   ~override~ // free action. dispellable everything
              ~mage06.itm~   ~override~ // dispellable haste
              ~npsw03.itm~   ~override~ // dispellable opcode 232, for the 'retribution' type effect on the sword
              ~slayerwp.itm~ ~override~ // dispellable protection from projectile: arrowhvy, axe, bolt, bullet, dagger, dart
              ~sw1hseek.itm~ ~override~ // dispellable apr bonus
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_resist_dispel = 1 resist_dispel = 2 END
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_resist_dispel = 3 resist_dispel = 2 END
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// big block of unusability stuff                   \\\\\
/////                                                  \\\\\

// BMU (sigh), scroll supposed to be completely restriction-free
COPY_EXISTING ~scrladj.itm~  ~override~ // firkraag's note was copied from mage scroll; clearing flags
              ~secret01.itm~ ~override~
              ~secret02.itm~ ~override~
              ~secret03.itm~ ~override~
              ~secret04.itm~ ~override~
  WRITE_LONG 0x1e 0 // removes race/class flags
  WRITE_BYTE 0x29 0 // removes kit flags, pt 1
  WRITE_BYTE 0x2b 0 // removes kit flags, pt 2
  WRITE_BYTE 0x2d 0 // removes kit flags, pt 3
  WRITE_BYTE 0x2f 0 // removes kit flags, pt 4
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~hamm06.itm~  ~override~ // dwarven thrower
              ~misc5x.itm~  ~override~ // harper pin
              ~misca6.itm~  ~override~ // patrol leader helmet
              ~npbow.itm~   ~override~ // bow of arvoreen
              ~npsw01.itm~  ~override~ // sword of arvoreen
              ~ring35.itm~  ~override~ // ring of lock picking
              ~wa2ring.itm~ ~override~ // mercykiller ring
  WRITE_BYTE 0x1e (THIS BOR BIT6) // adds bard flag
  BUT_ONLY

COPY_EXISTING ~brac06.itm~ ~override~ // Gauntlets of Ogre Power
  WRITE_BYTE 0x1e (THIS BAND `BIT6) // removes bard flag
  BUT_ONLY

COPY_EXISTING ~npplat.itm~  ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~  ~override~ // hallowed redeemer
              ~waninja.itm~ ~override~ // scarlet ninja-to
  WRITE_BYTE 0x1e (THIS BOR BIT5) // adds lc-axis neutral flag
  BUT_ONLY

COPY_EXISTING ~npchan.itm~ ~override~ // corthala family armor
              ~npsw04.itm~ ~override~ // Corthala Family Blade
  WRITE_BYTE 0x1e (THIS BOR BIT4) // adds lawful flag
  BUT_ONLY

COPY_EXISTING ~npchan.itm~ ~override~ // corthala family armor
              ~npplat.itm~ ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~ ~override~ // hallowed redeemer
              ~npsw04.itm~ ~override~ // Corthala Family Blade
  WRITE_BYTE 0x1e (THIS BOR BIT3) // adds ge-axis neutral flag
  BUT_ONLY

COPY_EXISTING ~npchan.itm~ ~override~ // corthala family armor
              ~npplat.itm~ ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~ ~override~ // hallowed redeemer
              ~npsw04.itm~ ~override~ // Corthala Family Blade
  WRITE_BYTE 0x1e (THIS BOR BIT1) // adds evil flag
  BUT_ONLY

COPY_EXISTING ~npchan.itm~  ~override~ // corthala family armor
              ~npplat.itm~  ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~  ~override~ // hallowed redeemer
              ~npsw04.itm~  ~override~ // Corthala Family Blade
              ~waninja.itm~ ~override~ // scarlet ninja-to
  WRITE_BYTE 0x1e (THIS BOR BIT0) // adds chaotic flag
  BUT_ONLY

COPY_EXISTING ~hamm06.itm~ ~override~ // dwarven thrower
              ~dagg18.itm~ ~override~ // shadow thief dagger
              ~misc9q.itm~ ~override~ // scimitar
              ~npchan.itm~ ~override~ // corthala family armor
  WRITE_BYTE 0x1f (THIS BOR BIT7) // adds f/m/c flag
  BUT_ONLY

COPY_EXISTING ~misc9q.itm~ ~override~ // scimitar
              ~npchan.itm~ ~override~ // corthala family armor
  WRITE_BYTE 0x1f (THIS BOR BIT6) // adds f/c flag
  BUT_ONLY

COPY_EXISTING ~brac17.itm~ ~override~ // gloves of pickpocketing
              ~hamm06.itm~ ~override~ // dwarven thrower
              ~npbow.itm~  ~override~ // bow of arvoreen
              ~npchan.itm~ ~override~ // corthala family armor
              ~npsw01.itm~ ~override~ // sword of arvoreen
  WRITE_BYTE 0x1f (THIS BOR BIT5) // adds f/m flag
  BUT_ONLY

COPY_EXISTING ~misc4q.itm~  ~override~ // the ogre's sword
              ~npchan.itm~  ~override~ // corthala family armor
              ~rodmace.itm~ ~override~ // mace from rod o' lordly might
              ~rods01.itm~  ~override~ // rod of absorption
              ~rods04.itm~  ~override~ // rod of smiting
              ~sw1h46.itm~  ~override~ // wakizashi
              ~sw1h47.itm~  ~override~ // wakizashi +1
              ~sw1h66.itm~  ~override~ // yamato +4
              ~sw1h99.itm~  ~override~ // sword of chaos
  WRITE_BYTE 0x1f (THIS BOR BIT4) // adds f/d flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~brac17.itm~ ~override~ // gloves of pickpocketing
              ~npchan.itm~ ~override~ // corthala family armor
  WRITE_BYTE 0x1f (THIS BOR BIT3) // adds f flag
  BUT_ONLY

COPY_EXISTING ~hamm06.itm~ ~override~ // dwarven thrower
              ~misc9q.itm~ ~override~ // scimitar
              ~npchan.itm~ ~override~ // corthala family armor
  WRITE_BYTE 0x1f (THIS BOR BIT2) // adds c/r flag
  BUT_ONLY

COPY_EXISTING ~potn56.itm~ ~override~ // potion of frost giant strength
  WRITE_BYTE 0x1f (THIS BAND `BIT1) // removes c/t flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~hamm06.itm~ ~override~ // dwarven thrower
              ~misc9q.itm~ ~override~ // scimitar
              ~npchan.itm~ ~override~ // corthala family armor
  WRITE_BYTE 0x1f (THIS BOR BIT1) // adds c/t flag
  BUT_ONLY

COPY_EXISTING ~hamm06.itm~  ~override~ // dwarven thrower
              ~npchan.itm~  ~override~ // corthala family armor
              ~ring35.itm~  ~override~ // ring of lock picking
              ~wa2ring.itm~ ~override~ // mercykiller ring
  WRITE_BYTE 0x1f (THIS BOR BIT0) // adds c/m flag
  BUT_ONLY

COPY_EXISTING ~npsw03.itm~ ~override~ // hallowed redeemer
  WRITE_BYTE 0x20 (THIS BOR BIT7) // adds elf flag
  BUT_ONLY

COPY_EXISTING ~dwsw1h01.itm~ ~override~ // drow scimitar +3
              ~npsw04.itm~   ~override~ // Corthala Family Blade (allows for backstabbing)
  WRITE_BYTE 0x20 (THIS BAND `BIT6) // removes thief flag
  BUT_ONLY


COPY_EXISTING ~scrl8e.itm~ ~override~ // protection from the elements
  WRITE_BYTE 0x20 (THIS BOR BIT6) // adds thief flag
  BUT_ONLY

COPY_EXISTING ~brac17.itm~ ~override~ // gloves of pickpocketing
              ~hamm06.itm~ ~override~ // dwarven thrower
              ~npbow.itm~  ~override~ // bow of arvoreen
              ~npsw01.itm~ ~override~ // sword of arvoreen
              ~staf14.itm~ ~override~ // staff of woodlands
  WRITE_BYTE 0x20 (THIS BOR BIT5) // adds ranger flag
  BUT_ONLY

COPY_EXISTING ~amul21.itm~ ~override~ // Amulet of power
  WRITE_BYTE 0x20 (THIS BAND `BIT5) // removes ranger flag
  BUT_ONLY

COPY_EXISTING ~brac17.itm~ ~override~ // gloves of pickpocketing
              ~hamm06.itm~ ~override~ // dwarven thrower
              ~misc3m.itm~ ~override~ // Harp of Discord
              ~misc3o.itm~ ~override~ // Methild's Harp
              ~npbow.itm~  ~override~ // bow of arvoreen
              ~npsw01.itm~ ~override~ // sword of arvoreen
  WRITE_BYTE 0x20 (THIS BOR BIT4) // adds paladin flag
  BUT_ONLY

COPY_EXISTING ~amul21.itm~ ~override~ // Amulet of power
  WRITE_BYTE 0x20 (THIS BAND `BIT4) // removes paladin flag
  BUT_ONLY

COPY_EXISTING ~misca6.itm~ ~override~ // patrol leader helmet
              ~npbow.itm~  ~override~ // bow of arvoreen
              ~npsw01.itm~ ~override~ // sword of arvoreen
  WRITE_BYTE 0x20 (THIS BOR BIT3) // adds m/t flag
  BUT_ONLY

COPY_EXISTING ~chan20.itm~   ~override~ // white dragon scale
              ~dwsw1h01.itm~ ~override~ // drow scimitar +3
              ~potn56.itm~   ~override~ // potion of frost giant strength
  WRITE_BYTE 0x20 (THIS BAND `BIT3) // removes m/t flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~misca6.itm~ ~override~ // patrol leader helmet
  WRITE_BYTE 0x20 (THIS BOR BIT2) // adds mage flag
  BUT_ONLY

COPY_EXISTING ~npchan.itm~ ~override~ // corthala family armor
              ~rods04.itm~ ~override~ // rod of smiting
  WRITE_BYTE 0x20 (THIS BOR BIT1) // adds f/t flag
  BUT_ONLY

COPY_EXISTING ~hamm06.itm~ ~override~ // dwarven thrower
              ~npbow.itm~  ~override~ // bow of arvoreen
              ~npchan.itm~ ~override~ // corthala family armor
              ~npsw01.itm~ ~override~ // sword of arvoreen
              ~npsw03.itm~ ~override~ // hallowed redeemer
              ~waninja.itm~ ~override~ // scarlet ninja-to
  WRITE_BYTE 0x20 (THIS BOR BIT0) // adds f/m/t flag
  BUT_ONLY

COPY_EXISTING ~hamm06.itm~  ~override~ // dwarven thrower
              ~hlolth.itm~  ~override~ // handmaiden's mace
              ~misc5x.itm~  ~override~ // harper pin
              ~npbow.itm~   ~override~ // bow of arvoreen
              ~npchan.itm~  ~override~ // corthala family armor
              ~npmisc1.itm~ ~override~ // Jansen Spectroscopes
              ~npplat.itm~  ~override~ // Firecam Full-Plate Armor
              ~npsw01.itm~  ~override~ // sword of arvoreen
              ~npsw02.itm~  ~override~ // yoshimo's katana
              ~npsw03.itm~  ~override~ // hallowed redeemer
              ~npsw04.itm~  ~override~ // Corthala Family Blade
              ~npsw05.itm~  ~override~ // chaos
              ~npsw06.itm~  ~override~ // entropy
              ~waninja.itm~ ~override~ // scarlet ninja-to
  WRITE_BYTE 0x21 (THIS BOR BIT7) // adds half-orc flag
  BUT_ONLY

COPY_EXISTING ~npmisc1.itm~ ~override~ // Jansen Spectroscopes
              ~sw1h46.itm~  ~override~ // wakizashi
              ~sw1h47.itm~  ~override~ // wakizashi +1
              ~sw1h66.itm~  ~override~ // yamato +4
  WRITE_BYTE 0x21 (THIS BOR BIT6) // adds druid flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~brac19.itm~ ~override~ // Gauntlets of crushing
              ~chan17.itm~ ~override~ // ashen scales
              ~dagg16.itm~ ~override~ // poisoned throwing daggers
              ~dagg18.itm~ ~override~ // shadow thief dagger
              ~dagg19.itm~ ~override~ // dagger of <CHARNAME>
              ~plat20.itm~ ~override~ // blue dragon plate
              ~shld26.itm~ ~override~ // shield of the lost +2 (non-metallic)
  WRITE_BYTE 0x21 (THIS BAND `BIT6) // removes druid flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amul16.itm~   ~override~ // metaspell influence amulet
              ~amul21.itm~   ~override~ // Amulet of power
              ~brac17.itm~   ~override~ // gloves of pickpocketing
              ~brac21.itm~   ~override~ // guantlets of ex specialization
              ~clck29.itm~   ~override~ // robe of apprenti
              ~dwshld01.itm~ ~override~ // drow shield +3
//              ~dwsw1h01.itm~ ~override~ // drow scimitar +3
              ~misc5t.itm~   ~override~ // shaman's staff
              ~misc9o.itm~   ~override~ // staff
              ~misca6.itm~   ~override~ // patrol leader helmet
              ~npplat.itm~   ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~   ~override~ // hallowed redeemer
              ~potn36.itm~   ~override~ // potion of master thievery
              ~ring08.itm~   ~override~ // ring of wizardry
              ~ring22.itm~   ~override~ // ring o' holiness
              ~ring35.itm~   ~override~ // ring of lock picking
              ~ring40.itm~   ~override~ // ring of acuity
              ~rods01.itm~   ~override~ // rod of absorption
              ~rods04.itm~   ~override~ // rod of smiting
              ~rods05.itm~   ~override~ // rod of terror
              ~rods06.itm~   ~override~ // rod of reversal
              ~scrl3g.itm~   ~override~ // vocalize
//              ~scrl56.itm~   ~override~ // cure serious wounds (Monks should be able to use Priest-only scrolls according to Bioware)
//              ~scrl58.itm~   ~override~ // free action
//              ~scrl59.itm~   ~override~ // neutralize poison
//              ~scrl5a.itm~   ~override~ // mental domination
//              ~scrl5b.itm~   ~override~ // defensive harmony
//              ~scrl5c.itm~   ~override~ // protection from lightning
//              ~scrl5d.itm~   ~override~ // protection from evil 10'
//              ~scrl5e.itm~   ~override~ // champion's strength
//              ~scrl5f.itm~   ~override~ // chaotic commands
              ~scrl5g.itm~   ~override~ // remove curse (wizard version)
//              ~scrl61.itm~   ~override~ // cure critical wounds
//              ~scrl62.itm~   ~override~ // flame strike
//              ~scrl63.itm~   ~override~ // raise dead
              ~scrl8b.itm~   ~override~ // summon nishruu
              ~scrl8c.itm~   ~override~ // stone to flesh
              ~sw2h15.itm~   ~override~ // the silver sword
//              ~wa2amu.itm~   ~override~ // sensate amulet (Monks should be able to use Priest-only items according to Bioware)
              ~wa2ring.itm~  ~override~ // mercykiller ring
              ~wand02.itm~   ~override~ // wand of fear
              ~wand04.itm~   ~override~ // wand of paralyzation
              ~wand05.itm~   ~override~ // wand of fire
              ~wand06.itm~   ~override~ // wand of frost
              ~wand07.itm~   ~override~ // wand of lightning
              ~wand09.itm~   ~override~ // wand of polymorphing
              ~wand10.itm~   ~override~ // wand of monster summoning
//              ~wand11.itm~   ~override~ // wand of the heavens (Monks should be able to use Priest-only items according to Bioware)
              ~wand13.itm~   ~override~ // wand of cloudkill
              ~wand14.itm~   ~override~ // web sack
              ~wand15.itm~   ~override~ // wand of apprenti
  WRITE_BYTE 0x21 (THIS BOR BIT5) // adds monk flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~boot01.itm~ ~override~ // boots of speed
  WRITE_BYTE 0x21 (THIS BAND  `BIT5) // removes monk flag
  BUT_ONLY

COPY_EXISTING ~npplat.itm~ ~override~ // Firecam Full-Plate Armor
              ~npsw03.itm~ ~override~ // hallowed redeemer
  WRITE_BYTE 0x21 (THIS BOR BIT4) // adds gnome flag
  BUT_ONLY

COPY_EXISTING ~npsw03.itm~ ~override~ // hallowed redeemer
  WRITE_BYTE 0x21 (THIS BOR BIT2) // adds halfling flag
  BUT_ONLY

COPY_EXISTING ~npsw03.itm~ ~override~ // hallowed redeemer
  WRITE_BYTE 0x21 (THIS BOR BIT1) // adds half-elf flag
  BUT_ONLY

COPY_EXISTING ~npsw03.itm~ ~override~ // hallowed redeemer
  WRITE_BYTE 0x21 (THIS BOR BIT0) // adds dwarf flag
  BUT_ONLY

COPY_EXISTING ~potn56.itm~ ~override~ // potion of frost giant strength
  WRITE_BYTE 0x29 (THIS BOR  BIT7) // adds wildmage flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amul21.itm~   ~override~ // Amulet of power
              ~dwplat01.itm~ ~override~ // drow full plate +5
              ~misc5x.itm~   ~override~ // harper pin
  WRITE_BYTE 0x29 (THIS BOR  BIT6) // adds barbarian flag
  BUT_ONLY

COPY_EXISTING ~brac21.itm~ ~override~ // gauntlet of ex specialization
  WRITE_BYTE 0x29 (THIS BAND  `BIT6) // removes barbarian flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
              ~chan17.itm~ ~override~ // ashen scales
              ~plat06.itm~ ~override~ // ankheg plate
              ~plat18.itm~ ~override~ // red dragon plate
              ~plat20.itm~ ~override~ // blue dragon plate
  WRITE_BYTE 0x29 (THIS BOR  BIT5) // add avenger flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~scrl5t.itm~ ~override~ // protection from electricity (mage)
              ~scrl9a.itm~ ~override~ // pierce shield
              ~scrl9b.itm~ ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT5) // removes avenger flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~chan20.itm~   ~override~ // white dragon scale
              ~dwplat01.itm~ ~override~ // drow full plate +5
              ~leat19.itm~   ~override~ // shadow dragon scale
              ~misc5x.itm~   ~override~ // harper pin
  WRITE_BYTE 0x29 (THIS BOR   BIT4) // adds shapeshifter flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~dwshld01.itm~ ~override~ // drow medium shield
              ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~scrl5t.itm~   ~override~ // protection from electricity (mage)
              ~scrl9a.itm~   ~override~ // pierce shield
              ~scrl9b.itm~   ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT4) // removes shapeshifter flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
  WRITE_BYTE 0x29 (THIS BOR  BIT3) // adds totemic druid flag
  BUT_ONLY

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~scrl5t.itm~   ~override~ // protection from electricity (mage)
              ~scrl9a.itm~   ~override~ // pierce shield
              ~scrl9b.itm~   ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT3) // removes totemic druid flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // wong fei ioun stone
              ~misc7t.itm~ ~override~ // moon dog figurine
              ~potn56.itm~ ~override~ // rogue potion of power
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
              ~scrl9b.itm~ ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT2) // removes lathander flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // wong fei ioun stone
              ~misc7t.itm~ ~override~ // moon dog figurine
              ~potn56.itm~ ~override~ // rogue potion of power
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
              ~scrl9b.itm~ ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT1) // removes helm flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // wong fei ioun stone
              ~misc7t.itm~ ~override~ // moon dog figurine
              ~potn56.itm~ ~override~ // rogue potion of power
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
              ~scrl9b.itm~ ~override~ // summon fiend
  WRITE_BYTE 0x29 (THIS BAND  `BIT0) // removes talos flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~ ~override~ // handmaiden's mace
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl8e.itm~ ~override~ // protection from the elements
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2b (THIS BAND  `BIT4) // remove swashbuckler flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~brac21.itm~ ~override~ // gauntlet of ex specialization
              ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~ ~override~ // handmaiden's mace
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2b (THIS BAND  `BIT3) // remove bounty hunter flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~brac21.itm~ ~override~ // gauntlet of ex specialization
              ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~ ~override~ // handmaiden's mace
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2b (THIS BAND  `BIT2) // remove assassin flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~blun14g.itm~  ~override~ // flail of ages +1, cold
              ~blun14h.itm~  ~override~ // flail of ages +1, fire
              ~blun14i.itm~  ~override~ // flail of ages +1, acid
              ~dwblun01.itm~ ~override~ // drow flail +3
              ~dwchan01.itm~ ~override~ // drow elven chain +3
              ~dwchan02.itm~ ~override~ // drow adamantine chain +5
              ~dwhalb01.itm~ ~override~ // drow halberd +3
              ~dwplat01.itm~ ~override~ // drow full plate +5
              ~dwsper01.itm~ ~override~ // drow lance +3
              ~dwsw1h01.itm~ ~override~ // Drow Scimitar +3
              ~dwsw1h02.itm~ ~override~ // drow long sword +3
              ~misc4q.itm~   ~override~ // The Ogre's Sword
              ~misc4u.itm~   ~override~ // Embarl's Dagger
              ~misc9q.itm~   ~override~ // scimitar
              ~npchan.itm~   ~override~ // corthala family armor
              ~sendai.itm~   ~override~ // sendai's flail
              ~sw1h20.itm~   ~override~ // scimitar
              ~sw1h22.itm~   ~override~ // Scimitar +1
              ~sw1h23.itm~   ~override~ // Scimitar +2, Rashad's Talon
              ~sw1h30.itm~   ~override~ // belm +2
              ~sw1h50.itm~   ~override~ // Shazzellim +1
              ~sw1h52.itm~   ~override~ // Scimitar +3
              ~sw1h56.itm~   ~override~ // Scimitar
              ~sw2h15.itm~   ~override~ // silver sword
  WRITE_BYTE 0x2b (THIS BOR BIT1) // adds beastmaster flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~scrl5t.itm~   ~override~ // protection from electricity (mage)
              ~scrl8e.itm~   ~override~ // protection from the elements
              ~scrl9a.itm~   ~override~ // pierce shield
  WRITE_BYTE 0x2b (THIS BAND `BIT1) // removes beastmaster flag
  BUT_ONLY

COPY_EXISTING ~dwchan01.itm~ ~override~ // drow elven chain +3
              ~dwchan02.itm~ ~override~ // drow adamantine chain +5
              ~dwplat01.itm~ ~override~ // drow full plate +5
              ~plat06.itm~   ~override~ // ankheg plate
              ~plat18.itm~   ~override~ // red dragon scale
              ~plat20.itm~   ~override~ // blue dragon plate
  WRITE_BYTE 0x2b (THIS BOR BIT0) // adds stalker flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~hlolth.itm~   ~override~ // handmaiden's mace
              ~scrl5t.itm~   ~override~ // protection from electricity (mage)
              ~scrl9a.itm~   ~override~ // pierce shield
  WRITE_BYTE 0x2b (THIS BAND `BIT0) // removes stalker flag
  BUT_ONLY

COPY_EXISTING ~dwchan01.itm~ ~override~ // drow elven chain +3
              ~dwchan02.itm~ ~override~ // drow adamantine chain +5
              ~dwplat01.itm~ ~override~ // drow full plate +5
              ~npchan.itm~   ~override~ // corthala family armor
  WRITE_BYTE 0x2d (THIS BOR BIT7) // adds archer flag
  BUT_ONLY

COPY_EXISTING ~hlolth.itm~   ~override~ // handmaiden's mace
              ~scrl5t.itm~   ~override~ // protection from electricity (mage)
              ~scrl9a.itm~   ~override~ // pierce shield
//              ~plat06.itm~   ~override~ // ankheg plate
//              ~plat18.itm~   ~override~ // red dragon scale
//              ~plat20.itm~   ~override~ // blue dragon plate
  WRITE_BYTE 0x2d (THIS BAND `BIT7) // removes archer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~scrl3h.itm~ ~override~ // protection from evil
  WRITE_BYTE 0x2d (THIS BOR BIT5) // adds transmuter flag
  BUT_ONLY

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2d (THIS BAND `BIT5) // removes transmuter flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2d (THIS BAND `BIT4) // removes necromancer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2d (THIS BAND `BIT3) // removes invoker flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2d (THIS BAND `BIT2) // removes illusionist flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~dwchan01.itm~ ~override~ // drow chain
              ~dwchan02.itm~ ~override~ // drow chain
              ~dwplat01.itm~ ~override~ // drow plate
              ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
              ~shld25.itm~   ~override~ // shield of harmony
  WRITE_BYTE 0x2d (THIS BAND `BIT1) // removes enchanter flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2d (THIS BAND `BIT0) // removes diviner flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2f (THIS BAND `BIT7) // removes conjurer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~scrla1.itm~   ~override~ // wizard eye
  WRITE_BYTE 0x2f (THIS BOR   BIT6) // adds abjurer flag
  BUT_ONLY

COPY_EXISTING ~helm34.itm~   ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~   ~override~ // handmaiden's mace
              ~potn56.itm~   ~override~ // rogue potion of power
  WRITE_BYTE 0x2f (THIS BAND `BIT6) // removes abjurer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~npsw04.itm~ ~override~ // Corthala Family Blade
              ~scrl5t.itm~ ~override~ // protection from electricity (mage)
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2f (THIS BAND `BIT5) // removes undead hunter flag
  BUT_ONLY

COPY_EXISTING ~npsw04.itm~ ~override~ // Corthala Family Blade
              ~scrl5t.itm~ ~override~ // protection from electricity (mage)
              ~scrl9a.itm~ ~override~ // pierce shield
              ~ttpot.itm~  ~override~ // potion of extra healing
  WRITE_BYTE 0x2f (THIS BAND `BIT4) // removes inquisitor flag
  BUT_ONLY

COPY_EXISTING ~dwxbow01.itm~ ~override~ // Drow Crossbow of Speed
              ~wasling.itm~  ~override~ // Sling of Everard +5
  WRITE_BYTE 0x2f (THIS BOR   BIT3) // adds cavalier flag
  BUT_ONLY

COPY_EXISTING ~npsw04.itm~ ~override~ // Corthala Family Blade
              ~scrl5t.itm~ ~override~ // protection from electricity (mage)
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2f (THIS BAND `BIT3) // removes cavalier flag
  BUT_ONLY

COPY_EXISTING ~brac19.itm~   ~override~ // Gauntlets of Crushing
              ~dart08.itm~   ~override~ // Crimson Dart +3
              ~dwxbow01.itm~ ~override~ // Drow Crossbow of Speed
              ~misc5x.itm~   ~override~ // harper pin
              ~npbow.itm~    ~override~ // bow of arvoreen
              ~wasling.itm~  ~override~ // Sling of Everard +5
  WRITE_BYTE 0x2f (THIS BOR BIT2) // adds kensai flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~ ~override~ // handmaiden's mace
              ~npchan.itm~ ~override~ // corthala family armor
              ~npsw04.itm~ ~override~ // Corthala Family Blade
              ~potn56.itm~ ~override~ // rogue potion of power
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2f (THIS BAND `BIT2) // removes kensai flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~amul27.itm~   ~override~ // Amulet of Seldarine
              ~amul28.itm~   ~override~ // Amulet of the Master Harper
              ~brac19.itm~   ~override~ // Gauntlets of Crushing
              ~dwclck01.itm~ ~override~ // Drow Piwafwi Cloak
              ~misc5x.itm~   ~override~ // harper pin
              ~misc72.itm~   ~override~ // claw of kazgaroth
              ~misc73.itm~   ~override~ // horn of kazgaroth
              ~misc9w.itm~   ~override~ // Drow Piwafwi Cloak
              ~regisamu.itm~ ~override~ // Ruby Pendant of Beguiling
              ~ring44.itm~   ~override~ // heartwood ring
  WRITE_BYTE 0x2f (THIS BOR BIT1) // add wizslayer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~clck09.itm~  ~override~ // mage robe of cold resistance
              ~clck10.itm~  ~override~ // mage robe of fire resistance
              ~clck11.itm~  ~override~ // mage robe of electric resistance
              ~clck12.itm~  ~override~ // knave's robe
              ~clck13.itm~  ~override~ // traveller's robe
              ~clck14.itm~  ~override~ // adventurer's robe
              ~clck15.itm~  ~override~ // robe of the good archmagi
              ~clck16.itm~  ~override~ // robe of the neutral archmagi
              ~clck17.itm~  ~override~ // robe of the evil archmagi
              ~clck18.itm~  ~override~ // knave's robe
              ~clck19.itm~  ~override~ // robe of the good archmagi
              ~clck29.itm~  ~override~ // robe of the apprenti
              ~hgwra02.itm~ ~override~ // robe of the good archmagi
              ~hlolth.itm~  ~override~ // handmaiden's mace
              ~ipotn08.itm~ ~override~ // healing potion
              ~npsw04.itm~  ~override~ // Corthala Family Blade
              ~potn08.itm~  ~override~ // healing potion
              ~potn17.itm~  ~override~ // elixir of health
              ~potn20.itm~  ~override~ // antidote
              ~potn25.itm~  ~override~ // healing potion (cursed)
              ~potn32.itm~  ~override~ // antidote (cursed)
              ~potn52.itm~  ~override~ // extra healing potion
              ~potn55.itm~  ~override~ // superior healing potion
              ~ring37.itm~  ~override~ // storm ring (nonmagical)
              ~ring38.itm~  ~override~ // dawn ring (nonmagical)
              ~scrl1w.itm~  ~override~ // fireshield (blue)
              ~scrl2b.itm~  ~override~ // stoneskin
              ~scrl5t.itm~  ~override~ // protection from electricity
              ~scrl9a.itm~  ~override~ // pierce shield
              ~ttpot.itm~   ~override~ // extra healing potion
              ~wa2robe.itm~ ~override~ // robe of vecna
  WRITE_BYTE 0x2f (THIS BAND  `BIT1) // removes wizardslayer flag
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~misc5x.itm~   ~override~ // harper pin
  WRITE_BYTE 0x2f (THIS BOR BIT0) // adds berserker flag
  BUT_ONLY

COPY_EXISTING ~helm34.itm~ ~override~ // Wong Fei's Ioun Stone
              ~hlolth.itm~ ~override~ // handmaiden's mace
              ~npsw04.itm~ ~override~ // Corthala Family Blade
              ~potn56.itm~ ~override~ // rogue potion of power
              ~scrl1w.itm~ ~override~ // fireshield (blue)
              ~scrl2b.itm~ ~override~ // stoneskin
              ~scrl5t.itm~ ~override~ // protection from electricity
              ~scrl7u.itm~ ~override~ // contingency
              ~scrl9a.itm~ ~override~ // pierce shield
  WRITE_BYTE 0x2f (THIS BAND `BIT0) // removes berserker flag
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// big batch o' scroll fixes (thanks devSin)        \\\\\
/////                                                  \\\\\

// this section really abuses the fact that header 0 is the cast header, header 1 is learn on all of these scrolls

// scroll fixes: casting opcode
COPY_EXISTING ~scrl1o.itm~ ~override~ // slow
              ~scrl1u.itm~ ~override~ // confusion
              ~scrl2f.itm~ ~override~ // cone of cold
              ~scrl5i.itm~ ~override~ // greater malison
              ~scrl70.itm~ ~override~ // color spray
              ~scrl7i.itm~ ~override~ // death spell
              ~scrl8p.itm~ ~override~ // prismatic spray
              ~scrl94.itm~ ~override~ // resist fear
              ~scrla1.itm~ ~override~ // wizard eye
              ~scrla3.itm~ ~override~ // glitterdust
              ~scrla7.itm~ ~override~ // remove magic
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 header = 0 opcode = 148 END
  BUT_ONLY

// scroll fixes: casting opcode
COPY_EXISTING ~scrl5b.itm~ ~override~ // defensive harmony
              ~scrl5d.itm~ ~override~ // protection from evil 10' radius
              ~scrlb4.itm~ ~override~ // wish
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 148 header = 0 opcode = 146 END
  BUT_ONLY IF_EXISTS

// scroll fixes: power
COPY_EXISTING //~scrl1o.itm~ ~override~ // slow
              //~scrl1u.itm~ ~override~ // confusion
              //~scrl2f.itm~ ~override~ // cone of cold
              ~scrl56.itm~ ~override~ // cure serious wounds
              ~scrl58.itm~ ~override~ // free action
              ~scrl59.itm~ ~override~ // neutralize poison
              //~scrl5i.itm~ ~override~ // greater malison
              ~scrl61.itm~ ~override~ // cure critical wounds
              ~scrl62.itm~ ~override~ // flame strike
              ~scrl63.itm~ ~override~ // raise dead
              //~scrl70.itm~ ~override~ // color spray
              //~scrl7i.itm~ ~override~ // death spell
              //~scrl8p.itm~ ~override~ // prismatic spray
              //~scrl94.itm~ ~override~ // resist fear
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 power = 0 END
  BUT_ONLY

// scroll fixes: effects target
COPY_EXISTING ~scrl1o.itm~ ~override~ // slow
              ~scrl1u.itm~ ~override~ // confusion
              ~scrl2f.itm~ ~override~ // cone of cold
              ~scrl5i.itm~ ~override~ // greater malison
              ~scrl70.itm~ ~override~ // color spray
              ~scrl7i.itm~ ~override~ // death spell
              ~scrl8p.itm~ ~override~ // prismatic spray
              ~scrl94.itm~ ~override~ // resist fear
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 target = 1 END
  BUT_ONLY

// targeting fixes
ACTION_CLEAR_ARRAY cd_scroll_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_scroll_fixes BEGIN

  scrl1o => 4 // slow
  scrl1u => 4 // confusion
  scrl2f => 4 // cone of cold
  scrl5i => 4 // greater malison
  scrl70 => 4 // color spray
  scrl7i => 4 // death spell
  scrl7m => 5 // true sight
  scrl80 => 5 // shocking grasp
  scrl8p => 4 // prismatic spray
  scrl8y => 1 // protection from energy
  scrl94 => 4 // resist fear
  scrla1 => 4 // wizard eye
  scrla3 => 4 // glitterdust
  scrla7 => 4 // remove magic
//  scrla8 => 1 // contagion
  scrlaj => 5 // farsight

END

ACTION_PHP_EACH cd_scroll_fixes AS scroll => fix BEGIN

  COPY_EXISTING ~%scroll%.itm~ ~override~
    LPF ALTER_HEADER INT_VAR header = 0 target = fix END // target on ability header
    BUT_ONLY

END

// range fixes
ACTION_CLEAR_ARRAY cd_scroll_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_scroll_fixes BEGIN

  scrl2f => 12 // cone of cold
  scrl5c =>  1 // protection from lightning
  scrl5f => 40 // chaotic commands
  scrl5i => 50 // greater malison
  scrl5k =>  1 // spirit armor
  scrl62 => 40 // flame strike
  scrl63 => 30 // raise dead
  scrl6j => 25 // spell thrust
  scrl6v => 40 // lower resistance
  scrl76 => 30 // infravision
  scrl7i => 50 // death spell
  scrl8c => 50 // stone to flesh
  scrl8h => 20 // warding whip
  scrl8p => 15 // prismatic spray
  scrl9h =>  1 // maze
  scrla1 => 30 // wizard eye
  scrla6 => 60 // spook
  scrla7 => 40 // remove magic
  scrla8 => 30 // contagion
  scrlai => 40 // ray of enfeeblement
  scrlak =>  1 // remove curse

END

ACTION_PHP_EACH cd_scroll_fixes AS scroll => fix BEGIN

  COPY_EXISTING ~%scroll%.itm~ ~override~
    LPF ALTER_HEADER INT_VAR header = 0 range = fix END // range on ability header
    BUT_ONLY

END

// protection from x scrolls not bypassing MR
COPY_EXISTING ~scrl03.itm~ ~override~
              ~scrl04.itm~ ~override~
              ~scrl05.itm~ ~override~
              ~scrl06.itm~ ~override~
              ~scrl08.itm~ ~override~
              ~scrl09.itm~ ~override~
              ~scrl15.itm~ ~override~
  SAY 0x08 #18094 // protection scroll, really just needed for scrl05
  LPF ALTER_EFFECT INT_VAR silent = 1 resist_dispel = 3 END
  LPF ALTER_HEADER INT_VAR header = 0 range = 30 END // protection from x scrolls should be visible range
  BUT_ONLY

// protection from x scrolls should be visible range (all others done immediately above)
COPY_EXISTING ~scrl07.itm~ ~override~
  LPF ALTER_HEADER INT_VAR header = 0 range = 30 END // protection from x scrolls should be visible range
  BUT_ONLY

// add miscast icons
COPY_EXISTING ~scrl11.itm~ ~override~ // cursed scroll of clumsiness
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 142 target = 1 resist_dispel = 1 duration = 600 parameter2 = 105 power = 4 END // add miscast magic icon
  BUT_ONLY

// add arcane spell failure (CamDawg)
COPY_EXISTING ~scrl12.itm~ ~override~ //cursed scroll of foolishness
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 60 match_parameter2 = 1 parameter2 = 0 END // clone divine miscast into arcane miscast
  BUT_ONLY

// add feeblemind icon (CamDawg)
COPY_EXISTING ~scrl18.itm~ ~override~ // cursed scroll of stupidity
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 76 opcode = 142 parameter2 = 48 END // clone feeblemind into feeblemind icon
  BUT_ONLY

// dispel magic scroll uses priest description of spell
COPY_EXISTING ~scrl1e.itm~ ~override~
  SAY IDENTIFIED_DESC #45821 // mage dispel magic descript
  BUT_ONLY

// a pair of scrolls should have min int, not min wis
COPY_EXISTING ~scrl5e.itm~ ~override~ // champion's strength
              ~scrl5f.itm~ ~override~ // chaotic commands
  WRITE_BYTE 0x2a 9 // min int
  WRITE_BYTE 0x2e 0 // min wis  
  BUT_ONLY

// corrects BAM assignment for Conjure Lesser Fire Elemental scroll
COPY_EXISTING ~scrl6x.itm~ ~override~
  WRITE_ASCII 0x3a SPWI516A  // inventory
  LPF ALTER_HEADER STR_VAR icon = SPWI516A END // icon on ability header
  BUT_ONLY

// mage's detect evil scroll using priest spell description
COPY_EXISTING ~scrl86.itm~ ~override~
  SAY 0x54 #12219
  BUT_ONLY

// melf's minute meteors' name
COPY_EXISTING ~scrla5.itm~ ~override~
  SAY NAME2 #38588 // melf's minute meteor > meteors
  BUT_ONLY

// misc spellhold scrolls should be category books, only allow 1 in stack
COPY_EXISTING ~scrla9.itm~  ~override~ // waneev's note
              ~scrlaa.itm~  ~override~ // waneev's note
              ~scrlab.itm~  ~override~ // monkey balls
              ~scrlac.itm~  ~override~ // amnish dragoon soup
              ~scrlad.itm~  ~override~ // baldur's delight
              ~scrlaf.itm~  ~override~ // ruby racks
              ~scrlag.itm~  ~override~ // crom faeyr scroll
              ~scrlhp.itm~  ~override~ // harper note
              ~scrlick.itm~ ~override~ // illithid correspndence
              ~scrlmz.itm~  ~override~ // note from Mazzy Fentan
  WRITE_BYTE  0x2a  0 // min int
  WRITE_SHORT 0x38  1 // 1 max stack
  BUT_ONLY

// more crom faeyr changes: unique icon, make magical
COPY_EXISTING ~scrlag.itm~ ~override~
  WRITE_ASCII 0x3a IMISC6F #8
  WRITE_ASCII 0x58 CMISC6F #8

// ray of enfeeblement lacks casting icon due to typo
COPY_EXISTING ~scrlai.itm~ ~override~
  LPF ALTER_HEADER INT_VAR header = 1 STR_VAR icon = spwi221a END // icon on learn spell ability header
  BUT_ONLY

/////                                                  \\\\\
///// other item file fixes                            \\\\\
/////                                                  \\\\\

// aegis fang shouldn't display name while unidentified
COPY_EXISTING ~aegis.itm~  ~override~
              ~aegis2.itm~ ~override~
  SAY 0x08 #6345 // "aegis fang" > "war hammer"
  BUT_ONLY

// missile weapons using wrong projectiles and being blocked by protection from normal missiles; see also arow02.itm and wasling.itm
COPY_EXISTING ~aegis.itm~    ~override~ // aegis fang
              ~ax1h05.itm~   ~override~ // throwing axe +2
              ~ax1h06.itm~   ~override~ // throwing axe +2
              ~ax1h08.itm~   ~override~ // hangard's axe +2
              ~ax1h09.itm~   ~override~ // rifthome axe +3
              ~ax1h16.itm~   ~override~ // k'logarath +4
              ~hamm06.itm~   ~override~ // dwarven thrower
  LPF ALTER_HEADER INT_VAR projectile = 7 match_type = 2 END // magic axe (7) from normal axe (10) on ranged headers
  BUT_ONLY IF_EXISTS

// amulet of shield not protecting against MM
COPY_EXISTING ~amul15.itm~   ~override~
  LPF DELETE_EFFECT INT_VAR check_globals = 0 END // delete all effects
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = spwi114 END // just cast shield spell directly
  BUT_ONLY

// amul23 and amul24 have different BAMs than amul22
COPY_EXISTING ~amul23.itm~ ~override~
              ~amul24.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  WRITE_ASCIIE 0x58 ~c%SOURCE_RES%~ #8
  BUT_ONLY

// missile weapons using wrong projectiles and being blocked by protection from normal missiles; see also aegis.itm et al and wasling.itm
COPY_EXISTING ~arow02.itm~   ~override~ // arrows +1
  LPF ALTER_HEADER INT_VAR projectile = 2 match_type = 2 END // magic arrow (2) from normal arrow (5) on ranged headers
  BUT_ONLY

// Arrows of Biting and Protector of the Dryads +2 have the "Use Strength" flag enabled
COPY_EXISTING ~arow05.itm~ ~override~ // arrows of biting
              ~arow12.itm~ ~override~ // arrows of biting
              ~arow14.itm~ ~override~ // poisoned arrows
              ~bow08.itm~  ~override~ // eagle bow
              ~bow99.itm~  ~override~ // eagle bow
  LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 flag_strength = 0 END // remove strength bonus flag
  BUT_ONLY

// miscast effects not accompanied by miscast icon
COPY_EXISTING ~ax1h07.itm~   ~override~ // 80% on hit, no icon
  LPF DELETE_EFFECT INT_VAR match_opcode = 142 END // delete existing miscast/spell failure icons
  LPF CLONE_EFFECT INT_VAR match_opcode = 60 match_parameter2 = 1 opcode = 142 parameter2 = 105 END
  BUT_ONLY

COPY_EXISTING ~ax1h10.itm~ ~override~
// first uncorrupt the item by deleting any bytes beyond the listed effects
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  READ_SHORT 0x70 fx_num
  t_fx_num = fx_num
  CLEAR_ARRAY ab_array
  GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
  PHP_EACH ab_array AS int => goa_abil_off BEGIN
    CLEAR_ARRAY fx_array
    GET_OFFSET_ARRAY2 fx_array goa_abil_off 0x6a 4 0x1e 2 0x20 2 0x30
    PHP_EACH fx_array AS int => goa_fx_off BEGIN
      t_fx_num += 1
    END
  END
  PATCH_IF 0x72 + abil_num*0x38 + 0x30*t_fx_num < BUFFER_LENGTH BEGIN
    DELETE_BYTES 0x72 + abil_num*0x38 + 0x30*t_fx_num BUFFER_LENGTH - (0x72 + abil_num*0x38 + 0x30*t_fx_num)
  END
  LPF DELETE_EFFECT INT_VAR match_opcode = 177 END // purge all old EFF-targeted damage
  LPF DELETE_EFFECT INT_VAR match_opcode =  55 END // purge all old slay effects
  PATCH_FOR_EACH resref IN mesdie die ax1h10a BEGIN // now replace them with good stuff
    LPF ADD_ITEM_EFFECT INT_VAR type = 99 opcode = 177 target = 2 parameter1 = 4 parameter2 = 3 timing = 1 STR_VAR resource = EVAL "%resref%" END
  END
  LPF ALTER_HEADER INT_VAR match_type = 2 flag_strength = 1 dicenumber = 1 END // add strength bonus to ranged ability, change to 1d6 from 3d6
  LPF ALTER_EFFECT INT_VAR header_type = 2 STR_VAR match_resource = "ax1h10a" resource = "ax1h10b" END
  BUT_ONLY

// Amkethran duplicate gem bag fix, part one of four (see amsmug01.sto amsmug02.sto, cdbag02i.sto, cdbag02j.sto)
ACTION_IF tob_game THEN BEGIN // ToB-only stuff check

  COPY_EXISTING ~bag02i.itm~ ~override/cdbag02i.itm~
                ~bag02i.itm~ ~override/cdbag02j.itm~

END

//Belt of Inertial Barrier displays MR icon instead of Resistance to Magic Energy (Wisp)
COPY_EXISTING ~belt10.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 63 parameter2 = 123 END
  BUT_ONLY

// black blade o' disaster using wrong power level for level drain
// black blade o' disaster is supposed to disintegrate, not slay
// soa black blade does not grant long sword grandmastery per descript; see also SoA-only spwi915.spl patch for duration, thac0 fixes
COPY_EXISTING ~blakblad.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 55 opcode = 238 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 10 power = 9 match_target = 2 END // all of the 10% effects (level drain et al) on target
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 10 power = 0 match_target = 1 END // all of the 10% effects (level drain et al) on self
  PATCH_IF !tob_game BEGIN // SoA-only fix; lacks grandmastery-when-wielded
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = 5 parameter2 = 90 timing = 2 resist_dispel = 2 END
  END
  BUT_ONLY

// two clubs using thrusting animations; other eight do not
COPY_EXISTING ~blun01.itm~ ~override~
              ~blun31.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 overhand = 50 backhand = 50 thrust = 0 END // balance of attack animations
  BUT_ONLY IF_EXISTS

//Give Root of the Problem a price (Wisp)
COPY_EXISTING ~blun10.itm~ ~override~
  WRITE_LONG 0x34 2750
  BUT_ONLY

// %$#*$&^ damage vs type opcode. remove global 'use eff' opcode; move to melee header (revised by Wisp)
// see macedisr.eff for second half of this fix
COPY_EXISTING ~blun12.itm~ ~override~
              ~deva.itm~   ~override~
              ~hamm10.itm~ ~override~
              ~hamm11.itm~ ~override~
  SPRINT neweff cddisr // deva, blun12
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_REGEXP "^hamm1[01]$" = 0) BEGIN SPRINT neweff ~%SOURCE_RES%~ END
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 STR_VAR match_resource = macedisr END // delete from global effects
  // have to add the match_param1/2 to avoid duping deva.itm's illusionary creature kill
  // remove saves for runehammers double-save fix, make both 'die'
  LPF ALTER_EFFECT INT_VAR                 silent = 1 check_globals = 0 match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 savingthrow = 0 savebonus = 0 END // remove dupe saves
  LPF ALTER_EFFECT INT_VAR                 silent = 1 check_globals = 0 match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 STR_VAR match_resource = ~mesdie~ resource = ~die~ END // change all to die
  LPF ALTER_EFFECT INT_VAR multi_match = 1 silent = 1 check_globals = 0 match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 STR_VAR match_resource = ~die~ resource = ~mesdie~ END // change only first one to use mesdie instead
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 STR_VAR match_resource = ~die~ resource = EVAL ~%neweff%~ insert = ~last~ END // add new eff to melee headers (extra 1d6+2 to undead)
  BUT_ONLY IF_EXISTS

// name fix for FoA +2
COPY_EXISTING ~blun14d.itm~ ~override~
              ~blun14e.itm~ ~override~
              ~blun14f.itm~ ~override~
  SAY NAME2 @125 // flail of ages +2
  BUT_ONLY

// fixes wrong BAMs for cold-acid flail of ages
COPY_EXISTING ~blun14e.itm~ ~override~
  WRITE_ASCII 0x3a ~IBLUN14E~
  WRITE_ASCII 0x58 ~CBLUN14E~
  LPF ALTER_HEADER INT_VAR match_type = 1 STR_VAR icon = IBLUN14E END // update icon on melee headers
  BUT_ONLY

// Club +3, Blackblood had Mace Animation. change it to club
COPY_EXISTING ~blun22.itm~ ~override~
  WRITE_ASCII 0x22 ~CL~
  BUT_ONLY

// level drain now handled by batch fixes
// changes the icon from generic mace to MoD's one. Also see macedisu.eff.
// new for v4; fix ^&%&# damage v opcode no-die bug
COPY_EXISTING ~blun25.itm~ ~override~
  WRITE_ASCII 0x3a ~iblun12~ #8 // use MoD BAM
  WRITE_ASCII 0x44 ~gblun06~ #8 // use MoD BAM
  WRITE_ASCII 0x58 ~cblun12~ #8 // use MoD BAM
  LPF ALTER_HEADER INT_VAR match_type = 1 STR_VAR icon = iblun12 END // update MoD icon on melee headers
  // change unneeded macedisr call to color pulse to match un-upgraded MoD
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 9 target = 1 parameter1 = ((202 << 16) + (255 << 24)) parameter2 = (21 + (110 << 16)) timing = 2 END // blue 202/green 255 cycle at loc 21, cycle speed 110
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 STR_VAR match_resource = macedisr END // delete eff on equipping effect
  // add new eff to melee headers, last (extra undead 1d6+1 damage)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 match_parameter1 = 4 match_parameter2 = 3 savingthrow = 0 savebonus = 0 END // remove dupe saves
  // now to fix eff order on melee attacks
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 STR_VAR match_resource = ~mesdie~ resource = ~die~ END // now both use 'die'
  LPF ALTER_EFFECT INT_VAR multi_match = 1 silent = 1 check_globals = 0 match_opcode = 177 STR_VAR match_resource = ~die~ resource = ~mesdie~ END // change only first one to use mesdie instead
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 check_globals = 0 match_opcode = 177 resist_dispel = 0 STR_VAR match_resource = ~die~ resource = ~macedisu~ insert = ~last~ END // add undead damage back to header
  BUT_ONLY

// club of detonation's 3 fire damage only occurring 20% of the time, instead of always
COPY_EXISTING ~blun26.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 match_parameter1 = 3 probability1 = 100 END
  BUT_ONLY IF_EXISTS

// changes upgraded club o detonation to use club paperdoll and not be a flail
COPY_EXISTING ~blun27.itm~ ~override~
  WRITE_ASCII 0x22 ~CL~  // club
  BUT_ONLY IF_EXISTS

//Flasher Master Bruiser Mates lack their advertised +1 to hit and have an undocumented save penalty (Wisp)
COPY_EXISTING ~bolt07.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END
  LPF ALTER_HEADER INT_VAR match_type = 2 to_hit = 1 END // add +1 thac0
  BUT_ONLY

// fixes price for 2 books to make them sellable
COPY_EXISTING ~book27.itm~ ~override~ // History of Shadowdale
              ~book39.itm~ ~override~ // History of the Drow
  WRITE_LONG 0x34 2
  BUT_ONLY

// boots of speed, grandmaster armor haste > movement rate bonus fix
COPY_EXISTING ~boot01.itm~ ~override~
              ~leat24.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 16 opcode = 126 parameter1 = 200 parameter2 = 2 resist_dispel = 0 END // haste to set 200% movement rate, no dispel
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~boot12.itm~ ~override~ // Gargoyle Boots
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 7 END // Stoneskin, primary Transmuter, secondary Combat Protections
  BUT_ONLY IF_EXISTS

// fixes swapped icons
COPY_EXISTING ~bow01.itm~ ~override~
              ~bow03.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  BUT_ONLY

// fixes incorrect BAMs for several bows
COPY_EXISTING ~bow01.itm~    ~override~
              ~bow03.itm~    ~override~
              ~bow09.itm~    ~override~
              ~bow11.itm~    ~override~
              ~bow12.itm~    ~override~
              ~bow14.itm~    ~override~
              ~bow15.itm~    ~override~
              ~bow16.itm~    ~override~
              ~bow17.itm~    ~override~
              ~bow18.itm~    ~override~
              ~bow22.itm~    ~override~
              ~bow23.itm~    ~override~
              ~gorwom2.itm~  ~override~
              ~npbow.itm~    ~override~
  READ_ASCII 0x3a bam
  LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 STR_VAR icon = EVAL ~%bam%~ END // location weapon slots
  BUT_ONLY IF_EXISTS

// fixes enchantment, weight, icons, and prof of short bow +1
COPY_EXISTING ~bow06.itm~ ~override~
  WRITE_ASCII 0x3a ~ibow06~ #8 // icons
  WRITE_ASCII 0x58 ~cbow06~ #8
  LPF ALTER_HEADER INT_VAR match_type = 4 range = 75 STR_VAR icon = ibow06 END
  BUT_ONLY

// remove apr opcode from ranged header
COPY_EXISTING ~bow08.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 1 END
  BUT_ONLY

// Heartseeker gives regular THAC0 instead of missile THAC0
COPY_EXISTING ~bow10.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 54 opcode = 167 END
  BUT_ONLY

// Standardize the school and secondary type of items which cast alteration spells (aVENGER)
COPY_EXISTING ~brac16.itm~ ~override~ // Bracers of Blinding Strike
              ~sw1h27.itm~ ~override~ // Arbane's Sword +2
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 13 END // imp haste, primary Transmuter, secondary Non-Combat
  BUT_ONLY

// fix power levels
COPY_EXISTING ~brac20.itm~ ~override~ // gloves of curing
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 1 END
  BUT_ONLY

// power/school/secondary for resist fear on magic flute
COPY_EXISTING ~brdflute.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 1 secondary = 2 STR_VAR match_icon = spwi210b END // resist fear, primary abjuration, secondary specific protections
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 power = 2 END // power level, only on resist fear (one visual at power 1)
  BUT_ONLY IF_EXISTS

//Correcting the damage of Bruenor's axe to be in line with the description (Wisp)
COPY_EXISTING ~bruenaxe.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 dicenumber = 1 END // 3d8 to 1d8
  BUT_ONLY

// disallows str bonus for magic bullets
COPY_EXISTING ~bull02.itm~   ~override~
              ~bull03.itm~   ~override~
              ~bull04.itm~   ~override~
              ~bull05.itm~   ~override~
              ~bull06.itm~   ~override~
              ~quiver05.itm~ ~override~
              ~quiver06.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 2 flag_strength = 0 END
  BUT_ONLY IF_EXISTS

// remove damage bonus to bring inline with tansheron
COPY_EXISTING ~cattibow.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 0 dicenumber = 1 END
  BUT_ONLY

// fixes chan +1 descript, weight
COPY_EXISTING ~chan02.itm~ ~override~
  SAY IDENTIFIED_DESC #16272 // was chain +2 descript
  WRITE_LONG 0x4c 20 // fixes item weight
  BUT_ONLY

// allow arbitrary return-to-human form from clock of the wuff
// see also cdwolfm.itm, shapeshifting spell batch
COPY_EXISTING ~clck04.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = "-1" END // delete all effects
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 215 target = 1 parameter2 = 1 duration = 3 STR_VAR resource = sppolymp END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 215 target = 1 parameter2 = 1 duration = 3 STR_VAR resource = polyback END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 111 target = 1 duration = 120 STR_VAR resource = cdwolfm END
  PATCH_FOR_EACH spell IN spin124 spin123 spin122 spwi491 spinhum spin160 spin160 spin160 BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 172 target = 1 timing = 1 STR_VAR resource = EVAL "%spell%" END
  END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 171 target = 1 timing = 1 STR_VAR resource = spin122 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 172 target = 1 timing = 4 duration = 120 STR_VAR resource = EVAL spin122 END

// algernon's cloak
COPY_EXISTING ~clck08.itm~ ~override~
  WRITE_ASCII 0x3a ~iclck08~ #8 // inventory icon
  BUT_ONLY

// mage robe of fire resistance uses a resist fire/cold icon insead of protection from fire
COPY_EXISTING ~clck10.itm~  ~override~ // mage robe of fire resistance
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 26 parameter2 = 16 END
  BUT_ONLY

// adding magic resistance icon
COPY_EXISTING ~clck15.itm~ ~override~
              ~clck16.itm~ ~override~
              ~clck17.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = 63 timing = 2 END

// add electrical resistance icon, reflect all electrical projectiles
COPY_EXISTING ~clck24.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = 27 timing = 2 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 197 match_parameter2 = 39 parameter2 = (cdbehbla - 1) END
  PATCH_FOR_EACH proj IN 80 81 82 83 84 205 206 212 219 BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode = 197 match_parameter2 = 39 parameter2 = proj END
  END
  BUT_ONLY

// Cloak of Mirroring also missing many spell immunity effects
COPY_EXISTING ~clck26.itm~   ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = 28 timing = 2 END // add protection from magic icon
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = sppr302 END // Call Lightning
  // Glyph of Warding, Holy Smite, Unholy Blight, Skull Trap, Cloud Kill, Blade Barrier, Death Fog, Fire Storm, Globe of Blades, Delayed Blast Fireball, Incendiary Cloud, Meteor Swarm, Wish Meteor Swarm, Wish Horrid Wilting
  PATCH_FOR_EACH spell IN sppr304 sppr313 sppr314 spwi313 spwi502 sppr603d spwi614 sppr705 sppr725d spwi712 spwi810 spwi911 spwish25 spwish32 BEGIN
    LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 206 STR_VAR match_resource = "sppr302" resource = EVAL ~%spell%~ END
  END
  BUT_ONLY

// cloaks using robe ground icons
COPY_EXISTING ~clck28.itm~ ~override~
              ~clck30.itm~ ~override~
  WRITE_ASCII 0x44 ~gclck01~ #8

// The Stiletto of Demarchess +2 and the Dart of Stunning should display the appropriate strings upon holding/stunning the target (Wisp)
COPY_EXISTING ~dagg17.itm~ ~override~ // Stiletto of Demarchess +2
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 109 opcode = 139 parameter1 = 14102 parameter2 = 0 timing = 1 duration = 0 END // clone paralyze into 'held' string
  BUT_ONLY

// shadow thief dagger has ranged APR effect though a melee weapon
COPY_EXISTING ~dagg18.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 1 END
  BUT_ONLY

// adds proper number of attacks for dart01, disallows str bonus
COPY_EXISTING ~dart01.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 match_opcode = 1 END // delete all apr effects, and...
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 3 parameter2 = 1 timing = 2 END // ... add back as global effect
  LPF ALTER_HEADER INT_VAR match_type = 2 flag_strength = 0 damage_type = 4 END // slashing > missile damage, disallow str bonus
  BUT_ONLY

// various items stun but use held icon; lack 'stunned' string (Wisp)
COPY_EXISTING ~dart03.itm~   ~override~
              ~dartmel.itm~  ~override~
              ~paracarr.itm~ ~override~
              ~paraghas.itm~ ~override~
              ~paraghou.itm~ ~override~
              ~wand04.itm~   ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 match_parameter2 = 13 parameter2 = 55 END // change held icon to stun
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 45 opcode = 139 parameter1 = 1280 timing = 1 duration = 0 END // clone stun into 'stunned' string
  BUT_ONLY IF_EXISTS

//Asp's Nest darts do 1 HP damage every second for 40 seconds but are stated to do 1 HP damage every 3 seconds for 120 seconds (Wisp)
COPY_EXISTING ~dart05.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  25 parameter1 = 3 parameter2 = 3 duration = 120 END // 1 poison damage/3 seconds for 120 seconds
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 resist_dispel = 0 duration = 120 END // adjust icon to match duration, dispel
  BUT_ONLY

// paralyze not universal; expiry sound plays too late
COPY_EXISTING ~demmau01.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 109 END
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 109 target = 2 parameter2 = 2 resist_dispel = 1 duration = 30 probability1 = 15 savingthrow = BIT2 END
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 42 duration = 30 END // fix expiry sound
  BUT_ONLY IF_EXISTS

// some effects on dispel magic subject to MR, though all other effects are not; see also sppr303 et al patch
COPY_EXISTING ~deva.itm~     ~override~ // deva weapon
              ~devaevil.itm~ ~override~ // deva weapon
              ~elemchan.itm~ ~override~ // chan weapon
              ~elemcryo.itm~ ~override~ // cryomix weapon
              ~elemhydr.itm~ ~override~ // cryomix weapon
              ~elemimix.itm~ ~override~ // imix weapon
              ~elemogre.itm~ ~override~ // ogremoch weapon
              ~elemsunn.itm~ ~override~ // sunnis weapon
              ~elemyanc.itm~ ~override~ // yan-c-bin weapon
              ~elemzaam.itm~ ~override~ // zaaman rul weapon
              ~planetar.itm~ ~override~ // planetar weapon
              ~ravag01.itm~  ~override~ // ravager weapon
  PATCH_FOR_EACH op IN 58 77 215 240 BEGIN // dispel, cure feeblemind, remove FM icon, lighting effects
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = op resist_dispel = 0 END
  END
  BUT_ONLY IF_EXISTS

// items not working due to mis-targeted effects
COPY_EXISTING ~dragring.itm~ ~override~
              ~jonhp1.itm~   ~override~
              ~lich.itm~     ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 target = 1 END // change all global effects to target: self
  BUT_ONLY

// removal of extraneous vocalize effect
COPY_EXISTING ~dwsw1h01.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 48 END // delete vocalize effect
  BUT_ONLY

// drow whips not actually using animations
COPY_EXISTING ~dwwhip.itm~   ~override~
              ~dwwhip01.itm~ ~override~
  WRITE_ASCII 0x22 "FL" #2
  BUT_ONLY
  
// energy blades using wrong power level for electrical damage
COPY_EXISTING ~eneblade.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 9 END // only one effect really
  BUT_ONLY IF_EXISTS

// string fixes
COPY_EXISTING ~enmace.itm~ ~override~
  SAY 0x8 @116 // Mace +3
  SAY 0xc @116
  BUT_ONLY

// string fixes
COPY_EXISTING ~enmorn.itm~ ~override~
  SAY 0x8 @117 // Morning Star +3~
  SAY 0xc @117
  BUT_ONLY

// string fixes
COPY_EXISTING ~ensw1h01.itm~ ~override~
  SAY 0x8 @118 // Long Sword +3
  SAY 0xc @118
  BUT_ONLY

// string fixes
COPY_EXISTING ~ensw1h02.itm~ ~override~
  SAY 0x8 @119 // Short Sword +3~
  SAY 0xc @119
  BUT_ONLY

// familiar icon restorations
COPY_EXISTING ~famfai25.itm~ ~override~
              ~famfair.itm~  ~override~
              ~fampsd.itm~   ~override~
              ~fampsd25.itm~ ~override~
  WRITE_ASCII 0x3a ~FAMPSD~ #8
  BUT_ONLY IF_EXISTS
  
// per descript flame blade is non-magical, does 1d4 slashing damage plus 1d2 + 4 fire damage
// actual item is magical, does 1d2 + 4 slashing damage plus 1d4 fire, and has a spurious thac0 bonus
COPY_EXISTING ~fblade.itm~ ~override~
  WRITE_BYTE 0x18 (THIS BAND `BIT6) // removes magical flag
  LPF ALTER_HEADER INT_VAR match_type = 1 to_hit = 0 dicesize = 4 damage = 0 END // change to 1d4+0 on melee header
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 parameter1 = 4 dicesize = 2 END // change fire damage to 1d2+4
  BUT_ONLY

// unparalyze sould plays too late
COPY_EXISTING ~ghoul1.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 42 duration = 30 END // fix expiry sound
  BUT_ONLY

// paralyze not universal
COPY_EXISTING ~ghoullor.itm~ ~override~
              ~lacedo.itm~   ~override~
              ~lacedo02.itm~ ~override~
              ~lich02.itm~   ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 109 END
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 insert_point = 0 opcode = 109 target = 2 parameter2 = 2 resist_dispel = 1 duration = 30 savingthrow = BIT2 END
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 42 duration = 30 END // fix expiry sound
  BUT_ONLY

// ghoul lords nauseated icon lasts too long, can't be dispelled like disease itself
COPY_EXISTING ~ghoullor.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 match_parameter2 = 7 resist_dispel = 1 duration = 100 END // nausea icon
  BUT_ONLY

// hammer does slashing damage
COPY_EXISTING ~giafir.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 damage_type = 2 END // slashing > crushing damage on melee headers
  BUT_ONLY IF_EXISTS

// giants don't have s2 animation
COPY_EXISTING ~giants01.itm~ ~override~
  WRITE_ASCII 0x22 ~AX~ #2 
  BUT_ONLY

// fix power levels
COPY_EXISTING ~globred2.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 5 END // cure intox is power 0, string is power 6
  BUT_ONLY IF_EXISTS

// golem immunities: hold, charm, fear, poison, backstab, sleep (these get rounded out in immunity batches)
COPY_EXISTING ~golbra.itm~   ~override~ // has none
              ~golcla.itm~   ~override~ // only has immunity to backstab
              ~golfle.itm~   ~override~ // only has immunity to backstab
              ~golmag01.itm~ ~override~ // none
              ~golstone.itm~ ~override~ // immunity to slow, petrification
              ~irongol.itm~  ~override~ // immunity to sleep, poison
  PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "golcla") AND // immunity to backstab
            ("%SOURCE_RES%" STRING_COMPARE_CASE "golfle")) BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 292 parameter2 = 1 target = 1 timing = 2 END
  END
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "irongol") BEGIN // immunity to sleep, poison
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 parameter2 = 25 target = 1 timing = 2 END // poison
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 parameter2 = 39 target = 1 timing = 2 END // sleep
  END
  PATCH_FOR_EACH op IN 5 106 109 BEGIN // immunity to charm, morale failure, hold
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 parameter2 = op target = 1 timing = 2 END // poison
  END
  BUT_ONLY IF_EXISTS
  
// blackmist casting blindness at wrong power level with wrong projectile
COPY_EXISTING ~halb06.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 projectile = 263 END // change to 10' projectile
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0                    power = 8 END // change power to match pw blind
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 power = 0 END // ...except for the lighting effects
  BUT_ONLY

// wave should kill salamanders; salas are flagged incorrectly (fixed in creature racial patches) and wear ring95 which
// prevents slay opcode. Change wave to use an eff with Kill Target opcode instead.
COPY_EXISTING ~halb09.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 55 opcode = 177 STR_VAR resource = death END // change slay to use eff > death.eff (already has correct ids target)
  BUT_ONLY

// damage/thac0 bonuses mistargeted, may not be applying
COPY_EXISTING ~hamm04.itm~ ~override~ // hammer +1/+4 v giantkin
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 parameter1 = 0 parameter2 = 2 END // eff bonuses may not be applying
  BUT_ONLY

// borok's fist should use its own, unique icon
COPY_EXISTING ~hamm05.itm~ ~override~
  WRITE_LONG  0x0c 61268 // unique name string
  WRITE_ASCII 0x3a ~ihamm05~ #8
  WRITE_ASCII 0x58 ~chamm05~ #8
  LPF ALTER_HEADER STR_VAR icon = ihamm05 END
  BUT_ONLY

// dwarven thrower: changes thrown icon, and fixes melee damage
COPY_EXISTING ~hamm06.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 damage = 3 END // melee damage now 2d4+3 (was 2d4+2)
  LPF ALTER_HEADER INT_VAR match_type = 2 STR_VAR icon = ihamm05 END // thrown icon
  BUT_ONLY

// add ability to kill ettins to Crom
COPY_EXISTING ~hamm09.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 55 match_parameter1 = 167 parameter1 = 199 parameter2 = 4 END // clone class/troll slay to race/ettin slay
  BUT_ONLY

// hastring didn't actually, umm, work and stuff.
COPY_EXISTING ~hastring.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 1 parameter1 = 1 parameter2 = 0 duration = 0 END // change APR from set = 0 to gain one attack
  BUT_ONLY

// Helm of Brilliance Sunray fix, icons
COPY_EXISTING ~helm16.itm~ ~override~
  WRITE_ASCII 0x3a ~ihelm16~ #8 // inventory icon
  WRITE_ASCII 0x58 ~chelm16~ #8 // description image
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 target = 1 STR_VAR match_resource = sppr707 END // change spell cast (146) to target self for sunray
  BUT_ONLY

// to avoid double immune messages, cast spell opcodes should all use power 0
COPY_EXISTING ~helm16.itm~  ~override~
//              ~helm17.itm~  ~override~ patched below w/ additional fixes
              ~staf11.itm~  ~override~
              ~staf13.itm~  ~override~
              ~sw1h31.itm~  ~override~
              ~sw1h51.itm~  ~override~
              ~wa2helm.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 power = 0 END // change spell cast (146) to power 0
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 148 power = 0 END // change spell cast (148) to power 0
  BUT_ONLY

// The Death Spell cast by the Skull of Death should not require a saving throw nor should it allow two magic resistance and power level checks (aVENGER)
COPY_EXISTING ~helm17.itm~   ~override~ // Skull of Death
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 power = 0 resist_dispel = 0 savingthrow = 0 END // all handled in the spell itself
  BUT_ONLY

// wong-fei using wrong regen icon
COPY_EXISTING ~helm34.itm~  ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 56 parameter2 = 87 END // correct portrait icon
  BUT_ONLY IF_EXISTS

// Illusionary Werewolves should not damage characters who are unarmed or wielding ranged weapons (aVENGER)
COPY_EXISTING ~illusion.itm~ ~override~ // Illusionary Werewolf weapon
  LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 damage_type = 0 dicenumber = 0 dicesize = 0 END // 0d0, damage type none
  BUT_ONLY

// familiar attacks lack icons
COPY_EXISTING ~imp.itm~      ~override~ // imp soa
              ~impclaw.itm~  ~override~ // imp tob
              ~impqua.itm~   ~override~ // quasit soa
              ~quasclaw.itm~ ~override~ // quasit tob
  WRITE_ASCII 0x3a ~iwolf~ #8
  LPF ALTER_HEADER STR_VAR icon = iwolf END // attack icon
  BUT_ONLY IF_EXISTS

//removes poison ability from imp and quasit attacks
COPY_EXISTING ~imp.itm~     ~override~
              ~impclaw.itm~ ~override~
              ~impqua.itm~  ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 25 END // delete poison effect
  LPF ALTER_HEADER INT_VAR match_type = 1 dicesize = 6 END  // change damage to 1d6 for melee
  BUT_ONLY IF_EXISTS

// changes to ipsion, which provides psionic protections for Mordy swords and ToB boneblades
// The innate immunity to psionics and mind-affecting spells of Mordenkainen Sword should not be dispellable (aVENGER)
COPY_EXISTING ~ipsion.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 101 match_parameter2 =  45 END // delete dupe stun immunities
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 101 match_parameter2 = 175 END // delete dupe hold immunities
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_headers = 0 power = 0 resist_dispel = 0 duration = 0 END // The innate immunity to psionics and mind-affecting spells of Mordenkainen Sword should not be dispellable (aVENGER)
  PATCH_FOR_EACH op IN 5 19 45 175 BEGIN // immunity to charm, intelligence modification, stun, hold
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 parameter2 = op target = 1 timing = 2 END // poison
  END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 169 parameter2 =    86 target = 1 timing = 2 END // int drain immunity should also suppress strings, icon (revised by Wisp)
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 parameter1 = 14021 target = 1 timing = 2 END // int drain immunity should also suppress strings, icon (revised by Wisp)
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 parameter1 = 32089 target = 1 timing = 2 END // int drain immunity should also suppress strings, icon (revised by Wisp)
  BUT_ONLY

// MMM APR fixes; shuffle APR in Boon/Offensive spin into shell spell for easier blockage (see also spcl521.spl, spcl521d.spl, spcl741.spl, spcl741d.spl)
ACTION_IF !tobex_game BEGIN // skip this song and dance if TobEx is present

  COPY_EXISTING ~melfmet.itm~  ~override~
    PATCH_FOR_EACH spell IN spcl521d spcl741d BEGIN // immunity to boon of lathander, offensive spin
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = "-1" timing = 2 STR_VAR resource = EVAL ~%spell%~ END // immunity to spells
    END
    BUT_ONLY

END

// monk fists are inconsistent w/ descript
COPY_EXISTING ~mfist3.itm~ ~override~
              ~mfist6.itm~ ~override~
  READ_LONG   0x60 enchantment ELSE 0
  LPF ALTER_HEADER INT_VAR match_type = 1 to_hit = enchantment damage = enchantment END  // change melee to-hit, damage to match enchantment
  BUT_ONLY

// string fixes
COPY_EXISTING ~mfist4.itm~ ~override~
  SAY 0x8 @120 // Fist +1
  SAY 0xc @120

// string fixes
COPY_EXISTING ~mfist5.itm~ ~override~
  SAY 0x8 @121 // fist +2
  SAY 0xc @121

// string fixes
COPY_EXISTING ~mfist6.itm~ ~override~
              ~mfist7.itm~ ~override~
  SAY 0x8 @122 // fist +3
  SAY 0xc @122

// string fixes
COPY_EXISTING ~mfist8.itm~ ~override~
  SAY 0x8 @123 // fist +4
  SAY 0xc @123
  IF_EXISTS

// int drain string from mind flayers showing up 300 seconds after occurring
COPY_EXISTING ~mindflay.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 139 timing = 1 duration = 0 END // 'devour brain' string
  BUT_ONLY

// minhp1 is not protecting from all types of possible death
COPY_EXISTING ~minhp1.itm~ ~override~
  PATCH_FOR_EACH string IN 14021 14024 14029 14034 14042 14047 14782 14791 32089 40968 40969 40979 41495 41616 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 target = 1 parameter1 = string timing = 2 END // immunity to strings
  END
  PATCH_FOR_EACH op IN 10 15 165 19 210 23 241 246 39 44 49 6 76 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = op timing = 2 END // immunity to effects
  END
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
// fixes for every book except misc3a9 (no page turn ability)
COPY_EXISTING ~misc3a.itm~  ~override~ // fireball
              ~misc3a1.itm~ ~override~ // invisibility
              ~misc3a2.itm~ ~override~ // protection from evil
              ~misc3a3.itm~ ~override~ // true seeing
              ~misc3a4.itm~ ~override~ // farsight
              ~misc3a5.itm~ ~override~ // spell turning
              ~misc3a6.itm~ ~override~ // wyvern call
              ~misc3a7.itm~ ~override~ // stinking cloud
              ~misc3a8.itm~ ~override~ // lightning bolt
  LPF ALTER_HEADER INT_VAR identify = 1 END // ID before use, all abilities
  // 'page turn' ability
  LPF ALTER_HEADER INT_VAR header = 1 STR_VAR icon = imisc3a END // icon
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 match_opcode = 122 STR_VAR match_resource = ~misc3a~ resource = ~misc3aa~ END // from original to 0-lore copy
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 match_opcode = 174 resist_dispel = 0 END // sound shouldn't be blocked by MR (only wrong on original)
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 target = 4 range = 30 END // ID before use, target/range fixes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 146 opcode = 148 target = 1 END // spell cast fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a.itm~ ~override/misc3aa.itm~ // zero lore copy
  WRITE_SHORT 0x42 0 // lore
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 123 STR_VAR match_resource = ~misc3a~ resource = ~misc3aa~ END // delete item on page turn

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a1.itm~ ~override~
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 range = 1 END // ID before use, target/range fixes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 146 resist_dispel = 0 END // spell cast fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a3.itm~ ~override~
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 target = 5 END // target/range fixes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 146 target = 1 END // spell cast fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a6.itm~ ~override~
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 target = 4 END // target/range fixes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 146 opcode = 148 END // spell cast fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a7.itm~ ~override~
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 target = 4 range = 30 END // target/range fixes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 146 opcode = 148 target = 1 END // spell cast fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a8.itm~ ~override~
  // spell cast ability
  LPF ALTER_HEADER INT_VAR header = 0 range = 30 END // target/range fixes
  BUT_ONLY

// book of infinite spells; see all misc3a*.itm
COPY_EXISTING ~misc3a9.itm~ ~override~ // burning hands, last page
  LPF ALTER_HEADER INT_VAR identify = 1 END // ID before use, all abilities
  BUT_ONLY

// figurines were using some screwy system of reappearing that broke; should be using charges
COPY_EXISTING ~misc3d.itm~ ~override~ // golden lion
              ~misc3e.itm~ ~override~ // black spider
              ~misc3f.itm~ ~override~ // jade hound
              ~misc7t.itm~ ~override~ // moon dog
  PATCH_FOR_EACH op IN 122 123 139 BEGIN
    LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 3 match_opcode = op END // immunity to effects
  END
  BUT_ONLY
  
// methlid's harp bugs
COPY_EXISTING ~misc3o.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 1 duration = 0 END // remove durations from permanent timing
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 240 parameter2 = 55 END // remove stun icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 162 opcode =  46 END // add unstun
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 162 opcode = 163 END // add free action
  BUT_ONLY

// portal key should be droppable
COPY_EXISTING ~misc4g.itm~ ~override~
  WRITE_BYTE 0x18 (THIS BOR BIT2) // add droppable flag
  BUT_ONLY

// harper pin supposed to be usable only by Jaheira
COPY_EXISTING ~misc5x.itm~ ~override~
  WRITE_BYTE 0x26 15 // min str
  WRITE_BYTE 0x2a 10 // min int
  WRITE_BYTE 0x2c 17 // min dex
  WRITE_BYTE 0x2e 14 // min wis
  WRITE_BYTE 0x30 17 // min con
  WRITE_BYTE 0x32 15 // min chr
  BUT_ONLY

// claw of kazgaroth equipping effects are disspellable
COPY_EXISTING ~misc72.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 resist_dispel = 2 END // not dispel/not bypass
  BUT_ONLY

// Fix the wrong enchantment level and missing poison icon on the Dagger of Venom (aVENGER)
COPY_EXISTING ~misc75.itm~ ~override~ // Dagger of Venom
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 25 opcode = 142 parameter1 = 0 parameter2 = 6 END // clone poison into display poison icon
  BUT_ONLY

// match secondary to underlying spell
COPY_EXISTING ~misc7n.itm~ ~override~ // wand of lightning (unused?)
  LPF ALTER_HEADER INT_VAR secondary = 10 END // secondary: offensive damage
  BUT_ONLY

// Boo gets a quickslot icon
COPY_EXISTING ~misc84.itm~ ~override~
  READ_LONG   0x64 abil_off
  WRITE_SHORT 0x68 (THIS + 1) // add one ability
  WRITE_LONG  0x6a (THIS + 0x38) // push back fx offsets for new ability
  READ_SHORT  0x70 fx_num
  INSERT_BYTES abil_off 0x38 // new ability
    WRITE_BYTE  (abil_off       ) 3          // magical ability
    WRITE_BYTE  (abil_off + 0x02) 3          // item slots
    WRITE_ASCII (abil_off + 0x04) ~imisc84~  // boo icon
    WRITE_SHORT (abil_off + 0x20) fx_num     // fx index
  BUT_ONLY

// duration for illithid control circlet charm effect
COPY_EXISTING ~misc9x.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 5 duration = 1800 END // from 999999
  BUT_ONLY
  
// ground icon for unused potion
COPY_EXISTING ~misca4.itm~ ~override~
  WRITE_ASCII 0x44 ~GPOTN01~ #8
  BUT_ONLY

// blackrazor missing many effects
COPY_EXISTING ~miscbc.itm~ ~override~
  // global effects
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  7 resist_dispel = 0 END // colors shouldn't be disspellable
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  98                      opcode = 142 parameter2 = 87 END // display regen portrait icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 101 match_parameter2 = 5 opcode = 142 parameter2 = 52 END // display mind shield portrait icon
  // melee hit stuff
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  44 probability1 = 15 END // strength bonus is supposed to be only on 15% of hits
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  44 opcode = 142 parameter2 = 21 END // display strength portrait icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  16 opcode = 142 parameter2 = 38 END // display haste portrait icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  17 opcode = 139 parameter1 = 14022 END // display 'healed' string
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 216 opcode = 139 parameter1 = 40979 END // display 'four levels drained' string
  BUT_ONLY

// cloning Neb's dagger to make a version with no charges
COPY_EXISTING ~nebdag.itm~ ~override/cdnebdag.itm~
  SAY DESC @106
  WRITE_SHORT 0x42 0 // sets lore to zero for instant ID
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 END // delete all melee effects
  LPF ALTER_HEADER INT_VAR match_type = 1 charges = 0 drained = 1 END  // regular, uncharged melee header
  BUT_ONLY

// altering Neb's dagger to change to cdnebdag when charges run out
COPY_EXISTING ~nebdag.itm~ ~override~
  WRITE_ASCII 0x10 ~cdnebdag~ // 'used up' item
  LPF ALTER_HEADER INT_VAR match_type = 1 drained = 2 flag_recharge = 0 END  // regular, uncharged melee header
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 match_opcode = 40 END // delete slow effect
  BUT_ONLY

// disables arcane spellcasting; charm now handled in effects batch below
COPY_EXISTING ~npchan.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 145 target = 1 timing = 2 END // disable spellcasting
  WRITE_ASCII 0x44 ~gleat01~ #8 // ground icon is now leather
  BUT_ONLY

// adds min stat requirements to Keldorn's armor
// Fix erroneously dispellable effects on Keldorn's armor (polytype)
COPY_EXISTING ~npplat.itm~ ~override~
  WRITE_BYTE 0x2a 2  // min int
  WRITE_BYTE 0x2e 16 // min wis
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 power = 0 resist_dispel = 0 duration = 0 END
  BUT_ONLY

// string fixes
COPY_EXISTING ~npsw02.itm~ ~override~
  SAY 0x8 @124 // Yoshimo's Katana +1
  SAY 0xc @124 // Yoshimo's Katana +1
  BUT_ONLY

// Valygar's katana
COPY_EXISTING ~npsw04.itm~ ~override~
  WRITE_BYTE 0x2e 14 // min wis
  BUT_ONLY

// The Silver Sword should display the "Vorpal Hit" message on a successful vorpal hit
COPY_EXISTING ~planetar.itm~ ~override~ // silver sword (planetar)
              ~sw2h15.itm~   ~override~ // silver sword
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 13 opcode = 139 parameter1 = 64285 parameter2 = 0 END
  BUT_ONLY IF_EXISTS

// both full plate +1 don't disable thieving button, one has bad min str
COPY_EXISTING ~plat05.itm~ ~override~
              ~plat14.itm~ ~override~
  WRITE_BYTE 0x26 15 // correct min str
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 144 parameter2 = 1 END // change first, and only first, effect to disable thieving from disable stealth
  BUT_ONLY

// three sets of full plate have extraneous movement rate and THAC0 penalties
COPY_EXISTING ~plat19.itm~ ~override~
              ~plat22.itm~ ~override~
              ~plat23.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  54 END // delete thac0 effect
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 126 END // delete movement speed effect
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 26 parameter2 = 16 END // for plat22 only, change resist fire/cold icon to pro-fire
  BUT_ONLY IF_EXISTS

// (zero-weight magical items) ogre's morningstar from polymorph self; uses generic morningstar description so not bulk-fixed
COPY_EXISTING ~plymstar.itm~ ~override~
  WRITE_LONG 0x4c 0 // fixes item weight
  BUT_ONLY

// Potion of Storm Giant Strength mis-targeted
COPY_EXISTING ~potn07.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 44 target = 1 END // strength effect target change from preset to self
  BUT_ONLY

// thrown oil of fiery burning shouldn't say 'gulp!', fix power levels
// oil of fiery burning damage incorrect
COPY_EXISTING ~potn13.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 139 END // delete 'gulp' effect
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 power = 0 END // shouldn't be blocked by spell protections
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 12 duration = 0 END // shouldn't be blocked by spell protections
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 12 match_savingthrow = 0 dicesize = 6 dicenumber = 2 END // non-save damage is 2d6 not 3d4
  BUT_ONLY
  
// potion of magic blocking should only block up to level 5 spells, per description
COPY_EXISTING ~potn33.itm~ ~override~
  FOR (index = 6 ; index < 10 ; ++index) BEGIN
    LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 102 match_parameter1 = index END // delete imunity to spell level #index
  END
  BUT_ONLY

// fix errors; full free action batch effects handled below
COPY_EXISTING ~potn45.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 =  5 parameter2 =  16 END // change charm immunity to haste immunity
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 = 45 parameter2 = 175 END // change stun immunity to hold immunity
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 duration = 600 END // correct portrait icon duration
  BUT_ONLY

// The Unholy Reaver should not dispel effects on its wielder and his allies
// It should dispel magic on the target of the Anti-Paladin's attack
COPY_EXISTING ~reaver.itm~ ~override~ // Unholy Reaver
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_target = 5 target = 2 resist_dispel = 2 END // preset target, bypass mr
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 duration = 0 STR_VAR match_resource = "spdisma" resource = "spdispma" END // typo
  BUT_ONLY

// ruby pendant lacks icon for ability
COPY_EXISTING ~regisamu.itm~ ~override~
  LPF ALTER_ITEM_HEADER STR_VAR icon = "spwi316b" END
  BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_elem_rings BEGIN

  ring27  => 187 // elemental_fire
  ring28  => 186 // elemental_air
  ring29  => 188 // elemental_earth

END

// fire/air/earth control ring has screwy charm effects; changed to use eff targeting on class fire/air/earth elemental
ACTION_PHP_EACH cd_elem_rings AS item => class BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      LPF ALTER_EFFECT  INT_VAR match_opcode = 5 opcode = 177 parameter1 = class parameter2 = 5 savingthrow = BIT0 savebonus = 2 STR_VAR resource = cdcmelem END // route charm through new EFF file
      LPF ALTER_HEADER  INT_VAR header = 0 range = 2 flag_recharge = 1 END // fix range, charges for charm
      LPF DELETE_EFFECT INT_VAR header = 1 END // delete effects for ability one...
      LPF DELETE_EFFECT INT_VAR header = 2 END // and two (added below as direct spell castings)
      BUT_ONLY

END

// delete effects simulating burning hands, flame strike for direct casting of spells
COPY_EXISTING ~ring27.itm~ ~override~
  LPF ALTER_HEADER INT_VAR header = 2 range = 5 projectile = 1 END // range, projectile of burning hands ability
  LPF ADD_ITEM_EFFECT INT_VAR header = 2 opcode = 146 target = 2 parameter1 = 6 parameter2 = 1 timing = 1 STR_VAR resource = sppr503 END
  LPF ADD_ITEM_EFFECT INT_VAR header = 3 opcode = 146 target = 2 parameter1 = 1 parameter2 = 1 timing = 1 STR_VAR resource = spwi103 END
  BUT_ONLY

// delete effects simulating improved invisibility for direct casting of spell
COPY_EXISTING ~ring28.itm~ ~override~
  LPF ALTER_HEADER INT_VAR header = 1 target = 1 range = 1 primary = 0 secondary = 0 END // use schools from spell itself
  LPF ADD_ITEM_EFFECT INT_VAR header = 2 opcode = 146 target = 2 parameter1 = 7 parameter2 = 1 timing = 1 STR_VAR resource = spwi405 END
  BUT_ONLY

// delete save bonuses, effects simulating stone to flesh for direct casting of spell
COPY_EXISTING ~ring29.itm~ ~override~
  FOR (op = 33 ; op < 38 ; ++op) BEGIN // delete save bonuses
    LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = op END
  END
  LPF ALTER_HEADER INT_VAR header = 1 range = 50 primary = 0 secondary = 0 END // use schools from spell itself
  LPF ADD_ITEM_EFFECT INT_VAR header = 2 opcode = 146 target = 2 parameter1 = 1 parameter2 = 1 timing = 1 STR_VAR resource = spwi625 END
  BUT_ONLY

// ring of gaxx displays wrong icon, Standardize the school and secondary type of items which cast alteration spells (aVENGER)
COPY_EXISTING ~ring39.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 13 STR_VAR match_icon = spwi613b END // haste, primary Transmuter, secondary Non-Combat
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 28 parameter2 = 63 END // change pro-magic icon to magic resistance
  BUT_ONLY

// Undead creatures are erroneously immune to Lightning Bolt instead of Hold Person (aVENGER)
COPY_EXISTING ~ring95.itm~ ~override~ // undead immunity ring
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 206 STR_VAR match_resource = "spwi308" resource = "spwi306" END // swap hold person for lightning bolt
  BUT_ONLY

// change rod-created items to reference clone instead of original
COPY_EXISTING ~rodmace.itm~  ~override~
              ~rodspear.itm~ ~override~
              ~rodsword.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 122 STR_VAR match_resource = ~rods02~ resource = ~rods02a~ END // change back to 0-lore rod
  LPF ALTER_HEADER INT_VAR match_type = 3 drained = 1 flag_recharge = 0 END // rod o' lordly might sub-items created with no charges
  BUT_ONLY

// rods02 requires lore (fixed in bulk fixes); however it would make it un-id'd every time you switched back to the rod.
// So, clone it with 0 lore and have other items create the clone instead of the original
COPY_EXISTING ~rods02.itm~ ~override/rods02a.itm~ // zero-lore copy
  WRITE_SHORT 0x42 0

// rod of smiting golem damage not set properly, should be in melee ability not global fx
COPY_EXISTING ~rods04.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 STR_VAR match_resource = smitgol1 END // delete this as global effect...
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 144 parameter2 = 4 timing = 1 STR_VAR resource = smitgol1 END // and add back as melee effect
  BUT_ONLY

// wrong quickbar icon for rod of terror
COPY_EXISTING ~rods05.itm~ ~override~
  LPF ALTER_ITEM_HEADER STR_VAR icon = "irods05" END

// string fix, color change
COPY_EXISTING ~rodsword.itm~ ~override~
  SAY 0x8 @115 // Flaming Long Sword +1
  SAY 0xc @115
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 7 parameter1 = 115 END // turns sword flaming
  BUT_ONLY

// The Reflection shield should protect the wielder against Ice Arrows (Wisp)
COPY_EXISTING ~shld24.itm~ ~override~
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 num_ab
  READ_LONG  0x6a fx_off
  READ_SHORT 0x70 num_fx
  FOR (i=0;i<num_fx;i+=1) BEGIN // first pass builds array of already blocked projectiles
    READ_SHORT fx_off + 0x30*i fx_type
    PATCH_IF fx_type = 197 BEGIN
      READ_LONG fx_off + 0x30*i + 0x8 pro
      SET $fl#pro_shld24("%pro%") = 1
    END
  END
  PATCH_FOR_EACH pro IN 1 3 4 6 9 11 13 14 15 16 19 26 29 31 34 102 BEGIN // now add any missing from this list (patched tob only missing 102, arrowflb)
    PATCH_IF !VARIABLE_IS_SET $fl#pro_shld24("%pro%") BEGIN
      INSERT_BYTES fx_off + 0x30*num_fx        0x30
      WRITE_SHORT  fx_off + 0x30*num_fx        197
      WRITE_BYTE   fx_off + 0x30*num_fx + 0x2  1
      WRITE_LONG   fx_off + 0x30*num_fx + 0x8  pro
      WRITE_BYTE   fx_off + 0x30*num_fx + 0xc  2
      WRITE_BYTE   fx_off + 0x30*num_fx + 0x12 100
      num_fx += 1
    END
  END
  WRITE_SHORT 0x70 num_fx
  FOR (i=0;i<num_ab;i+=1) BEGIN
    WRITE_SHORT ab_off + 0x38*i + 0x20 num_fx
    num_fx += SHORT_AT ab_off + 0x38*i + 0x1e
  END
  BUT_ONLY

// small shields granting ac bonus v missiles and should not
COPY_EXISTING ~shld25.itm~  ~override~ // shield o' harmony
              ~shld28.itm~  ~override~ // small shield +2
  READ_LONG   0x6a fx_off // can't use ALTER_EFFECT since it won't take negative values
  READ_SHORT  0x70 fx_num
  FOR (index = 0; index < fx_num; ++index) BEGIN
    READ_SHORT (fx_off +        (index * 0x30)) opcode
    READ_BYTE  (fx_off + 0x08 + (index * 0x30)) flag
    PATCH_IF ((opcode = 0) AND ((flag BAND BIT1) = BIT1)) BEGIN
      WRITE_LONG  (fx_off + 0x04 + (index * 0x30)) "-3"
    END
  END
  BUT_ONLY

// shield of harmony stuff could be dispelled
COPY_EXISTING ~shld25.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 resist_dispel = 0 END
  BUT_ONLY

// slay living using wrong power level for cosmetic visuals
COPY_EXISTING ~slaylive.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 power = 0 END
  BUT_ONLY

// sol's searing orb - undead blindness should be 6 rounds, 12 on failed save;
// non-undead should be blinded for 1d6 rounds, not fixed at 6 rounds
// see also cdsorb1.eff, cdsorb2.eff, udeadbli.eff
COPY_EXISTING ~sorb.itm~ ~override~
  PATCH_FOR_EACH op IN 177 139 74 142 BEGIN // delete everything except damage and visuals
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = op END
  END
  LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 177 target = 2 power = 6 parameter1 = 4 parameter2 = 3 timing = 1   STR_VAR resource = cdsorb2 END
  LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 177 target = 2 power = 6 parameter1 = 4 parameter2 = 3 duration = 1 STR_VAR resource = cdsorb1 END
  LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 177 target = 2 power = 6 parameter1 = 4 parameter2 = 3 timing = 1   STR_VAR resource = udead66 END
  FOR (index = 0 ; index < 2 ; ++index) BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 177 target = 2 power = 6 parameter1 = 4 parameter2 = 3 timing = (3 * index)
      savingthrow = index duration = 36 STR_VAR resource = udeadbli END
  END
  LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 139 target = 2 power = 6 parameter1 = 14674 timing = 1 resist_dispel = 1 savingthrow = BIT0 END
  SET prob = "-1"
  FOR (index = 1 ; index < 7 ; ++index) BEGIN
    PATCH_IF (index = 1) BEGIN SET span = 16 END ELSE BEGIN SET span = 17 END
    SET prob += span
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 74 target = 2 power = 6 resist_dispel = 1 duration = (6 * index)
      probability1 = prob probability2 = ((prob - span) + 1) savingthrow = BIT0 END
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 142 target = 2 power = 6 parameter2 = 8 resist_dispel = 1
      duration = (6 * index) probability1 = prob probability2 = ((prob - span) + 1) savingthrow = BIT0 END
  END

// spellhaunts have extraneous protection from spell effects
COPY_EXISTING ~spellh01.itm~ ~override~
  PATCH_FOR_EACH spell IN sppr316 spwi315 spwi323 BEGIN // none of these exist
    LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 206 STR_VAR match_resource = EVAL ~%spell%~ END
  END
  BUT_ONLY IF_EXISTS

// spear ranges
COPY_EXISTING ~sper02.itm~   ~override~ // spear +1
              ~sper03.itm~   ~override~ // spear +3 backbiter
              ~sper05.itm~   ~override~ // spear +2
              ~sper06.itm~   ~override~ // spear +3
              ~sper07.itm~   ~override~ // spear of the unicorn +2
              ~sper08.itm~   ~override~ // spear +3 impaler
              ~sper09.itm~   ~override~ // spear +1 halcyon
              ~sper10.itm~   ~override~ // spear of withering +4
              ~sper11.itm~   ~override~ // ixil's nail +4
              ~sper12.itm~   ~override~ // ixil's spike +6
              ~spermel.itm~  ~override~ // melissan's spear
              ~tasloiil.itm~ ~override~ // spear
              ~waspear.itm~  ~override~ // spear of kuldahar +3
  LPF ALTER_HEADER INT_VAR match_type = 1 range = 2 END // melee range
  BUT_ONLY IF_EXISTS

// disables erroneous save effects. healing bypass MR, and fixes enchanment; fix power levels; Staff of Curing does not cure portrait icons (Wisp)
COPY_EXISTING ~staf10.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_location = 3 power = 2 resist_dispel = 3 END // power, resist of heal effects
  FOR (index = 33 ; index < 38 ; ++index) BEGIN
    LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = index END // delete save bonii
  END
  PATCH_FOR_EACH parameter2 IN 5 6 7 BEGIN //Intoxication, poison, disease
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 240 target = 2 power = 2 parameter2 timing = 1 resist_dispel = 3 END
  END
  BUT_ONLY

// SotM needs a lot of rebuilding--dispel effects don't work in melee, several effects are mistargeted or dispellable, etc.
COPY_EXISTING ~staf11.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR header = 0 END // delete all effects from melee header
  LPF DELETE_EFFECT INT_VAR header = 2 END // delete all effects from spell trap header
  // global effects changes
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0                    resist_dispel = 0 END // bypass MR for all equipped effects...
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  20 resist_dispel = 1 END // except invisibility
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 139 target = 1 END // display string on self
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 141 target = 1 END // lighting effects on self
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 142 match_parameter2 = 28 parameter2 = 52 END // change pro-magic icon to mind shield
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  33 opcode = 100 parameter1 = 9 parameter2 = 7 END // add protection from creature type, summoned demon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode =  33 opcode = 219 parameter1 = 3 parameter2 = 8 END // add attack roll penalty, evil
  // melee dispel-on-hit header
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  58 target = 2 parameter1 = 30    parameter2 = 2  resist_dispel = 2 timing = 1 END // dispel magic at level 30 (leave rest to batch patches)
  // spell trap header
  //LPF ALTER_HEADER INT_VAR match_type = 3 primary = 1 secondary = 1 END // mirror image, primary abjuration, secondary spell protections // not needed, casts a spell trap copy directly
  LPF ADD_ITEM_EFFECT INT_VAR header = 3 opcode = 146 target = 1 power = 0 parameter1 = 10 parameter2 = 1 resist_dispel = 2 timing = 1 STR_VAR resource = staf11 END // cast spell directly
  BUT_ONLY // heh

// Staff of Power should actually cast Globe of Invulnerability,
// not some shabby facsimile. Two icons also incorrect.
COPY_EXISTING ~staf12.itm~   ~override~
  WRITE_ASCIIT 0x3a ~istaf12~  // correcting inventory icon
  LPF ALTER_HEADER INT_VAR match_type = 1 drained = 0 STR_VAR icon = istaf12 END // melee icon, melee charges do not deplete
  // globe header
  LPF ALTER_HEADER STR_VAR match_icon = ~spwi406b~ icon = ~spwi602b~ END // change globe ability icon from MGoI to GoI
  LPF DELETE_EFFECT   INT_VAR header = 2 END // delete all effects from GoI header
  LPF ADD_ITEM_EFFECT INT_VAR header = 3 opcode = 146 target = 1 parameter1 = 12 parameter2 = 1 timing = 1 timing = 1 STR_VAR resource = spwi602 END // cast GoI spell directly
  // lightning header
  LPF ALTER_EFFECT INT_VAR header = 1 power = 3 END
  LPF ALTER_EFFECT INT_VAR header = 1 match_opcode = 146 power = 0 parameter2 = 1 END
  BUT_ONLY // no bad joke for the end of this patch

// Standardize the school and secondary type of items which cast offensive spells (aVENGER)
COPY_EXISTING ~staf13.itm~ ~override~ // Staff of Thunder and Lightning
              ~ttwand.itm~ ~override~ // Wand of the Heavens
              ~wand03.itm~ ~override~ // Wand of Magic Missiles
              ~wand05.itm~ ~override~ // Wand of Fire
              ~wand06.itm~ ~override~ // Wand of Frost
              ~wand07.itm~ ~override~ // Wand of Lightning
              ~wand11.itm~ ~override~ // Wand of the Heavens
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 6 secondary = 10 END // all magical headers, primary Invoker, secondary offensive damage
  BUT_ONLY

// staff of the woodlands
COPY_EXISTING ~staf14.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 header = 1 match_opcode = 142 resist_dispel = 1 END // portrait icon should dispel
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 0 parameter2 = BIT4 END // barkskin is base ac, not a bonus
  FOR (index = 33 ; index < 37 ; ++index) BEGIN // all saves except vs. spell
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = index target = 1 parameter1 = 1 timing = 2 resist_dispel = 2 END // add +1 save bonii
  END
  BUT_ONLY

// Standardize the school and secondary type of items which cast alteration spells (aVENGER)
COPY_EXISTING ~staf16.itm~ ~override~ // Staff of Earth +2
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 13 STR_VAR match_icon = spwi625b END // stone to flesh, primary Transmuter, secondary Non-Combat
  BUT_ONLY

// staff of the ram +6 missing listed 1d4 pierce damage
COPY_EXISTING ~staf22.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 12 target = 2 parameter2 = (16 << 16) timing = 1 dicenumber = 1 dicesize = 4 END // add 1d4 piercing damage to melee attack
  BUT_ONLY IF_EXISTS

// add portrait icon for fire resistance
COPY_EXISTING ~sw1h15.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 =  16 timing = 2 END

// Standardize the school and secondary type of items which cast illusionary spells (aVENGER)
COPY_EXISTING ~sw1h26.itm~ ~override~ // Ilbratha +1
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 5 secondary = 3 END // mirror image, primary illusion, secondary illusionary protections
  BUT_ONLY

// arbane's sword has extraneous effects and prevents the wrong string
COPY_EXISTING ~sw1h27.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 13 END // haste, primary transmuter, secondary non-combat
//  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 296 END // immunity to spmindat.vvc
  PATCH_FOR_EACH op IN 45 154 157 158 BEGIN
    LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 101 match_parameter2 = op END // immunity to stun, entangle, grease, web
  END
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 101 match_parameter2 = 175 END // delete dupe hold immunity
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 169 match_parameter2 =   55 END // immunity to stun icon
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 169 match_parameter2 =  129 END // immunity to web icon
  LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 267 match_parameter1 = 1280 END // immunity to 'stunned' string
  BUT_ONLY

// daystar fix: does 2d8 + 6 vs. evil undead; should be 2d8 + 8
// harder than usual because of all overlapping effs; see also daystar1.eff, daystar2.eff
// move daystar1 to melee header, apply only v undead
// evaildam2 also needs to move, but handled in effing effs section
COPY_EXISTING ~sw1h31.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 177 STR_VAR match_resource = daystar1 END // delete from global effects
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 4 parameter2 = 3 timing = 1 STR_VAR resource = daystar1 END // add back as melee effect
  BUT_ONLY

// dragonslayer missing regeneration portrait icon, dragon damage gets an extra +2
COPY_EXISTING ~sw1h32.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 177 opcode = 142 parameter2 = 87 STR_VAR match_resource = ~sw1h32a~ resource = ~~ END // delete sw1h32a eff by turning into missing regen icon
  BUT_ONLY

// Standardize the school and secondary type of items which cast divination spells (aVENGER)
COPY_EXISTING ~sw1h32.itm~ ~override~ // Dragonslayer
              ~sw1h34.itm~ ~override~ // Albruin +1
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 3 secondary = 5 END // Detect Invisibility, primary Diviner, secondary Divination Attack
  BUT_ONLY

// adjatha's healing does not bypass MR and the sword has an incorrect strength requirement
COPY_EXISTING ~sw1h35.itm~ ~override~
  WRITE_BYTE 0x26 6 // strength requirement
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 resist_dispel = 0 END // not dispel/bypass
  BUT_ONLY

// Standardize the school and secondary type of items which cast alteration spells (aVENGER)
COPY_EXISTING ~sw1h36.itm~ ~override~ // Namarra +2
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 8 secondary = 11 END // silence, primary Transmuter, secondary Disabling
  BUT_ONLY
  
// shazzelim's damage does not match description
COPY_EXISTING ~sw1h50.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 damage = 1 END // melee, damage from 1d8+2 to 1d8+1
  BUT_ONLY

// fix power levels
COPY_EXISTING ~sw1h51.itm~ ~override~ // celestial fury
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 power = 1 END // power level for blindness stuff
  BUT_ONLY

COPY_EXISTING ~sw1h55.itm~ ~override~
  WRITE_ASCII 0x3a ~isw1h55~ #8 // inventory icon
  LPF ALTER_HEADER INT_VAR match_type = 1 STR_VAR icon = ~isw1h55~ END // melee icon
  BUT_ONLY

// Habib's mighty scimitar
COPY_EXISTING ~sw1h57.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 111 opcode = 255 parameter1 = 1 END // change from creating in mag wep slot to inventory
  BUT_ONLY

// foebane's LMD is bypassing MR
COPY_EXISTING ~sw1h63.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 END // delete all melee effects
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 146 target = 2 power = 1 parameter1 = 1 parameter2 = 1 timing = 1 resist_dispel = 1 STR_VAR resource = spin104a END // cast LMD directly
  BUT_ONLY IF_EXISTS

/// The Priest of Helm's Sword of Seeking should do 2d4 damage, not 1d8 (by Wisp)
COPY_EXISTING ~sw1hseek.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 dicenumber = 2 dicesize = 4 END // change to 2d4 on melee header
  BUT_ONLY

// create party-friendly harbinger for c6eric.cre and c6eric3.cre
COPY_EXISTING ~sw2h07.itm~ ~override/cdsw2h07.itm~
  LPF DELETE_EFFECT INT_VAR match_opcode = 146 STR_VAR match_resource = spwi304 END  // cast fireball
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = misc_17b END // play fireball sound

// Soul Reaver's THAC0 drain should bypass MR
COPY_EXISTING ~sw2h08.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 54 resist_dispel = 0 END // thac0 change bypasses MR
  BUT_ONLY

// Carsomyr's dispel on hit effect is incorrect
COPY_EXISTING ~sw2h10.itm~ ~override~
              ~sw2h19.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 identify = 0 dicesize = 12 END // no ID required for melee, use d12 for damage
  LPF ALTER_HEADER INT_VAR match_type = 3 identify = 1 target = 4 END    // ID required for dispel, fix target
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 opcode = 148 target = 1 END // thac0 change bypasses MR
  // melee dispel-on-hit header - same fix as staf11 except no op 141
  LPF DELETE_EFFECT INT_VAR header = 0 END // delete all effects from melee header
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  58 target = 2 parameter1 = 30    parameter2 = 2  resist_dispel = 2 timing = 1 END // dispel magic at level 30 (leave rest to batch patches)
  BUT_ONLY IF_EXISTS

// poison immunity for 'dead' trolls
COPY_EXISTING ~trolldie.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 31 opcode = 173 END // reduced damage from poison
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 31 opcode = 101 parameter1 = 0 parameter2 = 25 END // immunity to poison
  BUT_ONLY

// Wand of heavens claims 8d6 damage; actually does 12d4; fix flame strike animation while we're here
COPY_EXISTING ~ttwand.itm~ ~override~
              ~wand11.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 projectile = 1 END // remove old bg FS animation
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 dicesize = 6 dicenumber = 4 power = 5 END // (half) damage is 4d6, not 6d4 and fix power too
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "ttwand" = 0) BEGIN // ttwand lacks 215 opcodes
    LPF ADD_ITEM_EFFECT INT_VAR header = 1 opcode = 215 target = 2 timing = 1 resist_dispel = 1 STR_VAR resource = spflast2 END // play 3d effect
    LPF ADD_ITEM_EFFECT INT_VAR header = 1 opcode = 215 target = 2 timing = 1 resist_dispel = 1 STR_VAR resource = spflsrin END // play 3d effect
  END ELSE BEGIN // wand11 already has play 3d effect opcodes, but with wrong power
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 power = 0 END // visuals shouldn't be blocked by spell protections
  END
  BUT_ONLY

// add disease immunity to vampires; fleshed out in immunity batches below
COPY_EXISTING ~vampreg.itm~  ~override~
              ~vampreg1.itm~ ~override~
              ~vampreg2.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 78 timing = 2 END

// sensate amulet should increase max HP and not current & max HP to close healing loophole
COPY_EXISTING ~wa2amu.itm~   ~override~
              ~waspear.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 18 parameter2 = 3 END // increase max hp without increasing current
  BUT_ONLY

// Vhailor's simulcrum ability could fail on MR check
COPY_EXISTING ~wa2helm.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 resist_dispel = 0 END // cast simulcrum
  BUT_ONLY

// since beholder lightning bolt is now a separate projectile, need to add protection in shield of balduran
COPY_EXISTING ~wa2shiel.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 197 match_parameter2 = 208 parameter2 = (cdbehbla - 1) END // block new beholder projectile
  BUT_ONLY

// jerrod's mace has incorrect eff assignments
COPY_EXISTING ~wamace.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 STR_VAR match_resource = ~wamacea~ resource = ~damacea~ END // fix typo
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 177 STR_VAR match_resource = ~wamaceb~ resource = ~damaceb~ END // fix typo
  BUT_ONLY

// match secondary to underlying spell
COPY_EXISTING ~wand02.itm~ ~override~ // wand of fear
              ~wand04.itm~ ~override~ // wand of paralyzation
              ~wand08.itm~ ~override~ // wand of sleep
  LPF ALTER_HEADER INT_VAR match_type = 3 secondary = 11 END // all magical headers, secondary disabling
  BUT_ONLY

// match secondary to underlying spell
COPY_EXISTING ~wand10.itm~ ~override~ // wand of monster summoning
  LPF ALTER_HEADER INT_VAR match_type = 3 secondary = 6 END // all magical headers, secondary conjuration
  BUT_ONLY

// wand of wonder has incorrect max charges; causes it to be sold for 5x its value
COPY_EXISTING ~wand12.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 3 charges = 50 END
  BUT_ONLY

// wand of cloudkill bypassing MR, Standardize the school and secondary type of items which cast offensive spells (aVENGER)
// make it cast CK directly so that poison immunity can block it outright
COPY_EXISTING ~wand13.itm~ ~override~
  LPF DELETE_EFFECT END // delete existing effects
  LPF ALTER_ITEM_HEADER INT_VAR projectile = 1 END // remove projectile
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 148 target = 1 parameter1 = 10 parameter2 = 1 timing = 1 STR_VAR resource = spwi502 END // cast spell directly
  BUT_ONLY

// Standardize the school and secondary type of items which cast abjuration spells (aVENGER)
COPY_EXISTING ~wand18.itm~ ~override~ // Wand of Spell Striking
  LPF ALTER_HEADER INT_VAR match_type = 3 primary = 1 secondary = 4 END // breach and/or pierce magic, primary Abjurer, secondary Magic Attack
  BUT_ONLY IF_EXISTS

// wand of cursing should cause deafness but is not (though it does cause the portrait icon)
COPY_EXISTING ~wand19.itm~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 38 opcode = 80 END // clone blindness effect into deafness
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 139 match_parameter1 = 14002 parameter1 = 54318 END // clone 'silence' string into 'deaf'
  BUT_ONLY IF_EXISTS

// missile weapons using wrong projectiles and being blocked by protection from normal missiles; see also arow02.itm and aegis.itm et al
COPY_EXISTING ~wasling.itm~  ~override~ // sling of everard +5
  LPF ALTER_HEADER INT_VAR match_type = 2 projectile = 17 flag_strength = 0 END // magic bullet (17) from normal bullet (20) on ranged headers; no strength bonus
  BUT_ONLY

// allow arbitrary return-to-human form from clock of the wuff
// see also clck04.itm, shapeshifting spell batch
COPY_EXISTING ~wolfm.itm~ ~override/cdwolfm.itm~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 60 target = 1 timing = 2 parameter1 = 100 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 60 target = 1 timing = 2 parameter1 = 100 parameter2 = 1 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 53 target = 1 timing = 2 parameter1 = 31488 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 44 target = 1 timing = 2 parameter1 = 18 parameter2 = 1 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 15 target = 1 timing = 2 parameter1 = 17 parameter2 = 1 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 215 target = 1 parameter2 = 1 duration = 117 timing = 3 STR_VAR resource = sppolymp END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 215 target = 1 parameter2 = 1 duration = 117 timing = 3 STR_VAR resource = polyback END

// two xbows lacking min str requirements
COPY_EXISTING ~xbow15.itm~ ~override~
              ~xbow16.itm~ ~override~
  WRITE_BYTE 0x26 8 // min str
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// bulk spell fixes                                 \\\\\
/////                                                  \\\\\

// power level fixes
//Set power of summoning spells to 0, otherwise they can get blocked if the caster is under (M)GoI (code by Wisp; general idea by lotsa people)
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  melis03  => 0 // summon spells should be 0; Taint of the Slayer
  senspisu => 0 // summon spells should be 0; Summon Spider
  spcl621  => 0 // summon spells should be 0; Summon Spirit Animal
  spcl923  => 0 // summon spells should be 0; Summon Deva
  spin615  => 0 // summon spells should be 0; Knight
  spin616  => 0 // summon spells should be 0; Flames
  spin622  => 0 // summon spells should be 0; Skull
  spin770  => 0 // HELL_EXPLODE - string at 1, others 0
  spin771  => 0 // HELL_DAMAGE_HALF - string at 1, others 0
  spin923  => 7 // GENIE_LIMITED_WISH_HEAL_ALL - visual at 5, others 7
  spin958  => 4 // ULITHARID_CURE_SERIOUS_WOUNDS - visual at 1, detox 0, others 4
  sppr105  => 1 // (entangle): one sound effect opcode is power 2.
  sppr210  => 2 // (resist fire/cold): one display string at level 4 has power 0.
  sppr301  => 0 // summon spells should be 0; Animate Dead
  sppr306  => 3 // (protection from fire): The character color pulse effect at all levels is at power 1.
  sppr315  => 3 // (cure medium wounds): remove intoxication is at power 0.
  sppr401  => 4 // (cure serious wounds): remove intoxication is at power 0.
  sppr402  => 0 // summon spells should be 0; Animal summoning I
  sppr410  => 0 // summon spells should be 0; Call Woodland Beings
  sppr501  => 0 // summon spells should be 0; Animal summoning II
  sppr502  => 5 // (cure critical wounds): remove intoxication at power 0, 'healed' string at power 6.
  sppr512  => 5 // (greater command): visual effect on target at power 1
  sppr601  => 0 // summon spells should be 0; Aerial Servant
  sppr602  => 0 // summon spells should be 0; Animal summoning III
  sppr604  => 0 // summon spells should be 0; Conjure Animals
  sppr605  => 0 // summon spells should be 0; Conjure Fire Elemental
  sppr702  => 0 // summon spells should be 0; Conjure Earth Elemental
  sppr703  => 0 // summon spells should be 0; Gate
  sppr712  => 7 // (resurrection): most of the 'remove spell' opcodes are at power 5.
  sppr723  => 0 // summon spells should be 0; Elemental Summoning
  sppr724  => 0 // summon spells should be 0; Greater Elemental Summoning
  sppr726  => 0 // summon spells should be 0; Summon Deva
  sppr727  => 0 // summon spells should be 0; Summon Fallen Deva
  spra305  => 0 // summon spells should be 0; Animal summoning II
  spra306  => 0 // summon spells should be 0; Animal summoning III
  spwi106  => 1 // (blindness): 'blinded' string at power 0
  spwi111  => 1 // (infravision): 'infravision' string at power 0
  spwi113  => 1 // (protection from evil): 'protected from evil' string at power 0
  spwi210  => 2 // (resist fear): visual effect at power 1
  spwi223  => 2 // (deafness): visual effects at power 1
  spwi224  => 2 // (glitterdust): visual effect at power 4
  spwi309  => 0 // summon spells should be 0; Monster summoning I
  spwi407  => 0 // summon spells should be 0; Monster summoning II
  spwi410  => 4 // (remove curse): save visual effect, all at power 3
  spwi414  => 4 // (spirit armor): display string at power 3
  spwi423  => 0 // summon spells should be 0; Spider Spawn
  spwi501  => 0 // summon spells should be 0; Animate Dead
  spwi504  => 0 // summon spells should be 0; Monster summoning III
  spwi516  => 0 // summon spells should be 0; Conjure Lesser Fire Elemental
  spwi520  => 0 // summon spells should be 0; Conjure Lesser Air Elemental
  spwi521  => 0 // summon spells should be 0; Conjure Lesser Earth Elemental
  spwi523  => 5 // (sunfire): level 10 effects at power 3, level 11 damage effect at power 6
  spwi601  => 0 // summon spells should be 0; Invisible Stalker
  spwi613  => 6 // (improved haste): all effects save one at power 3
  spwi619  => 0 // summon spells should be 0; Wyvern Call
  spwi620  => 0 // summon spells should be 0; Conjure Fire Elemental
  spwi621  => 0 // summon spells should be 0; Conjure Air Elemental
  spwi622  => 0 // summon spells should be 0; Conjure Earth Elemental
  spwi623  => 0 // summon spells should be 0; Carrion Summons
  spwi624  => 0 // summon spells should be 0; Summon Nishruu
  spwi707  => 0 // summon spells should be 0; Cacofiend
  spwi711  => 7 // (sphere of chaos): play sound at power 4
  spwi716  => 0 // summon spells should be 0; Mordenkainen's Sword
  spwi717  => 0 // summon spells should be 0; Summon Efreeti
  spwi718  => 0 // summon spells should be 0; Summon Djinni
  spwi719  => 0 // summon spells should be 0; Summon Hakeashar
  spwi721  => 7 // (mass invisibility): actual invisibility effects at power 4 (this error is propagated when the Fixpack clones this effect for its additional spell protections)
  spwi803  => 8 // (protection from energy): visual effects at power 7
  spwi807  => 0 // summon spells should be 0; Summon Fiend
  spwi905  => 0 // summon spells should be 0; Gate
  spwi910  => 9 // (imprisonment): visual effects at power 8
  spwi922  => 9 // (dragon's breath): all effects at power 0
  spwi923  => 0 // summon spells should be 0; Summon Planetar
  spwi924  => 0 // summon spells should be 0; Summon Dark Planetar
  spwi925  => 9 // (comet): knockback at power 0, all others power 9
  spwish18 => 0 // summon spells should be 0; Summon Dark Planetar
  spwm154  => 0 // summon spells should be 0; Cacofiend
  sumslay  => 0 // summon spells should be 0; Summon Slayer Shadow
//  spin534  => 0 // summon spells should be 0; Call Dark Horde, unused
//  spin548  => 0 // summon spells should be 0; Gate, power is 0, unused
//  spin549  => 0 // summon spells should be 0; Summon the Infernal Host, power is 0
//  spin569  => 0 // summon spells should be 0; Summon Ice Salamander, power is 0
//  spin570  => 0 // summon spells should be 0; Summon Fire Elemental, power is 0, unused
//  spin631  => 0 // summon spells should be 0; Construct, unused
//  spin637  => 0 // summon spells should be 0; Guile, unused
//  spin638  => 0 // summon spells should be 0; Triumph, unused
//  spin677  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin730  => 0 // summon spells should be 0; Summon Fungus, power is 0
//  spin735  => 0 // summon spells should be 0; Wish, power is 0
//  spin841  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin842  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin843  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin844  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin845  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0, unused
//  spin855  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0
//  spin856  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0
//  spin857  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0
//  spin870  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0
//  spin880  => 0 // summon spells should be 0; <Invalid Strref -1>, power is 0
//  spra304  => 0 // summon spells should be 0; Animal summoning I, power is 0
//  spwi722  => 0 // summon spells should be 0; Limited Wish, power is 0

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = fix END
      BUT_ONLY IF_EXISTS

END

// spells using wrong power level (custom for dispel magic spells)
COPY_EXISTING ~spin866.spl~  ~override~ // FORCE_DISPEL_MAGIC
              ~sppr303.spl~  ~override~ // (divine dispel magic) : all opcodes are power 0 except its visual effect and removing feeblemind, which are at 3. To work v. magic protections, all of these should be 0.
              ~spwi302.spl~  ~override~ // (remove magic): visual effect at power 3, all others at 0. Should add removal of feeblemind to this spell.
              ~spwi326.spl~  ~override~ // (arcane dispel magic) : all opcodes are power 0 except its visual effect and removing feeblemind, which are at 3. To work v. magic protections, all of these should be 0.
              ~sw2h10dm.spl~ ~override~ // dispel magic (unused)
  PATCH_FOR_EACH op IN 58 77 215 240 BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = op resist_dispel = 0 power = 0 END // dispel, cure feeblemind stuff
  END
  BUT_ONLY

// massive number of spells should bypass MR and don't
COPY_EXISTING ~spin765.spl~ ~override~ // hell_lose xp
              ~spin766.spl~ ~override~ // hell_lose_dex
              ~spin767.spl~ ~override~ // hell_lose_hp
              ~spin768.spl~ ~override~ // hell_dispell [sic]
              ~spin770.spl~ ~override~ // hell_explode
              ~spin771.spl~ ~override~ // hell_damage_half
              ~spin772.spl~ ~override~ // hell_fear
              ~spin923.spl~ ~override~ // City of Caverns party heal before ettin and Illithid/Ularthid heal failed due to poor MR checks: City of Caverns party heal
              ~spin958.spl~ ~override~ // City of Caverns party heal before ettin and Illithid/Ularthid heal failed due to poor MR checks: Illithid/Ularthid heal
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 resist_dispel = 0 END // no dispel/bypass MR
  BUT_ONLY

// improved invisibility and its %$^%&$# saves
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_imp_invis BEGIN

  spdr401 => spdr401a // Invisible Stalker Improved Invisibility
  spin687 => spin687a // Create Shadows
  spin698 => spin698a // Cerebus Improved Invisibility
  spwi405 => spwi405a // improved invis (mage)
  spwi505 => spwi505a // shadow door (mage)
  spwi607 => spwi607a // Mislead
  spwi721 => spwi721a // mass invisibility

END

ACTION_IF tob_game THEN BEGIN // add tob-only spells to array

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_imp_invis BEGIN
  
    balth10 => balth10a // Shadow Stance!
    spin544 => spin544a // PSIONIC_SUPERIOR_INVISIBILITY
  
  END

END

ACTION_PHP_EACH cd_imp_invis AS spell => subspell BEGIN

  COPY_EXISTING ~%spell%.spl~ ~override/%subspell%.spl~ // first make shell spells containing just the saves
    WRITE_LONG 0x08 "-1" // blank name
    WRITE_LONG 0x0c "-1" // blank name
    PATCH_FOR_EACH op IN 139 215 0 141 174 236 33 34 35 36 37 69 BEGIN // delete all non-invisibility effects, including saves if present
      LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = op END
    END
    LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 20 match_parameter2 = 0 END // delete regular invis if present
    LPF ALTER_HEADER INT_VAR projectile = 1 END // remove projectile
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 20 opcode = 33 parameter1 = 4 parameter2 = 0 END // change invis into save vs. death
    FOR (index = 34 ; index < 38 ; ++index) BEGIN
      LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 33 opcode = index END // clone save vs. death into four other saves
    END
    BUT_ONLY
    

  COPY_EXISTING ~%spell%.spl~ ~override~ // improved invisibility is missing +4 save bonuses and should not be able to stack with itself
    // first delete saves if present (moved/added to subspell)
    FOR (index = 33 ; index < 38 ; ++index) BEGIN
      LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = index END // delete saves if present
    END
    // next cast the *a.spl as first effect to get the saves active
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 20 power = 0 match_parameter2 = 1 // clone imp. invisibility effect into...
      opcode = 146 parameter1 = 0 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = EVAL ~%subspell%~ insert = ~first~ END // instant cast of save subspell
    // next, add protections against ALL *a.spl spells to prevent cross-stacking
    PATCH_PHP_EACH cd_imp_invis AS spell2 => subspell2 BEGIN
      LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 20 match_parameter2 = 1 // clone imp. invisibility effect into...
        opcode = 206 parameter1 = 0 STR_VAR resource = EVAL ~%subspell2%~ insert = ~last~ END // prevents all save spells from applying at end of effect stack
    END
    BUT_ONLY

END

// many spells stack with themselves and should not
COPY_EXISTING ~dgfaith.spl~  ~override~ // armor of faith (abazigal)
              ~dgright.spl~  ~override~ // righteous magic (abazigal)
              ~spcl423.spl~  ~override~ // assassin poison weapon ability
              ~spcl907.spl~  ~override~ // hardiness hla
              ~spcl913.spl~  ~override~ // evasion hla
              ~spcl914.spl~  ~override~ // greater evasion hla
              ~spcl917.spl~  ~override~ // avoid death hla
              ~spin943.spl~  ~override~ // blur (air mephit) (65, 33-37, 0)
              ~sppr111.spl~  ~override~ // armor of faith (priest)
              ~sppr113.spl~  ~override~ // doooooooooooooom
              ~sppr210.spl~  ~override~ // resist fire/cold
              ~sppr306.spl~  ~override~ // protection from fire (priest)
              ~sppr406.spl~  ~override~ // defensive harmony
              ~sppr412.spl~  ~override~ // holy power
              ~sppr513.spl~  ~override~ // righteous magic (priest)
              ~spwi107.spl~  ~override~ // friends: special case, handled directly in its own patch // not anymore
              ~spwi201.spl~  ~override~ // blur (wizard)
              ~spwi209.spl~  ~override~ // luck
              ~spwi214.spl~  ~override~ // strength
              ~spwi319.spl~  ~override~ // protection from fire (wizard)
              ~spwi320.spl~  ~override~ // protection from cold
              ~spwi603.spl~  ~override~ // tenser's transformation
              ~spwi702.spl~  ~override~ // protection from the elements
              ~spwi703.spl~  ~override~ // projected image
              ~spwish12.spl~ ~override~ // hardiness via wish
  SET op = 142
  PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "spin943" = 0) OR
            ("%SOURCE_RES%" STRING_COMPARE_CASE "spwi201" = 0)) BEGIN SET op = 65 END // has no portrait icon
  PATCH_IF  ("%SOURCE_RES%" STRING_COMPARE_CASE "spwi703" = 0) BEGIN SET op = 236 END // has no portrait icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = op opcode = 206 parameter1 = RESOLVE_STR_REF (@114) parameter2 = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = ~last~ END
  BUT_ONLY IF_EXISTS

// scriptable spells
COPY_EXISTING ~spcl232.spl~ ~override~ // true sight
              ~spcl412.spl~ ~override~ // set snare
              ~spcl414.spl~ ~override~ // set special snare
              ~spcl621.spl~ ~override~ // summon spirit animal
              ~spcl721.spl~ ~override~ // storm shield
              ~spcl722.spl~ ~override~ // lightning bolt
              ~spcl731.spl~ ~override~ // seeking sword
              ~spcl732.spl~ ~override~ // true sight
              ~spcl741.spl~ ~override~ // boon of lathander
              ~spcl742.spl~ ~override~ // hold undead
              ~spcl910.spl~ ~override~ // set spike trap
              ~spcl911.spl~ ~override~ // set exploding trap
              ~spcl912.spl~ ~override~ // set time trap
              ~spcl923.spl~ ~override~ // summon deva
              ~spin683.spl~ ~override~ // figurine_spider_web
              ~spin696.spl~ ~override~ // moon_dog_howl
              ~spin697.spl~ ~override~ // haste_self
              ~spin698.spl~ ~override~ // non_detection_self
              ~spin891.spl~ ~override~ // moon_dog_fear
  WRITE_LONG 0x34 1
  BUT_ONLY IF_EXISTS

// Non-magical innate abilities shouldn't be affected by Wild Magic and Dead Magic zones (aVENGER)
COPY_EXISTING ~spcl152.spl~  ~override~ // Barbarian Rage
              ~spcl211.spl~  ~override~ // Paladin Lay On Hands
              ~spcl321d.spl~ ~override~ // Berserker Enrage secondary effect (becoming winded)
              ~spcl611.spl~  ~override~ // Druid Shapeshift Brown Bear
              ~spcl612.spl~  ~override~ // Druid Shapeshift Wolf
              ~spcl613.spl~  ~override~ // Druid Shapeshift Black Bear
              ~spcl632.spl~  ~override~ // Avenger Shapeshift Spider
              ~spcl633.spl~  ~override~ // Avenger Shapeshift Baby Wyvern
              ~spcl634.spl~  ~override~ // Avenger Shapeshift Fire Salamander
              ~spcl643.spl~  ~override~ // Shapeshifter Werewolf Transformation
              ~spcl644.spl~  ~override~ // Shapeshifter Greater Werewolf Transformation
              ~spcl815.spl~  ~override~ // Monk Lay On Hands
              ~sppr731.spl~  ~override~ // Druid Fire Elemental Transformation
              ~sppr732.spl~  ~override~ // Druid Earth Elemental Transformation
  WRITE_BYTE 0x19 (THIS BOR BIT6) // add the Non-Magical flag aka ignore wild/dead magic
  BUT_ONLY IF_EXISTS

// casting speed
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spcl144 => 0 // kensai kai -- non-interruptible innates
  spcl152 => 0 // barbarian rage -- non-interruptible innates
  spcl321 => 0 // berserker enrage -- non-interruptible innates
  spin104 => 1 // innate lmd
  spin117 => 0 // minsc berserk -- non-interruptible innates
  sppr104 => 2 // detect evil
  sppr113 => 9 // doooooooooooooom
  sppr214 => 2 // duhm divine
  sppr301 => 9 // animate dead - divine
  sppr302 => 9 // call lightning
  sppr305 => 5 // hold animal
  sppr309 => 8 // invisibility purge
  sppr310 => 5 // miscast magic
  sppr311 => 5 // rigid thinking
  sppr403 => 5 // free action
  sppr502 => 8 // cure crit wounds
//  sppr506 => 0 // iron skins
  sppr608 => 9 // harm
  sppr711 => 7 // regeneration
  sppr717 => 5 // creeping doom
  sppr720 => 9 // earthquake
  sppr723 => 9 // elemental summoning
  sppr724 => 9 // greater elemental summoning
  spwi101 => 1 // grease
  spwi102 => 9 // armor
  spwi123 => 9 // find familiar
  spwi124 => 5 // nahal's reckless dweomer
  spwi208 => 9 // know alignment
  spwi211 => 2 // melf's acid arrow
  spwi214 => 9 // strength
  spwi217 => 3 // aganazzar's scorcher
  spwi309 => 4 // mon summoning i
  spwi402 => 1 // dimension door
  spwi408 => 1 // stoneskin
  spwi420 => 9 // minor sequencer
  spwi501 => 9 // animate dead - arcane
  spwi504 => 5 // mon summoning iii
  spwi505 => 2 // shadow door
  spwi511 => 2 // protection from normal weapons
  spwi517 => 6 // protection from acid
  spwi522 => 5 // minor spell turning
  spwi608 => 6 // pierce magic
  spwi609 => 8 // true sight
  spwi617 => 9 // contingency
  spwi710 => 9 // spell sequencer
  spwi723 => 7 // improved chaos shield
  spwi802 => 6 // spell deflection
  spwi809 => 9 // spell trigger
  spwi811 => 9 // symbol fear (mage)
  spwi816 => 9 // symbol stun (mage)
  spwi817 => 9 // symbol death (mage)
  spwi897 => 9 // symbol death (enemy mage version)
  spwi898 => 9 // symbol stun (enemy mage version)
  spwi899 => 9 // symbol fear (enemy mage version)
  spwm123 => 9 // symbol fear (wild magic)

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      LPF ALTER_HEADER INT_VAR silent = 1 speed = fix END // speed on all headers
      BUT_ONLY IF_EXISTS

END

// range
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spdr101 => 90 // chromatic orb (avenger)
  spin104 => 90 // larloch's minor drain (innate)
  spin685 => 60 // magic missile (imoen/promenade cutscene version)
  spin937 => 30 // color spray (mephit innate)
  spin962 => 60 // magic missile (beholder version)
  sppr203 =>  0 // chant
  sppr703 => 30 // gate (divine)
  spwi003 => 60 // magic missile (trap wizard version)
  spwi101 => 30 // grease
  spwi103 =>  5 // burning hands
  spwi105 => 30 // color spray (arcane)
  spwi106 => 30 // blindness
  spwi111 => 30 // infravision
  spwi112 => 60 // magic missile (regular wizard version)
  spwi116 => 90 // sleep
  spwi118 => 90 // chromatic orb (arcane)
  spwi119 => 90 // larloch's minor drain (arcane)
  spwi125 => 30 // spook
  spwi313 => 30 // skull trap
  spwi409 => 30 // contagion
  spwi503 => 12 // cone of cold
  spwi905 => 30 // gate (arcane)

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      LPF ALTER_HEADER INT_VAR silent = 1 range = fix END // speed on all headers
      BUT_ONLY

END

// casting animation
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  keldorn =>  0 // hallowed redeemer feedback, none
  spwi114 => 15 // shield, evocation
  spwi124 => 15 // nahal's reckless dweomer, evocation
  spwi222 => 12 // chaos shield, abjuration
  spwi409 =>  9 // contagion, necromancy
  spwi424 => 16 // farsight, divination
  spwi505 => 13 // shadow door, illusion
  spwi515 => 16 // oracle, divination
  spwi723 => 12 // improved chaos shield, abjuration
  spwi818 => 15 // bigby's clenched fist, evocation
  spwi918 => 15 // bigby's crushing hand, evocation

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x22 fix
      BUT_ONLY IF_EXISTS

END

// primary school
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spin658 =>  0 // hell_buffet, schoolless
  spin695 =>  0 // dragon_wing_buffet, schoolless
  spin765  => 0 // hell_lose xp
  spin766  => 0 // hell_lose_dex
  spin767  => 0 // hell_lose_hp
  spin768  => 0 // hell_dispell [sic]
  spin769 =>  0 // hell_hold, schoolless
  spin770  => 0 // hell_explode
  spin771  => 0 // hell_damage_half
  spwi124 =>  6 // nahal's reckless dweomer, invoker
  spwi721 =>  5 // mass invisibility, illusion
  spwi920 =>  0 // hla: energy blades, schoolless
  spwi921 =>  0 // hla: improved alacrity, schoolless
  spwi922 =>  0 // hla: dragon's breath, schoolless
  spwi923 =>  0 // hla: summon planetar, schoolless
  spwi924 =>  0 // hla: summon dark planetar, schoolless
  spwi925 =>  0 // hla: comet, schoolless

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x25 fix
      BUT_ONLY IF_EXISTS

END

// secondary type
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  keldorn =>  2 // hallowed redeemer feedback, specific protections
  spin658 =>  0 // hell_buffet, none
  spin695 =>  0 // dragon_wing_buffet, none
  spin765  => 0 // hell_lose xp
  spin766  => 0 // hell_lose_dex
  spin767  => 0 // hell_lose_hp
  spin768  => 0 // hell_dispell [sic]
  spin769 =>  0 // hell_hold, none
  spin770  => 0 // hell_explode
  spin771  => 0 // hell_damage_half
  spwi920 =>  0 // hla: energy blades, none
  spwi921 =>  0 // hla: improved alacrity, none
  spwi922 =>  0 // hla: dragon's breath, none
  spwi923 =>  0 // hla: summon planetar, none
  spwi924 =>  0 // hla: summon dark planetar, none
  spwi925 =>  0 // hla: comet, none

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x27 fix
      BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// other spell fixes                                \\\\\
/////                                                  \\\\\

// abazigal hurts himself when revelaing his dragon form due to bad targeting
COPY_EXISTING ~abzaway.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 target = 8 END // damage, target: everyone except self
  BUT_ONLY IF_EXISTS

// resurrection(s) trying to remove incorrect spell resource
COPY_EXISTING ~bhaal4a.spl~  ~override~
              ~spja01.spl~   ~override~
              ~sppr504.spl~  ~override~
              ~sppr712.spl~  ~override~
              ~sppr729.spl~  ~override~
              ~spwish10.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 172 STR_VAR match_resource = ~spwi126~ resource = ~spin126~ END // spwi126 > spin126 (baby wyvern)
  BUT_ONLY IF_EXISTS
  
// armor of faith not covering acid, cold, fire, electricity, doubles against missiles
COPY_EXISTING ~dgfaith.spl~ ~override~
              ~sppr111.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 89 multi_match = 1 opcode = 27 END // change the dupe missile resistance to acid resistance
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 89 multi_match = 1 opcode = 28 END // clone missile resistance to cold resistance
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 89 multi_match = 1 opcode = 29 END // clone missile resistance to electricity resistance
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 89 multi_match = 1 opcode = 30 END // clone missile resistance to fire resistance
  BUT_ONLY IF_EXISTS

// dragon breath attacks missing vvc to play over victims
COPY_EXISTING ~drgrbrht.spl~ ~override~
              //~spin691.spl~  ~override~ refs bam just fine
              //~spin893.spl~  ~override~ refs bam just fine
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ~spgdraim~ resource = ~spsdimpa~ END // missing to existant resref
//  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ~spbdimsp~ resource = ~spsdimpa~ END // resource exists, no change
  BUT_ONLY IF_EXISTS

// hallowed redeemer feedback should not summon CWs
COPY_EXISTING ~keldorn.spl~ ~override~
  WRITE_SHORT 0x1c 2 // priest spell
  WRITE_BYTE  0x1e (THIS BAND `BIT6) // removes abjurer flag to match regular fireshield
  BUT_ONLY

// mislead clones can attack with an offhand weapon that adds to ApR, i.e. Belm
COPY_EXISTING ~mislead.spl~ ~override~
//  LPF ADD_SPELL_EFFECT INT_VAR type = 1 target = 1 duration = 120 opcode = 1 parameter1 = 0 parameter2 = 1 END // set 0
  LPF ADD_SPELL_EFFECT INT_VAR type = 1 target = 1 duration = 120 opcode = 1 parameter1 = 0 parameter2 = 2 END // set % to 0
  LPF ADD_SPELL_EFFECT INT_VAR type = 1 target = 1 duration = 120 opcode = 1 parameter1 = "-10" parameter2 = 0 END // increment -10

// projected images and simulcra shouldn't have slayer and pocket plane abilities
COPY_EXISTING ~projimag.spl~ ~override~
              ~simulacr.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 1 insert_point = 0 STR_VAR resource = spin822 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 1 insert_point = 0 STR_VAR resource = spin649 END
  BUT_ONLY

// tob spectator's geas missing sound
COPY_EXISTING ~senbehkd.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 target = 1 END // targeted elsewhere
  BUT_ONLY IF_EXISTS
  
// fixes exploit of casting restore on a simulcrum
COPY_EXISTING ~simulacr.spl~ ~override~
  PATCH_FOR_EACH spell IN spwish46 spwish07 sppr713 sppr417 BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 timing = 1 STR_VAR resource = EVAL ~%spell%~ END
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 101 parameter2 = 224 target = 1 timing = 1 END // immunity to restoration
  BUT_ONLY

// miscast effects not accompanied by miscast icon part 1/2 (see spin531.spl et al)
COPY_EXISTING ~spcl132.spl~  ~override~ // 25%, spell failure icon
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 match_parameter2 = 83 parameter2 = 105 END // change spell failure icon to miscast magic
  BUT_ONLY

// kensai weapon speed
COPY_EXISTING ~spcl143.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 4 last_missing = 4 END // makes header at level 4
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 0 level_cap = 36 step_size = 4 END // now extend to headers 8, 12, ... 36 (at level 36, speed drop makes even the slowest weapon 0--no need to go beyond)
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 190 parameter1 = (index + 1) END
  END
  BUT_ONLY

// kit innate speed increases should not be prevented by free action
COPY_EXISTING ~spcl151.spl~ ~override~ // barbarian
              ~spcl812.spl~ ~override~ // monk i
              ~spcl813.spl~ ~override~ // monk ii
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 126 opcode = 176 END // change movement speed opcode
  BUT_ONLY

// barbarian rage should be granting bonus to save v spell, not penalty
// removing stun icons & effects; see fx batches for the rest
COPY_EXISTING ~spcl152.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 37 parameter1 = 2 END // should be +2 bonus, not -2 penalty
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 37 opcode =  46 parameter1 = 0                 timing = 1 duration = 0 END // clone save bonus into unstun
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 37 opcode = 240 parameter1 = 0 parameter2 = 55 timing = 1 duration = 0 END // clone save bonus into remove stun icon
  BUT_ONLY

// fix MR-bypass of detect evil spells by using shell spell; fix range
COPY_EXISTING ~spcl212.spl~ ~override~ // detect evil (paladin innate)
              ~spin120.spl~ ~override~ // detect evil (bhaalspawn innate)
              ~spin696.spl~ ~override~ // moon dog sight
              ~sppr104.spl~ ~override~ // detect evil (cleric spell)
              ~spwi202.spl~ ~override~ // detect evil (mage spell)
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 115 opcode = 146 target = 2 parameter1 = 0 parameter2 = 1 STR_VAR resource = cddetevl END // change detect evil to shell spell
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "spin696") BEGIN   // adjust for everything except moon dog sight
    LPF ALTER_HEADER INT_VAR target = 5 projectile = cddetevl END // adjust to smaller, sight-ranged projectile
  END
  BUT_ONLY

// cavalier resist fear
COPY_EXISTING ~spcl222.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 resist_dispel = 3 END // could fail due to MR
  BUT_ONLY

// enrage's effects misordering prevents immunity to level drain strings; missing listed immunity to stun
COPY_EXISTING ~spcl321.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 STR_VAR match_resource = ~spcl321~ insert = ~last~ END // clone self-blocking spell to last effect
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 opcode = 240 parameter2 = 55 multi_match = 1 STR_VAR match_resource = spcl321 END // change first self-blocking spell to remove stun icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 169 match_parameter2 =  79 parameter2 = 55 END // clone prevent imprisonment icon to prevent stun icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 = 211 parameter2 = 45 END // clone imprisonment immunity to stun immunity
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 = 211 opcode = 46 timing = 1 duration = 0 END // clone imprisonment immunity to unstun
  BUT_ONLY

// enrage cooldown--suppress name from dialogue window and making THAC0 penalty appear in the window
COPY_EXISTING ~spcl321d.spl~ ~override~
  WRITE_LONG NAME1 0xffffffff // blank name
  WRITE_LONG NAME2 0xffffffff
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 STR_VAR match_resource = hitwindd END // delete eff file to apply directly
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 54 target = 1 parameter1 = "-2" duration = 30 END // using this since ALTER/CLONE won't take negative parameters
  BUT_ONLY
  
// all versions of ff should set variable
COPY_EXISTING ~spcl342.spl~ ~override~
  LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 265 target = 1 parameter1 = 1 timing = 1 STR_VAR resource = tutfam01 END
  BUT_ONLY

// otilukes--use shell spell so all effects get applied; see also spwi413a-c.spl, spcl415a-c.spl
COPY_EXISTING ~spcl415.spl~ ~override~ // otiluke's sphere (special snare)
              ~spwi413.spl~ ~override~ // otiluke's sphere (arcane)
  WRITE_BYTE 0x19 (THIS & `BIT2) // removes break invis flag
  SET header_match = 3 // special snare
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "spwi413" = 0) BEGIN
    SET header_match = 1 // arcane spell
  END
  LPF DELETE_EFFECT INT_VAR header = (header_match - 1) END // why do DELETE_EFFECT and ADD_SPELL_EFFECT count headers differently? because screw you, that's why.
  PATCH_IF header_match = 1 BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR header = header_match opcode = 206 target = 1           parameter1 = 0xffffffff duration = 1 STR_VAR resource = EVAL ~%SOURCE_RES%a~ END // self immunity to spell a
    LPF ADD_SPELL_EFFECT INT_VAR header = header_match opcode = 206 target = 1           parameter1 = 0xffffffff duration = 1 STR_VAR resource = EVAL ~%SOURCE_RES%b~ END // self immunity to spell b
  END
  LPF ADD_SPELL_EFFECT INT_VAR header = header_match opcode = 146 target = 2 power = 4 parameter2 = 1 timing = 1 resist_dispel = 1 savingthrow = 1 STR_VAR resource = EVAL ~%SOURCE_RES%a~ END // cast shell spell (non-self)
  PATCH_IF header_match = 1 BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR header = header_match opcode = 146 target = 2           parameter2 = 1 timing = 1 resist_dispel = 3                 STR_VAR resource = EVAL ~%SOURCE_RES%b~ END // non-self immunity to spell c
    LPF ADD_SPELL_EFFECT INT_VAR header = header_match opcode = 146 target = 2           parameter2 = 1 timing = 1 resist_dispel = 3                 STR_VAR resource = EVAL ~%SOURCE_RES%c~ END // cast shell spell (self)
  END
  BUT_ONLY

// 109 targeting issue
// handle separately since it's on its own specific header (paralyze targeting)
COPY_EXISTING ~spcl415.spl~ ~override~ // target 2, power 3, 3/1, dur 30, spell -1 (min lev 11)
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 match_opcode = 142 END
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 match_opcode = 215 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 3 parameter1 = 1 parameter2 = 3 resist_dispel = 1
    timing = 1 savingthrow = BIT0 savebonus = "-1" header = 2 STR_VAR resource = cdheld END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 3 parameter1 = 1 parameter2 = 3 resist_dispel = 1
    duration = 30 savingthrow = BIT0 savebonus = "-1" header = 2 STR_VAR resource = cdhda30 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 3 parameter1 = 1 parameter2 = 3 resist_dispel = 1
    duration = 30 savingthrow = BIT0 savebonus = "-1" header = 2 STR_VAR resource = cdhdb30 END

// assassin and skald thac0 bonuses not displaying in character record
COPY_EXISTING ~spcl421.spl~  ~override~
              ~spcl541.spl~  ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 opcode = 278 parameter1 = 1 parameter2 = 0 STR_VAR resource = ~~ END // change from thac0 via eff to direct 278 opcode
  BUT_ONLY

// offensive spin fixes (haste > movement bonus + APR)
// MMM APR fixes; shuffle APR to shell spell for easier blockage (see also spcl521d.spl, spcl741.spl, spcl741d.spl, melfmet.itm)
COPY_EXISTING ~spcl521.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 opcode =  54 parameter1 = 2 parameter2 = 0 STR_VAR resource = ~~ END // change from thac0 via eff to direct 54 opcode
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  16 opcode = 101 parameter2 = 16 END // change haste to immunity: haste
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  65 opcode = 126 parameter1 = 200 parameter2 = 2 END // clone blur into movement speed: 200%
  PATCH_IF !tobex_game BEGIN // skip this song and dance if TobEx is present
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode =  65 opcode = 146 parameter1 = 0 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = spcl521d END // clone blur into spell for APR
  END ELSE BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode =   1 target = 1 parameter1 =    1 duration = 24 resist_dispel = 2 END // +1 apr
  END
  BUT_ONLY

ACTION_IF !tobex_game BEGIN // skip this song and dance if TobEx is present

  // offensive spin sub-spell (needed for MMM/spin conflicts)
  // MMM APR fixes; shuffle APR to shell spell for easier blockage (see also spcl521.spl, spcl741.spl, spcl741d.spl, melfmet.itm)
  COPY_EXISTING ~spcl521.spl~ ~override/spcl521d.spl~
    SAY NAME1 #-1
    SAY NAME2 #-1 // erase the name of the secondary spells to prevent feedback duplication
    WRITE_ASCIIT  0x10   ~~  // completion sound
    WRITE_SHORT   0x22  0x00 // casting graphics
    LPF DELETE_EFFECT END // delete all existing effects
    LPF ADD_SPELL_EFFECT INT_VAR opcode =   1 target = 1 parameter1 =    1 duration = 24 resist_dispel = 2 END // +1 apr
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = "-1" duration = 24 resist_dispel = 2 STR_VAR resource = spwi325 END // block MMM
    BUT_ONLY

END

// defensive spin fixes
COPY_EXISTING ~spcl522.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 126 opcode = 176 END // change to non-blockable movement rate set
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 opcode = 101 parameter2 = 126 STR_VAR match_resource = spcl522 END // insert immunity to movement rate change right before self-immunity
  BUT_ONLY

ACTION_IF !tobex_game BEGIN // skip this song and dance if TobEx is present

  // MMM APR fixes; shuffle APR to shell spell for easier blockage (see also spcl521.spl, spcl521d.spl, spcl741.spl, melfmet.itm)
  COPY_EXISTING ~spcl741.spl~ ~override/spcl741d.spl~
    LPF DELETE_EFFECT END // delete all existing effects
    SAY NAME1 #-1 
    SAY NAME2 #-1 // erase the name of the secondary spells to prevent feedback duplication
    WRITE_ASCIIT  0x10   ~~  // completion sound
    WRITE_SHORT   0x22  0x00 // casting graphics
    READ_SHORT 0x68 abil_num
    FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR header = index opcode =   1 target = 1 parameter1 =    1 duration = (54 + (6 * index)) power = 4 resist_dispel = 3 END // +1 apr
      LPF ADD_SPELL_EFFECT INT_VAR header = index opcode = 206 target = 1 parameter1 = "-1" duration = (54 + (6 * index)) power = 4 resist_dispel = 3 STR_VAR resource = spwi325 END // block MMM
    END
    BUT_ONLY
  
  // MMM APR fixes; shuffle APR to shell spell for easier blockage (see also spcl521.spl, spcl521d.spl, spcl741d.spl, melfmet.itm)
  COPY_EXISTING ~spcl741.spl~  ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 1 opcode = 146 parameter1 = 0 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = spcl741d END // change apr to sub-spell with APR
    BUT_ONLY

END

// monk weapon speed
COPY_EXISTING ~spcl816.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 12 last_missing = 12 END // add level 12 header
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 190 parameter1 = (index + 1) END // set speed bonuses
  END
  BUT_ONLY

// greater/deathblow shouldn't be using quivering palm eff (allows spurious save); see also spcl902a.eff and spcl903a.eff
COPY_EXISTING ~spcl902.spl~ ~override~ // deathblow
              ~spcl903.spl~ ~override~ // greater deathblow
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 248 STR_VAR match_resource = ~quivvis~ resource = EVAL "%SOURCE_RES%a" END // change eff file
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 249 STR_VAR match_resource = ~quivvis~ resource = EVAL "%SOURCE_RES%a" END // change eff file
  BUT_ONLY IF_EXISTS

// power attack not displaying string on ranged attacks
COPY_EXISTING ~spcl906.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 248 opcode = 249 STR_VAR match_resource = ~spcl906b~ END // add ranged effect call to spcl906b
  BUT_ONLY IF_EXISTS

// hardiness missing high level headers
COPY_EXISTING ~spcl907.spl~  ~override~ // hardiness hla
              ~spwish12.spl~ ~override~ // hardiness via wish
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_duration = 60 duration = 54 END
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 0 step_dur = 3 step_size = 2 level_cap = 30 min_dur = 5 END // extends headers to level 30
  BUT_ONLY IF_EXISTS

// Prevent Enhanced Bard Song from reverting back to the default version after bard characters leave and rejoin the party (aVENGER)
COPY_EXISTING ~spcl920.spl~ ~override~     // Enhanced Bard Song
  PATCH_FOR_EACH spell IN spcl542 spcl751 fjbard fjblade BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 9 STR_VAR resource = EVAL ~%spell%~ END
  END
  BUT_ONLY IF_EXISTS

// ranger tracking
COPY_EXISTING ~spcl922.spl~  ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 171 opcode = 172 END // clone gain ability into remove ability
  BUT_ONLY IF_EXISTS

// chromatic orb has a few incorrect targets and MR checks
// Chromatic Orb is supposed to cause blindness at level one but instead causes -4 penalties to AC, THAC0, and saves. (CamDawg)
COPY_EXISTING ~spdr101.spl~ ~override~ // chromatic orb, stalker version
              ~spwi118.spl~ ~override~ // chromatic orb, wiz version
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 34 END // delete save vs. wand from level one header
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 35 END // delete save vs. polymorph from level one header
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 36 END // delete save vs. breath from level one header
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 37 END // delete save vs. spell from level one header
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 0 match_duration = 10 duration =  6 END // fix level 1 durations
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode =  0 opcode = 74 target = 2 END // fix level 1 ac penalty target, change to blindness
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 54 opcode = 142 parameter2 = 8 END // change level 1 thac0 penalty to blindness icon
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 33 opcode = 139 parameter1 = 14674 timing = 1 duration = 0 END // change level 1 save vs. death to blinded string
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 4 match_duration = 20 duration = 18 END // fix level 5 durations
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 4 match_opcode = 142  duration = 18 power = 1 resist_dispel = 1 END // fix level 5 portrait icon
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 7 match_opcode = 139  resist_dispel = 1 END // level 10 display string
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 header = 8 match_opcode =  55  resist_dispel = 2 END // level 12 slay
  BUT_ONLY

// larloch minor drain fixes, part 1/3 (see second spin104a.spl, spin104.spl)
// primary/secondary, mr, power, etc. should all be checked in the primary spell
COPY_EXISTING ~spin104.spl~ ~override/spin104a.spl~ // innate LMD
              ~spwi119.spl~ ~override/spwi119a.spl~ // mage LMD
  WRITE_LONG NAME1 0xffffffff // blank name
  WRITE_LONG NAME2 0xffffffff
  WRITE_BYTE 0x25 0 // no primary
  WRITE_BYTE 0x27 0 // no secondary
  LPF ALTER_HEADER INT_VAR silent = 1 projectile = 1 END // no projectile
  LPF ALTER_EFFECT INT_VAR silent = 1 match_resist_dispel = 1 resist_dispel = 3 power = 0 END // change to bypass mr
  LPF ALTER_EFFECT INT_VAR silent = 1 match_resist_dispel = 2 resist_dispel = 0 power = 0 END // change to bypass mr
  BUT_ONLY

// larloch minor drain fixes, part 2/3 (see second spin104a.spl, spwi119a.spl, spin104.spl)
// innate LMD should do fixed damage of 4, not 1d4
COPY_EXISTING ~spin104a.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 12 parameter1 = 4 dicenumber = 0 END // from 1d4+0 to 0d4+4
  BUT_ONLY

// vamp touch fixes, part 1/2 (see spin106.spl et al)
// primary/secondary, mr, power, etc. should all be checked in the primary spell
COPY_EXISTING ~spin106.spl~ ~override/spin106a.spl~ // innate vamp touch
              ~spin106.spl~ ~override/spin106b.spl~ // innate vamp touch
              ~spin997.spl~ ~override/spin997a.spl~ // innate vamp touch
              ~spin997.spl~ ~override/spin997b.spl~ // innate vamp touch
              ~spwi314.spl~ ~override/spwi314a.spl~ // mage vamp touch
              ~spwi314.spl~ ~override/spwi314b.spl~ // mage vamp touch
  WRITE_LONG NAME1 0xffffffff // blank name
  WRITE_LONG NAME2 0xffffffff
  WRITE_BYTE 0x25 0 // no primary
  WRITE_BYTE 0x27 0 // no secondary
  LPF ALTER_HEADER INT_VAR silent = 1 projectile = 1 END // no projectile
  PATCH_IF ("%DEST_RES%" STRING_COMPARE_REGEXP "^.+b$") BEGIN // if non-self spell, a
    LPF DELETE_EFFECT INT_VAR silent = 1 match_target = 1 END // delete self-targeted effects
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 resist_dispel = 3 duration = 300 STR_VAR resource = EVAL ~%SOURCE_RES%b~ END
  END ELSE BEGIN // self-spell, b
    LPF DELETE_EFFECT INT_VAR silent = 1 match_target = 2 END // delete preset-targeted effects
    LPF ALTER_EFFECT  INT_VAR silent = 1 match_target = 1 match_opcode = 206 STR_VAR match_resource = spin106 resource = spin106b END // immunity to b-spell, not main
    LPF ALTER_EFFECT  INT_VAR silent = 1 match_target = 1 match_opcode = 206 STR_VAR match_resource = spin997 resource = spin997b END // immunity to b-spell, not main
    LPF ALTER_EFFECT  INT_VAR silent = 1 match_target = 1 match_opcode = 206 STR_VAR match_resource = spwi314 resource = spwi314b END // immunity to b-spell, not main
  END
  // anything that's left should skip MR and power levels
  LPF ALTER_EFFECT  INT_VAR silent = 1 match_resist_dispel = 1 resist_dispel = 3 power = 0 END // change to bypass mr
  LPF ALTER_EFFECT  INT_VAR silent = 1 match_resist_dispel = 2 resist_dispel = 0 power = 0 END // change to bypass mr
  BUT_ONLY

// vamp touch fixes, part 2/2 (see spin106a.spl et al)
// larloch minor drain fixes, part 3/3 (see spin104a.spl x2)
// makes MR-checking "shell" spell that also eliminates self-cast exploit
COPY_EXISTING ~spin104.spl~ ~override~ // innate LMD
              ~spin106.spl~ ~override~ // innate vamp touch
              ~spin997.spl~ ~override~ // innate vamp touch
              ~spwi119.spl~ ~override~ // mage LMD
              ~spwi314.spl~ ~override~ // mage vamp touch
  LPF DELETE_EFFECT INT_VAR silent = 1 END // delete all effects
  READ_SHORT  0x68 abil_num
  PATCH_IF (abil_num > 1) BEGIN
    READ_LONG   0x64 abil_off
    WRITE_LONG  0x6a (THIS - (0x28 * (abil_num - 1))) // update fx offset for deleted ability headers
    WRITE_SHORT 0x68 1 // cut down to one ability
    DELETE_BYTES (abil_off + 0x28) (0x28 * (abil_num - 1)) // delete all but the first ability
  END
  SET sp_power = 3
  PATCH_IF ((~%SOURCE_RES%~ STRING_COMPARE_CASE "spin104" = 0) OR
            (~%SOURCE_RES%~ STRING_COMPARE_CASE "spwi119" = 0)) BEGIN
    SET sp_power = 1
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 power = sp_power parameter2 = 1 timing = 1 resist_dispel = 1 STR_VAR resource = EVAL ~%SOURCE_RES%a~ END // cast spell that does the work
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 insert_point = 0 parameter1 = "-1" STR_VAR resource = EVAL ~%SOURCE_RES%~ END // block self from main spell
  BUT_ONLY

// removing horror's undocumented save bonus
// create unique spinny animation for horror (see cdhorror, fear immunity batches)
COPY_EXISTING ~spin105.spl~  ~override~ // bhaalspawn innate horror
              ~spwi205.spl~  ~override~ // wizard spell horror
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END // no save bonus
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = "spmindat" resource = "cdhorror" END // swap animation
  BUT_ONLY

// summon dread wolf referring to non-existant BG reference
COPY_EXISTING ~spin114.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 67 STR_VAR resource = wolfdr01 END // dread wolf
  BUT_ONLY

/* allow arbitrary return-to-human form from clock of the wuff
// see also clck04, cdwolfm
COPY_EXISTING ~spin122.spl~ ~override~
              ~spin123.spl~ ~override~
              ~spin124.spl~ ~override~
              ~spin150.spl~ ~override~
              ~spin151.spl~ ~override~
              ~spin152.spl~ ~override~
              ~spin153.spl~ ~override~
              ~spin154.spl~ ~override~
              ~spin155.spl~ ~override~
              ~spin156.spl~ ~override~
              ~spin157.spl~ ~override~
              ~spinhum.spl~ ~override~
              ~spwi489.spl~ ~override~
              ~spwi490.spl~ ~override~
              ~spwi491.spl~ ~override~
              ~spwi493.spl~ ~override~
              ~spwi494.spl~ ~override~
              ~spwi495.spl~ ~override~
              ~spwi496.spl~ ~override~
              ~spwi497.spl~ ~override~
              ~spwi498.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 112 target = 1 timing = 1 STR_VAR resource = cdwolfm END */

// abazigal's shockwave miscast effects
COPY_EXISTING ~spin531.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 60 parameter1 = 90 END // was 0% for divine miscast, now 90% to match arcane
  BUT_ONLY IF_EXISTS

// miscast effects not accompanied by miscast icon part 2/2 (see spcl132.spl)
COPY_EXISTING ~spin531.spl~  ~override~ // 90%, abazigal's shockwave
              ~sppr310.spl~  ~override~ // 80%, no icon
              ~sppr319.spl~  ~override~ // 50%, no icon
              ~sppr986.spl~  ~override~ // 80%, no icon
              ~spwish30.spl~ ~override~ // 80%, no icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 60 match_parameter2 = 0 opcode = 142 parameter2 = 105 END // clone arcane spell failure to miscast icon
  BUT_ONLY IF_EXISTS

// hive mother anti-magic ray not affecting divine spellcasting
COPY_EXISTING ~spin550.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 60 parameter1 = 100 END // was 0% for divine miscast, now 100% to match arcane
  BUT_ONLY IF_EXISTS
  
// spell failure effects not accompanied by spell failure icon pt 1/2, see spin689.spl
COPY_EXISTING ~spin550.spl~  ~override~ // 100%, need to fix divine, includes innates
              ~spin646.spl~  ~override~ // 100%, anti-magic zone type
              ~spin712.spl~  ~override~ // 100%, sets attacks to 0
              ~spin731.spl~  ~override~ // 100%, party-wide
//              ~spin779.spl~  ~override~ // 100%, already has spell failure icon
              ~spin992.spl~  ~override~ // 100%, no icon
              ~sppr517.spl~  ~override~ // 100%, no icon
              ~sppr717.spl~  ~override~ // 100%, no icon
              ~spwm128.spl~  ~override~ // 100%, no icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 60 opcode = 142 parameter2 = 83 END // clone spell failure to spell failure icon
  BUT_ONLY IF_EXISTS

// slow shouldn't stack
COPY_EXISTING ~spin575.spl~  ~override~ // vortex web
              ~spin977.spl~  ~override~ // slow (golem)
              ~spin983.spl~  ~override~ // slow (innate)
              ~spwi312.spl~  ~override~ // slow (mage)
              ~spwish25.spl~ ~override~ // slow (wish)
              ~spwm164.spl~  ~override~ // slow (wild surge)
  PATCH_FOR_EACH spell IN spin575 spin977 spin983 spwi312 spwish25 spwm164 BEGIN
    PATCH_IF ("%spell%" STRING_COMPARE_CASE "%SOURCE_RES%") BEGIN // if not a self-reference
      LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 40 opcode = 206 parameter1 = 0 STR_VAR resource = EVAL ~%spell%~ END
    END
  END
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 40 opcode = 206 parameter1 = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = ~last~ END // protection from self goes last
  BUT_ONLY IF_EXISTS

// change old lightning spbehbla.pro spells to new cdbehbla.pro projectile
COPY_EXISTING ~spin579.spl~ ~override~ // power_amp
              ~spin989.spl~ ~override~ // beholder_lightning_bolt
  LPF ALTER_HEADER INT_VAR silent = 1 projectile = cdbehbla END
  BUT_ONLY IF_EXISTS

// imprisoned summon fix: see gender.ids, cdwi917a.spl, cdwi910.eff, cdwi917a.eff, spwi917.spl
COPY_EXISTING ~spin580.spl~ ~override~
              ~spin626.spl~ ~override~
              ~spin788.spl~ ~override~
              ~spwi910.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 211 opcode = 177 parameter1 = 6 parameter2 = 7 silent = 1 STR_VAR resource = ~cdwi910~ insert = ~above~ END
  BUT_ONLY IF_EXISTS

// The beneficial effects of the Deck of Many Things should bypass the user's Magic Resistance and/or Globe of Invulnerability (aVENGER)
COPY_EXISTING ~spin606.spl~ ~override~     // DECK_SUN (Party gains 300,000 Experience Points)
              ~spin607.spl~ ~override~     // DECK_JESTER (The user gains 50,000 Experience Points)
              ~spin609.spl~ ~override~     // DECK_GEM (The user gains several gems)
              ~spin610.spl~ ~override~     // DECK_FATES (The user gains a +1 bonus to all stats for 1 day)
              ~spin611.spl~ ~override~     // DECK_COMET (The user gains a permanent 5% increase to Fire Resistance)
              ~spin618.spl~ ~override~     // DECK_MOON (The user gains a permanent increase of 10 Hit Points)
              ~spin619.spl~ ~override~     // DECK_THRONE (Party gains 1,000,000 Experience Points)
              ~spin621.spl~ ~override~     // DECK_KEY (The user gains a Ring of Protection +3)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 0 END // bypass spell protections
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_resist_dispel = 1 resist_dispel = 2 END // bypass mr
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_resist_dispel = 3 resist_dispel = 2 END // bypass mr
  BUT_ONLY IF_EXISTS

// xp effect not being applied with correct timing
COPY_EXISTING ~spin607.spl~ ~override~
              ~spin640.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 104 timing = 1 END // instant/perm
  BUT_ONLY IF_EXISTS

// talons icon eff has different timing than other effects
COPY_EXISTING ~spin613.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 283 timing = 9 duration = 0 END // one timing is wrong, duration on both
  BUT_ONLY IF_EXISTS

// deck of many things, moon spell
COPY_EXISTING ~spin618.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 18 parameter2 = 0 END // increase max hp w/ current hp increase
  BUT_ONLY IF_EXISTS

// deck of many things, emperor spell
COPY_EXISTING ~spin632.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 101 match_parameter2 = 5 END // purge dupe charm immunity
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 duration = 50400 END // 7-day duration for charm immunity
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 duration = 50400 END // 7-day duration for expiry sound
  BUT_ONLY IF_EXISTS

// associated hold effects playing on non-affected creatures; sppr305 done separately since it is also missing headers and other issues
COPY_EXISTING ~spin648.spl~ ~override~ // target 2, power 1, 3/1, dur 60, no save
              ~spin988.spl~ ~override~ // target 2, power 3, 3/1, dur 60, spell -1
              ~sppr208.spl~ ~override~ // target 2, power 2, 3/1, dur 60, spell 0
              ~sppr989.spl~ ~override~ // target 2, power 3, 3/1, dur 60, spell -1
              ~spwi306.spl~ ~override~ // target 2, power 3, 3/1, dur 60, spell -1
              ~spwm122.spl~ ~override~ // target 2, power 2, 3/1, dur 60, spell 0
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 142 END // delete existing portrait icon
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 215 END // delete existing visual
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 139 END // delete existing string
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 175 opcode = 177 timing = 1 duration = 0 STR_VAR resource = cdheld END // string
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 175 opcode = 177 STR_VAR resource = cdhda60 END // 60 seconds of icon
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 175 opcode = 177 STR_VAR resource = cdhdb60 END // 60 seconds of swirly animation
  BUT_ONLY IF_EXISTS

// familiars and summons should be pushed back when hell door opens, part 2/2 (see cut85a.bcs)
// wing buffet, hell knockback shouldn't be stopped by spell protections (see spin695.spl)
COPY_EXISTING ~spin658.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_target = 2 target = 4 power = 0 END
  BUT_ONLY

// edwina <> edwin fixes
COPY_EXISTING ~spin661.spl~ ~override~
              ~spin662.spl~ ~override~
              ~spin916.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 71 timing = 1 END // timing is wrong
  BUT_ONLY

// Edwin's Nether Scroll bonuses; removes spurious save check on play sound and changes effects to bypass MR
COPY_EXISTING ~spin664.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savingthrow = 0 resist_dispel = 0 END // no save, bypass MR
  BUT_ONLY

// Anomen's title change; effects should be permanent and persist through resurrection
COPY_EXISTING ~spin678.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 timing = 1 END // timing is wrong
  BUT_ONLY

// magic missile damage fix
COPY_EXISTING ~spin685.spl~ ~override~ // Imoen/promenade cutscene version
              ~spin962.spl~ ~override~ // beholder version
              ~spwi003.spl~ ~override~ // trap wizard version
              ~spwi112.spl~ ~override~ // regular wizard version
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 parameter1 = 1 dicesize = 4 dicenumber = 1 END // from 2d2 to 1d4+1
  BUT_ONLY
  
// entangle effects not setting entangle icon
COPY_EXISTING ~spin688.spl~ ~override~
              ~spwm111.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 154 opcode = 142 parameter2 = 144 END // clone entange effect into icon
  BUT_ONLY

// spell failure effects not accompanied by spell failure icon pt 2/2, see spin550.spl et al
COPY_EXISTING ~spin689.spl~  ~override~ // 100%, has miscast
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 match_parameter2 = 105 parameter2 = 83 END // change miscast magic icon to spell failure
  BUT_ONLY

// dragon_silence's lowest level header should be level 1, not 20
COPY_EXISTING ~spin692.spl~ ~override~
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  PATCH_IF (abil_num != 0) BEGIN
    WRITE_SHORT (abil_off + 0x10) 1 // set to level 1
  END
  BUT_ONLY

// wing buffet, hell knockback shouldn't be stopped by spell protections (see spin658.spl)
COPY_EXISTING ~spin695.spl~ ~override~ // wing buffet
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 0 END
  BUT_ONLY

// fix icon for moon dog's true sight
COPY_EXISTING ~spin696.spl~ ~override~
  WRITE_ASCII 0x3a ~SPWI609C~ #8
  LPF ALTER_HEADER INT_VAR silent = 1 STR_VAR icon = SPWI609B END
  BUT_ONLY

// hell spell fixes; change alignment deferred to teardoor and ar2900 scripts and wrong timing on bonuses
COPY_EXISTING ~spin747.spl~ ~override~ // selfish, evil
              ~spin749.spl~ ~override~ // wrath, evil
              ~spin750.spl~ ~override~ // wrath, good
              ~spin751.spl~ ~override~ // pride, evil
              ~spin753.spl~ ~override~ // fear, evil
              ~spin755.spl~ ~override~ // greed, evil
  LPF DELETE_EFFECT INT_VAR match_opcode = 57 END // delete alignment change (now handled via teardoor.bcs, ar2900.bcs)
  PATCH_FOR_EACH op IN 6 10 44 49 104 108 BEGIN // CHR/CON/STR/WIS/XP Bonus, rep change
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = op timing = 1 duration = 0 END // timing: inst/perm
  END
  BUT_ONLY

// Evil hell rewards have some effects with non-zero power levels and bad targeting (Wisp)
COPY_EXISTING ~spin747.spl~ ~override~ // selfish, evil
              ~spin749.spl~ ~override~ // wrath, evil
              ~spin751.spl~ ~override~ // pride, evil
              ~spin753.spl~ ~override~ // fear, evil
              ~spin755.spl~ ~override~ // greed, evil
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 target = 2 power = 0 END
  BUT_ONLY

// hell selfish penalties should be schoolless, bypass MR-- hold is special; needs to be dispellable by demon
COPY_EXISTING ~spin769.spl~ ~override~ // hell_hold
  PATCH_FOR_EACH spell IN cditmdm0 cditmdm1 sppr303 spwi326 spcl231 spin112 spin550 spin646 spin647 spin703 spin712 /* spin768 */ spin779 spin866 spin992 spwi010 spwi302 sw2h10dm // spells, spin768 is HELL_DISPELL (sic)
    arow07 carsomyr deva devaevil elemchan elemcryo elemhydr elemimix elemogre elemsunn elemyanc elemzaan finsol01 misc5c planetar plot04i potn33 ravag01 reaver staf11 sw2h10 sw2h19 BEGIN // items
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 1 resist_dispel = 3 STR_VAR resource = EVAL ~%spell%~ END
  END
  BUT_ONLY

// Using the Psionic Maze ability should display the Maze graphical animation over the target
COPY_EXISTING ~spin774.spl~ ~override~ // Maze (psionic)
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 END // Spell Effect: Play Sound Effect [174]
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 opcode = 215 power = 8 parameter2 = 1 timing = 0 duration = 3 STR_VAR resource = spmaze1 END
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = spmaze1 resource = spmaze2 END
  BUT_ONLY

// slayer change - investigate further
// typo in resource references
COPY_EXISTING ~spin783.spl~ ~override~
              ~spin852.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ~sparmor~ resource = ~spsarmor~ END // typo
  BUT_ONLY

// use new flame strike animation, not the old one
COPY_EXISTING ~spin799.spl~  ~override~ // dace's flame strike
              ~sppr985.spl~  ~override~ // trap flamestrike
              ~spwi979.spl~  ~override~ // sarevok_strike
              ~spwm186.spl~  ~override~ // wild surge charm--was using flame strike anim
  LPF ALTER_HEADER INT_VAR silent = 1 projectile = 1 END // remove old bg FS animation
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 timing = 1 resist_dispel = 1 STR_VAR resource = spflast2 END // play 3d effect
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 timing = 1 resist_dispel = 1 STR_VAR resource = spflsrin END // play 3d effect
  BUT_ONLY

// ployer's curse kills low-hp jaheira, hanging the cutscene
COPY_EXISTING ~spin919.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 208 target = 2 parameter1 = 1 duration = 1 END // brief minimum hp so she survuves
  BUT_ONLY

// color spray fixes - range is fixed elsewhere, removes undocumented save penalty
COPY_EXISTING ~spin937.spl~ ~override~ // mephit version
              ~spwi105.spl~ ~override~ // regular mage version
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END // no save bonus
  BUT_ONLY
  
// cutscene petrification visual effect allows save
COPY_EXISTING ~spin950.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 savingthrow = 0 END // remove save from visual
  BUT_ONLY

// spells to transform trolls back from dead to alive
COPY_EXISTING ~spin955.spl~ ~override/cdtroll1.spl~
              ~spin955.spl~ ~override/drshnl21.spl~
              ~spin955.spl~ ~override/eletro01.spl~
              ~spin955.spl~ ~override/firtrl01.spl~
              ~spin955.spl~ ~override/kptrol13.spl~
              ~spin955.spl~ ~override/obsice01.spl~
              ~spin955.spl~ ~override/pptroll1.spl~
              ~spin955.spl~ ~override/sutroll.spl~
              ~spin955.spl~ ~override/torgal3.spl~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 151 STR_VAR match_resource = ~troll01~ resource = EVAL ~%DEST_RES%~ END
  BUT_ONLY

// MF psionic blast only spell to use spflayer.vvc; changing to use spmindat.vvc--calls same BAM to be played plus sound
// changing this reduces having to add protection from animation: spflayer in a lot of other files
COPY_EXISTING ~spin974.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ~spflayer~ resource = ~spmindat~ END
  BUT_ONLY

// beholder death ray should be subject to MR
COPY_EXISTING ~spin991.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 13 resist_dispel = 1 END // dispel/not bypass
  BUT_ONLY

// typo in spl reference
COPY_EXISTING ~spinhum.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 172 STR_VAR match_resource = ~spinhun~ resource = ~spinhum~ END // typo
  BUT_ONLY IF_EXISTS

// harper's call not causing attribute penalties in descript
COPY_EXISTING ~spja01.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_target = 2 resist_dispel = 0 power = 0 END // ensure resurrected gets all effects
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 1 opcode = 208 target = 2 parameter1 = 1 duration = 1 END // brief minimum hp so that the resurrected can survive the CON drop
  FOR (index = 0 ; index < 6 ; ++index) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 999 opcode = 177 target = 2 parameter1 = 2 parameter2 = 2 duration = 60 STR_VAR resource = EVAL ~cdspja0%index%~ END
  END
  BUT_ONLY

// entangle doesn't have its listed save bonus for target
COPY_EXISTING ~sppr105.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 3 END // save at +3
  BUT_ONLY

// barkskin AC bonus incorrect at higher levels
COPY_EXISTING ~sppr202.spl~ ~override~
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < abil_num ; ++index) BEGIN // not wrong until header 4
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 0 parameter1 = (6 - ((index + 3) / 4)) END // index essentially standing in for level here
  END
  BUT_ONLY

// chant doesn't provide the listed save bonuses nor the penalties to opponents. Also shouldn't stack (handled by sub-spells sppr203d/e.spl in bulk copy).
COPY_EXISTING ~sppr203.spl~  ~override~
  LPF ALTER_HEADER INT_VAR target = 5 projectile = 1 END
  LPF DELETE_EFFECT INT_VAR header = 0 END // purge all effects from header
  PATCH_FOR_EACH res IN sppr203d sppr203e BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = EVAL ~%res%~ END // cast sub-spell
  END
  BUT_ONLY

// resist fire/cold has incorrect duration at level 20; stacking issue already covered below
COPY_EXISTING ~sppr210.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 200 duration = 120 END // only at level 20
  BUT_ONLY
  
// animate dead has several minor errors in effect headers and is missing headers for levels 6-9
COPY_EXISTING ~sppr301.spl~ ~override~
  LPF ALTER_HEADER STR_VAR icon = sppr301b END // spell was copied from arcane animate dead, still uses arcane icon on most headers
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 6 last_missing = 9 END // clones ability 1 and associated effects into missing headers
  READ_SHORT 0x68 abil_num // get updated abil_num
  FOR (index = 0 ; index < (abil_num - 1) ; ++index) BEGIN // now fix percentages (skip last header)
    LPF ALTER_EFFECT INT_VAR silent = 1 header = index check_globals = 0                 match_opcode = 177 power = 0 probability1 = 100                probability2 = ((75 - (index * 5)) + 1) END // x+1 to 100 for two
    LPF ALTER_EFFECT INT_VAR silent = 1 header = index check_globals = 0 multi_match = 1 match_opcode = 177 power = 0 probability1 = (75 - (index * 5)) probability2 = 0 END // 0-x for one skeleton
  END
  LPF ALTER_EFFECT INT_VAR silent = 1 header = (abil_num - 1) check_globals = 0 match_opcode = 177 power = 0 probability1 = 100 probability2 = 0 END // 100% for skeleton warrior on last header
  BUT_ONLY

// hold animal has incorrect casting time and has a spurious save penalty
// associated hold effects playing on non-affected creatures, touchup: handle separately due to durations
COPY_EXISTING ~sppr305.spl~ ~override~
  // get level 1 effects correct
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END // no save bonus
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 175 opcode = 177 timing = 1 duration = 0 STR_VAR resource = cdheld END // clone hold into string
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 175 opcode = 177 STR_VAR resource = cdhda60 END // clone hold into icon
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 opcode = 177 parameter1 = 2 parameter2 = 3 duration = 60 STR_VAR resource = cdhdb60 END // change swirly 215 into eff, fix duration
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 12 min_lev_alt = 5 END // extends headers to level 20, adding 2 rounds of duration every header
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN // now correct eff durations
    SET dur = ((12 * index) + 60)
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index STR_VAR match_resource = "cdhda60" resource = EVAL ~cdhda%dur%~ END
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index STR_VAR match_resource = "cdhdb60" resource = EVAL ~cdhdb%dur%~ END
  END

// remove paralysis not removing icons on target
COPY_EXISTING ~sppr308.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 240 target = 2 END // removing icons on caster, not target
  BUT_ONLY

// holy smite/unholy blight missing level 20 abilities, a few effects on holy smite mistargeted and/or wrong duration at lev 6, 7
// holy smite/unholy blight damage fixes, part 3 (CamDawg)
// instead of doing, say, 12d4 damage (6d4 on save), was doing 24d2 damage (12d2 on save)
// see also sp313l02.eff through sp313l10.eff
COPY_EXISTING ~sppr313.spl~ ~override~ // holy smite
              ~sppr314.spl~ ~override~ // unholy blight
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "sppr313" = 0) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 duration = 6 timing = 0 STR_VAR match_resource = blind END // only wrong at levels 6,7 for holy smite
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 duration = 0 timing = 1 STR_VAR match_resource = hitinvwa END // only wrong at levels 6,7 for holy smite
    SET ex_dur = 6 // blindness is one round for holy smite
  END ELSE BEGIN
    SET ex_dur = 24 // roll penalties are four rounds for unholy blight
  END
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 0 base_dur = ex_dur END // extends headers to level 20
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT (abil_off + 0x10 + (0x28 * index)) level
    READ_SHORT (abil_off + 0x1e + (0x28 * index)) abil_fx_num
    READ_SHORT (abil_off + 0x20 + (0x28 * index)) abil_fx_idx
    PATCH_IF (level = 1) BEGIN SET level = 5 END
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) eff_file
      PATCH_IF ("%eff_file%" STRING_COMPARE_REGEXP "SP313L[012][0-9]" = 0) BEGIN // if damage eff
        READ_LONG (fx_off + 0x24 + (0x30 * (abil_fx_idx + index2))) save // is either 0 ir 1, allows for correct odd level damage
        SET efflev = ((level + save) / 2)
        PATCH_IF (efflev = 10) BEGIN
          WRITE_ASCIIE (fx_off + 0x1a + (0x30 * (abil_fx_idx + index2))) "%efflev%"
        END ELSE BEGIN
          WRITE_ASCIIE (fx_off + 0x1a + (0x30 * (abil_fx_idx + index2))) "0%efflev%"
        END
      END
    END
  END
  BUT_ONLY

// free action misc errors and missing lev 19, 20 headers
// all supplementary effects handled in effects batches below
COPY_EXISTING ~sppr403.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 =   5 opcode = 126 parameter1 = 100 parameter2 = 2 END // change charm immunity to movement rate set to 100%
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 101 match_parameter2 = 185 opcode = 169 parameter2 = 145 target = 2 END // change special hold immunity to prevent grease icon, fix target
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 60 END // extends headers to level 20, adjusts duration as it goes
  BUT_ONLY

// neutralize poison failing to cure disease due to bad target
COPY_EXISTING ~sppr404.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 79 target = 2 END // fix target to preset
  BUT_ONLY

// opcode has wrong target in death ward
COPY_EXISTING ~sppr409.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 282 target = 2 END // fix target to preset
  BUT_ONLY

// holy power missing level 19, 20 abilities
COPY_EXISTING ~sppr412.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 duration = 0 END // removing duration from permanent effects
  LPF CD_EXTEND-O-MATIC END // extends headers to level 20, adjusts duration as it goes
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode =  54 parameter1 = ((8 + (20 - abil_num)) - index) END // thac0 goes down 1/level to 1 at level 20
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode =  18 parameter1 = (index + (21 - abil_num)) END // hp bonus goes up 1/level t0 20 at level 20
  END
  BUT_ONLY

// champion's strength missing level 19,20 headers, wrong durations at levels 17,18
COPY_EXISTING ~sppr507.spl~  ~override~ // champion's strength
  READ_SHORT 0x68 abil_num
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = (abil_num - 2) match_duration = 288 duration = 306 END // one play sound effect (lev 17)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = (abil_num - 2) match_duration = 304 duration = 306 END // all other effects (lev 17)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = (abil_num - 1) match_duration = 288 duration = 324 END // one play sound effect (lev 18)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = (abil_num - 1) match_duration = 322 duration = 324 END // all other effects (lev 18)
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 18 END // extends headers to level 20, adjusts duration as it goes
  BUT_ONLY

// chaotic commands not protecting against psionic maze
COPY_EXISTING ~sppr508.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 STR_VAR match_resource = ~spwi813~ resource = ~spin774~ END // clone arcane maze to psionic maze protection
  BUT_ONLY

// Greater Command should use the same projectile at all levels. In term, the spell will no longer affect the caster's allies at lower levels
COPY_EXISTING ~sppr512.spl~ ~override~ // Greater Command
  LPF ALTER_HEADER INT_VAR projectile = 159 END // use INAREANP.PRO for the projectile
  BUT_ONLY

// two durations are incorrect for righteous magic; stacking handled below
COPY_EXISTING ~sppr513.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header =  1 match_opcode = 250 duration =  60 END // one round short at level 10
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 11 match_opcode = 142 duration = 120 END // extra two seconds at level 20
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 11 match_opcode = 206 duration = 120 END // gets cloned from faulty 142
  BUT_ONLY

// power issue with false dawn
COPY_EXISTING ~sppr609.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 power = 0 END // caster's own protections could block
  BUT_ONLY

// The Physical Mirror spell should protect the recipient against Bolts of Lightning (crossbow ammo type), Firetooth bolts (crossbow) and Arrows of Fire (bow ammo type). (Wisp)
COPY_EXISTING ~sppr613.spl~ ~override~
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 num_ab
  READ_LONG  0x6a fx_off
  READ_SHORT 0x70 fx_idx
  num_fx = 0
  FOR (i=0;i<num_ab;i+=1) BEGIN
    fx_idx += num_fx
    READ_SHORT  ab_off + 0x28*i + 0x1e num_fx
    WRITE_SHORT ab_off + 0x28*i + 0x20 fx_idx
    FOR (j=0;j<num_fx;j+=1) BEGIN // first pass catalogs all projectile immunities
      READ_SHORT fx_off + 0x30*(fx_idx + j) fx_type
      PATCH_IF fx_type = 197 BEGIN
        READ_LONG  fx_off + 0x30*(fx_idx + j) + 0x8 pro
        SET $fl#pro_sppr613("%i%" "%pro%") = 1
      END
    END
    PATCH_FOR_EACH pro IN 1 3 4 6 9 11 13 14 15 16 19 26 29 31 34 102 BEGIN // now add any missing (patched tob missing 3 13 15 by default)
      PATCH_IF !VARIABLE_IS_SET $fl#pro_sppr613("%i%" "%pro%") BEGIN
        INSERT_BYTES fx_off + 0x30*num_fx        0x30
        WRITE_SHORT  fx_off + 0x30*num_fx        197
        WRITE_BYTE   fx_off + 0x30*num_fx + 0x2  1
        WRITE_LONG   fx_off + 0x30*num_fx + 0x8  pro
        WRITE_BYTE   fx_off + 0x30*num_fx + 0xc  2
        WRITE_BYTE   fx_off + 0x30*num_fx + 0x12 100
        num_fx += 1
      END
    END
    WRITE_SHORT ab_off + 0x28*i + 0x1e num_fx
  END
  BUT_ONLY

// (un)holy word not displaying deaf portrait icon for deafened characters; see icondeaf.eff
COPY_EXISTING ~sppr710.spl~ ~override~
              ~sppr715.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 STR_VAR match_resource = ~deafness~ resource = ~icondeaf~ END // clone deafness eff to icon eff
  BUT_ONLY

// wrong vvc for regen at lvl 15
COPY_EXISTING ~sppr711.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ~magres~ resource = ~icwrati~ END // wrong vvc
  BUT_ONLY

//Creeping Doom's panic effect does not work; replace the repeating eff with delayed effects (Wisp)
//see also flpr717a.spl
COPY_EXISTING ~sppr717.spl~ ~override~ //Creeping Doom
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 272 opcode = 146 parameter1 = 0 parameter2 = 1 timing = 1 duration = 0
    savingthrow = 0 savebonus = 0 STR_VAR match_resource = "panic" resource = "flpr717a" END // change panic eff to cast spell that causes panic instead
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 match_duration = 0 duration =  6 timing = 4 END // now clone into second round panic
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 match_duration = 0 duration = 12 timing = 4 END // ... and third round panic
  BUT_ONLY

// energy blades
COPY_EXISTING ~sppr721.spl~ ~override~
              ~spwi920.spl~ ~override~
  READ_LONG  0x34 level
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = level END // set effect power to spell level (6 by default, wrong for both)
  BUT_ONLY IF_EXISTS

// Fix Storm of Vengeance not displaying the relevant icon on poisoned targets (polytype)
COPY_EXISTING ~sppr722.spl~ ~override~ // Storm of Vengeance
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 25 opcode = 142 parameter1 = 0 parameter2 = 6 END // clone poison into poisoned icon
  BUT_ONLY IF_EXISTS

// bad targets for pfnm sound effect targeting
COPY_EXISTING ~spra303.spl~ ~override~ // avenger
              ~spwi311.spl~ ~override~ // generic arcane
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 target = 2 END
  BUT_ONLY

// duration of avenger version of animal summoning ii is wrong, should be 3 turns (180)
COPY_EXISTING ~spra305.spl~ ~override~
  LPF ALTER_SPELL_EFFECT INT_VAR duration_high = 180 END
  BUT_ONLY

// A few trap spells ignore magic resistance, but they probably should not since all other trap spells don't (aVENGER)
COPY_EXISTING ~spwi001.spl~ ~override~ // Fireball (trap)
              ~spwi002.spl~ ~override~ // Lightning Bolt (trap)
              ~spwi003.spl~ ~override~ // Magic Missile (trap)
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 resist_dispel = 1 END
  BUT_ONLY

// grease fixes
COPY_EXISTING ~spwi101.spl~ ~override~
  WRITE_ASCII 0x3a ~spwi101c~ // fixes icon
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 10 duration = 24 END // level 1 durations wrong
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 18 END // extends headers to level 20, adjusts duration as it goes
  BUT_ONLY

// armor fixes
COPY_EXISTING ~spwi102.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 3000 duration = 2700 END // nine hours is 2700, not 3000
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 duration = 2 END // play 3d effect runs a smidge long
  BUT_ONLY

// burning hands fixes
COPY_EXISTING ~spwi103.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 165 duration = 2 END // pauses an extra second on first header
  BUT_ONLY

// friends fixes; partially dropped because of multiple chr stacking. :(
COPY_EXISTING ~spwi107.spl~ ~override~
  // it's missing every even-numbered header. For real. And it has a level 21 header for good measure.
  LPF DELETE_SPELL_HEADER INT_VAR min_level = 21 header_type = 1 END
  FOR (index = 2 ; index < 21 ; index += 2) BEGIN
    LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = index last_missing = index END // clones ability 1 and associated effects into missing headers
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = (index - 1) match_duration = (18 + (6 * index)) duration = (24 + (6 * index)) END // corrects duration for newly created headers
  END
  BUT_ONLY

// duration for protection from evil wrong at many levels
// armor glow missing at level one (CamDawg)
COPY_EXISTING ~spwi113.spl~ ~override~
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR header = index check_headers = 1 duration_high = (12 * index) END
  END
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_opcode = 9 match_parameter2 = (6 + (20 << 16)) parameter2 = (5 + (20 << 16)) END // clone hair pulse to armor pulse
  BUT_ONLY

// The Shield spell and the Shield Amulet should properly protect the recipient against TRAP_MAGIC_MISSILE (amul15.itm now just casts this directly) (Nythrun and aVENGER)
COPY_EXISTING ~spwi114.spl~ ~override~ // Shield (WIZARD_SHIELD)
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 STR_VAR match_resource = ~spwi112~ resource = ~spwi003~ END
  BUT_ONLY

// spook's 'panic' string not properly quashed by MR
// one sound can't be saved against
COPY_EXISTING ~spwi125.spl~ ~override~
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index resist_dispel = 1 savingthrow = BIT0 savebonus = (0 - index) END
  END
  BUT_ONLY

// mage detect evil using priest spell description
COPY_EXISTING ~spwi202.spl~ ~override~
  SAY 0x50 #12219
  BUT_ONLY

// horror duration incorrect
COPY_EXISTING ~spwi205.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 18 duration = 60 END // 1 turn, not 3 rounds
  BUT_ONLY

// luck not providing saving throw and thieving skill bonuses; duration incorrect
COPY_EXISTING ~spwi209.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 20 duration = 18 END // small duration correction
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 duration = 1 STR_VAR match_resource = eff_m05 END // small duration correction
  PATCH_FOR_EACH op IN 59 90 91 92 275 276 277 BEGIN // all 7 thieving skill opcodes
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 133 opcode = op parameter1 = 5 parameter2 = 0 END // +5% thieving
  END
  FOR (index = 33 ; index < 38 ; ++index) BEGIN // all 5 save opcodes
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 133 opcode = index END // +1 save
  END
  BUT_ONLY

// portrait icon for melf's acid arrow bypasses MR though spell itself does not
COPY_EXISTING ~spwi211.spl~ ~override~
  LPF ALTER_HEADER INT_VAR type = 1 END // lev 1 header i ranged, not melee
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 resist_dispel = 1 END
  BUT_ONLY

// Ray of Enfeeblement spell (SPWI221.SPL) has incorrect save penalty on its "Display String" effect
COPY_EXISTING ~spwi221.spl~ override
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END
  BUT_ONLY

// incorrect duration, targeting for invis 10'
COPY_EXISTING ~spwi307.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 target = 2 END // was only playing sounds on caster
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 300 duration = 7200 END // 24 hours, not 1
  BUT_ONLY

// duration for monster summoning i, ii incorrect when cast at many levels
COPY_EXISTING ~spwi309.spl~ ~override~
              ~spwi407.spl~ ~override~
  SET base_dur = 54 // spwi407
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "spwi309" = 0) THEN BEGIN SET base_dur = 36 END
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR header = index check_headers = 1 duration_high = (base_dur + (6 * index)) END
  END
  BUT_ONLY

// protection from fire and protection from cold missing lev 19, 20 abilities (revised by Wisp)
COPY_EXISTING ~spwi319.spl~ ~override~
              ~spwi320.spl~ ~override~
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 60 END // extends headers to level 20, adding i turn of duration every step
  BUT_ONLY

// confusion (mage) missing -2 save penalty
COPY_EXISTING ~spwi401.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_savingthrow = BIT0 savebonus = "-2" END
  BUT_ONLY

// poly other shouldn't have save bonuses
COPY_EXISTING ~spwi415.spl~ ~override~
              ~spwm183.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END
  BUT_ONLY

// Secret Word should be able to dispel a Globe of Invulnerability (aVENGER)
COPY_EXISTING ~spwi419.spl~ ~override~ // Secret Word
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 5 END // give juuuust enough power to overcome GoI
  BUT_ONLY

// mage farsight has wonky durations
COPY_EXISTING ~spwi424.spl~ ~override~
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR header = index check_headers = 1 duration_high = (54 + (6 * index)) END
  END
  BUT_ONLY

// cone of cold missing level 10 ability header, wrong range at level 9, inconsistent save v non-save damage split
COPY_EXISTING ~spwi503.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 10 last_missing = 10 END // insert lev 10 header
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 12 match_savingthrow = BIT0 dicenumber = (((index + 9) + 1) / 2) parameter1 = (((index + 9)    ) / 2) END // ex lev9: 5d4+4, lev10: 5d4+5
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 12 match_savingthrow = 0    dicenumber = (((index + 9)    ) / 2) parameter1 = (((index + 9) + 1) / 2) END // ex lev9: 4d4+5, lev10: 5d4+5
  END
  BUT_ONLY

// mon sum iii has wrong probabilities at level 1
COPY_EXISTING ~spwi504.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_probability2 = 99 probability2 = 61 END
  BUT_ONLY

// hold monster swirly graphic craps out early; should also set icon and display 'held' string
COPY_EXISTING ~spwi507.spl~ ~override~
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 215 duration = (54 + (6 * index)) END
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 215 opcode = 142 parameter2 = 13 END // clone into held icon
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 215 opcode = 139 parameter1 = 14102 timing = 1 duration = 0 END // string 'held'
  END
  BUT_ONLY

// wrong string for pfnw's protection from pfmw spell
COPY_EXISTING ~spwi511.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 parameter1 = 36743 STR_VAR match_resource = spwi611 END
  BUT_ONLY

// spell shield does not have listed durations due to missing ability headers
COPY_EXISTING ~spwi519.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 288 resist_dispel = 0 duration = 162 END // first fix effects
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 18 min_lev_alt = 9 END // extends headers to level 20, adding 3 rounds of duration every header
  BUT_ONLY

 // minor spell turning reflecting spells of level 5-7; should only be 1-4
COPY_EXISTING ~spwi522.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 200 match_parameter2 = 5 END // delete lev 5 protections
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 200 match_parameter2 = 6 END // delete lev 6 protections
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 200 match_parameter2 = 7 END // delete lev 7 protections
  BUT_ONLY

// invisible stalkers only summoned for 8 hours, should be 9
COPY_EXISTING ~spwi601.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 2400 duration = 2700 END
  BUT_ONLY

// spell shield is hardcoded to display icon #73 which is currently protection from magical energy; need to swap - see also statdesc.2da, states.bam, states2.bam
COPY_EXISTING ~spwi606.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 142 match_parameter2 = 73 parameter2 = 123 END // change to new pro-magic energy icon
  BUT_ONLY

// conjure fire elemental should be restricted from diviners (spell school)
COPY_EXISTING ~spwi620.spl~ ~override~
  WRITE_BYTE 0x1f (THIS BOR BIT0) // adds diviner flag
  BUT_ONLY

// conjure air/earth elemental missing level 12-14 ability headers
COPY_EXISTING ~spwi621.spl~ ~override~
              ~spwi622.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 12 last_missing = 14 END // insert lev 12-14 headers
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 177 duration = (660 + (60 * index)) END // 60/level
  END
  BUT_ONLY

// cacofiends summoned for 120s; should be 90
COPY_EXISTING ~spwi707.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 177 duration = 90 END // 15 rds per descript
  BUT_ONLY

// sphere of chaos sleep duration is off by a second
COPY_EXISTING ~spwi711.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 39 duration = 9 END // add one second
  BUT_ONLY
  
// delayed blast fireball should do 15d6 damage, not 14d6 + 15
COPY_EXISTING ~spwi712.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 match_savingthrow = BIT0 dicenumber = 8 parameter1 = 0 END // addt'l 8d6 on failed save
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 match_savingthrow = 0    dicenumber = 7 parameter1 = 0 END // 7d6 always
  BUT_ONLY

// Prismatic Spray was erroneously blinding targets up to level five, rather than seven (Nythrun)
COPY_EXISTING ~spwi714.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 74 dicenumber = 7 END // dicenumber doubles as max level
  BUT_ONLY

// mordy sword should break invisibility
COPY_EXISTING ~spwi716.spl~ ~override~
  WRITE_BYTE 0x19 (THIS BOR BIT2) // adds break invis flag
  BUT_ONLY

// summon hakeshar missing lev 14, 15 headers
COPY_EXISTING ~spwi719.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 14 last_missing = 15 END // insert lev 14, 15 headers
  READ_SHORT 0x68 abil_num ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 177 duration = (126 + (6 * index)) END // 48 + 6/level
  END
  BUT_ONLY

// Pierce Shield now provides feedback when it lowers the target's magic resistance (Nythrun)
COPY_EXISTING ~spwi805.spl~ ~override~
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    SET string = (index + 32869)
    PATCH_IF (index > 3) BEGIN SET string += 2 END // skips a small gap in strings
    LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 166 opcode = 139 timing = 1 duration = 0 parameter1 = string STR_VAR resource = "" insert = "last" END
  END
  BUT_ONLY
  
// lowest level casting of symbol, stun and symbol, fear have inconsistent sound effect; casting times fixed elsewhere
COPY_EXISTING ~spwi811.spl~ ~override~
              ~spwi816.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 STR_VAR match_resource = "eff_m03" resource = "eff_p04" END
  BUT_ONLY

// power word: blind should last 6 rounds, not 10
COPY_EXISTING ~spwi815.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 60 duration = 36 END
  BUT_ONLY

// enemy versions of symbol spells missing casting sounds
COPY_EXISTING ~spwi897.spl~ ~override~
              ~spwi898.spl~ ~override~
              ~spwi899.spl~ ~override~
              ~spwm123.spl~ ~override~
  WRITE_ASCII 0x10 ~cas_m03~ #8
  BUT_ONLY

// create custom spell trap for SotM
COPY_EXISTING ~spwi902.spl~ ~override/staf11.spl~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 108 duration = 2400 END

// pit fiends around for an extra two seconds
COPY_EXISTING ~spwi905.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 200 duration = 198 END
  BUT_ONLY

// absolute immunity has wrong duration
COPY_EXISTING ~spwi907.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 28 duration = 24 END
  BUT_ONLY
  
// energy drain lighting effect is not bypassing MR like all other effects
COPY_EXISTING ~spwi914.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 resist_dispel = 0 END
  BUT_ONLY

ACTION_IF !tob_game THEN BEGIN // SoA-only fix

  // SoA black blade missing lev 19, 20 headers; see also blakblad.itm SoA patch for prof fix
  COPY_EXISTING ~spwi915.spl~ ~override~
    LPF CD_EXTEND-O-MATIC END // extends headers to level 20
    READ_SHORT  0x68 abil_num
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 54 parameter1 = (21 - (21 - (abil_num + index))) parameter2 = 1 END // set thac0
      LPF ALTER_SPELL_EFFECT INT_VAR header = (index + 1) duration_high = ((21 - (abil_num + index)) * 6) END // duration = 1rd/level
    END
    BUT_ONLY

END

// imprisoned summon fix: see gender.ids, cdwi917a.spl, cdwi910.eff, cdwi917a.eff, spin580.spl, spin626.spl, spin788.spl, spwi910.spl, spwi917.spl
COPY_EXISTING ~spwi917.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 4 duration = 2 STR_VAR resource = cdwi917a END
  BUT_ONLY

// bigby's crushing hand should be saving against paralyzation; some second round saves are incorrect
COPY_EXISTING ~spwi918.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 9 END // most effects power 3 or 8
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 match_timing = 0 timing = 4 duration =  6 END // sound should play at end of round
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 match_timing = 3 timing = 4 duration = 12 END // sound should play at end of round
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 4 match_opcode = 12 match_duration = 6 savebonus = "-4" END // first damage is no save, second is at -4, final at -2
  BUT_ONLY IF_EXISTS

// mage HLAs should be schoolless
COPY_EXISTING ~spwi920.spl~ ~override~ // energy blades
              ~spwi921.spl~ ~override~ // improved alacrity
              ~spwi922.spl~ ~override~ // dragon's breath
              ~spwi923.spl~ ~override~ // summon planetar
              ~spwi924.spl~ ~override~ // summon dark planetar
              ~spwi925.spl~ ~override~ // comet
  WRITE_SHORT 0x1e 0 // removes exclusion flags
  BUT_ONLY IF_EXISTS

// dragon's breath has spurious save penalty vs. sleep, missing sleep portrait icon
COPY_EXISTING ~spwi922.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = 0 END // sleep save at -10
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 39 opcode = 142 parameter2 = 14 END // clone sleep into sleep icon
  BUT_ONLY IF_EXISTS

// typo in wav reference
COPY_EXISTING ~spwi995.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 STR_VAR resource = eff_m09 END // original ref had trailing space
  BUT_ONLY

// time stop/imp alacrity from wish has wrong opcode for IA
COPY_EXISTING ~spwish17.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 139 END // string is blank; delete
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 189 opcode = 188 parameter2 = 1 END // wrong opcode for alacrity
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 174 duration = 24 STR_VAR match_resource = eff_m29 END // wrong time for expiry sound
  BUT_ONLY IF_EXISTS

// Wish: Breach on all enemies in the area should not affect friendly summoned creatures and turn them hostile. Fixed by using a different targeting method and projectile
COPY_EXISTING ~spwish38.spl~  ~override~ // Wish: Breach on all enemies in the area
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 target = 2 END // original ref had trailing space
  LPF ALTER_HEADER INT_VAR projectile = 254 END // bignarea
  BUT_ONLY IF_EXISTS

// wild surge: destroy gold is supposed to target self, not preset target
COPY_EXISTING ~spwm117.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 target = 1 END // wrong target
  BUT_ONLY

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

COPY_EXISTING ~25spell.sto~ ~override~ // Arcana Archives
  SAY 0x0c #70882 // Arcana Archives
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~25spell2.sto~ ~override~ // Arcana Archives
  SAY 0x0c #73982 // Arcana Archives
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 stock = 2 flag_identified = 1 infinite = 0 STR_VAR match_item = scrl2h END // shadow door was infinite, now two
  BUT_ONLY IF_EXISTS

// Amkethran duplicate gem bag fix, part two of four (see cdbag02i.itm, cdbag02j.itm, cdbag02i.sto, cdbag02j.sto)
COPY_EXISTING ~amsmug01.sto~ ~override~ // amkethran smugglers
  LPF ALTER_STORE_ITEM STR_VAR match_item = "bag02d" item = "cdbag02i" END // swap bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 10 STR_VAR match_item = rods06 END // overcharged: rod of reversal has 10 max charges
  BUT_ONLY IF_EXISTS

// Amkethran duplicate gem bag fix, part three of four (see cdbag02i.itm, cdbag02j.itm, cdbag02i.sto, cdbag02j.sto)
COPY_EXISTING ~amsmug02.sto~ ~override~ // amkethran smugglers
  LPF ALTER_STORE_ITEM STR_VAR match_item = "bag02d" item = "cdbag02j" END // swap bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 2 STR_VAR match_item = boot12 END // undercharged: gargoyle boots
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 10 STR_VAR match_item = rods06 END // overcharged: rod of reversal has 10 max charges
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~arled.sto~    ~override~ // shadow thief fence
              ~trgeni01.sto~ ~override~ // khan zahraa
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw1h44 END // katana +1 was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw2h02 END // two-handed sword +1 was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = halb02 END // halberd +1 was infinite, unidentified
  BUT_ONLY

// starting ToB bags of holding have uncharged items
COPY_EXISTING ~bag20.sto~ ~override~ // ToB goodie bag
              ~bag22.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag21.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = brac16 END // add one charge to bracers of blinding strike
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag23.sto~ ~override~ // ToB goodie bag
              ~bag24.sto~ ~override~ // ToB goodie bag
              ~bag25.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  LPF ALTER_STORE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h27 END // add one secondary charge to arbanes's sword (haste ability)
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag26.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 40 stock = 2 STR_VAR match_item = bull03 END // 80 stacks of one bullet => two stacks of 40 bullets
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag27.sto~ ~override~ // ToB goodie bag
              ~bag28.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 40 stock = 2 STR_VAR match_item = bull03 END // 80 stacks of one bullet => two stacks of 40 bullets
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3d END // add one charge to joolon
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag29.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 40 stock = 1 STR_VAR match_item = dart04 END // 40 stacks of one dart of wounding => one stacks of 40 darts of wounding
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bag30.sto~ ~override~ // ToB goodie bag
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3d END // add one charge to joolon
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3e END // add one charge to kilthix
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 1 STR_VAR match_item = misc3i END // add one charge to silver horn of valhalla
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 3 STR_VAR match_item = misc3m END // add three charges to harp of discord
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~bdbart01.sto~ ~override~ // five flagons
              ~ffbart.sto~   ~override~ // five flagons
              ~lehtinan.sto~ ~override~ // copper coronet
              ~thumb.sto~    ~override~ // sea's bounty
  LPF ALTER_STORE_ITEM INT_VAR infinite = 0 STR_VAR match_item = book40 END // history of the fateful coin was infinite (bg holdover)
  BUT_ONLY

COPY_EXISTING ~bernard2.sto~ ~override~ // copper coronet
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 charge1 = 1 infinite = 0 STR_VAR match_item = scrl8p END // prismatic spray was infinite, unidentified
  BUT_ONLY

COPY_EXISTING ~bshop01.sto~ ~override~ // merchant
              ~trcar04.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 50 charge2 = 50 STR_VAR match_item = wand05 END // undercharged: wand of fire
  BUT_ONLY

COPY_EXISTING ~bshop02.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 5 STR_VAR match_item = ax1h04 END // throwing axes were in stacks of 1; changing to 5
  BUT_ONLY

// Amkethran duplicate gem bag fix, part four of four (see amsmug01.sto, amsmug02.sto, cdbag02i.itm, cdbag02j.itm)
COPY_EXISTING ~bag02i.sto~ ~override/cdbag02i.sto~
              ~bag02i.sto~ ~override/cdbag02j.sto~
  IF_EXISTS

COPY_EXISTING ~garlena.sto~  ~override~ // Temple of Helm (Sister Garlena) (Watcher's Keep)
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 5 STR_VAR match_item = ax1h04 END // throwing axes were in stacks of 1; changing to 5
  LPF ALTER_STORE_ITEM INT_VAR charge2 = 0 STR_VAR match_item = restore END // overcharged: restoration scroll has no secondary ability
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~gorch.sto~    ~override~ // gorch
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = blun21 END // mace +2 was infinite, unidentified
  BUT_ONLY

COPY_EXISTING ~hgkar01.sto~  ~override~ // karthis al-hezzar
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = blun31 END // club +3 was infinite, unidentified
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~ppstor01.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR infinite = 0 STR_VAR match_item = dart02 END // darts +1 were infinite though they had a stock set
  BUT_ONLY

// The merchant who gets assaulted by a thug at the City Gates no longer shares a store with the
// Brynnlaw storekeeper (see also soa-dlg.d) (Nythrun and devSin)
COPY_EXISTING ~ppstor01.sto~ ~override/cdaemerc.sto~ // merchant
  WRITE_LONG 0x14 145 // reduce sell markup

COPY_EXISTING ~ribald.sto~   ~override~ // adventurers' mart
  LPF ALTER_STORE_ITEM INT_VAR match_stock = 5 STR_VAR match_item = "shld06" item = "shld05" END // stores with dupe large shield +1 entries; should be normal large shields
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = ring03 END // ring of animal freindship was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 charge1 = 1 charge2 = 1 STR_VAR match_item = ring28 END // ring of air control was infinite, unidentified, undercharged
  BUT_ONLY

COPY_EXISTING ~ribald2.sto~  ~override~ // adventurers' mart
              ~trmer04.sto~  ~override~ // anvil of the right
  LPF ALTER_STORE_ITEM INT_VAR infinite = 0 STR_VAR match_item = potn19 END // potions of agility were infinite though they had a stock set
  BUT_ONLY

COPY_EXISTING ~ribald3.sto~  ~override~ // adventurers' mart; also has infinite freedom scrolls, but those are by design
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 0 charge2 = 1 flag_identified = 1 infinite = 0 STR_VAR match_item = bow10 END // heartseeker had charge on wrong header, was unidentified and infinite
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw2h09 END // warblade was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw2h11 END // two-handed sword +2 was infinite, unidentified
  BUT_ONLY

COPY_EXISTING ~sahpr1.sto~   ~override~ // Temple of Sekolah (Priestess of Sekolah)
              ~suelf10.sto~  ~override~ // Temple of Rillifane (Rierra)
  LPF ALTER_STORE_ITEM INT_VAR charge2 = 0 STR_VAR match_item = restore END // overcharged: restoration scroll has no secondary ability
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 5 STR_VAR match_item = ax1h04 END // throwing axes were in stacks of 1; changing to 5
  WRITE_LONG 0x14 120 // fixes markup issues at Sahuagin and Suldanesselar stores
  WRITE_LONG 0x18 50
  BUT_ONLY

COPY_EXISTING ~sartem01.sto~ ~override~ // waukeen's wares
  LPF ALTER_STORE_ITEM INT_VAR infinite = 0 STR_VAR match_item = miscau END // elven holy water was infinite though they had a stock set
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~shop02.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR match_stock = 5 STR_VAR match_item = "shld06" item = "shld05" END // stores with dupe large shield +1 entries; should be normal large shields

COPY_EXISTING ~shop03.sto~   ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR infinite = 0 STR_VAR match_item = bull02 END // bullets +1 were infinite though they had a stock set
  BUT_ONLY

COPY_EXISTING ~shop07.sto~   ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = shld05 END // large shield was infinite, unidentified
  BUT_ONLY

COPY_EXISTING ~trmer02.sto~  ~override~ // Merchant (Merchant) (Trademeet)
  LPF ALTER_STORE_ITEM INT_VAR charge2 = 1 STR_VAR match_item = sw1h33 END // add one secondary charge to ras
  BUT_ONLY

COPY_EXISTING ~trmer04a.sto~ ~override~ // anvil of the right
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw2h11 END // two-handed sword +2 was infinite, unidentified
  BUT_ONLY

COPY_EXISTING ~type2.sto~    ~override~ // adventurers' mart, unused
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = chan05 END // splint mail +1 was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw1h44 END // katana +1 was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR                     infinite = 0 STR_VAR match_item = potn19 END // potions of agility were infinite though they had a stock set
  BUT_ONLY

COPY_EXISTING ~uddrow23.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM INT_VAR flag_identified = 1 infinite = 0 STR_VAR match_item = sw2h07 END // harbinger +3 was infinite, unidentified
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 0 charge2 = 30 charge3 = 30 STR_VAR match_item = staf16 END // charges: had 20 melee charges (should be 0), none for either magical ability
  BUT_ONLY

COPY_EXISTING ~uddrow24.sto~ ~override~ // shop
  LPF ALTER_STORE_ITEM INT_VAR charge1 = 0 STR_VAR match_item = rods04 END // overcharged: rod of smiting
  BUT_ONLY

/////                                                  \\\\\
///// pro fixes                                        \\\\\
/////                                                  \\\\\

// color spray arc fix
COPY_EXISTING ~cspray.pro~ ~override~
  WRITE_SHORT 0x206 426 // now ranges to 30', per descript
  BUT_ONLY

// power word blind should also affect party/allies
COPY_EXISTING ~holdsumm.pro~ ~override~
  WRITE_BYTE 0x200 ((THIS BAND `BIT4) BAND `BIT6) // removes ally/self exemption flags
  BUT_ONLY

// missing shadow
COPY_EXISTING ~iceglyp.pro~ ~override~
  WRITE_BYTE 0x100 (THIS BAND `BIT7) // removes translucent flag
  BUT_ONLY

// meteor swarm should be 4 rounds, per descript
COPY_EXISTING ~metswarm.pro~ ~override~
  WRITE_BYTE 0x216 4 // should last 4 rounds per descript (four repetitions)
  BUT_ONLY

// shadow/bam missing
COPY_EXISTING ~spattck2.pro~ ~override~
  WRITE_BYTE  0x100 (THIS BOR BIT5) // add 'light spot enabled' flag
  WRITE_ASCII 0x10c ~spshadow~ #8   // shadow BAM
  WRITE_BYTE  0x115 0               // shadow number
  BUT_ONLY

// another shadow
COPY_EXISTING ~spgreorb.pro~ ~override~
  WRITE_BYTE  0x100 (THIS BOR BIT5) // add 'light spot enabled' flag
  WRITE_ASCII 0x10c ~spchrorb~ #8   // shadow BAM
  BUT_ONLY

/////                                                  \\\\\
///// vvc fixes                                        \\\\\
/////                                                  \\\\\

// adds unused sequence
COPY_EXISTING ~spattck1.vvc~ ~override~
  WRITE_LONG 0x68 0x02 // glowing ball forms
  WRITE_LONG 0x6c 0x01 // glowing ball static
  BUT_ONLY

// wrong sounds for aura o' flamin' death
COPY_EXISTING ~spauraff.vvc~ ~override~
  WRITE_ASCII 0x78 ~eff_p114~ #8
  WRITE_ASCII 0x80 ~aft_p28~ #8
  BUT_ONLY IF_EXISTS

// play intro, then normal sequence
COPY_EXISTING ~spbldbtm.vvc~ ~override~
              ~spbldtop.vvc~ ~override~
  WRITE_LONG 0x68 0x02 // blades appearing
  WRITE_LONG 0x6c 0x01 // circling
  WRITE_LONG 0x90 0x03 // disappearing
  BUT_ONLY

// should have three variations
COPY_EXISTING ~spcloud1.vvc~ ~override~
  WRITE_LONG 0x68 0x03
  WRITE_LONG 0x6c 0x04
  BUT_ONLY

// create unique spinny animation for horror (see spin105, spwi205, fear immunity batches)
COPY_EXISTING ~spmindat.vvc~ ~override/cdhorror.vvc~

// enables outro in place of middle for smoother animation
COPY_EXISTING ~spwood.vvc~ ~override~
  WRITE_LONG 0x68 0x01
  WRITE_LONG 0x6c 0x03
  BUT_ONLY

/////                                                  \\\\\
///// eff fixes                                        \\\\\
/////                                                  \\\\\

// new eff for holy smite; needs to display blind icon for blinded creatures
COPY_EXISTING ~blind.eff~    ~override/cdblind.eff~
  WRITE_LONG 0x10 142 // display portrait icon
  WRITE_LONG 0x20   8 // icon: blinded

// new eff for holy smite; needs to display blinded string for blinded creatures
COPY_EXISTING ~blind.eff~    ~override/cdblind1.eff~
  WRITE_LONG 0x10   139 // display string
  WRITE_LONG 0x1c 14674 // string: blinded
  WRITE_LONG 0x24     1 // Instant/Permanent
  WRITE_LONG 0x28     0 // duration

// bolt of glory should do same type of damage to all creatures
COPY_EXISTING ~boltdem.eff~  ~override~
              ~boltelem.eff~ ~override~
              ~boltprim.eff~ ~override~
              ~boltund.eff~  ~override~
  WRITE_SHORT 0x22 64 // magic
  BUT_ONLY

// Good aligned mages should suffer spell failure when affected by Unholy Word
COPY_EXISTING ~casfailm.eff~ ~override~
  WRITE_SHORT 0x20 0     // parameter 2
  BUT_ONLY

// sol's searing orb - see also sorb.itm, cdsorb2.eff, udeadbli.eff
COPY_EXISTING ~cdelfcm3.eff~ ~override/cdsorb1.eff~ // prevent second blinded string for undead
  WRITE_LONG 0x1c 14674 // string

COPY_EXISTING ~cdblind1.eff~ ~override/cdheld.eff~
  WRITE_LONG 0x1c 14102 // 'held'
  WRITE_LONG 0x24 1     // instant/perm
  WRITE_LONG 0x28 0     // duration

COPY_EXISTING ~cdblind.eff~ ~override/cdhda30.eff~
  WRITE_LONG 0x20 13 // held icon
  WRITE_LONG 0x28 30 // duration

COPY_EXISTING ~dawnvis.eff~ ~override/cdhdb30.eff~
  WRITE_LONG  0x18 0          // power
  WRITE_LONG  0x28 30         // duration
  WRITE_ASCII 0x30 ~spmindat~ // swirly anim

// create extended effs for hold animal; use abil_num from spell patch
OUTER_FOR (index = 60 ; index < 241 ; index += 12) BEGIN

  COPY_EXISTING ~cdhda30.eff~ ~override/cdhda%index%.eff~
                ~cdhdb30.eff~ ~override/cdhdb%index%.eff~
    WRITE_LONG 0x28 index // duration

END

// daystar damage vs. evil undead fixes, pt. 2 (see sw1h31.itm, daystar2.eff)
COPY_EXISTING ~daystar1.eff~ ~override~
  WRITE_LONG  0x10        177 // use eff file
  WRITE_LONG  0x14          2 // preset target
  WRITE_LONG  0x1c          3 // against mask_evil...
  WRITE_LONG  0x20          8 // ...from align.ids
  WRITE_LONG  0x24          1 // timing: permanent
  WRITE_ASCII 0x30 ~daystar2~ // then actually go to another eff for +2 damage
  WRITE_LONG  0x38          0 // blank leftover dice num
  WRITE_LONG  0x3c          0 // blank leftover dice size
  WRITE_LONG  0x60          0 // blank leftover param3

// changes Flame o' North's damage bonus to CE from all E (gets changed to different op later in effing effs, but still needs this fix first)
COPY_EXISTING ~flamenor.eff~ ~override~
  WRITE_LONG 0x1c 51
  BUT_ONLY

// new eff for (un)holy word to display deaf icon; see sppr710.spl, sppr715.spl
COPY_EXISTING ~iconslow.eff~ ~override/icondeaf.eff~
  WRITE_LONG 0x20 112 // deafened

// change from damage v type (179) to damage (12); see blun12.itm (revised by Wisp)
COPY_EXISTING ~macedisr.eff~ ~override/cddisr.eff~
  WRITE_LONG  0x10 12 // opcode: damage
  WRITE_LONG  0x1c  2 // fixed damage
  WRITE_SHORT 0x20  0 // straight butter, baby
  WRITE_SHORT 0x60  0 // param3 not needed
  BUT_ONLY

// changes bonus damage to undead for IMoD (blun25.itm) to 1d6 + 1 to bring total to 2d6 +4 (revised by Wisp)
COPY_EXISTING ~cddisr.eff~ ~override/macedisu.eff~
  WRITE_LONG 0x1c 1 // fixed damage

// new eff to fix runehammer damage
COPY_EXISTING ~cddisr.eff~ ~override/hamm10.eff~
  WRITE_LONG 0x38 2 // 2d
  WRITE_LONG 0x3c 4 // 4
  WRITE_LONG 0x1c 4 // +4

// new eff to fix runehammer damage
COPY_EXISTING ~hamm10.eff~ ~override/hamm11.eff~
  WRITE_LONG 0x1c 5 // +5

// sol's searing orb - see also sorb.itm, cdsorb1.eff, udeadbli.eff
COPY_EXISTING ~mesdie.eff~ ~override/cdsorb2.eff~ // show lone blinded string for undead
  WRITE_LONG 0x1c 14674 // string
  WRITE_LONG 0x40 0 // no save
  WRITE_LONG 0x44 0

// Polymorph other was setting victim's attacks per round to zero; adjusted to one. See spwi415.spl for one other fix.
COPY_EXISTING ~plyrate.eff~ ~override~
  WRITE_LONG 0x1c 1
  BUT_ONLY

// assassin poison should be a save v death, not save v everything
COPY_EXISTING ~spcl422.eff~ ~override~
  WRITE_LONG 0x40 BIT2 // saving throw vs. death ONLY
  BUT_ONLY

// more assassin poison; damage is incorrect (corrected to 2dam/sec)
COPY_EXISTING ~spcl422a.eff~ ~override~
  WRITE_LONG 0x1c 2 // +2 damage, not 1
  WRITE_LONG 0x20 2 // type is incorrect (Nythrun)
  BUT_ONLY

// deathblow has no save and doesn't affect >10 HD creatures
COPY_EXISTING ~quivvis.eff~ ~override/spcl902a.eff~
  WRITE_LONG  0x40  0 // no save
  WRITE_SHORT 0x58 10 // max HD 10

// holy smite/unholy blight damage fixes, part 1 (CamDawg)
// see also sppr313.spl, sppr314.spl
COPY_EXISTING ~sp313l05.eff~ ~override~
              ~sp313l06.eff~ ~override~
              ~sp313l07.eff~ ~override~
              ~sp313l08.eff~ ~override~
              ~sp313l09.eff~ ~override~
              ~sp313l10.eff~ ~override~
  WRITE_LONG 0x3c 4 // dice size 2 > 4
  BUT_ONLY

// holy smite/unholy blight damage fixes, part 2 (CamDawg)
// see also sppr313.spl, sppr314.spl
OUTER_SET "dicethrown" = 2
COPY_EXISTING ~sp313l05.eff~ ~override/sp313l02.eff~
              ~sp313l06.eff~ ~override/sp313l03.eff~
              ~sp313l07.eff~ ~override/sp313l04.eff~
  WRITE_LONG 0x38 "%dicethrown%" // # dice
  SET "dicethrown" = ("%dicethrown%" + 1)

// greater deathblow has no save and doesn't affect >10 HD creatures
COPY_EXISTING ~spcl902a.eff~ ~override/spcl903a.eff~
  WRITE_SHORT 0x58 12 // max HD 12

// display string effs with incorrect timing
COPY_EXISTING ~spcl906b.eff~ ~override~
              ~spin935d.eff~ ~override~
              ~stunstrg.eff~ ~override~
  WRITE_LONG 0x24 1 // Instant/Permanent
  WRITE_LONG 0x28 0 // duration
  BUT_ONLY IF_EXISTS

// greater earth elem (cleric) is hostile, should match summoner's allegiance
COPY_EXISTING ~speart3p.eff~ ~override~
  WRITE_LONG 0x20 0 // set to Match target
  BUT_ONLY

// removing saves from effs; save chances are already handled by the spell file
COPY_EXISTING ~udead66.eff~  ~override~  // sol's +6d6 deep-fry
              ~undchstr.eff~ ~override~  // "controlled" str
              ~undchvis.eff~ ~override~  // spnwchrm for hold spells? l a z y
  WRITE_LONG 0x40 0x0
  BUT_ONLY

// sol's searing orb - see also sorb.itm, cdsorb1.eff, cdsorb2.eff
COPY_EXISTING ~udeadbli.eff~ ~override~ // fix duration for sol's searing orb blindness vs. undead
  WRITE_LONG 0x28 36 // duration

/////                                                  \\\\\
///// bam fixes                                        \\\\\
/////                                                  \\\\\

//Correct invalid animation frames & offsets
COPY_EXISTING ~mtang21.bam~  ~override~ //Tanar'ri (Balor)
              ~mtang21e.bam~ ~override~
              ~mtang22.bam~  ~override~
              ~mtang22e.bam~ ~override~
              ~mtang23.bam~  ~override~
              ~mtang23e.bam~ ~override~
              ~mwyvg21.bam~  ~override~ //Wyvern
              ~mwyvg22.bam~  ~override~
              ~mwyvg23.bam~  ~override~
              ~mwyvg24.bam~  ~override~
  INNER_PATCH ~%SOURCE_RES%~ BEGIN
    READ_ASCII 6 bn (1) //Read BAM series (1-4)
  END
  READ_ASCII 0x0 sg (4) //Signature
  PATCH_IF (~%sg%~ STRING_EQUAL_CASE ~BAMC~ = 1) BEGIN //If compressed
    READ_LONG 0x8 dl //Uncompressed data length
    DECOMPRESS_REPLACE_FILE 0xc (SOURCE_SIZE - 0xc) dl
    sz = dl
  END ELSE BEGIN
    sz = SOURCE_SIZE
  END
  READ_SHORT 0x8 fm //Frame count
  READ_LONG 0xc ft //Frame entry offset
  FOR (f1 = 0; f1 < fm; f1 += 1) BEGIN //Frame loop
    READ_SHORT (ft + (f1 * 0xc)) fw //Frame width
    READ_SHORT (ft + (f1 * 0xc) + 2) fh //Frame height
    PATCH_IF ((fw < 1) AND (fh != 0)) OR ((fh < 1) AND (fw != 0)) BEGIN //If invalid dimensions
      WRITE_SHORT (ft + (f1 * 0xc)) 0 //Correct width
      WRITE_SHORT (ft + (f1 * 0xc) + 2) 0 //Correct height
      fw = 0
      fh = 0
    END
    PATCH_IF bn = 1 BEGIN //If upper left frame
      WRITE_SHORT (ft + (f1 * 0xc) + 4) fw //X coordinate
      WRITE_SHORT (ft + (f1 * 0xc) + 6) (fh + 40) //Y coordinate
    END ELSE BEGIN
      PATCH_IF bn = 2 BEGIN //If upper right frame
        WRITE_SHORT (ft + (f1 * 0xc) + 4) 0 //X coordinate
        WRITE_SHORT (ft + (f1 * 0xc) + 6) (fh + 40) //Y coordinate
      END ELSE BEGIN
        PATCH_IF bn = 3 BEGIN //If lower left frame
          WRITE_SHORT (ft + (f1 * 0xc) + 4) fw //X coordinate
          WRITE_SHORT (ft + (f1 * 0xc) + 6) 40 //Y coordinate
        END ELSE BEGIN
          PATCH_IF bn = 4 BEGIN //If lower right frame
            WRITE_SHORT (ft + (f1 * 0xc) + 4) 0 //X coordinate
            WRITE_SHORT (ft + (f1 * 0xc) + 6) 40 //Y coordinate
          END
        END
      END
    END
  END
  COMPRESS_REPLACE_FILE 0 sz 9
  INSERT_BYTES 0x0 0xc
  WRITE_ASCII 0x0 ~BAMCV1  ~
  WRITE_LONG 0x8 dl
  BUT_ONLY

/////                                                  \\\\\
///// music fixes                                      \\\\\
/////                                                  \\\\\

// from remix, devsin
COPY_EXISTING ~music/bc1.mus~ ~music~
  REPLACE_TEXTUALLY ~J1 +B1 +@TAG +ZJ~ ~J1  BC1   B1        @TAG ZJ~
  BUT_ONLY

COPY_EXISTING ~music/bc2.mus~ ~music~
  REPLACE_TEXTUALLY ~K1 +A2 +@TAG +Z~ ~K1  BC2   A2        @TAG Z~
  BUT_ONLY

COPY_EXISTING ~music/bd1.mus~ ~music~
  REPLACE_TEXTUALLY ~E2 +B1 +@TAG +ZB~ ~E2  BD1   B1        @TAG ZB~
  BUT_ONLY

COPY_EXISTING ~music/bd2.mus~ ~music~
  REPLACE_TEXTUALLY ~F2 +B1 +@TAG +ZF~ ~F2  BD2   B1        @TAG ZF~
  BUT_ONLY

COPY_EXISTING ~music/bd2/bd2zg2.acm~ ~music/bd3/bd3zg2.acm~

COPY_EXISTING ~music/bd3.mus~ ~music~
  REPLACE_TEXTUALLY ~G2 +A2 +@TAG +ZG2~ ~G2  BD3   A2        @TAG ZG2~
  BUT_ONLY

COPY_EXISTING ~music/bf1.mus~ ~music~
  REPLACE_TEXTUALLY ~H2 +B1 +@TAG +Z~ ~H2  BF1   B1        @TAG Z~
  BUT_ONLY

COPY_EXISTING ~music/bf2.mus~ ~music~
  REPLACE_TEXTUALLY ~J3 +B1 +@TAG +ZJ3~ ~J3  BF2   B1        @TAG ZJ3~
  BUT_ONLY

COPY_EXISTING ~music/bjr.mus~ ~music~
  REPLACE_TEXTUALLY "H2 +B1" "H2 BJR B1"
  REPLACE_TEXTUALLY ZA Z
  SET_2DA_ENTRY 11 2 3 ZB
  BUT_ONLY  

COPY_EXISTING ~music/bm1.mus~ ~music~
  INSERT_2DA_ROW 11 3 "J1 @TAG ZA"
  INSERT_2DA_ROW 12 3 "J2 BM1 B1 @TAG ZB"
  REPLACE_TEXTUALLY ~H1 +B2~ ~H1~
  SET_2DA_ENTRY 1 0 1 13

COPY_EXISTING ~music/bm2.mus~ ~music~
  REPLACE_TEXTUALLY ~N2 +B1 +@TAG +ZA~ ~N1  BM2   B1        @TAG ZA~
  BUT_ONLY

COPY_EXISTING ~music/bp1.mus~ ~music~
  REPLACE_TEXTUALLY ~H3 +B1 +@TAG +ZA~ ~H3  BP1   B1        @TAG ZA~
  BUT_ONLY

COPY_EXISTING ~music/bp2.mus~ ~music~
  REPLACE_TEXTUALLY ~K1 +B1 +@TAG +Z~ ~K1  BP2   B1        @TAG Z~
  BUT_ONLY

COPY_EXISTING ~music/brd.mus~ ~music~
  REPLACE_TEXTUALLY ~B2 +A1 +@TAG +ZB~ ~B2  BRD   A1        @TAG ZB~
  BUT_ONLY

COPY_EXISTING ~music/bsd.mus~ ~music~
  REPLACE_TEXTUALLY ~F2A +A2 +@TAG +ZF2~ ~F2A  BSD  A2        @TAG ZF2~
  BUT_ONLY

COPY_EXISTING ~music/bst.mus~ ~music~
  REPLACE_TEXTUALLY ~J1 +C1 +@TAG +ZH2~ ~J1  BST   C1        @TAG ZH2~
  BUT_ONLY

COPY_EXISTING ~music/harp_v2.mus~ ~music~
  SET_2DA_ENTRY 6 0 1 ~1C~ // the current entry, C (HARP_V2C.ACM), doesn't exist
  BUT_ONLY

COPY_EXISTING ~music/harp_v6.mus~ ~music~
  REPLACE_TEXTUALLY ~^5~ ~6~
  BUT_ONLY

COPY_EXISTING ~music/mx0202.mus~ ~music/mxkalah.mus~
  REPLACE_TEXTUALLY ~0202~ ~KALAH~

COPY_EXISTING ~music/mx0202.mus~ ~music/mxkhali.mus~
  REPLACE_TEXTUALLY ~0202~ ~KHALI~

COPY_EXISTING ~music/mx0202.mus~ ~music/mxthief.mus~
  REPLACE_TEXTUALLY ~0202~ ~THIEF~

ACTION_IF tob_game THEN BEGIN // ToB-only stuff check

  COPY_EXISTING ~music/dream2.mus~ ~music~
    REPLACE_TEXTUALLY ~DREAM~ ~COMBO~

  COPY_EXISTING ~music/mb.mus~ ~music~
    REPLACE_TEXTUALLY ~_F3 +_A2 +@TAG +_G1~ ~_F3  MB  _A2    @TAG _G1~
    BUT_ONLY

  COPY_EXISTING ~music/vb.mus~ ~music~
    REPLACE_TEXTUALLY ~_H2 +_B2 +@TAG +_I1~ ~_H2  VB    _B2        @TAG _I1~
    BUT_ONLY

END

/////                                                  \\\\\
///// immunity effects batch patches                   \\\\\
/////                                                  \\\\\

// basic idea: if you're protected from, say, confusion, the macro will make sure that the confusion icon and 'confused' messages are also suppressed
// not all are immunities - a few other batches are included as well

// shuffled off into include instead of having 1500 lines of copy, function, but_only
INCLUDE ~bg2fixpack/lib/batch_patches.tpa~

/////                                                  \\\\\
///// effing effs                                      \\\\\
/////                                                  \\\\\

/* So, items with damage bonuses via opcode 179 have a couple of issues and are addressed in this section.
Opcode 179 doesn't stack so it interferes with kit bonuses vs. creature type. If applied as a global effect
it also bleeds over into the second weapon if used on a dual-weilder. Opcode 178 (thac0 bonuses vs type)
has the same dual-wield exploit but there's no way to fix it.*/

// wrong target, ids for burning earth's troll damage/thac0
COPY_EXISTING ~trolld1.eff~  ~override~ // will be overwritten later, but needed temporarily to get targeting correct
              ~trollh1.eff~  ~override~
//  WRITE_LONG 0x14 2 // preset target
  WRITE_LONG 0x20 4 // race.ids
  BUT_ONLY

// thac0 bonuses (global effects): wrong targets and/or odd timings
COPY_EXISTING ~damaceb.eff~  ~override~ // invalid target (-1)
              ~equal09.eff~  ~override~ // timing = limited
              ~equal10.eff~  ~override~ // timing = limited
              ~equal11.eff~  ~override~ // timing = limited
              ~equal12.eff~  ~override~ // timing = limited
              ~equal13.eff~  ~override~ // timing = limited
              ~equal14.eff~  ~override~ // timing = limited
              ~equal15.eff~  ~override~ // timing = limited
              ~equal16.eff~  ~override~ // timing = limited
              ~evilhit2.eff~ ~override~
              ~trollh1.eff~  ~override~ // invalid target (-1)
              ~undhit1.eff~  ~override~
  WRITE_LONG 0x14 0 // target: none
  WRITE_LONG 0x24 2 // timing: instant/while equipped
  BUT_ONLY

// shared effs
COPY_EXISTING ~dragdam3.eff~ ~override/cdhalb04.eff~ // used by cavalier kit bonus, make new eff for dragon's bane
COPY_EXISTING ~lycandam.eff~ ~override/cdsw1h03.eff~ // lycandam also used by amul18, so make new one for kondar

// first, move effs from global effects to melee/ranged headers
COPY_EXISTING ~blun18.itm~   ~override~ // skullcrusher - move skullcr
              ~blun23.itm~   ~override~ // bone club +2, +3 vs. undead - move unddam1
              ~carsomyr.itm~ ~override~ // Carsomyr +5, damage applied globally (2h, unused) - move holyaven
              ~chevil10.itm~ ~override~ // Flame Of The North, damage applied globally (undroppable sarevok weapon) - move flamenor
              ~dagg09.itm~   ~override~ // silver dagger - move lycndam3
              ~halb04.itm~   ~override~ // Dragon's Bane +3, damage applied globally (2h) - move dragdam3, rename to cdhalb04
              ~hamm04.itm~   ~override~ // hammer +1, +4 vs. giantkin - move giantdam
              ~hsword.itm~   ~override~ // Holy Sword, damage applied globally (unused) - move holyaven
              ~phanblad.itm~ ~override~ // phantom blade - move phanblad
              ~sw1h03.itm~   ~override~ // sword +1, +3 vs. shapeshifters - move doppdam, move and rename lycandam to cdsw1h03
              ~sw1h18.itm~   ~override~ // sword o' balduran - move lycndam4
              ~sw1h24.itm~   ~override~ // flame tongue - move trolld1, ft1-3dam
              ~sw1h31.itm~   ~override~ // daystar - move evildam2 (dbldead, daystar1 already here)
              ~sw1h54.itm~   ~override~ // equalizer - move equal01-8
              ~sw1h62.itm~   ~override~ // foebane +3 - move sw1h62b-f
              ~sw1h63.itm~   ~override~ // foebane +5 - move sw1h62b-f
              ~sw1h64.itm~   ~override~ // purifier +4 - move sw1h64a
              ~sw1h65.itm~   ~override~ // purifier +5 - move sw1h65a
              ~sw2h10.itm~   ~override~ // Carsomyr +5, damage applied globally (2h) - move holyaven
              ~sw2h12.itm~   ~override~ // Flame Of The North, damage applied globally (2h) - move flamenor
              ~sw2h19.itm~   ~override~ // Carsomyr +6, damage applied globally (2h) - move sw2h19a
              ~wamace.itm~   ~override~ // jerrod's mace - move damacea
  READ_LONG   0x64 "abil_off"
  READ_SHORT  0x68 "abil_num"
  READ_LONG   0x6a "fx_off"
  READ_SHORT  0x70 "fx_num"
  SET delta = 0
  FOR (index = 0 ; index < fx_num ; ++index) BEGIN
    READ_SHORT ("%fx_off%" +        ("%index%" * 0x30)) "type"
    READ_ASCII ("%fx_off%" + 0x14 + ("%index%" * 0x30)) "eff_file"
    PATCH_IF (("%type%" = 177) AND
              (("%eff_file%" STRING_COMPARE_CASE "DAMACEA" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "DOPPDAM" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "DRAGDAM3" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "EVILDAM2" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "FLAMENOR" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "GIANTDAM" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "HOLYAVEN" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "LYCANDAM" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "LYCNDAM3" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "LYCNDAM4" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "PHANBLAD" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "SKULLCR" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "SW2H19A" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "UNDDAM1" = 0) OR
               ("%eff_file%" STRING_COMPARE_CASE "TROLLD1" = 0) OR
               ("%eff_file%" STRING_COMPARE_REGEXP "EQUAL0[1-8]" = 0) OR
               ("%eff_file%" STRING_COMPARE_REGEXP "FT[1-3]DAM" = 0) OR
               ("%eff_file%" STRING_COMPARE_REGEXP "SW1H6[45]A" = 0) OR
               ("%eff_file%" STRING_COMPARE_REGEXP "SW1H62[BCDEF]" = 0))) BEGIN
      PATCH_IF ("%eff_file%" STRING_COMPARE_CASE "lycandam" = 0) BEGIN SPRINT eff_file "cdsw1h03" END
      PATCH_IF ("%eff_file%" STRING_COMPARE_CASE "dragdam3" = 0) BEGIN SPRINT eff_file "cdhalb04" END
      READ_ASCII   ("%fx_off%" +        ("%index%" * 0x30)) "clone" (48)
      DELETE_BYTES ("%fx_off%" +        ("%index%" * 0x30)) 0x30
      SET fx_num = (fx_num - 1)
      SET delta  = (delta - 1)
      SET index  = (index - 1)
      INNER_ACTION BEGIN // lazy, just grab ids targeting from eff

        COPY_EXISTING ~%eff_file%.eff~ ~override~
          READ_ASCII 0x1c "ids" (8)
          BUT_ONLY

      END
      FOR (index2 = 0 ; index2 < abil_num ; index2 = index2 + 1) BEGIN
        READ_BYTE   ("%abil_off%" +        ("%index2%" * 0x38)) "type"
        READ_SHORT  ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) "abil_fx_idx"
        SET "abil_fx_idx" = "%abil_fx_idx%" + "%delta%"
        WRITE_SHORT ("%abil_off%" + 0x20 + ("%index2%" * 0x38)) "%abil_fx_idx%"
        PATCH_IF (("%type%" = 1) OR ("%type%" = 2)) BEGIN
          INSERT_BYTES (fx_off +        (0x30 * abil_fx_idx)) 0x30
          WRITE_ASCIIE (fx_off +        (0x30 * abil_fx_idx)) "%clone%"
          WRITE_SHORT  (fx_off +        (0x30 * abil_fx_idx)) 177     // use eff
          WRITE_BYTE   (fx_off + 0x02 + (0x30 * abil_fx_idx))   2     // preset target
          WRITE_ASCIIE (fx_off + 0x04 + (0x30 * abil_fx_idx)) "%ids%" // ids target from eff
          WRITE_BYTE   (fx_off + 0x0c + (0x30 * abil_fx_idx))   1     // instant/perm
          WRITE_ASCIIE (fx_off + 0x14 + (0x30 * abil_fx_idx)) "%eff_file%" #8 // for the items with changing effs
          READ_SHORT  ("%abil_off%" + 0x1e + ("%index2%" * 0x38)) "abil_fx_num"
          WRITE_SHORT ("%abil_off%" + 0x1e + ("%index2%" * 0x38)) ("%abil_fx_num%" + 1)
          SET delta = (delta + 1)
        END
      END
    END
  END
  WRITE_SHORT  0x70 "%fx_num%"
  BUT_ONLY IF_EXISTS

// since targeting is changing, patch separately
COPY_EXISTING ~hamm06.itm~ ~override~ // dwarven thrower
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 177 END
  PATCH_FOR_EACH race IN 142 113 BEGIN // giants and ogres
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 177 target = 2 timing = 1 parameter1 = race parameter2 = 4 type = 2 STR_VAR resource = "cdhamm06" END // ranged
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 177 target = 2 timing = 1 parameter1 = race parameter2 = 4 type = 1 STR_VAR resource = "hamm06" END   // melee
  END

// now patch effs from above to do damage directly instead of damage vs. type (damage matches weapon)
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_effing_effs BEGIN
  cdhalb04 =>  16 // Dragon's Bane +3
  cdsw1h03 => 256 // new eff for bastard sword +1, +3 vs. shapeshifters
  damacea  =>   0 // jerrod's mace
  doppdam  => 256 // bastard sword +1, +3 vs. shapeshifters
  equal01  => 256 // equalizer
  equal02  => 256 // equalizer
  equal03  => 256 // equalizer
  equal04  => 256 // equalizer
  equal05  => 256 // equalizer
  equal06  => 256 // equalizer
  equal07  => 256 // equalizer
  equal08  => 256 // equalizer
  evildam2 => 256 // daystar
  flamenor => 256 // flame of the north
  ft1dam   => 256 // flame tongue
  ft2dam   => 256 // flame tongue
  ft3dam   => 256 // flame tongue
  giantdam =>   0 // hammer +1, +4 vs giantkin
  hamm06   =>   0 // dwarven thrower
  holyaven => 256 // carsomyr +5
  lycndam3 =>  16 // silver dagger
  lycndam4 => 256 // sword o' balduran
  phanblad => 256 // phantom blade
  skullcr  =>   0 // skullcrusher's eff
  sw1h62b  => 256 // foebane(s)
  sw1h62c  => 256 // foebane(s)
  sw1h62d  => 256 // foebane(s)
  sw1h62e  => 256 // foebane(s)
  sw1h62f  => 256 // foebane(s)
  sw1h64a  => 256 // purifier +4
  sw1h65a  => 256 // purifier +5
  sw2h19a  => 256 // carsomyr +6
  trolld1  => 256 // flame tongue
  unddam1  =>   0 // bone club eff
END

ACTION_PHP_EACH cd_effing_effs AS eff => damtype BEGIN

  COPY_EXISTING ~%eff%.eff~ ~override~
    WRITE_LONG  0x10 12      // change to damage
    WRITE_LONG  0x14 2       // preset target
    WRITE_SHORT 0x20 0       // normal
    WRITE_SHORT 0x22 damtype // damage type
    WRITE_LONG  0x24 1       // timing: instant/permanent
    READ_LONG   0x60 dam     // read and move
    WRITE_LONG  0x60 0       // from here
    WRITE_LONG  0x1c dam     // to there
    IF_EXISTS

END

// create hamm06 copy for missile damage
COPY_EXISTING ~hamm06.eff~ ~override/cdhamm06.eff~
  WRITE_SHORT 0x22 128 // missile damage type

// effs for root of the problem
COPY_EXISTING ~giandam.eff~  ~override~
              ~monstdam.eff~ ~override~
              ~undeddam.eff~ ~override~
  WRITE_LONG 0x14 2 // preset target
  WRITE_LONG 0x1c 2 // should be doing two extra damage, not three
  WRITE_LONG 0x60 0 // param3 should be 0
  BUT_ONLY

// effs for dragon slayer
COPY_EXISTING ~sw1h32c.eff~ ~override~
  WRITE_LONG 0x60 0 // param3 should be 0
  BUT_ONLY

// wrong target for bone club's +1 thac0 vs undead
COPY_EXISTING ~undhit1.eff~  ~override~
//  WRITE_LONG 0x14 2 // preset target
  WRITE_LONG 0x1c 4 // undead from
  WRITE_LONG 0x20 3 // general.ids
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Game text update                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Game text update light                           \\\\\
/////                                                  \\\\\

BEGIN @1000 DESIGNATED 1000 // GTU Light (by Wisp)
SUBCOMPONENT @1  // Game text update
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE
REQUIRE_PREDICATE ((FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtul.tra~) AND
                   (FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtul.tpa~)) @5

LOAD_TRA ~bg2fixpack/languages/%LANGUAGE%/gtul.tra~
INCLUDE  ~bg2fixpack/languages/%LANGUAGE%/gtul.tpa~

/////                                                  \\\\\
///// Game text update, original Baldurdash            \\\\\
/////                                                  \\\\\

BEGIN @1001 DESIGNATED 1001 // GTU Classic (from Baldurdash, by Kevin Dorner)
SUBCOMPONENT @1  // Game text update
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE
REQUIRE_PREDICATE ((FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtu.tra~) AND
                   (FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtu.tpa~)) @5

LOAD_TRA ~bg2fixpack/languages/%LANGUAGE%/gtu.tra~
INCLUDE  ~bg2fixpack/languages/%LANGUAGE%/gtu.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// modder pack                                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3 DESIGNATED 2 // modder pack
DEPRECATED @22 //Remove this line if you want the component back

INCLUDE ~bg2fixpack/lib/modder.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BETA Core Fixes                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @20 DESIGNATED 3 // Beta Core Fixes
REQUIRE_PREDICATE MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~0~ @33 // skip if core isn't installed

INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions
INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches.tpa~ // batch patches, mainly immunity stuff

/////                                                  \\\\\
///// string fixes                                     \\\\\
/////                                                  \\\\\

// first column is strref to be updated, second column is tra reference (e.g. 150 uses @150 from the tra file)
// item speed and weight fixes now done as part of the item fixes
COPY ~bg2fixpack/lib/strings.tpa~ ~bg2fixpack/lib/strings_beta.tpa~
ACTION_CLEAR_ARRAY cd_string_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_string_fixes BEGIN

   8212 => 227 // NRD has a special casting time equivalent to the selected spell, not a fixed casting time of 5
   8848 => 223 // Jiscanan is the younger son, not the oldest
  11015 => 226 // shield of the archons does not work against dispel magic
  17709 => 233 // wand of cloudkill forces save vs. poison at -4, not spell; aoe is 15' radius, not 30'
  20882 => 254 // missing thac0/damage bonuses, wrong speed and min strength for balduran's sword
  25943 => 234 // contingency duration is essentially permanent
  32128 => 231 // arundel's staff: belhifet is a devil, not a demon
  32145 => 232 // jerrod's mace: belhifet is a devil, not a demon
  38570 => 230 // false dawn does not last 5 rounds, it's instant with a delayed confusion effect; false dawn aoe is 15' radius, not 30'
//  39529 => 257 // save vs. poison is at +6 ; revise pixie prick item instead
  39536 => 258 // document the save bonus of delver's plate
  40634 => 220 // moon dog's healing lick heals 20 HP not 8
  44795 => 225 // option to sacrifice skill is not an option
  48015 => 224 // Keldorn spends a day, not a week, with his family
  48184 => 221 // Neb is a gnome, not a dwarf
  48202 => 216 // Neb is a gnome, not a dwarf
  50121 => 222 // Neb is a gnome, not a dwarf

  // most spell AoE descripts fixed automaticaly below, but a handful also mention their AoE in the descript so we have to do them manually
   7469 => 239 // mass cure aoe is 15' radius, not 20'
  12175 => 249 // cloudkill aoe is 15' radius, not 40' x 20' x 20' cloud; has to go here since AoE covers two lines in header
  12178 => 247 // ice storm aoe is 15' radius, not 30'
  12193 => 244 // haste aoe is 15' radius, not 40' cube
  12213 => 236 // strength of one aoe is party members in 15' radius, not 'all party'
  12216 => 235 // bless aoe is 15' radius, not 50' cube
  14285 => 241 // sunray aoe is 15' radius, not 20'
  17647 => 245 // skull trap aoe is 12' radius, not 30', and trigger distance is 8' not 20'
  17657 => 252 // wand of sleep aoe is 15' radius, not 40' cube/20' radius
  22174 => 248 // emotion aoe is 15' radius, not 30' nor visual sight of caster
  22830 => 237 // defensive harmony aoe is 15' radius, not 10'
  25623 => 238 // true seeing aoe is 120' radius, not 70'
  25764 => 242 // holy word aoe is 15' radius, not 30'
  25872 => 246 // detect illusion aoe is 15' radius, not 20'
  25922 => 250 // oracle aoe is 120' radius, not 60'
  25936 => 251 // true sight aoe is 120' radius, not 70'
  35658 => 243 // unholy word aoe is 15' radius, not 30'
  39430 => 253 // true seeing via book of infinite spells aoe is 120' radius, not 70'
  45007 => 240 // pixie dust aoe is 15' radius, not 30'
//  17709 => 253 // wand of cloudkill aoe is 15' radius, not 30' - fixed above already

END

ACTION_IF tob_game THEN BEGIN // tob strings

  // supplement above array with tob strings
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_string_fixes BEGIN

    64326 => 228 // improved chaos shield lasts two hours, not two turns
    66369 => 229 // serpent shaft poison lasts two rounds, not one

  END

END

ACTION_PHP_EACH cd_string_fixes AS string => tra BEGIN

  ACTION_IF FILE_CONTAINS_EVALUATED(~bg2fixpack/languages/%LANGUAGE%/setup.tra~ ~^[ %TAB%]*@%tra%[ %TAB%]*=~) BEGIN // check string is in language's tra file

    COPY ~bg2fixpack/lib/strings_beta.tpa~ ~bg2fixpack/lib/strings_beta.tpa~ // build library of string sets
      REPLACE_TEXTUALLY ~\(//replace\)~ ~STRING_SET %string% @%tra%
\1~

  END

END

REINCLUDE ~bg2fixpack/lib/strings_beta.tpa~ // now execute newly built library

// listed AoE for just about every AoE spell is wrong. These ones can be fixed automatically:
ACTION_CLEAR_ARRAY cd_aoe_string_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_aoe_string_fixes BEGIN

   3963 =>  15 // holy smite aoe is 15' radius, not 20'
   4350 =>  15 // unholy blight aoe is 15' radius, not 20'
   6939 =>  15 // greater command aoe is 15' radius, not 20'
   7371 =>  15 // necklace of missiles aoe is 15' radius, not 30'
   7622 =>  15 // death fog aoe is 15' radius, not 30'
   7664 =>  15 // incendiary cloud aoe is 15' radius, not 30'
   7680 =>  15 // horrid wilting aoe is 15' radius, not 30' cube
   7712 =>  15 // wail of the banshee aoe is 15' radius, not 30'
   7918 =>  15 // death spell aoe is 15' radius, not 30'
  10985 =>  15 // confusion aoe is 15' radius, not 60' cube
  12145 =>  15 // remove magic aoe is 15' radius, not 30' cube
  12151 =>  15 // resist fear aoe is 15' radius, not 30'
  12154 =>  15 // grease aoe is 15' radius, not 30' x 30' square
  12163 => 120 // detect invisible aoe is 120' radius, not 70'
  12177 =>  15 // fireball aoe is 15' radius, not 30' cube
  12181 =>  15 // stinking cloud aoe is 15' radius, not 30'
  12188 =>  15 // horror aoe is 15' radius, not 30' cube
  12196 =>  15 // slow aoe is 15' radius, not 40' cube
  12199 =>  15 // dispel magic (divine) aoe is 15' radius, not 30' cube
  12200 =>  12 // glyph of warding aoe is 12' radius, not 30'
  12204 =>  15 // remove fear aoe is 15' radius, not 30'
  12205 =>  15 // remove paralysis aoe is 15' radius, not 20'
  12208 =>  15 // entangle aoe is 15' radius, not 40'
  12217 =>  15 // chant aoe is 15' radius, not 30'
  12221 => 120 // invisibility purge aoe is 120' radius, not 30'
  17287 =>  15 // wand of fear aoe is 15' radius, not 30'
  17362 =>  12 // symbol, fear (arcane) aoe is 12' radius, not 30'
  20579 =>  15 // potion of explosions aoe is 15' radius, not 30'
  22186 =>  15 // greater malison aoe is 15' radius, not 60' cube
  22611 =>  15 // chaos aoe is 15' radius, not 60' cube
  25755 =>  15 // fire storm aoe is 15' radius, not 20'
  25954 =>  15 // sphere of chaos aoe is 15' radius, not 30'
  25959 =>  12 // delayed blast fireball aoe is 12' radius, not 30' (triggers at 8')
  26347 =>  15 // meteor swarm aoe is 15' radius, not 30'
  32428 =>  15 // mass invisibility aoe is 15' radius, not 30'
  38595 =>  15 // glitterdust aoe is 15' radius, not 20'
  39427 =>  15 // fireball via book of infinite spells aoe is 15' radius, not 30'
  39436 =>  15 // stinking cloud via book of infinite spells aoe is 15' radius, not 30'
  39453 =>  15 // sunray via daystar aoe is 15' radius, not 30'
  39520 =>  15 // sonic boom via sling of arvoreen aoe is 15' radius, not 60'
  39567 =>  15 // helm of brilliance fireball, sunray aoe is 15' radius, not 30'
  39568 =>  15 // death spell via skull of death aoe is 15' radius, not 30'
  39671 =>  15 // dispel fear via azlaer's harp aoe is 15' radius, not 30'
  39957 =>  12 // symbol, stun (arcane) aoe is 12' radius, not 30'
  39965 =>  15 // sunfire aoe is 15' radius, not 30'
  39967 =>  12 // symbol, death (arcane) aoe is 12' radius, not 30'
  44946 =>  15 // nature's beauty aoe is 15' radius, not 5'
  45055 =>  15 // earthquake aoe is 15' radius, not 40'
  45058 =>  12 // symbol, death (divine) aoe is 12' radius, not 30'
  45059 =>  12 // symbol, stun (divine) aoe is 12' radius, not 30'
  45821 =>  15 // dispel magic (arcane) aoe is 15' radius, not 30' cube
//  14320 =>  12 // symbol, fear (divine) aoe is 12' radius, not 30'; fixed in @141
//  25894 =>  15 // teleport field aoe is 15' radius, not 30'; fixed in @156
//  38570 =>  15 // false dawn aoe is 15' radius, not 30' - fixed above

END

ACTION_IF tob_game THEN BEGIN // tob fixes

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_aoe_string_fixes BEGIN

    63220 =>  15 // dragon's breath aoe is 15' radius, not 30'
    63584 =>  15 // comet aoe is 15' radius, not 30'
    63743 =>  15 // storm of vengeance aoe is 15' radius, not 30'

  END

END

OUTER_SPRINT text_match @34  // regexp for finding AoE info in spell descript header
OUTER_SPRINT radius_text @35 // outro text for revised AoE info

ACTION_PHP_EACH cd_aoe_string_fixes AS string => radius BEGIN

  ACTION_GET_STRREF string desc

  OUTER_PATCH_SAVE desc ~%desc%~ BEGIN
    REPLACE_TEXTUALLY ~%text_match%~ ~\1%radius%%radius_text%~
  END
  
  STRING_SET_EVALUATE string ~%desc%~

END

/////                                                  \\\\\
///// ids fixes                                        \\\\\
/////                                                  \\\\\

// dragon breath attacks hit multiple times - separate into visual & damage spells a la red/green dragon breath
// see also spin595.spl, spin596.spl, spin691.spl, spin832.spl, spin833.spl, spin893.spl, dragyell.bcs, dragbrow.bcs, dragblac.bcs, draghell.bcs, dragsilv.bcs, shadra01.bcs, test99.bcs
COPY_EXISTING ~dragblck.pro~ ~override/cddrblck.pro~ // create non-repeating, non-animating projectiles (dragred vs redhit)
              ~dragsilv.pro~ ~override/cddrsilv.pro~
  WRITE_SHORT 0x00a 60     // speed increase
  WRITE_ASCII 0x010 ~~  #8 // remove travel sound
  WRITE_ASCII 0x100 ~~ #12 // remove area flags, animation BAM
  WRITE_ASCII 0x11c ~~ #23 // remove palette, projectile color 1-7, smoke puff delay, smoke color 1-7
  WRITE_BYTE  0x133 1      // face target: do not face target
  WRITE_SHORT 0x134 0      // no smoke animation
  WRITE_SHORT 0x200 BIT0   // visible flag
  WRITE_SHORT 0x204 256    // trap size
  WRITE_SHORT 0x206 256    // explosion size
  WRITE_SHORT 0x210 100    // explosion frequency
  WRITE_BYTE  0x216 1      // animation repetitions
  WRITE_SHORT 0x224 0      // cone width
ADD_PROJECTILE ~override/cddrblck.pro~
ADD_PROJECTILE ~override/cddrsilv.pro~

/////                                                  \\\\\
///// misc 2da fixes                                   \\\\\
/////                                                  \\\\\

// avenger druids getting imp. invisibility one level too early
COPY_EXISTING ~clabdr04.2da~ ~override~
  REPLACE_TEXTUALLY ~[ %TAB%]GA_SPDR401[ %TAB%]~ ~ ****       ~
APPEND ~clabdr04.2da~ ~ABILITY7    ****        ****        ****        ****        ****        ****        GA_SPDR401  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****    ****        ****        ****        ****        ****        ****        ****       ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****~

// fallen paladins/rangers should not cast spells via script; see also spcl234d.spl
COPY_EXISTING ~clabpa05.2da~ ~override~ // fallen paladin kit
              ~clabrn05.2da~ ~override~ // fallen ranger kit
  SET_2DA_ENTRY 3 1 41 AP_SPCL234D      // permanently disables divine spellcasting
  PRETTY_PRINT_2DA

// pellan's +2 large shield is unique; leaving lone copy on Chak, see also gorcamb.cre
COPY_EXISTING ~rndequip.2da~ ~override~
              ~rndwep.2da~   ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]SHLD\)19\([ %TAB%]\)~ ~\130\2~

// cure medium wounds lacks a description when offered in temple services
OUTER_SET string = RESOLVE_STR_REF (@217)
APPEND ~speldesc.2da~ ~SPPR315     %string%~

/////                                                  \\\\\
///// mass compile/copy actions actions                \\\\\
/////                                                  \\\\\

// list these individually for reference instead of a bulk copy
COPY ~bg2fixpack/copy/ar6011sr.bmp~ ~override~ // swampy water produces metallic footsteps
     ~bg2fixpack/copy/cdca30.eff~   ~override~ // new template eff for target-limited charm spells; creates charm icon via eff
     ~bg2fixpack/copy/cdwi212.spl~  ~override~ // mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
     ~bg2fixpack/copy/cdwi311.spl~  ~override~ // mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
     ~bg2fixpack/copy/cdwi408.spl~  ~override~ // mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
     ~bg2fixpack/copy/mlizca1.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizca1e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizca2.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizca2e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcca.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizccae.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcgh.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcghe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcgu.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcgue.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsc.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsce.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsd.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsde.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsl.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsle.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcsp.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcspe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcwk.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizcwke.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizha1.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizha1e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizha2.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizha2e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhca.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhcae.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhgh.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhghe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhgu.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhgue.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsc.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsce.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsd.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsde.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsl.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsle.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhsp.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhspe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhwk.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizhwke.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqa1.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqa1e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqa2.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqa2e.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqca.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqcae.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqgh.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqghe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqgu.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqgue.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsc.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsce.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsd.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsde.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsl.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsle.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqsp.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqspe.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqwk.bam~  ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/mlizqwke.bam~ ~override~ // lizardmen weapon anims from IWD
     ~bg2fixpack/copy/spcl234d.spl~ ~override~ // fallen paladins/rangers should not cast spells via script; see also clabpa05.2da and clabrn05.2da
     ~bg2fixpack/copy/spin722a.eff~ ~override~ // great druid title change should retain 'fighter' part fot multi/dual-classes; see also spin722.spl, spin722b.eff, spin722c.eff
     ~bg2fixpack/copy/spwi117a.spl~ ~override~ // for chill touch fixes; see chillt.itm, spwi117a.eff
     ~bg2fixpack/copy/wphaxinv.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wphaxoin.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wphflinv.bam~ ~override~ // paperdoll flail coloring fix
     ~bg2fixpack/copy/wphmcinv.bam~ ~override~ // paperdoll mace coloring fix
     ~bg2fixpack/copy/wplaxinv.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wplaxoin.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wplflinv.bam~ ~override~ // paperdoll flail coloring fix
     ~bg2fixpack/copy/wplmcinv.bam~ ~override~ // paperdoll mace coloring fix
     ~bg2fixpack/copy/wpmaxinv.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpmaxoin.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpmflinv.bam~ ~override~ // paperdoll flail coloring fix
     ~bg2fixpack/copy/wpmmcinv.bam~ ~override~ // paperdoll mace coloring fix
     ~bg2fixpack/copy/wpnaxinv.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpnaxoin.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpnflinv.bam~ ~override~ // paperdoll flail coloring fix
     ~bg2fixpack/copy/wpnmcinv.bam~ ~override~ // paperdoll mace coloring fix
     ~bg2fixpack/copy/wpsaxinv.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpsaxoin.bam~ ~override~ // paperdoll axe coloring fix
     ~bg2fixpack/copy/wpsflinv.bam~ ~override~ // paperdoll flail coloring fix
     ~bg2fixpack/copy/wpsmcinv.bam~ ~override~ // paperdoll mace coloring fix
COMPILE ~bg2fixpack/dlg/beta_soa-dlg.d~
        ~bg2fixpack/compile/bystand1.baf~ // ler should approach PC after irenicus/cw carnage; see also bystand1.cre

ACTION_IF tob_game THEN BEGIN // additional copy & compile for ToB

  COPY ~bg2fixpack/copy-tob/xl3000.2da~   ~override~ // corrected travel links for watcher's keep
       ~bg2fixpack/copy-tob/xnewarea.2da~ ~override~ // corrected travel links for watcher's keep

  COMPILE ~bg2fixpack/dlg/beta_tob-dlg.d~            // all ToB dialogue fixes
  
  // can skip oasis by using WK > amketheran link
  COPY_EXISTING ~worldm25.wmp~ ~override~
    LPF DELETE_WORLDMAP_LINKS STR_VAR from_area = ar3000 to_area = ar5500 END
    BUT_ONLY

END


/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

// weidu doesn't evaluate vars in d code, so we do it with decompile/patch/compile on bhoisig, bhnalla, demson, borinall, travin

// telwyn should not spawn for cleric-rangers; see also ar0900.bcs, ar0901.bcs, ar0904.bcs; only ar0901.bcs actually causes problems, but may as well standardize all these triggers while we're here
COPY_EXISTING ~bhoisig.dlg~  ~override~
              ~bhnalla.dlg~  ~override~
              ~borinall.dlg~ ~override~
              ~travin.dlg~   ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~OR(5)[ %TAB%%LNL%%MNL%%WNL%]+\(Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\)~
      ~OR(6) \1 Class(Player1,CLERIC_RANGER)~
  END
  BUT_ONLY

COPY_EXISTING ~bhoisig.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~\(!Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_MAGE_CLERIC)\)~
      ~\1 !Class(Player1,CLERIC_RANGER)~
  END
  BUT_ONLY

// various dead/alive checks on the farmers missing half of them; see also d code in beta_soa-dlg.d
COPY_EXISTING ~demson.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~OR(3)[ %TAB%%LNL%%MNL%%WNL%]+Dead("plfarm01")[ %TAB%%LNL%%MNL%%WNL%]+Dead("plfarm02")[ %TAB%%LNL%%MNL%%WNL%]+Dead("plfarm03")~ // replies 3 & 5 on state 47
      ~OR(6) Dead("plfarm01") Dead("plfarm02") Dead("plfarm03") Dead("plfarm04") Dead("plfarm05") Dead("plfarm06")~
  END
  BUT_ONLY
  
// give summoned ettercaps and wyverns the ability to use their poison abilities
EXTEND_TOP ~ettercap.bcs~ ~bg2fixpack/baf/ettercap.baf~
EXTEND_TOP ~wyvern.bcs~   ~bg2fixpack/baf/wyvern.baf~

COPY_EXISTING ~murdbegg.dlg~ ~override~ // rampah's dialogue can be interrpted by vamp encounter, borking his info dump and giving you hides
              ~yochlol.dlg~  ~override~ // vic's yochlol will attack since priestess's dialogue doesn't pause the game
  WRITE_LONG 0x30 0

/////                                                  \\\\\
///// player AI script fixes                           \\\\\
/////                                                  \\\\\

// thief scout lacks class checks in hide() block, can also end up attacking friendlies
COPY ~scripts/thief4.bs~ ~scripts/thief4.bs~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Delay(3)~ ~Delay(3) OR(3) Class(Myself,THIEF_ALL)  Class(Myself,RANGER_ALL) Class(Myself,MONK)~
    REPLACE_TEXTUALLY ~AttackReevaluate(LastSeenBy(Myself),30)~ ~AttackReevaluate(NearestEnemyOf(Myself),30)~
  END
  BUT_ONLY

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

// anomen quest messenger can spawn in random encounter areas and during combat
// another double partyrested() trigger; see also banomen.dlg
COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(!Exists("Terl")\)~ ~\1 CombatCounter(0) !AreaCheck("AR0041") !AreaCheck("AR0042") !AreaCheck("AR0043") !AreaCheck("AR0044") !AreaCheck("AR0045") !AreaCheck("AR0046") !AreaCheck("AR2601")~
    REPLACE_TEXTUALLY ~\(Global("BAnomen4","LOCALS",0)[ %TAB%%%LNL%%MNL%%WNL%]+THEN[ %TAB%%%LNL%%MNL%%WNL%]+RESPONSE #100[ %TAB%%%LNL%%MNL%%WNL%]+\)~ ~\1SetGlobal("BAnomen4","LOCALS",1)~
  END
  BUT_ONLY

// kill block from wrong area
COPY_EXISTING ~ar0327.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~Global("PalernSpawn","AR0328",0)~ ~False()~
  END
  BUT_ONLY

// prevent dupe escorts st the CC
// gorf can get lost walking over for mazzy fight; spawn him closer
COPY_EXISTING ~ar0406.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(CreateCreature("\)\(escort[123]\)~ ~ActionOverride("\2",DestroySelf()) \1\2~
    REPLACE_TEXTUALLY ~CreateCreatureOffScreen("gorf",8)~   ~CreateCreature("gorf",[1125.1980],10)~
  END
  BUT_ONLY

// prevent paac from sending knights home offscreen; see also obssol01.bcs
COPY_EXISTING ~ar0411.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("SpawnPaac","GLOBAL",1)~ ~False()~ // moving this block to a range check in obssol01
  END
  BUT_ONLY

// if player starts limited wish quest before visiting bridge district, area not revealed
COPY_EXISTING ~ar0500.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN 
    REPLACE_TEXTUALLY ~\(CreateCreature("LOUT",\[1839\.2871\],8)\)~ ~\1 Continue()~
  END
  BUT_ONLY

// more prophets for the Unseeing Eye; see also csgaal.dlg, prophet.bcs
COPY_EXISTING ~ar0500.bcs~ ~override~
              ~ar0700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("GaalSpoke","GLOBAL",1)\)~ ~\1 !Dead("UnseeingEye")~ // don't spawn if eye already dead
  END
  BUT_ONLY

// can ask dryads for flask even if you've already stolen it from them; add journal entry
EXTEND_BOTTOM ~ar0602.bcs~ ~bg2fixpack/baf/ar0602.baf~

// if kalah kills quayle in mustard-slime form, also set quayle's normal DV to dead; see BAERIE.dlg 120
EXTEND_BOTTOM ~ar0607.bcs~ ~bg2fixpack/baf/quaylem.baf~

// if player escapes CI solo and/or with Imoen, post-cutscene minsc/jaheira/yoshimo can fire days, weeks, years later
COPY_EXISTING ~ar0700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("PostCutSpeak","AR0700",0)\)~ ~Global("GaelanMove","GLOBAL",0) \1~ // gaelanmove is set when you meet gaelan when you enter the slums
  END
  BUT_ONLY

// mourners never leave, often have nothing to say; see also mourner3.cre, mourner3.dlg, mourner4.dlg
EXTEND_BOTTOM ~ar0800.bcs~ ~bg2fixpack/baf/mourner3.baf~

// dual-classing to a non-neutral cleric during the Unseeing Eye quest breaks the quest; see also ar0902.bcs, ar0904.bcs, bhoisig.dlg
EXTEND_BOTTOM ~ar0900.bcs~ ~bg2fixpack/baf/ar0900.baf~
EXTEND_BOTTOM ~ar0901.bcs~ ~bg2fixpack/baf/ar0901a.baf~

// dual-classing to a non-neutral cleric during the Unseeing Eye quest breaks the quest; see also ar0900.bcs, ar0901.bcs, bhoisig.dlg
COPY_EXISTING ~ar0902.bcs~ ~override~
              ~ar0904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Alignment(Player1,MASK_\(GOOD\|EVIL\))\)~ ~\1 !Global("cd_beholder_dc_shenanigans","GLOBAL",1)~
  END

// telwyn should not spawn for cleric-rangers; see also bhoisig.dlg, bhnalla.dlg, borinall.dlg, travin.dlg; only ar0901.bcs actually causes problems, but may as well standardize all these triggers while we're here
COPY_EXISTING ~ar0900.bcs~ ~override~ // main temple district script
              ~ar0901.bcs~ ~override~ // helm temple
              ~ar0904.bcs~ ~override~ // talos temple
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(5)[ %TAB%%LNL%%MNL%%WNL%]+\(Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\)~
      ~OR(6) \1 Class(Player1,CLERIC_RANGER)~
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "ar0901" = 0) BEGIN // one more just for ar0901.bcs
      REPLACE_TEXTUALLY ~\(!Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_MAGE_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+\)~
        ~\1 !Class(Player1,CLERIC_RANGER)~
    END
  END
  BUT_ONLY

// journal entry mentions note AND journal; should wait until party has both
COPY_EXISTING ~ar1102.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR(2)~ ~~
  END
  BUT_ONLY

// samia's party can spawn even if she's dead; see also akae.dlg
COPY_EXISTING ~ar1202.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("SpawnSamiaFriends","AR1202",0)[ %TAB%%LNL%%MNL%%WNL%]+\)!Dead("Samia")~ ~\1~
  END
  BUT_ONLY
  
// killing lonk and going downstairs can possibly break sequence here
EXTEND_BOTTOM ~ar1515.bcs~ ~bg2fixpack/baf/ar1515.baf~

// if player already has tear, hell demon should go away instead of offering the cloak
COPY_EXISTING ~ar2901.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]Global("Player1Fear","GLOBAL",1)\)~ ~OR(2) Global("HellFearTear","GLOBAL",1) \1~
  END
  BUT_ONLY

// orphan-paladin quest in graveyard can be interrupted by actor-active flags; see also ar0800.are
EXTEND_BOTTOM ~arenthis.bcs~ ~bg2fixpack/baf/kamir.baf~
EXTEND_BOTTOM ~kamir.bcs~    ~bg2fixpack/baf/kamir.baf~
EXTEND_BOTTOM ~risa.bcs~     ~bg2fixpack/baf/kamir.baf~

// prevent haz from dying from dead grimwarders; see also arnfgt06.dlg
COPY_EXISTING ~arnfgt06.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("GrimwarderAttack","AR0801",1)\)~ ~\1 !NumDead("bodfgt01",2)~
    REPLACE_TEXTUALLY ~Range(LastSeenBy(Myself),6)~ ~Range([PC],12)~ // increase range so that he always gets in his first dialogue
  END
  BUT_ONLY
  
// gauths and neholders will continue to fire eye beams when asleep
EXTEND_TOP ~behdir01.bcs~ ~bg2fixpack/baf/sleep_no_action.baf~ // director
EXTEND_TOP ~beheld01.bcs~ ~bg2fixpack/baf/sleep_no_action.baf~ // elder orb
EXTEND_TOP ~behold01.bcs~ ~bg2fixpack/baf/sleep_no_action.baf~ // observer, bogstandard beholder
EXTEND_TOP ~behspe01.bcs~ ~bg2fixpack/baf/sleep_no_action.baf~ // spectator
EXTEND_TOP ~gauth01.bcs~  ~bg2fixpack/baf/sleep_no_action.baf~ // gauth

// ellisme clone will stand around when out of spells dur to some bad vars
COPY_EXISTING ~clone1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("DimensionDoor","GLOBAL",12)~ ~~ // prevents attack() at bottom of script
    REPLACE_TEXTUALLY ~Global("CloneBehavior","LOCALS",3)~ ~False()~ // already have a MI block, this unbounded one could stall script
    REPLACE_TEXTUALLY ~SetGlobal("DimensionDoor","GLOBAL",3)~ ~SetGlobal("CloneBehavior","LOCALS",3)~ // wrong var
  END
  BUT_ONLY

// unseeing eye sacrifice cutscene can be interrupted/broken; change to detect any ally/summons
COPY_EXISTING ~cltcut01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Detect(\[PC\])~ ~Detect([GOODCUTOFF])~
  END
  BUT_ONLY

// death ward actually prevents irenicus' insta-kill at the asylum
COPY_EXISTING ~cut41k.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY  ~\(ReallyForceSpell(Player1,JON_ALL_DEAD)\)~ ~\1 Kill(Player2) Kill(Player3) Kill(Player4) Kill(Player5) Kill(Player6) Kill(Player1)~
  END
  BUT_ONLY

// if aerie is in slot 6, she's too far away to start the dialogue with quayle after kalah's death; moving to 536.555
COPY_EXISTING ~cut86a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ActionOverride(Player6,LeaveAreaLUA("AR0607","",\[\)[0-9]+\.[0-9]+\(\],0))\)~ ~\1536.555\2~
  END
  BUT_ONLY

// wrong string over head (20820 - string of Firkraag)
COPY_EXISTING ~delon.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayStringHead(Myself,20820)~ ~DisplayStringHead(Myself,20821)~
  END
  BUT_ONLY

// djinnis used 'contingency-stoneskin' string before PFMW; also alter script to be used when summoned as allies
COPY_EXISTING ~djinni01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~DisplayString(Myself,29736)~ ~DisplayString(Myself,40252)~ // contingency-stoneskin to contingency-PFMW
    REPLACE_TEXTUALLY ~Allegiance(Myself,ENEMY)~ ~See(NearestEnemyOf(Myself))~    // use same abilities when summoned
  END
  BUT_ONLY

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~djinni01.bcs~ ~override~ // summoned efreeti shouldn't go to gas
              ~efreet01.bcs~ ~override~ // summoned efreeti shouldn't go to gas
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalLT("\(Djinni\|Efreeti\)GasForm","LOCALS",3)\)~ ~\1 !Gender(Myself,SUMMONED)~
  END
  BUT_ONLY

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~djinni02.bcs~ ~override/cdgasnd.bcs~ // new script for gaseous forms, djinni
              ~efreet02.bcs~ ~override/cdgasne.bcs~ // new script for gaseous forms, efreeti
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,\(DJINNI\|EFREETI\)_PHYSICAL_FORM_CHANGE)~ ~ReallyForceSpellRES("%DEST_RES%2",Myself)~
  END
  BUT_ONLY

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~djinni04.bcs~ ~override~ // patch noble efreeti script to use new gaseoous form spell
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,DJINNI_GAS_FORM_CHANGE)~ ~ReallyForceSpellRES("cdgasnd",Myself)~
  END
  BUT_ONLY
  
// sort out journal entries for attacking firkraag; see also firkra02.bcs
EXTEND_TOP ~dragred.bcs~ ~bg2fixpack/baf/dragred.baf~

// dragon breath attacks hit multiple times - separate into visual & damage spells a la red/green dragon breath
// see also cddrblck.pro, cddrsilv.pro, spin595.spl, spin596.spl, spin691.spl, spin832.spl, spin833.spl, spin893.spl
// update scripts which use these spells to do double-casts instead
COPY_EXISTING ~dragblac.bcs~ ~override~ // black dragon
              ~draghell.bcs~ ~override~ // black dragon
              ~test99.bcs~   ~override~ // black dragon
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),BLACK_DRAGON_BREATH)~ ~ForceSpell(\1,BLACK_DRAGON_BREATH) ReallyForceSpellRES("spin691v",\1)~
  END
  BUT_ONLY

COPY_EXISTING ~dragsilv.bcs~ ~override~ // silver dragon
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),SILVER_DRAGON_BREATH_PARALIZATION)~ ~ForceSpell(\1,SILVER_DRAGON_BREATH_PARALIZATION) ReallyForceSpellRES("spin832v",\1)~
    REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),SILVER_DRAGON_BREATH_COLD)~         ~ForceSpell(\1,SILVER_DRAGON_BREATH_COLD) ReallyForceSpellRES("spin833v",\1)~
  END
  BUT_ONLY

COPY_EXISTING ~shadra01.bcs~ ~override~ // shadow dragon
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),SHADOW_DRAGON_BREATH)~ ~ForceSpell(\1,SHADOW_DRAGON_BREATH) ReallyForceSpellRES("spin893v",\1)~
  END
  BUT_ONLY
  
// dead ardulace is required to open the sealed drow city gates, but she disappears once the city is hostile
COPY_EXISTING ~dwplot.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("ADrowTimerHasExpired","GLOBAL",1)\)~ ~\1 !Name("Ardulace",Myself)~
  END
  BUT_ONLY

// edwin shouldn't try to use the nether scroll in dead/wild magic areas; edwin's low-hp banter with aerie uses wrong var, hp threshold... see also bedwin.dlg
COPY_EXISTING ~edwin.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalTimerExpired("EdwinScroll","GLOBAL")\)~ ~\1 !AreaCheck("ar3004") !AreaCheck("ar3005") !AreaCheck("ar3008") !AreaCheck("ar3009")~ // 4, 8 dead; 5, 9 wild
    REPLACE_TEXTUALLY ~Global("BEdwin10","LOCALS",0)\([ %TAB%%LNL%%MNL%%WNL%]+Global("EdwintalksAerieJames","LOCALS",0)\)~ ~Global("BEdwin1","LOCALS",0)\1~ // correct var
  END
  BUT_ONLY

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~efreet04.bcs~ ~override~ // patch noble efreeti script to use new gaseoous form spell
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,EFREETI_GAS_FORM_CHANGE)~ ~ReallyForceSpellRES("cdgasne",Myself)~
  END
  BUT_ONLY

// various creatures use the gp** scripts to escape, breaking other stuff: gparcher, gpkensai
COPY_EXISTING ~gparcher.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!HasItem("POTN55",Myself)\)~ ~\1 !Name("chang01",Myself) !Name("pbhunt02",Myself)~ // angelo, bounty hunter
  END
  BUT_ONLY

// more kangaxx-proofing; see also klkang.dlg
EXTEND_BOTTOM ~hlkang.bcs~ ~bg2fixpack/baf/hlkang.baf~

// without bounds, ilyich will keep setting these vars and shouting when attacked instead of counter-attacking; see also ilyich.dlg
COPY_EXISTING ~ilyich.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(IF[ %TAB%%LNL%%MNL%%WNL%]+AttackedBy(\[ANYONE\],DEFAULT)\)\([ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+Shout(89)[ %TAB%%LNL%%MNL%%WNL%]+SetGlobal("duergarleader","LOCALS",9)[ %TAB%%LNL%%MNL%%WNL%]+\)\(END\)~
      ~\1 !Global("duergarleader","LOCALS",9) \2 Continue() \3~
  END
  BUT_ONLY

// jaheira can leave if PC is jerk about dead khalid; imoen's dialogue should check she's not leaving before trying to comfort her; see imoenj.dlg
COPY_EXISTING ~imoen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]See("Jaheira")\)~ ~InParty("jaheira") \1~
    REPLACE_TEXTUALLY ~\(!See("Jaheira")\)~ ~OR(2) !InParty("jaheira") \1~
  END
  BUT_ONLY

// even if you cure them, jaella and lissa die if you wait too long to speak to jan
COPY_EXISTING ~jaella.bcs~ ~override~
              ~lissa.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(OR(2)[ %TAB%%LNL%%MNL%%WNL%]+GlobalTimerExpired("JanDidPlot","GLOBAL")[ %TAB%%LNL%%MNL%%WNL%]+GlobalTimerExpired("JaellaDead","GLOBAL")\)~ ~\1 !GlobalGT("JanLissaPlot","GLOBAL",9)~
  END
  BUT_ONLY

// various spawns during jaheira's quest shouldn't happen in graveyard or shade lord areas
// jaheira also starts a few banters w/o being in party
// jaheira should start her 'i'm cured' dialogue even if not present when you get her hair; see also jaheiraj.dlg
COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+SetGlobal("JaheiraBanditPlot","GLOBAL",2)\)~ ~!AreaCheck("ar1404") \1~ // bandit spawn
    REPLACE_TEXTUALLY ~\(THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+SetGlobal("JaheiraHarperPlot","GLOBAL",1)\)~ ~!AreaCheck("ar0800") !AreaCheck("ar1404") \1~ // Meronia spawn x2
    REPLACE_TEXTUALLY ~\(THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+SetGlobal("RevianeSpawn","LOCALS",2)\)~ ~!AreaCheck("ar1404") \1~ // reviane
    REPLACE_TEXTUALLY ~\(!Range(\[NEUTRAL\],10)[ %TAB%%LNL%%MNL%%WNL%]THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+IncrementGlobal("DerminSpawn","GLOBAL",1)\)~
      ~!AreaCheck("ar0800") !AreaCheck("ar1404") \1~ // dermin x6
    REPLACE_TEXTUALLY ~\(THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE[ %TAB%]+#100[ %TAB%%LNL%%MNL%%WNL%]+ChangeAIScript("",DEFAULT)\)~ ~!AreaCheck("ar0800") !AreaCheck("ar1404") \1~ // terminsel
    REPLACE_TEXTUALLY ~\(Global("JaheiraMatch","GLOBAL",0)\)~ ~\1 InParty(Myself)~
    APPEND_FILE ~bg2fixpack/baf/jaheira.baf~ // jaheira should start her 'i'm cured' dialogue even if not present when you get her hair, see also jaheiraj.dlg; also initiates dialogue when returning from meriona
  END
  BUT_ONLY

// under very unusual circumstances, keldorn can create multiple zombies to fight; keldorn won't leave after complaining
COPY_EXISTING ~keldorn.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalLT("ActiveKeldorn","LOCALS",2)\)~ ~!Exists("KELZOMB") \1~ // add check for zombie
    REPLACE_TEXTUALLY ~"ActiveKeldorn","LOCALS"~ ~"ActiveKeldorn","AR0701"~ // LOCALS go away on cre-swap
    // 0>1 = visited ar1000, keldorn mentions home; 1>2 - keldorn complains you haven't visited; 2 - keldorn leaves; 3 - keldorn has visited
    REPLACE_TEXTUALLY ~GlobalLT("KeldornComplain","LOCALS",2)~ ~GlobalLT("KeldornComplain","LOCALS",3)~
  END
  BUT_ONLY

// lester and nevin can stop fighting from time to time
EXTEND_BOTTOM ~lester.bcs~ ~bg2fixpack/baf/lester.baf~

// no more infinite stoneskins
COPY_EXISTING ~mage12c.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(DisplayString(Myself,29736)\)~ ~\1 SetGlobal("Prep","LOCALS",2)~
  END
  BUT_ONLY

// can talk constantly due to mismatch trigger for fatigued-Valygar banter
COPY_EXISTING ~mazzy.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~TimeOfDay(DAY)~ ~TimeOfDay(NIGHT)~
  END
  BUT_ONLY
  
// scripting for restored minsc-mazzy banter; see bminsc.dlg
// minsc injured dialogues need better triggers, see also minscj.dlg
COPY_EXISTING ~minsc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~IncrementGlobal("MinscInjured","LOCALS",1)~ ~~ // dialogues increment this variable already; incrementing potentially skips a dialogue
    REPLACE_TEXTUALLY ~GlobalLT("MinscInjured","LOCALS",3)~ ~GlobalLT("MinscInjured","LOCALS",2)~ // only three injured dialogues, not four
    REPLACE_TEXTUALLY ~Global("MinscInjured","LOCALS",3)~   ~Global("MinscInjured","LOCALS",2)~ // only three injured dialogues, not four
    APPEND_FILE ~bg2fixpack/baf/minsc-mazzy.baf~
  END
  BUT_ONLY

// moon doggie will burn its three mirror images since it doesn't check if it's mirror imaged already
COPY_EXISTING ~moondog.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(HaveSpell(HASTE_SELF)\)~ ~\1 !StateCheck(Myself,STATE_MIRRORIMAGE)~ // add statecheck
  END
  BUT_ONLY
  
// prevent nalia from complaining if you are at her keep
COPY_EXISTING ~nalia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalTimerExpired("ArrivedNaliaKeep","GLOBAL")\)~ ~\1 !AreaCheck("AR1300")~
  END
  BUT_ONLY

// prevent paac from sending knights home offscreen; see also ar0411.bcs
EXTEND_BOTTOM ~obssol01.bcs~ ~bg2fixpack/baf/obssol01.baf~ // move paac spawn to range check on reyna

// perth doesn't go hostile if attacked; see also ppcowled.cre
COPY_EXISTING ~initrg15.bcs~ ~override/perth.bcs~
  DECOMPILE_AND_PATCH BEGIN
    APPEND_FILE ~bg2fixpack/baf/perth.baf~
  END

// player can miss lilarcor if inventiry is full during cutcene
COPY_EXISTING ~pipe04.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GiveItemCreate("SW2H14",LastTrigger,0,0,0)~ ~~ // move from original order...
    REPLACE_TEXTUALLY ~\(SetGlobal("PipeOrder","AR0404",4)\)~ ~\1 GiveItemCreate("SW2H14",LastTrigger,0,0,0)~ // to below TakePartytItem calls
  END
  BUT_ONLY

// move bounding var to dialogue for better reliability; see ppbhaal.dlg
COPY_EXISTING ~ppbhaal.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("spoke","LOCALS",1)~ ~~
  END
  BUT_ONLY

// dace's apparition's body can hang out due to flaky die() trigger
COPY_EXISTING ~ppjoye2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Die()~ ~StateCheck(Myself,STATE_REALLY_DEAD)~
  END
  BUT_ONLY

// don't cast buffs on enemies plz
COPY_EXISTING ~priest5.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),CLERIC_DRAW_UPON_HOLY_MIGHT)~ ~Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)~
  END
  BUT_ONLY

// more prophets for the Unseeing Eye; see also ar0500.bcs, ar0700.bcs, csgaal.dlg
COPY_EXISTING ~prophet.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(GlobalGT("DestroyProphets","GLOBAL",0)\)~ ~OR(2) \1 !AreaCheck("ar0900")~
  END
  BUT_ONLY

// slaver guard outside never goes hostile even if ship cleared out
EXTEND_BOTTOM ~slaver1.bcs~ ~bg2fixpack/baf/slaver1.baf~

// animal summons behaving stupidly due to over-shouting
COPY_EXISTING ~sumsht01.bcs~ ~override~
              ~sumsht02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Allegiance(Myself,ALLY)[ %TAB%%LNL%%MNL%%WNL%]+See(\[ANYONE\])[ %TAB%%LNL%%MNL%%WNL%]+ActionListEmpty()~ ~False()~
    REPLACE_TEXTUALLY ~\(Allegiance(Myself,ENEMY)[ %TAB%%LNL%%MNL%%WNL%]+\)See(\[ANYONE\])\([ %TAB%%LNL%%MNL%%WNL%]+ActionListEmpty()\)~ ~\1 See([GOODCUTOFF]) \2~
  END
  BUT_ONLY

// player can activate CI exit from furnace room
COPY_EXISTING ~tele0700.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Range(LastTrigger,25)~ ~Range(LastTrigger,15)~
  END
  BUT_ONLY

// skin dancer drops skin at fixed point instead of at his death point; see also trskin03.dlg
COPY_EXISTING ~trskin03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(DropItem("MISC79",\[\)[0-9]+\.[0-9]+\(\])\)~ ~\1-1.-1\2~ // change coords to -1, -1
  END
  BUT_ONLY

// player can break ust natha if (s)he beats phaere to the door post-gettin' it on
EXTEND_BOTTOM ~udphae01.bcs~ ~bg2fixpack/baf/udphae01_bottom.baf~

// interrupting solaufein while pulling mind flayers out of the astral breaks everything; see also udsola01.bcs
EXTEND_BOTTOM ~udsola01.bcs~ ~bg2fixpack/baf/udsola01.baf~

// solaufein egg drop trigger can be set off by anyone
COPY_EXISTING ~udsoltri.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~IsOverMe([^)]+)~ ~IsOverMe([PC])~
  END
  BUT_ONLY

// turning viconia hostile prevents her death at the stake; see also viconia.bcs vicg1.dlg, victown1.dlg, viconi.dlg
COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(IF[ %TAB%%LNL%%MNL%%WNL%]+!?Allegiance(Myself,\(ENEMY\|NEUTRAL\))\)~ ~\1 Global("ViconiaFree","AR1000",0)~ // intercept her changing to her combat scripting while still bound
    REPLACE_TEXTUALLY ~\(Kill(Myself)\)~ ~SetInterrupt(FALSE) TriggerActivation("ViconiaStake",FALSE) ReallyForceSpell("viconia",CLERIC_FLAME_STRIKE) Wait(1) \1~ // change her simple kill to also include the visuals and other goodies
    APPEND_FILE ~bg2fixpack/baf/viconia.baf~ // this prevents her from combat actions while bound
  END

// hareishan will intiate dialogue through walls and areas
COPY_EXISTING ~vvhareis.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("vvhareisspoke","LOCALS",0)\)~ ~\1 InMyArea(Player1)~
  END
  BUT_ONLY

// player can watch the ice mephit and thief do nothing
COPY_EXISTING ~waitpc2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Range(LastSeenBy(Myself),[0-9]+)~ ~Range(LastSeenBy(Myself),25)~ // vic starts dislaogue so you can't just attack her first
  END
  BUT_ONLY
  
// scripting for restored yoshimo-aerie and yoshimo-minsc banters; see byoshim.dlg
EXTEND_BOTTOM ~yoshimo.bcs~ ~bg2fixpack/baf/yoshimo-aerie.baf~

ACTION_IF tob_game THEN BEGIN // tob stuff
  
  // player1 = mazed means abazigal doesn't deliver his death dialogue, breaking game
  COPY_EXISTING ~abazdrag.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~StartDialog\(ue\)?NoSet(Player1)~ ~StartDialogueNoSet([PC])~
    END
    BUT_ONLY 
  
  // ar3008 spams dead magic every script round due to bad variable scope
  COPY_EXISTING ~ar3008.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"AR3004"~ ~"AR3008"~
    END
    BUT_ONLY

  // force-talking draconis can cause bad things; see also bazdra01.dlg
  COPY_EXISTING ~bazdra01.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~NumTimesTalkedToLT(2)~ ~NumTimesTalkedToGT(0) Global("cd_transform","LOCALS",0)~
    END
    BUT_ONLY

  // gauths and neholders will continue to fire eye beams when asleep
  EXTEND_TOP ~behhiv01.bcs~ ~bg2fixpack/baf/sleep_no_action.baf~ // hive mother
  
  // saradush prison spirit should open a cell door even if player1 is not in the area; change cutscene to be driven by sarspir instead of player1; see also sarspir.bcs
  COPY_EXISTING ~cut224a.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~CutSceneId(Player1)~ ~CutSceneId("sarspir")~
      REPLACE_TEXTUALLY ~[ %TAB%]\(JumpToPoint(\[944\.369\])\)[ %TAB%%LNL%%MNL%%WNL%]~ ~ ActionOverride(Player1,\1) ~
      REPLACE_TEXTUALLY ~[ %TAB%]\(Face(6)\)[ %TAB%%LNL%%MNL%%WNL%]~                   ~ ActionOverride(Player1,\1) ~
      REPLACE_TEXTUALLY ~ActionOverride("sarspir",CreateVisualEffectObject("SPDEATH3",Myself))[ %TAB%%LNL%%MNL%%WNL%]+ActionOverride("sarspir",DestroySelf())~ ~~ // remove sarspir desruction (goes to sarspir's script)
      REPLACE_TEXTUALLY ~ActionOverride("sarspir",\(.+\))$~ ~\1~ // change all other AO sarspir to just action
    END
    BUT_ONLY

  // characters killed in soa finale should have their equipment go with them to tob
  COPY_EXISTING ~cut59a.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(ApplySpell(Player2,CLERIC_RESURRECTION)\)~ ~CopyGroundPilesTo("ar4000",[1116.1805]) \1~
    END
    BUT_ONLY
  
  // dragon breath attacks hit multiple times - separate into visual & damage spells a la red/green dragon breath
  // see also cddrblck.pro, cddrsilv.pro, spin595.spl, spin596.spl, spin691.spl, spin832.spl, spin833.spl, spin893.spl
  // update scripts which use these spells to do double-casts instead
  COPY_EXISTING ~dragbrow.bcs~ ~override~ // brown dragon (draconis)
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),BROWN_DRAGON_BREATH)~ ~ForceSpell(\1,BROWN_DRAGON_BREATH) ReallyForceSpellRES("spin596v",\1)~
    END
    BUT_ONLY

  COPY_EXISTING ~dragyell.bcs~ ~override~ // yellow dragon (unused, but fix anyway)
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ForceSpell(\([^,]+\),YELLOW_DRAGON_BREATH)~ ~ForceSpell(\1,YELLOW_DRAGON_BREATH) ReallyForceSpellRES("spin595v",\1)~
    END
    BUT_ONLY 
  
  // various creatures use the gp** scripts to escape, breaking other stuff: gparcher, gpkensai
  COPY_EXISTING ~gpkensai.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(!HasItem("POTN55",Myself)\)~ ~\1 !Name("chtaz01",Myself)~ // tamoko
    END
    BUT_ONLY

  // illasera's script skips some spellcasting due to bad var counts
  COPY_EXISTING ~illasera.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~SetGlobal("EasyIllaseraSpell","LOCALS",3)~ ~SetGlobal("EasyIllaseraSpell","LOCALS",4)~ // enables slow, skulltrap blocks
      REPLACE_TEXTUALLY ~\([ %TAB%]Global("EasyIllaseraSpell","LOCALS",4)\)~
        ~\1 OR(3) !LevelGT(Player1,18) !NumInPartyGT(1) Global("IllaseraSpell","LOCALS",1)
            OR(3) !LevelGT(Player1,21) !NumInPartyGT(1) Global("IllaseraSpell","LOCALS",2)~ // but preserve current order, e.g. let symbol stun and ADHW still go first if applicable
    END
    BUT_ONLY

  // shouldn't get 'need more str' message in YS lair if you actually are strong enough
  COPY_EXISTING ~ittempl2.bcs~ ~override~
                ~ittempl3.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(!GlobalTimerNotExpired("SideDoor5204Timer","AR5204")\)~ ~\1 CheckStatLT(LastTrigger,19,STR) !CheckStat(LastTrigger,100,STREXTRA)~
    END
    BUT_ONLY
  
  // no dupe shakti figurines
  EXTEND_BOTTOM ~sarkis01.bcs~ ~bg2fixpack/baf/sarkis01.baf~
  EXTEND_BOTTOM ~sarkis03.bcs~ ~bg2fixpack/baf/sarkis01.baf~

  // saradush prison spirit should open a cell door even if player1 is not in the area; change cutscene to be driven by sarspir instead of player1; see also cut224a.bcs
  EXTEND_BOTTOM ~sarspir.bcs~ ~bg2fixpack/baf/sarspir.baf~
  
  // viekang not detecting all forms of fear spells, add basic panic statecheck too
  COPY_EXISTING ~sarvie01.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN 
      REPLACE_TEXTUALLY ~OR(7)~
        ~OR(12) StateCheck(Myself,STATE_PANIC) SpellCastOnMe([ANYONE],WIZARD_SPOOK) SpellCastOnMe([ANYONE],CLERIC_INSECT_PLAGUE) SpellCastOnMe([ANYONE],CLERIC_CREEPING_DOOM) SpellCastOnMeRES("spwm123",[ANYONE])~
    END
    BUT_ONLY
  
  // can get stuck w/o advancement if sendai is silenced when she's supposed to deliver her dying dialogue
  EXTEND_BOTTOM ~sendai.bcs~ ~bg2fixpack/baf/sendai.baf~ // if silenced, will vocalize and then init dialogue

  // devil shades can sit around and do nothing when below 50% hp
  COPY_EXISTING ~shadfi02.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~HPPercentGT(Myself,49)~ ~OR(2) !HPPercentLT(Myself,50) !Global("MoreShadows","LOCALS",0) OR(2) !HPPercentLT(Myself,30) !Global("CuredSelf","LOCALS",0)~
    END
    BUT_ONLY

END
/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

COPY_EXISTING ~ar0011.are~ ~override~ // Candlekeep Dream 1 (Do you remember Candlekeep?)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit2621 END // fix orientation: unused, SE
  BUT_ONLY

COPY_EXISTING ~ar0012.are~ ~override~ // Candlekeep Library (Main Door Trigger)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit0012 END // fix orientation: unused, NW
  BUT_ONLY

COPY_EXISTING ~ar0013.are~ ~override~ // Candlekeep Library (Stairway Triggers Up and Down)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit2321 END // fix orientation: unused, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit2623 END // fix orientation: unused, NW
  BUT_ONLY

COPY_EXISTING ~ar0014.are~ ~override~ // Candlekeep Library (Stairway Trigger Down)
              ~ar0083.are~ ~override~ // Candlekeep Library (Dream: PC learns to control the Slayer)
              ~ar0084.are~ ~override~ // Candlekeep Library (Stairway trigger down)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit2622 END // fix orientation: unused, SE
  BUT_ONLY

COPY_EXISTING ~ar0028.are~ ~override~ // Candlekeep Dream (After PC Soultheft)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit0028 END // fix orientation: unused, SE
  BUT_ONLY

COPY_EXISTING ~ar0071.are~ ~override~ // Spellhold (Imoen Soultheft Cutscene -- "Silence dog, you have no purpose...")
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit1508 END // fix orientation: unused, NW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1515 END // fix orientation: unused, SE
  BUT_ONLY

// adds dungeon flag
COPY_EXISTING ~ar0203.are~ ~override~ // sewers under temple
  WRITE_BYTE 0x48 (THIS BOR BIT5)
  BUT_ONLY

COPY_EXISTING ~ar0204.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR region_name = Tran0202 END // transition down uses stair icon
  BUT_ONLY

COPY_EXISTING ~ar0205.are~ ~override~ // The Beholder Hideout (Unseeing Eye Plot)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = Exit0201 END // fix orientation: from ghoul villge, N
  BUT_ONLY

COPY_EXISTING ~ar0300.are~ ~override~ // The Docks
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = EXIT0305 END // fix orientation: from main entrance of shadow thief lair, ESE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = Exit0306 END // fix orientation: from door just south of main entrance of shadow thief lair, ESE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = Exit0309 END // fix orientation: harper roof from second level of harper HQ, ESE
  BUT_ONLY

// secret door already detected
COPY_EXISTING ~ar0305.are~ ~override~ // Shadow Thieves Guild Entrance
  LPF ALTER_AREA_DOOR INT_VAR flag_detected = 0 STR_VAR door_name = "Secret 01" END
  BUT_ONLY

COPY_EXISTING ~ar0313.are~ ~override~ // Sea Bounty Tavern 1st Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit0314 END // fix orientation: from second floor, NE
  BUT_ONLY

COPY_EXISTING ~ar0326.are~ ~override~ // Corthala House 2nd Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit0325 END // fix orientation: from first floor, NE
  BUT_ONLY

COPY_EXISTING ~ar0327.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 STR_VAR container_name = "Container 7" END // container flagged as trapped, no actual script
  BUT_ONLY

// haz can escape, as trigger is not tied to door
COPY_EXISTING ~ar0329.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR flag_impassable_npc = 1 flag_connect_to_door = 1 STR_VAR region_name = Tran300b END // flag transition as impassable by npcs, connect to the door
  LPF ALTER_AREA_DOOR STR_VAR door_name = "Door08" travel_trigger = "Tran300b" END // associate the door with the transition
  BUT_ONLY

COPY_EXISTING ~ar0400.are~ ~override~ // Slums
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  3 STR_VAR entrance_name = Exitsphe END // fix orientation: platform in front of sphere from main slums, WSW
  BUT_ONLY

// both ilmater temples have entrances outside of the usable area; see also ar0703.are
COPY_EXISTING ~ar0408.are~ ~override~
  LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 314 y_coord = 392 STR_VAR entrance_name = Exit0400 END
  BUT_ONLY

COPY_EXISTING ~ar0413.are~ ~override~ // Sphere: Enginge Room
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit0412a END // fix orientation: from planar sphere ice room, NE
  BUT_ONLY

COPY_EXISTING ~ar0415.are~ ~override~ // Living Room 1st Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = Exit0416 END // fix orientation: from upstairs, E
  BUT_ONLY

COPY_EXISTING ~ar0500.are~ ~override~ // Bridge District
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit0522 END // fix orientation: from Five Flagons (stronghold), SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit0504 END // fix orientation: from Saerk Estate ground floor, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit0505a END // fix orientation: from Saerk Estate, dead end balcony, SW
  LPF ALTER_AREA_DOOR INT_VAR flag_locked = 1 STR_VAR door_name = Door0505b END // only one door to saerk's place is locked
  BUT_ONLY

// fixes wonky container overlay for lightning trap
COPY_EXISTING ~ar0502.are~ ~override~ // tanner's home, second level
  READ_SHORT 0x5a trig_num
  READ_LONG  0x5c trig_off
  READ_LONG  0x7c vert_off
  SET vert_delta = 0
  FOR (index = 0 ; index < trig_num ; ++index) BEGIN
    READ_SHORT (trig_off + 0x22 + (index * 0xc4)) lbound // container overlay corrections
    WRITE_LONG (trig_off + 0x2c + (index * 0xc4)) (THIS + vert_delta)
    PATCH_IF (lbound = 450) BEGIN // lightining trap [sic]
      READ_SHORT (trig_off + 0x2a + (index * 0xc4)) vert_num
      READ_LONG  (trig_off + 0x2c + (index * 0xc4)) vert_idx
      FOR (index2 = 0 ; index2 < vert_num ; ++index2) BEGIN
        READ_SHORT (vert_off +        ((vert_idx + index2) * 0x04)) x_coord
        READ_SHORT (vert_off + 0x02 + ((vert_idx + index2) * 0x04)) y_coord
        PATCH_IF (((x_coord = 487) AND (y_coord = 385)) OR
                  ((x_coord = 548) AND (y_coord = 446))) BEGIN
          DELETE_BYTES (vert_off +        ((vert_idx + index2) * 0x04)) 0x04
          SET vert_num   -= 1
          SET index2     -= 1
          SET vert_delta -= 1
          SET vert_cutoff = vert_idx
        END
      END
      WRITE_SHORT (trig_off + 0x2a + (index * 0xc4)) vert_num
    END
  END
  PATCH_IF vert_delta != 0 BEGIN
    WRITE_SHORT 0x80 (THIS + vert_delta)
    // now to update vertex indices elsewhere
    READ_LONG  0x70 cont_off
    READ_SHORT 0x74 cont_num
    FOR (index = 0 ; index < cont_num ; ++index) BEGIN
      READ_SHORT  (cont_off + 0x50 + (index * 0xc0)) vert_idx
      PATCH_IF vert_idx > vert_cutoff BEGIN
        WRITE_SHORT  (cont_off + 0x50 + (index * 0xc0)) (THIS + vert_delta)
      END
    END
    READ_LONG  0xa4 door_num
    READ_LONG  0xa8 door_off
    FOR (index = 0 ; index < door_num ; ++index) BEGIN
      PATCH_FOR_EACH off IN 0x2c 0x34 0x48 0x50 BEGIN
        READ_SHORT  (door_off + off + (index * 0xc8)) vert_idx
        PATCH_IF vert_idx > vert_cutoff BEGIN
          WRITE_SHORT  (door_off + 0x50 + (index * 0xc8)) (THIS + vert_delta)
        END
      END
    END
    // and now general offsets
    WRITE_SHORT 0x80 (THIS + vert_delta) // number of vertices
    PATCH_FOR_EACH off IN 0x54 0x5c 0x60 0x68 0x70 0x78 0x84 0x88 0x90 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 0xc4 0xcc BEGIN
      READ_LONG off offset
      PATCH_IF offset > vert_off BEGIN
        WRITE_LONG off (THIS + (vert_delta * 0x04))
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar0508.are~ ~override~ // Kidnappers' Hideout 2nd Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = Exit0507 END // fix orientation: from first floor, N
  BUT_ONLY

// standardize on one stoneskin scroll; see also garlena.sto, sahpr1.sto, suefl10.sto, type1.sto, udduer01.sto
COPY_EXISTING ~ar0510.are~ ~override~ // Five Flagons Inn Theater
  LPF ALTER_AREA_ITEM STR_VAR match_item = scrl1v item = scrl2b END
  BUT_ONLY

COPY_EXISTING ~ar0516.are~ ~override~ // Planar Prison
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 11 STR_VAR entrance_name = Exit0517 END // fix orientation: from fleshy trap, ENE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  5 STR_VAR entrance_name = Exit0519 END // fix orientation: from fleshy trap, WNW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  5 STR_VAR entrance_name = Exit0518 END // fix orientation: from fleshy trap, WNW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  7 STR_VAR entrance_name = Exit0520 END // fix orientation: from fleshy trap, NNW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  7 STR_VAR entrance_name = Exit0521 END // fix orientation: from fleshy trap, NNW
  BUT_ONLY

COPY_EXISTING ~ar0602.are~ ~override~ // Irenicus's Dungeon 1st Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit0601 END // fix orientation: from plane of air, SE
  BUT_ONLY

// fixes wonky container overlay for throne
COPY_EXISTING ~ar0603.are~ ~override~ // opening dungeon, second level
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  READ_LONG  0x7c vert_off
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    READ_SHORT (cont_off + 0x20 + (index * 0xc0)) x_coord // container overlay corrections
    PATCH_IF (x_coord = 1933) BEGIN // statue (container 21)
      READ_LONG   (cont_off + 0x50 + (index * 0xc0)) vert_idx
      WRITE_SHORT (vert_off +        ((vert_idx +  0) * 0x04)) 2001 // x0
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  0) * 0x04))  379 // y0
      WRITE_SHORT (vert_off +        ((vert_idx +  1) * 0x04)) 2019 // x1
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  1) * 0x04))  370 // y1
      WRITE_SHORT (vert_off +        ((vert_idx +  2) * 0x04)) 2016 // x2
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  2) * 0x04))  347 // y2
      WRITE_SHORT (vert_off +        ((vert_idx +  3) * 0x04)) 2016 // x3
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  3) * 0x04))  328 // y3
      WRITE_SHORT (vert_off +        ((vert_idx +  4) * 0x04)) 2000 // x4
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  4) * 0x04))  308 // y4
      WRITE_SHORT (vert_off +        ((vert_idx +  5) * 0x04)) 1980 // x5
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  5) * 0x04))  303 // y5
      WRITE_SHORT (vert_off +        ((vert_idx +  6) * 0x04)) 1976 // x6
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  6) * 0x04))  326 // y6
      WRITE_SHORT (vert_off +        ((vert_idx +  7) * 0x04)) 1965 // x7
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  7) * 0x04))  337 // y7
      WRITE_SHORT (vert_off +        ((vert_idx +  8) * 0x04)) 1941 // x8
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  8) * 0x04))  360 // y8
      WRITE_SHORT (vert_off +        ((vert_idx +  9) * 0x04)) 1952 // x9
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx +  9) * 0x04))  372 // y9
      WRITE_SHORT (vert_off +        ((vert_idx + 10) * 0x04)) 1981 // x10
      WRITE_SHORT (vert_off + 0x02 + ((vert_idx + 10) * 0x04))  392 // y10
    END
  END
  BUT_ONLY

// helpful folks disappear before they can actually help; see also vvhelp.cre, vvhelp.dlg, vvpriest.cre
COPY_EXISTING ~ar0700.are~ ~override~
  LPF ALTER_AREA_ACTOR INT_VAR flag_time_0 = 1 flag_time_1 = 1 flag_time_2 = 1 flag_time_3 = 1 flag_time_4 = 1 flag_time_19 = 1 flag_time_20 = 1 flag_time_21 = 1 flag_time_22 = 1 flag_time_23 = 1 // present only at night
    flag_time_5 = 0 flag_time_6 = 0 flag_time_7 = 0 flag_time_8 = 0 flag_time_9 = 0 flag_time_10 = 0 flag_time_11 = 0 flag_time_12 = 0 flag_time_13 = 0 flag_time_14 = 0 flag_time_15 = 0 flag_time_16 = 0 flag_time_17 = 0 flag_time_18 = 0
    STR_VAR actor_name = "Priest of Talos" END

// both ilmater temples have entrances outside of the usable area; see also ar0408.are
COPY_EXISTING ~ar0703.are~ ~override~
  LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 314 y_coord = 392 STR_VAR entrance_name = Exit0700 END
  BUT_ONLY

COPY_EXISTING ~ar0709.are~ ~override~ // Den of the Seven Vales 1st Floor
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = Exit0712 END // fix orientation: from Mencar upstairs, E
  BUT_ONLY

// orphan-paladin quest in graveyard can be interrupted by actor-active flags; see also arenthis.bcs, kamir.bcs, risa.bcs
COPY_EXISTING ~ar0800.are~ ~override~
  READ_LONG  0x54 act_off
  READ_SHORT 0x58 act_num
  FOR (index = 0 ; index < act_num; ++index) BEGIN
    READ_ASCII (act_off + 0x80 + (index * 0x110)) cre_file
    PATCH_IF (("%cre_file%" STRING_COMPARE_CASE "arenthis" = 0) OR ("%cre_file%" STRING_COMPARE_CASE "kamir" = 0) OR ("%cre_file%" STRING_COMPARE_CASE "mourner7" = 0)) BEGIN
      WRITE_LONG (act_off + 0x40 + (index * 0x110)) `0 // flags active at all times; active/inactive now handled by thier scripts
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar0801.are~ ~override~ // Bodhi's Hideout (sided with Aran)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  7 STR_VAR entrance_name = Exit0800b END // fix orientation: from graveyard (eastmost entrance), NNW
  BUT_ONLY

// copy map notes from de'Arnise keep to stronghold de'Arnise keep; see ar1304-7
COPY_EXISTING ~ar1304.are~ ~override~
  ADD_MAP_NOTE #1357 #2806 ~gray~ #48434 // side entrance
  ADD_MAP_NOTE #1038 #3216 ~gray~ #48435 // the fort
  ADD_MAP_NOTE #2868 #1886 ~gray~ #48436 // gate lever
  ADD_MAP_NOTE #2112 #1984 ~gray~ #48437 // dog kennels

// copy map notes from de'Arnise keep to stronghold de'Arnise keep; see ar1304-7
COPY_EXISTING ~ar1305.are~ ~override~
  ADD_MAP_NOTE #869 #446 ~gray~ #48438 // basement exit

// copy map notes from de'Arnise keep to stronghold de'Arnise keep; see ar1304-7
COPY_EXISTING ~ar1306.are~ ~override~
  ADD_MAP_NOTE  #567 #1855 ~gray~ #48439 // Exit to Outside
  ADD_MAP_NOTE #1554 #1375 ~gray~ #48440 // Exit to Courtyard
  ADD_MAP_NOTE #1924 #1107 ~gray~ #48440 // Exit to Courtyard
  ADD_MAP_NOTE #2367 #1001 ~gray~ #48441 // Stairs to Second Floor
  ADD_MAP_NOTE #1969  #760 ~gray~ #48442 // kitchen

// copy map notes from de'Arnise keep to stronghold de'Arnise keep; see ar1304-7
COPY_EXISTING ~ar1307.are~ ~override~
  ADD_MAP_NOTE #1182  #812 ~gray~ #48443 // Stairs to Basement
  ADD_MAP_NOTE #2128  #840 ~gray~ #48444 // Stairs to First Floor
  ADD_MAP_NOTE #1193  #313 ~gray~ #48445 // Stairs to Roof
  ADD_MAP_NOTE #1782 #1395 ~gray~ #48445 // Stairs to Roof
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 STR_VAR container_name = "Container 13" END // container flagged as trapped, no actual script
  BUT_ONLY

COPY_EXISTING ~ar1500.are~ ~override~ // Spellhold
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1515 END // fix orientation: from Spellhold interior, SE
  BUT_ONLY

// two containers on top of each other; shift one
COPY_EXISTING ~ar1507.are~ ~override~
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  READ_LONG  0x7c vert_off
  SET delta_x = 612
  SET delta_y = 40
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    READ_SHORT (cont_off + 0x20 + (index * 0xc0)) x_pos
    PATCH_IF (x_pos = 339) BEGIN // book of the dead 3
      WRITE_SHORT (cont_off + 0x20 + (index * 0xc0)) (THIS + delta_x) // location x
      WRITE_SHORT (cont_off + 0x22 + (index * 0xc0)) (THIS + delta_y) // location y
      WRITE_SHORT (cont_off + 0x34 + (index * 0xc0)) (THIS + delta_x) // launch x
      WRITE_SHORT (cont_off + 0x36 + (index * 0xc0)) (THIS + delta_y) // launch y
      WRITE_SHORT (cont_off + 0x38 + (index * 0xc0)) (THIS + delta_x) // bounding box left
      WRITE_SHORT (cont_off + 0x3a + (index * 0xc0)) (THIS + delta_y) // bounding box top
      WRITE_SHORT (cont_off + 0x3c + (index * 0xc0)) (THIS + delta_x) // bounding box right
      WRITE_SHORT (cont_off + 0x3e + (index * 0xc0)) (THIS + delta_y) // bounding box bottom
      READ_LONG   (cont_off + 0x50 + (index * 0xc0)) vert_idx
      READ_LONG   (cont_off + 0x54 + (index * 0xc0)) vert_num
      FOR (index2 = 0 ; index2 < vert_num ; ++index2) BEGIN
        WRITE_SHORT (vert_off +        ((index2 + vert_idx) * 0x04)) (THIS + delta_x)
        WRITE_SHORT (vert_off + 0x02 + ((index2 + vert_idx) * 0x04)) (THIS + delta_y)
      END
      SET index = cont_num // kill loop
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar1513.are~ ~override~ // Bodhi's Hunt Level 2
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1512 END  // fix orientation: both entrances from first bodhi hunt area, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1512a END // fix orientation: both entrances from first bodhi hunt area, SE
  BUT_ONLY

COPY_EXISTING ~ar1514.are~ ~override~ // Bodhi's Hunt Level 3 (1st Slayer Change Happens here)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit1507 END // fix orientation: from Spellhold (Lonk the Sane), SW
  BUT_ONLY

COPY_EXISTING ~ar1515.are~ ~override~ // Spellhold (Irenicus' Living Room)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit1516 END // fix orientation: from lower levels, NE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit1500 END // fix orientation: from outside, NW
  BUT_ONLY

COPY_EXISTING ~ar1516.are~ ~override~ // Spellhold (Soultheft Level)
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = Exit1508 END // fix orientation: from lower levels, N
  BUT_ONLY

COPY_EXISTING ~ar1600.are~ ~override~ // Brynnlaw
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1601 END // fix orientation: from Desharik, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1605 END // fix orientation: from Perth, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit1606 END // fix orientation: from Cayla, SE
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit1602 END // fix orientation: from inn, SW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit1603 END // fix orientation: from shop, SW
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit1604 END // fix orientation: from temple, SW
  BUT_ONLY

COPY_EXISTING ~ar1601.are~ ~override~ // Desharik's Estate
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit1600 END // fix orientation: from outside, NW
  BUT_ONLY

COPY_EXISTING ~ar2100.are~ ~override~ // Underdark
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = Exit2402 END // fix orientation: from kuo-toa, E
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = Exit2101 END // fix orientation: from beholders, N
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  6 STR_VAR entrance_name = Exit2400 END // fix orientation: from illithid, NW
  BUT_ONLY

COPY_EXISTING ~ar2102.are~ ~override~ // Adalon's Cave
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit2100 END // fix orientation: from exterior, SE
  BUT_ONLY

// overlapping containers; disabling empty one
COPY_EXISTING ~ar2400.are~ ~override~ // underdark illithid lair
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  READ_LONG  0x7c vert_off
  SET vert_delta = 0
  SET cont_delta = 0
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    WRITE_LONG (cont_off + 0x50 + (index * 0xc0)) (THIS + vert_delta) // update container vertice indices on the fly
    READ_SHORT (cont_off + 0x20 + (index * 0xc0)) x_coord // check x-coordinate for match
    PATCH_IF (x_coord = 1157) BEGIN // container "amp"
      READ_LONG   (cont_off + 0x50 + (index * 0xc0)) vert_idx
      READ_SHORT  (cont_off + 0x54 + (index * 0xc0)) vert_num
      SET vert_cutoff = vert_idx
      SET vert_delta -= vert_num
      DELETE_BYTES (vert_off +        (vert_idx * 0x04)) (vert_num * 0x04) // delete vertices for container
      DELETE_BYTES (cont_off +        (index * 0xc0)) 0xc0 // delete container
      SET index      -= 1
      SET cont_num   -= 1
      SET cont_delta -= 1
    END
  END
  PATCH_IF cont_delta != 0 BEGIN
    WRITE_SHORT 0x74 (THIS + cont_delta) // update number of containers
    WRITE_SHORT 0x80 (THIS + vert_delta) // update number of vertices
    // update general offsets
    PATCH_FOR_EACH off IN 0x54 0x5c 0x60 0x68 0x70 0x78 0x7c 0x84 0x88 0x90 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 0xc4 0xcc BEGIN
      READ_LONG off offset
      PATCH_IF offset > vert_off BEGIN
        WRITE_LONG off (THIS + (vert_delta * 0x04))
      END
      PATCH_IF offset > cont_off BEGIN
        WRITE_LONG off (THIS + (cont_delta * 0xc0))
      END
    END
    // now to update vertex indices elsewhere
    READ_SHORT 0x5a trig_num
    READ_LONG  0x5c trig_off
    FOR (index = 0 ; index < trig_num ; ++index) BEGIN
      READ_SHORT  (trig_off + 0x2c + (index * 0xc4)) vert_idx
      PATCH_IF vert_idx > vert_cutoff BEGIN
        WRITE_SHORT  (trig_off + 0x2c + (index * 0xc4)) (THIS + vert_delta)
      END
    END
    READ_LONG  0xa4 door_num
    READ_LONG  0xa8 door_off
    FOR (index = 0 ; index < door_num ; ++index) BEGIN
      PATCH_FOR_EACH off IN 0x2c 0x34 0x48 0x50 BEGIN
        READ_LONG (door_off + off + (index * 0xc8)) vert_idx
        PATCH_IF vert_idx > vert_cutoff BEGIN
          WRITE_LONG (door_off + off + (index * 0xc8)) (THIS + vert_delta)
        END
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~ar2401.are~ ~override~ // Cave Between Underdark and Exit from Underdark
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 14 STR_VAR entrance_name = Exit2500 END // fix orientation: from surface, SE
  BUT_ONLY

COPY_EXISTING ~ar2402.are~ ~override~ // Kuo Toa in Underdark
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = Exit2401 END // fix orientation: from drow-surface elf fight area, ESE
  BUT_ONLY

COPY_EXISTING ~ar2600.are~ ~override~ // tethyr wood
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran2603 END // transition should be party required
  BUT_ONLY

COPY_EXISTING ~ar2603.are~ ~override~ // tethyr wood
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR region_name = Tran2600 END // transition should be party required
  BUT_ONLY

COPY_EXISTING ~ar2801.are~ ~override~ // House of High Priestess of Suldanesselar
  LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = Exit2800 END // fix orientation: from exterior, N
  BUT_ONLY
  
ACTION_IF tob_game THEN BEGIN

  COPY_EXISTING ~ar3016.are~ ~override~
    LPF ALTER_AREA_ITEM STR_VAR match_item = "book59" item = "book44" END // book59 is special book to advance tanner murder plot with Fael
    BUT_ONLY

  COPY_EXISTING ~ar5000.are~ ~override~ // Saradush
    LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit5010 END // from Santele's home, SW
    LPF ALTER_AREA_ENTRANCE INT_VAR orient = 15 STR_VAR entrance_name = Exit5003 END // fix orientation: from tavern, SSE
    BUT_ONLY

  COPY_EXISTING ~ar5009.are~ ~override~ // House (no exit?)
    LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit5008 END // fix orientation: unused, NE
    BUT_ONLY

  COPY_EXISTING ~ar5010.are~ ~override~
    LPF ALTER_AREA_ANIMATION INT_VAR match_x_coord = 1110 x_coord = 108 y_coord = 367 END // one candle had coords outside the area
    BUT_ONLY
  
  COPY_EXISTING ~ar5012.are~ ~override~
    LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 STR_VAR container_name = "Container 1" END // container flagged as trapped, no actual script
    BUT_ONLY

  COPY_EXISTING ~ar5013.are~ ~override~ // Sewers beneath Saradush
    LPF ALTER_AREA_ENTRANCE INT_VAR orient = 13 STR_VAR entrance_name = Exit5007 END // fix orientation: from gromnir's basement, ESE
    BUT_ONLY

  COPY_EXISTING ~ar5200.are~ ~override~ // Fire Giant Entrance Area
    LPF ALTER_AREA_ENTRANCE INT_VAR orient =  4 STR_VAR entrance_name = ExitW END // fix orientation: from worldmap, east edge, W
    BUT_ONLY

  COPY_EXISTING ~ar5500.are~ ~override~ // Amkethran
    LPF ALTER_AREA_ENTRANCE INT_VAR orient =  4 STR_VAR entrance_name = ExitE END // fix orientation: from worldmap, east edge, W
    BUT_ONLY

  COPY_EXISTING ~ar6101.are~ ~override~ // Main Entrance Sendai's Lair
    LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entrance_name = Exit6100 END // fix orientation: from exterior, E
    BUT_ONLY

  COPY_EXISTING ~ar6105.are~ ~override~ // Sendai's Lair -- Diaytha, Abishai
    LPF ALTER_AREA_ENTRANCE INT_VAR orient =  2 STR_VAR entrance_name = Exit6106 END // fix orientation: from Egeissag, SW
    BUT_ONLY
  
  // dimension door at Sendai's enclave has funky lighting
  COPY_EXISTING ~ar6108.are~ ~override~ // sendai's enclave
    LPF ALTER_AREA_ANIMATION INT_VAR flag_not_light_source = 1 STR_VAR match_name = spdimndr END
    BUT_ONLY

  COPY_EXISTING ~ar6111.are~ ~override~ // Woodcutter's House -- Sendai's Area
    LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entrance_name = Exit6100 END // fix orientation: from exterior, NE
    BUT_ONLY

  COPY_EXISTING ~ar6400.are~ ~override~ // River Area
    LPF ALTER_AREA_ENTRANCE INT_VAR orient =  8 STR_VAR entrance_name = ExitS END // fix orientation: from worldmap, south edge, N
    BUT_ONLY

END

/////                                                  \\\\\
///// bulk creature file fixes                         \\\\\
/////                                                  \\\\\

// kit.ids fixes
ACTION_CLEAR_ARRAY cd_bulk_cre_changes_kit
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_kit BEGIN

  // when moving to core, remove kit.ids corrections in creature copies for anomen, imoen, mazzy, HD, keldorn, and korgan
  akae     => 0x40030000 // akae - kensai
  anomen10 => 0x40000000 // anomen - trueclass
  anomen12 => 0x40000000 // anomen - trueclass
  anomen6  => 0x40000000 // anomen - trueclass
  anomen7  => 0x40000000 // anomen - trueclass
  anomen8  => 0x40000000 // anomen - trueclass
  anomen9  => 0x40000000 // anomen - trueclass
  bhelm    => 0x40140000 // guardian vottnar - priest of helm
  chalcy02 => 0x400a0000 // favored of cyric - assassin
  chang01  => 0x40070000 // angelo - archer
  chang02  => 0x40070000 // angelo - archer
  disrup01 => 0x40020000 // wizard slayer - wizardslayer
  dpplay01 => 0x40080000 // used as player1 copy in dream cutscene - stalker
  dpplay02 => 0x40080000 // used as player1 copy in dream cutscene - stalker
  gaelan   => 0x400c0000 // gaelan bayle - swashbuckler
  genth01  => 0x400b0000 // thief - bounty hunter
  genth02  => 0x400c0000 // thief - swashbuckler
  gorwom02 => 0x40070000 // the huntress - archer
  haer10   => 0x400d0000 // haer'dalis - blade
  haer11   => 0x400d0000 // haer'dalis - blade
  haer13   => 0x400d0000 // haer'dalis - blade
  haer15   => 0x400d0000 // haer'dalis - blade
  haer19   => 0x400d0000 // haer'dalis - blade
  imoen    => 0x40000000 // imoen - trueclass
  imoen10  => 0x40000000 // imoen - trueclass
  imoen15  => 0x40000000 // imoen - trueclass
  imoen211 => 0x40000000 // imoen - trueclass
  imoen213 => 0x40000000 // imoen - trueclass
  imoen6   => 0x40000000 // imoen - trueclass
  jaga2    => 0x40030000 // kail - kensai
  jaga4    => 0x400c0000 // iko - swashbuckler
  keldor10 => 0x40050000 // keldorn - inquisitor
  keldor12 => 0x40050000 // keldorn - inquisitor
  keldor14 => 0x40050000 // keldorn - inquisitor
  keldor8  => 0x40050000 // keldorn - inquisitor
  keldor9  => 0x40050000 // keldorn - inquisitor
  korgan11 => 0x40010000 // korgan - berserker
  korgan12 => 0x40010000 // korgan - berserker
  korgan15 => 0x40010000 // korgan - berserker
  korgan8  => 0x40010000 // korgan - berserker
  korgan9  => 0x40010000 // korgan - berserker
  mazzy11  => 0x40000000 // mazzy - trueclass
  mazzy12  => 0x40000000 // mazzy - trueclass
  mazzy15  => 0x40000000 // mazzy - trueclass
  mazzy8   => 0x40000000 // mazzy - trueclass
  mazzy9   => 0x40000000 // mazzy - trueclass
  meronia  => 0x400d0000 // meronia - blade
  pace     => 0x400c0000 // pace - swashbuckler
  parfig16 => 0x40020000 // jalin tix - wizardslayer
  parthf   => 0x400b0000 // alnarow - bounty hunter
  sevdru01 => 0x40120000 // alatelo de bonito - beast friend (originally used 0x4019)
  ysgp02   => 0x40030000 // merlinious - kensai
  ysgp03   => 0x40070000 // tibbit - archer

END

ACTION_PHP_EACH cd_bulk_cre_changes_kit AS cre_file => n_kit BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_LONG 0x244 n_kit
    BUT_ONLY IF_EXISTS

END

ACTION_CLEAR_ARRAY cd_bulk_cre_changes_animate
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_animate BEGIN

  celvan   => 24836 // celvan uses halfling avatar, to FIGHTER_MALE_GNOME
  cyrando  => 24580 // cyrando uses halfling avatar, to CLERIC_MALE_GNOME
  daelf    => 25105 // elven mage uses a human avatar, MAGE_FEMALE_HUMAN to MAGE_FEMALE_ELF
  daelf2   => 25105 // elven mage uses a human avatar, MAGE_FEMALE_HUMAN to MAGE_FEMALE_ELF

END

ACTION_PHP_EACH cd_bulk_cre_changes_animate AS cre_file => n_animate BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_LONG 0x28 n_animate
    BUT_ONLY

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_cre_changes_dv BEGIN // most are corrected to just use their file anme as DV

  mourner3 => mourner3   // mourners never leave, often have nothing to say; see also ar0800.bcs, mourner3.dlg, mourner4.dlg

END


ACTION_PHP_EACH cd_bulk_cre_changes_dv AS cre_file => n_dv BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_ASCIIE 0x280 ~%n_dv%~ #32
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// creature file fixes                              \\\\\
/////                                                  \\\\\

// pregen daria, lessa hve wrong sex
COPY_EXISTING ~characters/mage.chr~     ~characters~
              ~characters/thief.chr~    ~characters~
              ~characters/tobmage.chr~  ~characters~
              ~characters/tobthief.chr~ ~characters~
  WRITE_BYTE 0x29b 2 // sex: female
  IF_EXISTS

// abazigal can be killed in human form
COPY_EXISTING ~abazigal.cre~ ~override~
  ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~NONE~ ~RRING AMULET~
  IF_EXISTS

// aerie missing memorized spells - all aerie versions are short two level 1, two level 2 memorized spells
COPY_EXISTING ~aerie6.cre~  ~override~
              ~aerie7.cre~  ~override~
              ~aerie9.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr101~ #0 ~priest~ // 10-12 already have bless, sooooo
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // every one gets doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr201~ #1 ~priest~ // every one gets aid
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~ // 10-12 aready have slow poison, too
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2

  BUT_ONLY

COPY_EXISTING ~aerie10.cre~ ~override~
              ~aerie11.cre~ ~override~
              ~aerie12.cre~ ~override~
  ADD_MEMORIZED_SPELL ~sppr106~ #0 ~priest~ // add magic stone
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // every one gets doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr201~ #1 ~priest~ // every one gets aid
  ADD_MEMORIZED_SPELL ~sppr210~ #1 ~priest~ // resist fire/cold
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  BUT_ONLY IF_EXISTS

// joinable npcs have already-cast spells
COPY_EXISTING ~aerie10.cre~  ~override~
              ~anomen9.cre~  ~override~
              ~cernd13.cre~  ~override~
              ~cernd13b.cre~ ~override~
              ~cernd14.cre~  ~override~
              ~famcat.cre~   ~override~
              ~famcat25.cre~ ~override~
              ~famfer.cre~   ~override~
              ~famfer25.cre~ ~override~
              ~famrab.cre~   ~override~
              ~famrab25.cre~ ~override~
              ~jahei14.cre~  ~override~
              ~jaheir12.cre~ ~override~
  READ_LONG 0x2b0 mem_off
  READ_LONG 0x2b4 mem_num
  FOR (index = 0 ; index < mem_num ; ++index) BEGIN
    WRITE_SHORT (mem_off + 0x08 + (0x0c * index)) 1 // change from spell cast (0) to memorized (1)
  END
  BUT_ONLY IF_EXISTS

// monks get fisted added by the engine automatically, so remove items and unset selected weapon slot
ACTION_FOR_EACH monk IN ammgrd01 ammgrd02 ammgrd03 ammgrd04 ammgrd05 ammlegs ammonk01 ammonk02 ammonk03 ammonk04
                        ammonk05 ammonk06 balelite balth bazmonk cutamgrd killmonk parmonk senmonk thief7 tobpar03 BEGIN

  COPY_EXISTING ~%monk%.cre~ ~override~
    REMOVE_CRE_ITEM mfist3 mfist4 mfist5 mfist6 mfist7 mfist8
    WRITE_BYTE 0x53 1 // apr
    READ_LONG  0x2b8 slot_off
    WRITE_SHORT (slot_off + 0x4c) 1000 // equipped weapon
    BUT_ONLY IF_EXISTS

END

// anomen missing memorized spells
COPY_EXISTING ~anomen6.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~     // dispel magic
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ (2) // 2x cure serious wounds
  BUT_ONLY

// anomen missing memorized spells
COPY_EXISTING ~anomen7.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr107~ #0 ~priest~ // protection from evil
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~ // slow poison
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // second cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ // cure critical wounds
  BUT_ONLY

// anomen missing memorized spells
COPY_EXISTING ~anomen8.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr107~ #0 ~priest~     // protection from evil
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~     // slow poison
  ADD_MEMORIZED_SPELL ~sppr413~ #3 ~priest~     // negative plane protection
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ (2) // 2x cure critical wounds
  BUT_ONLY

// anomen missing memorized spells
COPY_EXISTING ~anomen9.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr413~ #3 ~priest~ // negative plane protection
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ // cure critical wounds
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  BUT_ONLY

// anomen missing memorized spells
COPY_EXISTING ~anomen10.cre~ ~override~
              ~anomen12.cre~ ~override~
  ADD_MEMORIZED_SPELL ~sppr106~ #0 ~priest~ // magic stone
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr214~ #1 ~priest~ // duhm
  ADD_MEMORIZED_SPELL ~sppr310~ #2 ~priest~ // miscast magic
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  ADD_MEMORIZED_SPELL ~sppr609~ #5 ~priest~ // false dawn
  BUT_ONLY IF_EXISTS

// should defend self if attacked
COPY_EXISTING ~arenthis.cre~ ~override~
  WRITE_ASCII 0x268 ~pries10b~ #8
  BUT_ONLY

// summoned bears lack generic attack script
COPY_EXISTING ~bearblsu.cre~ override
              ~beargrsu.cre~ override
  WRITE_ASCII 0x268 ~wtasight~ #8
  BUT_ONLY

// female merchants with a male sound file
COPY_EXISTING ~bshop02.cre~ ~override~
              ~shop2.cre~   ~override~
  WRITE_LONG 0x118 "-1" // blanks common4, "Calimshites!  Tethyrians!  Waterdhavians!  They all make their way here eventually to Amn, they do." [malmer04]
  BUT_ONLY
  
// generic city boys shouldn't use giran's scripting
COPY_EXISTING ~burch1.cre~ ~override~
  WRITE_ASCII 0x250 ~~ #8 // blanks boy1, makes him leave after hannah is rescued
  BUT_ONLY

// ler should approach PC after irenicus/cw carnage; see also bystand1.bcs
COPY_EXISTING ~bystand1.cre~ ~override~
  WRITE_ASCII 0x250 ~bystand1~ #8   // free slot: Class script
  BUT_ONLY

// ruby pendant charm is supposed to be once-per-day; see also regisamu.itm
COPY_EXISTING ~c6regis.cre~ ~override~
              ~c6regis2.cre~ ~override~
  REPLACE_CRE_ITEM ~regisamu~ #1 #0 #0 ~NONE~ ~AMULET~ // add charges
  BUT_ONLY

// cernd missing memorized spells - all versions are short two level 1, two level 2, one level 3, one level 4 memorized spells
COPY_EXISTING ~cernd10.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr111~ #0 ~priest~ // armor of faith
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // every one gets doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr207~ #1 ~priest~ // good berries
  ADD_MEMORIZED_SPELL ~sppr209~ #1 ~priest~ // know alignment
  ADD_MEMORIZED_SPELL ~sppr310~ #2 ~priest~ // miscast magic
  ADD_MEMORIZED_SPELL ~sppr410~ #3 ~priest~ // call woodland beings
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// cernd missing memorized spells - all versions are short two level 1, two level 2, one level 3, one level 4 memorized spells
COPY_EXISTING ~cernd12.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ // second cure light wounds
  ADD_MEMORIZED_SPELL ~sppr110~ #0 ~priest~ // shillelagh
  ADD_MEMORIZED_SPELL ~sppr207~ #1 ~priest~ // good berries
  ADD_MEMORIZED_SPELL ~sppr210~ #1 ~priest~ // resist fire/cold
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr410~ #3 ~priest~ // call woodland beings
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// cernd missing memorized spells - all versions are short two level 1, two level 2, one level 3, one level 4 memorized spells
COPY_EXISTING ~cernd13.cre~  ~override~
              ~cernd13b.cre~ ~override~
              ~cernd14.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ // second cure light wounds
  ADD_MEMORIZED_SPELL ~sppr110~ #0 ~priest~ // shillelagh
  ADD_MEMORIZED_SPELL ~sppr210~ #1 ~priest~ // resist fire/cold
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~ // slow poison
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr416~ #3 ~priest~ // cloak of fear
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY IF_EXISTS

// consistent tazok
COPY_EXISTING ~chtaz01.cre~ ~override~
              ~tazok.cre~   ~override~
  WRITE_LONG  0x28 28672 // animation: half-ogre
  WRITE_BYTE 0x272   113 // race: ogre
  WRITE_BYTE 0x27b    35 // alignment: ne
  BUT_ONLY IF_EXISTS

// abazigal's dragon form not immune to time stop due to bad parameter
COPY_EXISTING ~dragblue.cre~ ~override~ // abazigal, 1 effect(s) altered
  LPF ALTER_EFFECT INT_VAR match_opcode = 310 match_parameter2 = 0 parameter2 = 1 END // fix broken timestop immunity
  BUT_ONLY IF_EXISTS

// make dream imoen invincible, just in case
COPY_EXISTING ~drim02.cre~ ~override~
  ADD_CRE_ITEM ~minhp1~ #0 #0 #0 ~NONE~ ~AMULET RRING~

// 12hd earth elemental via druid summon should be equal to mage-summoned 12hd earth elemental
COPY_EXISTING ~elearpr.cre~ ~override~
  WRITE_BYTE 0x238 17 // strength
  BUT_ONLY

// conjure air elemental lacks 24 HD elemental; see also spair1-3.eff
// elairsu2 is actually a spot-on match for the 16 hd elem so make it elairsu3; make elairsu4 from old elairsu3
COPY_EXISTING ~elairsu3.cre~ ~override/elairsu4.cre~
              ~elairsu2.cre~ ~override/elairsu3.cre~

// conjure air elemental lacks 24 HD elemental; see also spair1-3.eff
COPY_EXISTING ~elairsu2.cre~ ~override~
  WRITE_SHORT 0x024    96 // current hp
  WRITE_SHORT 0x026    96 // max hp
  WRITE_LONG  0x028 29473 // smaller air elemental anim
  WRITE_BYTE  0x052     9 // thac0
  WRITE_BYTE  0x054     7 // save vs death
  WRITE_BYTE  0x055     9 // save vs wand
  WRITE_BYTE  0x056     8 // save vs poly
  WRITE_BYTE  0x057     8 // save vs breath
  WRITE_BYTE  0x058    10 // save vs spell
  WRITE_BYTE  0x234    12 // level (hd)
  WRITE_BYTE  0x238    17 // strength

// conjure air elemental lacks 24 HD elemental; see also spair1-3.eff
COPY_EXISTING ~elairsu4.cre~ ~override~
  WRITE_SHORT 0x024   192 // current hp
  WRITE_SHORT 0x026   192 // max hp
  WRITE_BYTE  0x052     0 // thac0
  WRITE_BYTE  0x054     2 // save vs death
  WRITE_BYTE  0x055     4 // save vs wand
  WRITE_BYTE  0x056     3 // save vs poly
  WRITE_BYTE  0x057     3 // save vs breath
  WRITE_BYTE  0x058     5 // save vs spell
  WRITE_BYTE  0x234    24 // level (hd)
  WRITE_BYTE  0x238    22 // strength
  
// 16 HD fire elementals have less STR than 12 HD
COPY_EXISTING ~elfirpr2.cre~ ~override~
              ~elfirsu3.cre~ ~override~
  WRITE_BYTE 0x238 18 // strength
  BUT_ONLY

// standardize conjure (lesser) air/earth/fure elementals; see also elfirsu1.cre, elearsu2.cre, elearsu4.cre
COPY_EXISTING ~elairsu1.cre~ ~override~
              ~elearsu1.cre~ ~override~
  WRITE_BYTE  0x270 128 // neutral
  WRITE_BYTE  0x238  13 // strength

// standardize conjure (lesser) air/earth/fure elementals; see also elfirsu1.cre, elairsu1.cre, elearsu1.cre, elearsu4.cre
COPY_EXISTING ~elearsu2.cre~ ~override~
  WRITE_BYTE 0x241  0 // no racial enemy
  WRITE_BYTE 0x238 17 // strength

// standardize conjure (lesser) air/earth/fure elementals; see also elfirsu1.cre, elairsu1.cre, elearsu1.cre, elearsu2.cre
COPY_EXISTING ~elearsu4.cre~ ~override~
  WRITE_BYTE  0x241  0 // no racial enemy
  WRITE_BYTE  0x238 22 // strength

// standardize conjure (lesser) air/earth/fure elementals; see also elairsu1.cre, elearsu1.cre, elearsu2.cre, elearsu4.cre
COPY_EXISTING ~elfirsu1.cre~ ~override~
  WRITE_BYTE  0x270 128 // neutral
  WRITE_BYTE  0x238  14 // strength

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~gasform1.cre~ ~override/cdgasnd.cre~ // new gaseous forms for noble djinni
              ~gasform3.cre~ ~override/cdgasne.cre~ // new gaseous forms for noble efreeti
  WRITE_ASCIIE 0x260 ~%DEST_RES%~ #8 // replaces djinn02/efreet02 script

// pellan's +2 large shield is unique; leaving lone copy on Chak, see also rndequip.2da, rndwep.2da
COPY_EXISTING ~gorcamb3.cre~ ~override~ // tiefling
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "shld19" item = "shld30" END // swap for generic +2 large shield
  BUT_ONLY IF_EXISTS

// xp for killing habib
COPY_EXISTING ~habib.cre~  ~override~
              ~habib2.cre~ ~override~
  WRITE_LONG 0x14 15
  BUT_ONLY

// mage has override script set in ar0711.are actor listing, so override script in cre file isn't used and he does nothing
COPY_EXISTING ~hlmage.cre~ ~override~
  WRITE_ASCII 0x268 ~mage12a~ #8 // default script
  BUT_ONLY

// two imoen's missing level 1-3 mmorized spells
COPY_EXISTING ~imoen15.cre~  ~override~
  ADD_MEMORIZED_SPELL ~spwi105~ #0 ~wizard~ // color spray
  ADD_MEMORIZED_SPELL ~spwi211~ #1 ~wizard~ // melf's acid arrow
  ADD_MEMORIZED_SPELL ~spwi302~ #2 ~wizard~ // remove magic
  BUT_ONLY IF_EXISTS

// two imoen's missing level 1-3 mmorized spells
COPY_EXISTING ~imoen213.cre~ ~override~
  ADD_MEMORIZED_SPELL ~spwi112~ #0 ~wizard~ // second magic missile (nothing else in spell book)
  ADD_MEMORIZED_SPELL ~spwi211~ #1 ~wizard~ // melf's acid arrow
  ADD_MEMORIZED_SPELL ~spwi302~ #2 ~wizard~ // remove magic
  BUT_ONLY

// dupe rings of wizardry; see ring08.itm, ohringwi.itm, jaga3.cre
COPY_EXISTING ~jaga3.cre~ ~override~
  LPF ALTER_CREATURE_ITEM STR_VAR match_item = "ring08" item = "ohringwi" END // swap for non-charname clone

// jaheira missing memorized spells
COPY_EXISTING ~jaheir7.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  BUT_ONLY

// jaheira missing memorized spells
COPY_EXISTING ~jaheir8.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr409~ #3 ~priest~ // death ward
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  BUT_ONLY

// jaheira missing memorized spells
COPY_EXISTING ~jaheir11.cre~ ~override~
  ADD_MEMORIZED_SPELL ~sppr110~ #0 ~priest~ // shillelagh
  ADD_MEMORIZED_SPELL ~sppr111~ #0 ~priest~ // armor of faith
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~ // slow poison
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr409~ #3 ~priest~ // death ward
  ADD_MEMORIZED_SPELL ~sppr416~ #3 ~priest~ // cloak of fear
  ADD_MEMORIZED_SPELL ~sppr506~ #4 ~priest~ // iron skins
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  BUT_ONLY

// jaheira missing memorized spells
COPY_EXISTING ~jaheir12.cre~ ~override~
              ~jahei14.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr101~ #0 ~priest~ // bless
  ADD_MEMORIZED_SPELL ~sppr110~ #0 ~priest~ // shillelagh
  ADD_MEMORIZED_SPELL ~sppr111~ #0 ~priest~ // armor of faith
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr210~ #1 ~priest~ // resist fire/cold
  ADD_MEMORIZED_SPELL ~sppr212~ #1 ~priest~ // slow poison
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr319~ #2 ~priest~ // summon insects
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr409~ #3 ~priest~ // death ward
  ADD_MEMORIZED_SPELL ~sppr416~ #3 ~priest~ // cloak of fear
  ADD_MEMORIZED_SPELL ~sppr506~ #4 ~priest~ // iron skins
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  ADD_MEMORIZED_SPELL ~sppr613~ #5 ~priest~ // physical mirror
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  BUT_ONLY IF_EXISTS

// should defend self if attacked
COPY_EXISTING ~kamir.cre~ ~override~
  WRITE_ASCII 0x268 ~wtasight~ #8
  BUT_ONLY

// restoring dialog
COPY_EXISTING ~kpsold03.cre~ ~override~
  WRITE_ASCII 0x2cc ~kpsold03~ #8
  BUT_ONLY

// messenger should initiate dialogue with player1 (should be AO'd into dialogue, but sometimes fails)
COPY_EXISTING ~mgass01.cre~ ~override~
  WRITE_ASCII 0x258 ~initmain~ #8

// poly self > ogre palette is random, so colors constantly shift - set to same val as EE
COPY_EXISTING ~plyogre.cre~ ~override~
  WRITE_BYTE 0x2d 36 // minor
  WRITE_BYTE 0x2e 23 // major
  WRITE_BYTE 0x2f 16 // skin
  BUT_ONLY

// perth doesn't go hostile if attacked; see also perth.bcs
COPY_EXISTING ~ppcowled.cre~ ~override~
  WRITE_ASCII 0x248 perth   #8
  WRITE_ASCII 0x250 gpshout #8
  BUT_ONLY

// can't stake dace if killed before he can speak; init script blocks die() scripting
COPY_EXISTING ~ppjoye.cre~ ~override~
  WRITE_ASCII 0x248 ~ppjoye~   #8 // die script gets priority
  WRITE_ASCII 0x250 ~inpanhsg~ #8 // slight change in init script
  BUT_ONLY

// quayle shouldn't know necromancy
COPY_EXISTING ~quayle.cre~  ~override~
              ~quaylem.cre~ ~override~
  READ_BYTE 0x234 level
  REMOVE_KNOWN_SPELL ~spwi119~ ~spwi205~ // lmd, horror
  ADD_KNOWN_SPELL ~spwi118~ #0 ~wizard~ // add chromatic orb
  ADD_KNOWN_SPELL ~spwi223~ #1 ~wizard~ // add deafness
  CLEAR_ARRAY cd_spell_sub
  DEFINE_ASSOCIATIVE_ARRAY cd_spell_sub BEGIN // only subbing in memorized spells
    spwi119 => spwi118 // chromatic orb for lmd
    spwi205 => spwi223 // deafness for horror
  END
  PATCH_FOR_EACH offset IN 0x2b0 BEGIN // memorized only
    LPF cd_spellbook_sub END
  END
  BUT_ONLY
  
// renfeld's attackers should yield XP
COPY_EXISTING ~rethug02.cre~ ~override~ // mage
  WRITE_LONG 0x14 350 // xp
  BUT_ONLY

COPY_EXISTING ~rethug03.cre~ ~override~ // thug
  WRITE_LONG 0x14 325 // xp
  BUT_ONLY

// rylock can reward 200 or 300 gold for quests, but only had 125 in his pocket
COPY_EXISTING ~rylock.cre~ ~override~
  WRITE_LONG 0x1c 300 // gold
  BUT_ONLY

// saradush fire giants never fight back, shouldn't give XP
COPY_EXISTING ~sarfaki2.cre~ ~override~
              ~sarfakie.cre~ ~override~
  WRITE_LONG 0x14 0 // xp value
  BUT_ONLY IF_EXISTS

// shade lord's first spell is PW: stun; since he lacks the standard undead immunity item he inevitably stuns himself
COPY_EXISTING ~shadel.cre~ ~override~
  ADD_CRE_ITEM ~ring95~ #0 #0 #0 ~NONE~ ~LRING~
  BUT_ONLY

// frennedan the doppleganger can't actually kill you
COPY_EXISTING ~shape.cre~ ~override~
  ADD_CRE_ITEM ~dopple~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  BUT_ONLY

// efreeti summoned from bottle should use abilities like efreeti summoned via spell
COPY_EXISTING ~sumefree.cre~ ~override~
  WRITE_ASCII 0x260 ~efreet01~ #8 // general script

// druid elemental summons have wrong HD (was 12, should be 16), etc.
COPY_EXISTING ~swaair01.cre~ ~override~
              ~swaear01.cre~ ~override~
              ~swafir01.cre~ ~override~
  WRITE_SHORT 0x52   5 // thaco
  WRITE_BYTE  0x54   4 // save v death
  WRITE_BYTE  0x55   6 // save v wands
  WRITE_BYTE  0x56   5 // save v poly
  WRITE_BYTE  0x57   4 // save v breath
  WRITE_BYTE  0x58   7 // save v spells
  WRITE_BYTE  0x234 16 // level
  WRITE_BYTE  0x238 19 // strength
  WRITE_BYTE  0x23f 18 // morale
  WRITE_BYTE  0x240  1 // break
  WRITE_SHORT 0x242 30 // recovery
  BUT_ONLY IF_EXISTS

// fixed, though appear to be unused
COPY_EXISTING ~swaair02.cre~ ~override~
              ~swaear02.cre~ ~override~
              ~swafir02.cre~ ~override~
  WRITE_SHORT 0x52   0 // thaco
  WRITE_BYTE  0x54   2 // save v death
  WRITE_BYTE  0x55   4 // save v wands
  WRITE_BYTE  0x56   3 // save v poly
  WRITE_BYTE  0x57   3 // save v breath
  WRITE_BYTE  0x58   5 // save v spells
  WRITE_BYTE  0x234 24 // level
  WRITE_BYTE  0x238 22 // strength
  WRITE_BYTE  0x23f 18 // morale
  WRITE_BYTE  0x240  1 // break
  WRITE_SHORT 0x242 30 // recovery
  BUT_ONLY IF_EXISTS

// torgal lacks standard troll immunities like confusion
COPY_EXISTING ~torgal.cre~  ~override~
              ~torgal2.cre~ ~override~
              ~torgal3.cre~ ~override~
  ADD_CRE_ITEM ~trollimm~ #0 #0 #0 ~NONE~ ~LRING RRING~
  BUT_ONLY

// females with male sounds
COPY_EXISTING ~udelf03.cre~ ~override~
  WRITE_LONG 0x0a4 11063 // Good day and hello to you.
  WRITE_LONG 0x0ec 11075 // [felff05]
  WRITE_LONG 0x0f0 11076 // [felff06]
  WRITE_LONG 0x10c 11070 // Few of the fair folk concern themselves with the affairs of the state.
  WRITE_LONG 0x110 11065 // I much prefer the wooded regions, but one must be flexible in these times.
  WRITE_LONG 0x114 11063 // Good day and hello to you.
  WRITE_LONG 0x118 11070 // Few of the fair folk concern themselves with the affairs of the state.
  BUT_ONLY

// restore dialog
COPY_EXISTING ~vicg3.cre~ ~override~
  WRITE_ASCII 0x2cc ~vicg3~ #8
  BUT_ONLY

// viconia missing memorized spells
COPY_EXISTING ~viconi6.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ // second cure light wounds
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr206~ #1 ~priest~ // flame blade
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr405~ #3 ~priest~ // mental domination
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// viconia missing memorized spells
COPY_EXISTING ~viconi8.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr103~ #0 ~priest~ // second cure light wounds
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr205~ #1 ~priest~ // find traps
  ADD_MEMORIZED_SPELL ~sppr206~ #1 ~priest~ // flame blade
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr403~ #3 ~priest~ // free action
  ADD_MEMORIZED_SPELL ~sppr405~ #3 ~priest~ // mental domination
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// viconia missing memorized spells
COPY_EXISTING ~viconi9.cre~  ~override~
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ (2) // 2x doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr206~ #1 ~priest~     // flame blade
  ADD_MEMORIZED_SPELL ~sppr211~ #1 ~priest~     // silence 15' radius
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~     // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~     // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~     // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr403~ #3 ~priest~     // free action
  ADD_MEMORIZED_SPELL ~sppr405~ #3 ~priest~     // mental domination
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~     // cure critical wounds
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// viconia missing memorized spells
COPY_EXISTING ~viconi11.cre~ ~override~
  ADD_MEMORIZED_SPELL ~sppr111~ #0 ~priest~ // armor of faith
  ADD_MEMORIZED_SPELL ~sppr113~ #0 ~priest~ // doooooooooooooom
  ADD_MEMORIZED_SPELL ~sppr206~ #1 ~priest~ // flame blade
  ADD_MEMORIZED_SPELL ~sppr211~ #1 ~priest~ // silence 15' radius
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr403~ #3 ~priest~ // free action
  ADD_MEMORIZED_SPELL ~sppr405~ #3 ~priest~ // mental domination
  ADD_MEMORIZED_SPELL ~sppr416~ #3 ~priest~ // cloak of fear
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ // cure critical wounds
  ADD_MEMORIZED_SPELL ~sppr503~ #4 ~priest~ // flame strike
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY

// viconia missing memorized spells
COPY_EXISTING ~viconi13.cre~ ~override~
              ~viconi16.cre~ ~override~
  ADD_MEMORIZED_SPELL ~sppr111~ #0 ~priest~ // armor of faith
  ADD_MEMORIZED_SPELL ~sppr106~ #0 ~priest~ // magic stone
  ADD_MEMORIZED_SPELL ~sppr202~ #1 ~priest~ // barkskin
  ADD_MEMORIZED_SPELL ~sppr206~ #1 ~priest~ // flame blade
  ADD_MEMORIZED_SPELL ~sppr211~ #1 ~priest~ // silence 15' radius
  ADD_MEMORIZED_SPELL ~sppr214~ #1 ~priest~ // duhm
  ADD_MEMORIZED_SPELL ~sppr303~ #2 ~priest~ // dispel magic
  ADD_MEMORIZED_SPELL ~sppr306~ #2 ~priest~ // protection from fire
  ADD_MEMORIZED_SPELL ~sppr311~ #2 ~priest~ // rigid thinking
  ADD_MEMORIZED_SPELL ~sppr314~ #2 ~priest~ // unholy blight
  ADD_MEMORIZED_SPELL ~sppr315~ #2 ~priest~ // cure medium wounds
  ADD_MEMORIZED_SPELL ~sppr401~ #3 ~priest~ // cure serious wounds
  ADD_MEMORIZED_SPELL ~sppr403~ #3 ~priest~ // free action
  ADD_MEMORIZED_SPELL ~sppr405~ #3 ~priest~ // mental domination
  ADD_MEMORIZED_SPELL ~sppr412~ #3 ~priest~ // holy power
  ADD_MEMORIZED_SPELL ~sppr416~ #3 ~priest~ // cloak of fear
  ADD_MEMORIZED_SPELL ~sppr502~ #4 ~priest~ // cure critical wounds
  ADD_MEMORIZED_SPELL ~sppr503~ #4 ~priest~ // flame strike
  ADD_MEMORIZED_SPELL ~sppr601~ #5 ~priest~ // aerial servant
  ADD_MEMORIZED_SPELL ~sppr607~ #5 ~priest~ // heal
  // A_M_S bumps these up, but need to lower again to acocunt for WIS bonuses
  READ_LONG 0x2a8 slot_off
  WRITE_SHORT (slot_off + 0x02 + (0 * 0x10)) (THIS - 2) // lev1
  WRITE_SHORT (slot_off + 0x02 + (1 * 0x10)) (THIS - 2) // lev2
  WRITE_SHORT (slot_off + 0x02 + (2 * 0x10)) (THIS - 1) // lev3
  WRITE_SHORT (slot_off + 0x02 + (3 * 0x10)) (THIS - 1) // lev4
  BUT_ONLY IF_EXISTS

// helpful folks disappear before they can actually help; see also vvhelp.dlg, ar0700.are
COPY_EXISTING ~vvhelp.cre~   ~override~
              ~vvpriest.cre~ ~override~
  WRITE_ASCII 0x248 ~~ #8 // delete vvshaesc from override
  BUT_ONLY

/////                                                  \\\\\
///// bulk item file fixes                             \\\\\
/////                                                  \\\\\

// item type fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  miscan   =>  1 // galvena's medallion
  miscb1   =>  1 // Talisman of Rillifane
  sw1h54c  => 34 // pommel jewel of the equalizer

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_SHORT 0x1c fix
    BUT_ONLY

END

// lore fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  ax1h06   =>  65 // throwing axe +2, unused
  ax1h07   =>  65 // bala's axe (bg lore value), unused but restored by UB
  boot12   =>  55 // available via killing carras; already ID'd in store
  dagg04   =>  60 // dagger +2 longtooth (bg lore value), unused
  misc3e   =>  75 // black spider figurine
  misc3f   =>  75 // jade hound, unused
  misc3j   =>   0 // bronze horn of valhalla (upgrades come identified)
  misc3k   =>   0 // iron horn of valhalla (upgrades come identified)
  plat09   =>  50 // mithral field plate armor +2, unused
  potn40   =>   0 // cursed potion of invulnerability shouldn't require ID
  quiver02 =>  20 // case of plenty +1
  regisamu =>  65 // ruby pendant
  ring43   =>   0 // oaken ring
  sw1h24   =>  70 // flame tongue (bg lore value)

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_SHORT 0x42 fix
    BUT_ONLY IF_EXISTS

END

// enchantment fixes
ACTION_CLEAR_ARRAY cd_bulk_item_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_item_fixes BEGIN

  skelwasu => 1 // two-handed sword +1 used by summoned skeleton warriors
  wamace   => 5 // jerrod's mace (+5 vs. demons)

END

ACTION_PHP_EACH cd_bulk_item_fixes AS item => fix BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_LONG  0x60 fix
    BUT_ONLY

END

// adds magical flag to a mess of undroppable items to prevent black dragon from taking them; other items lacking magical flag
COPY_EXISTING ~regisamu.itm~ ~override~  // ruby pendant
  WRITE_BYTE 0x18 (THIS BOR BIT6)
  BUT_ONLY

// dispel magic via items can not be blocked; route through spell to allow for op 206 to block if needed - see also cditmdm0.spl, cditmdm1.spl, cd_item_dispel_magic item array
ACTION_CLEAR_ARRAY cd_item_dispel_magic
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_item_dispel_magic BEGIN

  arow07   => 99 // always dispel
  carsomyr => 99 // always dispel (melee), cast always dispel (magical)
  deva     =>  0 // caster level, power 15
  devaevil =>  0 // caster level, power 15
  elemchan =>  0 // caster level, power 15
  elemcryo =>  0 // caster level, power 15
  elemhydr =>  0 // caster level, power 15
  elemimix =>  0 // caster level, power 15
  elemogre =>  0 // caster level, power 15
  elemsunn =>  0 // caster level, power 15
  elemyanc =>  0 // caster level, power 15
  elemzaam =>  0 // caster level, power 15
  finsol01 => 22 // specific level 22
  misc5c   => 99 // always (magical header)
  planetar =>  0 // caster level, power 15
  plot04i  => 99 // always
  potn33   => 99 // always
  ravag01  => 25 // specific level 25
  reaver   => 99 // always
  scrl07   => 99 // always (cast)
  staf11   => 30 // specific level 30
  sw2h10   => 30 // specific level 30
  sw2h19   => 30 // specific level 30

END

ACTION_PHP_EACH cd_item_dispel_magic AS item => level BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    PATCH_IF level = 99 BEGIN SET slevel = 0 SET level = 0 END ELSE BEGIN SET slevel = 1 END // cditmdm0 always dispels, cditmdm1 uses level (up to 50)
    LPF ALTER_EFFECT INT_VAR match_opcode = 58 opcode = 146 parameter1 = level parameter2 = 1 STR_VAR resource = EVAL ~cditmdm%slevel%~ END // change dispel to cast spell
    // delete associated dispel magic effects
    LPF DELETE_EFFECT INT_VAR match_opcode =  77 END // cure feeblemind
    LPF DELETE_EFFECT INT_VAR match_opcode =  81 END // cure deafness
    LPF DELETE_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14056 END // 'dispel effects' string
    LPF DELETE_EFFECT INT_VAR match_opcode = 177 match_parameter1 = 7 match_parameter2 = 7 STR_VAR match_resource = destself END // destroy illusionary creatures
    LPF DELETE_EFFECT INT_VAR match_opcode = 215 STR_VAR match_resource = spdispma END // DM visuals
    LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 =  48 END // remove icon: feeblemind
    LPF DELETE_EFFECT INT_VAR match_opcode = 240 match_parameter2 = 112 END // remove icon: deaf
    IF_EXISTS

END

// targeted charm spells w/ portrait icons; see also series of cdc[abcde]XXX.eff files created
COPY_EXISTING ~regisamu.itm~ ~override~ // 60s dire charm icon
              ~ring03.itm~   ~override~ // 120s eff_m22a expiry sound
              ~staf14.itm~   ~override~ // spnwchrm, 300s charm icon
              ~spcl311.spl~  ~override~ // 120s charm icon, spnwchrm
              ~spcl641.spl~  ~override~ // spnwchrm, 7200s eff_m22a expiry sound
              ~spin108.spl~  ~override~ // 120s charm icon
              ~spin119.spl~  ~override~ // 100s charm icon
              ~spin980.spl~  ~override~ // 100s charm icon, 100s eff_e07 expiry sound, spnwchrm
              ~sppr204.spl~  ~override~ // spnwchrm, 60s charm icon, 60s eff_m22a expiry shound - also fixes wrong save on sound (174)
              ~sppr982.spl~  ~override~ // 48s dire charm icon
              ~spwi104.spl~  ~override~ // 30s charm icon, spnwchrm, 30s eff_e07 expiry sound
              ~spwi316.spl~  ~override~ // 30s dire charm icon, spnwchrm, 30s eff_e07 expiry sound - also fixes one charm effect save wrong
              ~spwi943.spl~  ~override~ // 120s dire charm icon
  READ_ASCII 0x00 sig (3)
  SET abil_length = 0x28
  PATCH_IF ("%sig%" STRING_COMPARE_CASE "ITM" = 0) BEGIN
    SET abil_length += 0x10
  END
  SET fx_delta = 0
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
    WRITE_SHORT (abil_off + 0x20 + (abil_length * index)) (THIS + fx_delta)
    READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    SET charm = 0
    SET icon0 = 0
    SET icon1 = 0
    SET sound0 = 0
    SET sound1 = 0
    SET sparklies = 0
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
      READ_LONG  (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) param2
      READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) resource
      PATCH_IF (opcode = 5) BEGIN
        SET charm = 1 // sanity check
        READ_LONG (fx_off + 0x0e + (0x30 * (abil_fx_idx + index2))) duration
      END ELSE
      PATCH_IF ((opcode = 142) AND (param2 = 0)) BEGIN
        SET icon0 = 1
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
        SET index2 -= 1
        SET fx_delta -= 1
        SET abil_fx_num -= 1
      END ELSE
      PATCH_IF ((opcode = 142) AND (param2 = 1)) BEGIN
        SET icon1 = 1
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
        SET index2 -= 1
        SET fx_delta -= 1
        SET abil_fx_num -= 1
      END ELSE
      PATCH_IF ((opcode = 174) AND ("%resource%" STRING_COMPARE_CASE ~eff_m22a~ = 0)) BEGIN
        SET sound0 = 1
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
        SET index2 -= 1
        SET fx_delta -= 1
        SET abil_fx_num -= 1
      END ELSE
      PATCH_IF ((opcode = 174) AND ("%resource%" STRING_COMPARE_CASE ~eff_e07~ = 0)) BEGIN
        SET sound1 = 1
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
        SET index2 -= 1
        SET fx_delta -= 1
        SET abil_fx_num -= 1
      END ELSE
      PATCH_IF ((opcode = 215) AND ("%resource%" STRING_COMPARE_CASE ~spnwchrm~ = 0)) BEGIN
        SET sparklies = 1
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
        SET index2 -= 1
        SET fx_delta -= 1
        SET abil_fx_num -= 1
      END
    END
    PATCH_IF ((charm = 1) AND (icon0 +icon1 + sound0 + sound1 + sparklies != 0)) BEGIN // don't bother if no charm found, or if no ancillaries found
      FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
        READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
        PATCH_IF (opcode = 5) BEGIN
          READ_ASCII   (fx_off +        (0x30 * (abil_fx_idx + index2))) clone (48)
          PATCH_IF (icon0 = 1) BEGIN
            INSERT_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
              WRITE_ASCIIE (fx_off +        (0x30 * (abil_fx_idx + index2))) ~%clone%~ #48
              WRITE_SHORT  (fx_off +        (0x30 * (abil_fx_idx + index2))) 177
              WRITE_LONG   (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) 3 // from general.ids
              WRITE_ASCIIE (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) ~cdca%duration%~ #8
            SET index2 += 1
            SET fx_delta += 1
            SET abil_fx_num += 1
          END
          PATCH_IF (icon1 = 1) BEGIN
            INSERT_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
              WRITE_ASCIIE (fx_off +        (0x30 * (abil_fx_idx + index2))) ~%clone%~ #48
              WRITE_SHORT  (fx_off +        (0x30 * (abil_fx_idx + index2))) 177
              WRITE_LONG   (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) 3 // from general.ids
              WRITE_ASCIIE (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) ~cdcb%duration%~ #8
            SET index2 += 1
            SET fx_delta += 1
            SET abil_fx_num += 1
          END
          PATCH_IF (sound0 = 1) BEGIN
            INSERT_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
              WRITE_ASCIIE (fx_off +        (0x30 * (abil_fx_idx + index2))) ~%clone%~ #48
              WRITE_SHORT  (fx_off +        (0x30 * (abil_fx_idx + index2))) 177
              WRITE_LONG   (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) 3 // from general.ids
              WRITE_BYTE   (fx_off + 0x0c + (0x30 * (abil_fx_idx + index2))) 4 // timing: delay/permanent
              WRITE_ASCIIE (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) ~cdcc%duration%~ #8
            SET index2 += 1
            SET fx_delta += 1
            SET abil_fx_num += 1
          END
          PATCH_IF (sound1 = 1) BEGIN
            INSERT_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
              WRITE_ASCIIE (fx_off +        (0x30 * (abil_fx_idx + index2))) ~%clone%~ #48
              WRITE_SHORT  (fx_off +        (0x30 * (abil_fx_idx + index2))) 177
              WRITE_LONG   (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) 3 // from general.ids
              WRITE_BYTE   (fx_off + 0x0c + (0x30 * (abil_fx_idx + index2))) 4 // timing: delay/permanent
              WRITE_ASCIIE (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) ~cdcd%duration%~ #8
            SET index2 += 1
            SET fx_delta += 1
            SET abil_fx_num += 1
          END
          PATCH_IF (sparklies = 1) BEGIN
            INSERT_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30
              WRITE_ASCIIE (fx_off +        (0x30 * (abil_fx_idx + index2))) ~%clone%~ #48
              WRITE_SHORT  (fx_off +        (0x30 * (abil_fx_idx + index2))) 177
              WRITE_LONG   (fx_off + 0x08 + (0x30 * (abil_fx_idx + index2))) 3 // from general.ids
              WRITE_LONG   (fx_off + 0x0e + (0x30 * (abil_fx_idx + index2))) 4 // duration
              WRITE_ASCII  (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) ~cdcvis~ #8
            SET index2 += 1
            SET fx_delta += 1
            SET abil_fx_num += 1
          END
        END // opcode = 5
      END // index2 for loop
    END // charm = 1
    WRITE_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
  END // index for loop
  BUT_ONLY

/////                                                  \\\\\
///// item file fixes                                  \\\\\
/////                                                  \\\\\

// pair disease only with disease icons; see also ghast1.itm, paraghas.itm, magispwr.itm, spidwr1.itm, sharswd.itm
COPY_EXISTING ~acidooz3.itm~ ~override~ // slow w/ disease icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 7 parameter2 = 41 END // changed to slow icon

// aegis fang should look like a hammer
COPY_EXISTING ~aegis.itm~  ~override~ // aegis fang
              ~aegis2.itm~ ~override~ // aegis fang
  WRITE_ASCII 0x3a ~ihamm06~ #8
  WRITE_ASCII 0x44 ~ghamm01~ #8
  WRITE_ASCII 0x58 ~chamm06~ #8
  LPF ALTER_HEADER STR_VAR icon = ihamm06 END
  BUT_ONLY

// descript/item range don't match
COPY_EXISTING ~amul01.itm~ ~override~ // necklace of missiles
  LPF ALTER_HEADER INT_VAR silent = 1 range = 50 END
  BUT_ONLY

// ability requires ID
COPY_EXISTING ~amul26.itm~   ~override~ // amulet of cheetah speed - haste
              ~boot12.itm~   ~override~ // gargoyle boots - stoneskin
              ~regisamu.itm~ ~override~ // ruby pendant - charm
              ~misc3e.itm~   ~override~ // black spider figurine - summon kitthix
              ~misc3f.itm~   ~override~ // jade hound - summon jade hound
  LPF ALTER_HEADER INT_VAR silent = 1 identify = 1 END // id to use
  BUT_ONLY IF_EXISTS
  
// unused, but fix anyway
COPY_EXISTING ~ax1h05.itm~ ~override~ // throwing axe +2
  LPF ALTER_HEADER INT_VAR silent = 1 match_type = 2 flag_strength = 1 END // add strength bonus flag to ranged header
  BUT_ONLY

// bala's axe shows string 'magic resistance lowered', but what it actually does is miscast magic
// add once-per-day dispel magic ability
COPY_EXISTING ~ax1h07.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 139 match_parameter1 = 14790 parameter1 = 54303 END
  LPF ADD_ITEM_HEADER INT_VAR required_id = 1 target = 4 target_count = 1 range = 40 charges = 1 depletion = 3 flags = BIT11 damage_type = 1 overhand = 100 STR_VAR icon = SPPR303B END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 148 target = 1 timing = 1 parameter2 = 1 STR_VAR resource = sppr303 END
  BUT_ONLY
  
// basilisk petrification should force, you know, a save against petrification
COPY_EXISTING ~basigaze.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_savingthrow = BIT0 savingthrow = BIT4 END // save vs. spell > petrification
  
// basilisk petrification should force, you know, a save against petrification
COPY_EXISTING ~basilg1.itm~ ~override~
              ~basill1.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_savingthrow = BIT2 savingthrow = BIT4 END // save vs. poison > petrification

// fire damage probabilities incorrect
COPY_EXISTING ~blun26.itm~   ~override~ // club of detonation +3 needs 4 probabilities adjusted - fix separately
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 20 match_probability2 = 0 probability1 = 19 END // fire damage should be 20% of hits, not 21% (0-19)
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 27 match_probability2 = 20 probability1 = 26 END // fireball should be 7% of hits, not 8%, not overlap with fire damage (20-26)
  BUT_ONLY

// fire damage probabilities incorrect
COPY_EXISTING ~blun27.itm~   ~override~ // club of detonation +5 needs 3 probabilities adjusted - fix separately
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 =  30 match_probability2 =  0 probability1 = 29 END // fire damage should be 30% of hits, not 31% (0-29)
  BUT_ONLY

// both versions of foa +4 use foa +3 quick icon
COPY_EXISTING ~blun30c.itm~ ~override~
              ~blun30d.itm~ ~override~
  LPF ALTER_HEADER INT_VAR header_type = 1 STR_VAR icon= EVAL ~i%SOURCE_RES%~ END
  BUT_ONLY IF_EXISTS

// items with only fire resistance should use protection from fire icon, not resist fire/cold icon
COPY_EXISTING ~blun35.itm~  ~override~ // Ice Star +4
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 26 parameter2 = 16 END // protection from fire
  BUT_ONLY IF_EXISTS

// items with only cold resistance should use protection from cold icon, not resist fire/cold icon
COPY_EXISTING ~boot03.itm~  ~override~ // boots of the north
              ~plot01p.itm~ ~override~ // Old Slippers
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 26 parameter2 = 25 END // protection from cold
  BUT_ONLY IF_EXISTS

// inconsistent handwear: bracers should use (unidentified) bracer name and descript and ground icon; see also brac17-20.itm, npmisc2.itm
COPY_EXISTING ~brac11.itm~ ~override~ // bracers of binding
              ~brac16.itm~ ~override~ // bracers of blinding strike
              ~brac22.itm~ ~override~ // paladin's bracers (ground icon)
              ~brac23.itm~ ~override~ // blessed bracers (ground icon)
              ~brac26.itm~ ~override~ // tzu-zan's bracers (ground icon)
  WRITE_LONG  0x08 6341 // bracers
  WRITE_ASCII 0x44 ~gbrac01~ #8 // bracer ground icon
  WRITE_LONG  0x50 6791 // generic bracer descript
  BUT_ONLY IF_EXISTS

// inconsistent handwear: gauntlet/gloves should use (unidentified) gauntlet name and descript and ground icon; see also brac11.itm, brac16.itm, brac22-23.itm, brac26.itm, npmisc2.itm
COPY_EXISTING ~brac17.itm~ ~override~ // gloves of pickpocketing
              ~brac18.itm~ ~override~ // gloves of missile snaring
              ~brac19.itm~ ~override~ // gauntlets of crushing
              ~brac20.itm~ ~override~ // gloves of healing
  WRITE_LONG  0x08 6797 // gauntlets
  WRITE_ASCII 0x44 ~gbrac06~ #8 // gauntlet ground icon
  WRITE_LONG  0x50 6798 // generic gauntlet descript
  BUT_ONLY

// touch attacks providing spurious thac0 bonuses
COPY_EXISTING ~chillt.itm~   ~override~ // chill touch
              ~ghoult.itm~   ~override~ // ghoul touch
              ~sgrasp01.itm~ ~override~ // shocking grasp
              ~sgrasp02.itm~ ~override~ // shocking grasp
              ~sgrasp03.itm~ ~override~ // shocking grasp
              ~sgrasp04.itm~ ~override~ // shocking grasp
              ~sgrasp05.itm~ ~override~ // shocking grasp
              ~sgrasp06.itm~ ~override~ // shocking grasp
              ~sgrasp07.itm~ ~override~ // shocking grasp
              ~sgrasp08.itm~ ~override~ // shocking grasp
              ~sgrasp09.itm~ ~override~ // shocking grasp
              ~sgrasp10.itm~ ~override~ // shocking grasp
              ~sgrasp11.itm~ ~override~ // shocking grasp
              ~sgrasp12.itm~ ~override~ // shocking grasp
              ~sgrasp13.itm~ ~override~ // shocking grasp
              ~sgrasp14.itm~ ~override~ // shocking grasp
              ~sgrasp15.itm~ ~override~ // shocking grasp
              ~sgrasp16.itm~ ~override~ // shocking grasp
              ~sgrasp17.itm~ ~override~ // shocking grasp
              ~sgrasp18.itm~ ~override~ // shocking grasp
              ~sgrasp19.itm~ ~override~ // shocking grasp
              ~sgrasp20.itm~ ~override~ // shocking grasp
  LPF ALTER_HEADER INT_VAR silent = 1 match_type = 1 to_hit = 0 dicesize = 2 dicenumber = 1 damage_type = 5 END // no thac0 bonus, change to 1d2 fist damage
  BUT_ONLY

// chill touch shouldn't apply to constructs and/or undead ("This energy attacks the life force of any living creature"); see spwi117a.spl, spwi117a.eff
COPY_EXISTING ~chillt.itm~   ~override~ // chill touch
  LPF DELETE_EFFECT END // deletes all existing effects
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 144 parameter2 = 4 duration = 1 STR_VAR resource = spwi117a END // race: golem
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 =   4 parameter2 = 3 duration = 1 STR_VAR resource = spwi117a END // general: undead
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = spwi117a END
  BUT_ONLY
  
// mustard jelly via cloak of the sewers should match poly self
COPY_EXISTING ~clck27.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR match_resource = polyochr resource = jellmu END
  BUT_ONLY

// pixie prick poison effect should be at +0 save, not +6
COPY_EXISTING ~dagg13.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 savebonus = 0 END
  BUT_ONLY

// life stealer should show a string when it drains levels
COPY_EXISTING ~dagg20.itm~ ~override~ // crom faeyr
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 216 opcode = 139 parameter1 = 41495 END // clone drain into string
  BUT_ONLY

// melee hit probabilities incorrect
COPY_EXISTING ~dagg22.itm~   ~override~ // dagger of the star +5 needs 3 probabilities adjusted
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 =  15 match_probability2 =  0 probability1 = 14 END // invisibility should be 15% of hits, not 16%
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 100 match_probability2 = 94 probability2 = 95 END // bonus fire/electrical damage should be on 5% of hits, not 6%
  BUT_ONLY

// melee hit probabilities incorrect
COPY_EXISTING ~dwwhip.itm~   ~override~ // skull needs 6 probabilities adjusted
              ~dwwhip01.itm~ ~override~ // skull needs 6 probabilities adjusted
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 =  80 match_probability2 = 60 probability1 = 79 END // stop percentages from overlapping
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 =  60 match_probability2 = 40 probability1 = 59 END // stop percentages from overlapping
  BUT_ONLY

// water weirds shouldn't get pushed around by wing buffets
COPY_EXISTING ~elemhydr.itm~ ~override~ // olhydra's weapon
              ~waterele.itm~ ~override~ // greater water elemental weapon
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 235 timing = 2 END // immunity to wing buffet
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 126 opcode = 176 END // for good measure, use 176 to restrict movement (elementals only)
  BUT_ONLY IF_EXISTS

// haste ability requires ID
COPY_EXISTING ~ensw2h.itm~   ~override~ // axe from enchanted weapon
              ~hamm09.itm~   ~override~ // crom faeyr
  LPF ALTER_HEADER INT_VAR silent = 1 range = 1 END // had spear range
  BUT_ONLY

// pair disease only with disease icons; see also acidooz3.itm, magispwr.itm, spidwr1.itm, sharswd.itm
COPY_EXISTING ~ghast1.itm~   ~override~ // thac0 penalty w/ disease icon
              ~paraghas.itm~ ~override~ // thac0 penalty w/ disease icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 7 parameter2 = 126 END // changed to nausea icon

// items which have protections from lightning bolt should get protection from new custom spell; see also cdstaf12.spl, staf12.itm
COPY_EXISTING ~gorfirg.itm~  ~override~
              ~gormisti.itm~ ~override~
              ~spellh01.itm~ ~override~
              ~stalker.itm~  ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi308 resource = cdstaf12 END
  BUT_ONLY IF_EXISTS

// melee hit probabilities incorrect - poison at 41%, stun at 30% (stun correct)
COPY_EXISTING ~gorsnake.itm~ ~override~ // skull needs 5 probabilities adjusted -
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 40 match_probability2 =  0 probability1 = 39 END // poison should be 40%, not 41%
  BUT_ONLY

// none of these are  magical
COPY_EXISTING ~helm33.itm~ ~override~ // gold horned helmet
              ~misc4y.itm~ ~override~ // necklace of talos
              ~misc5e.itm~ ~override~ // harper amulet
              ~misc5r.itm~ ~override~ // the lover's ring
              ~ring43.itm~ ~override~ // oaken ring
  WRITE_BYTE 0x18 (THIS BAND `BIT6) // removes magical flag
  BUT_ONLY IF_EXISTS
  
// gems using generic ground treasure or sack icon instead of gem
ACTION_FOR_EACH item IN hgstone misc16 misc17 misc18 misc19 misc20 misc21 misc22 misc23 misc24 misc25 misc26 misc27 misc28
                        misc29 misc30 misc31 misc32 misc33 misc34 misc35 misc36 misc37 misc38 misc39 misc40 misc41 misc6n
                        misc6o misc6t misc6z miscbh plot02c plot02d plot02e sw1h54c ttgem01 ttgem02 tttreas BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~
    WRITE_ASCII 0x44 ~ggem01~ #8
    BUT_ONLY IF_EXISTS

END

// handmaiden's mace is supposed to do 2 pts poison damage/round for 10 rounds; was actually doing 2 per second for 10 seconds
COPY_EXISTING hlolth.itm override
  LPF ALTER_EFFECT INT_VAR match_opcode = 25 parameter1 = 3 parameter2 = 3 duration = 60 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 duration = 60 END
  BUT_ONLY

// mask of king strohm will float above an attacking monk; just remove helmet animation
COPY_EXISTING ~key20.itm~ ~override~
  WRITE_SHORT 0x22 0

// stun length should be four rounds, not 20s
COPY_EXISTING ~kuobolt.itm~  ~override~
              ~kuobolt2.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 20 duration  = 24 END
  BUT_ONLY IF_EXISTS

// mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
COPY_EXISTING ~mage03.itm~ ~override~ // ring which grants the mirror image effect while equipped
  LPF ALTER_EFFECT INT_VAR match_opcode = 119 opcode = 146 power = 0 parameter1 = 0 parameter2 = 1 resist_dispel = 3 STR_VAR resource = cdwi212 END // change MI to instant spell cast
  BUT_ONLY

// mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
COPY_EXISTING ~magebrac.itm~ ~override~ // bracers which grant the Protection From Normal Missiles effect while equipped
  LPF DELETE_EFFECT END // delete all exidting effects
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 timing = 2 parameter2 = 1 resist_dispel = 3 STR_VAR resource = cdwi311 END
  BUT_ONLY

// pair disease only with disease icons; see also acidooz3.itm, ghast1.itm, paraghas.itm, sharswd.itm
COPY_EXISTING ~magispwr.itm~ ~override~ // strength penalty w/ disease icon
              ~spidwr1.itm~  ~override~ // strength penalty w/ disease icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 7 parameter2 = 91 END // changed to ability score drained icon

// harp of discord would still play expiry sound, spraklies on successful save
COPY_EXISTING ~misc3m.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 savingthrow = BIT0 resist_dispel = 1 END // sparklies also bypassed MR
  BUT_ONLY

// bad ground icons
COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
              ~misc7y.itm~ ~override~ // thrall collar
  WRITE_ASCII 0x44 gamul02 #8 // corrects ground icon
  BUT_ONLY

// vampiric wraith attack drains one level, but combat feedback says two
COPY_EXISTING ~mistva2.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40968 parameter1 = 41495 END // change 'two levels drained' to 'one level drained'
  BUT_ONLY

// mazzy's bow should use ground icon of short bow, not long bow
COPY_EXISTING ~npbow.itm~ ~override~
  WRITE_ASCII 0x44 gbow02 #8
  BUT_ONLY

// inconsistent handwear: gauntlet/gloves should use (unidentified) gauntlet name and descript and ground icon; see also brac11.itm, brac16-20.itm, brac22-23.itm, brac26.itm, npmisc2.itm
COPY_EXISTING ~npmisc2.itm~ ~override~ // Jansen Techno-Gloves
  WRITE_ASCII 0x44 ~gbrac06~ #8 // gauntlet ground icon (was generic sack icon)
  BUT_ONLY

// planetar weapon is a clone of silver sword, but uses mace icon in quickbar
COPY_EXISTING ~planetar.itm~ ~override~
  LPF ALTER_HEADER INT_VAR match_type = 1 STR_VAR icon = isw2h15 END // icon on melee header
  BUT_ONLY IF_EXISTS

// spider polymorphs should be immune to web
COPY_EXISTING ~plyspid.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 157 timing = 2 END // add immunity to web; batch patches add the rest below
  BUT_ONLY

// baby wyvern poison only applies to creatures above 6 HD
COPY_EXISTING ~plywyvrn.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 25 dicesize = 0 END // dicesize also min level
  BUT_ONLY

// potion of power is supposed to increase HiS, not MS
COPY_EXISTING ~potn41.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 59 opcode = 275 END
  BUT_ONLY

// dense pudding disease effect allows save, but icon does not
COPY_EXISTING ~pudden01.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 savingthrow = BIT2 END // save vs. death like the paired disease effect
  BUT_ONLY IF_EXISTS

// ruby pendant charm is supposed to be once-per-day; see also c6regis.cre, c6regis2.cre
COPY_EXISTING ~regisamu.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR charges = 1 flag_recharge = 1 END // "Dire Charm once per day" .
  BUT_ONLY

// descript/item range don't match
COPY_EXISTING ~ring03.itm~ ~override~ // Ring of Animal Friendship
  LPF ALTER_HEADER INT_VAR silent = 1 range = 40 END
  BUT_ONLY

// dupe rings of wizardry; see ring08.itm, ohringwi.itm, jaga3.cre
COPY_EXISTING ~ring08.itm~ ~override~ // can't be worn with itself or its clone
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 180 STR_VAR match_resource = "ring08" resource = "ohringwi" END

// dupe rings of wizardry; see ring08.itm, ohringwi.itm, jaga3.cre
COPY_EXISTING ~ring08.itm~ ~override/ohringwi.itm~ // clone into new item with new descript
  SAY 0x54 @218 // descript w/o mentioning being commissioned by <CHARNAME>

// descript/item range don't match, needs ID
COPY_EXISTING ~ring20.itm~ ~override~ // ring of energy
  LPF ALTER_HEADER INT_VAR silent = 1 range = 120 identify = 1 END
  BUT_ONLY
  
// air form of chromatic demon should be subject to poison; see also spin602-5.spl
COPY_EXISTING ~ringdemn.itm~ ~override/cdrngchr.itm~ // clone demon ring into one that allows poison damage
  LPF DELETE_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 25 END // poison immunity
  LPF DELETE_EFFECT INT_VAR match_opcode = 173 END // poison damage resistance
  LPF DELETE_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14662 END // immunity to string: poisoned
  LPF DELETE_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14017 END // immunity to string: poison

// rod of resurrection using slightly different animation than the spell; route through VVC instead of playing BAM directly
COPY_EXISTING ~rods03.itm~ ~override~ // Rod of Resurrection
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 215 STR_VAR resource = icraisei END
  BUT_ONLY

// wrong save for spear from rod of lordly might
COPY_EXISTING ~rodspear.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 12 savebonus = 6 END // extra damage should be save at +6
  BUT_ONLY

// save type, duration wrong
COPY_EXISTING ~sahbolt.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_savingthrow = BIT0 savingthrow = BIT2 END // anything that was save vs. spell is now save vs. death
  LPF ALTER_EFFECT INT_VAR match_duration = 36 duration = 30 END // duration is 5 rounds, not 6
  BUT_ONLY

// double projectiles when casting spells from scrolls
COPY_EXISTING ~scrl1j.itm~ ~override~ // imp invisibility
              ~scrla3.itm~ ~override~ // glitterdust
  LPF ALTER_HEADER INT_VAR silent = 1 projectile = 1 END // projectile comes from spell itself
  BUT_ONLY

// pair disease only with disease icons; see also acidooz3.itm, ghast1.itm, paraghas.itm, magispwr.itm, spidwr1.itm
COPY_EXISTING ~sharswd.itm~ ~override~ // poison using disease icon; slow with fatigue icon, no ability score drained icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 7 parameter2 = 6 END // disease to poison
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 39 parameter2 = 41 END // fatigue to slow
  LPF CLONE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 41 parameter2 = 91 probability2 = 0 END // create new ability score drained portrait icon
  BUT_ONLY IF_EXISTS

// shillelagh: "This spell enables the caster to create a magical cudgel that has a + 1 bonus to its attack roll and inflicts 2d4 points of damage on opponents."
COPY_EXISTING ~shille.itm~ ~override~ // wand of spell striking
  LPF ALTER_HEADER INT_VAR silent = 1 to_hit = 1 dicesize = 4 dicenumber = 2 damage = 0 END // 2d4+0, +1 to hit
  BUT_ONLY

// darksteel shield lacking its extra +1 vs. missiles for being a large shield
COPY_EXISTING ~shld31.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR target = 1 parameter1 = 1 parameter2 = BIT1 insert_point = `0 END
  BUT_ONLY IF_EXISTS

// pin damage from ixil's spike is mistimed
COPY_EXISTING ~sper12.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_timing = 1 duration = 0 END // anal retentive... permanent effect w/ non-zero duration
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 12 match_timing = 4 match_duration = 18 duration = 6 END // move this up for 1d6+5 damage at t+0, 6, 12 seconds (was t+0, 12, 18)
  BUT_ONLY IF_EXISTS

// creatures which resist/are immune to staff of power's pseudo-lightning bolt can still get stunned by it; see also cdstaf12.spl, gorfirg.itm, gormisti.itm, spellh01.itm, stalker.itm
COPY_EXISTING ~staf12.itm~   ~override~
  LPF ALTER_EFFECT INT_VAR header = 1 STR_VAR match_resource = spwi308 resource = cdstaf12 resist_dispel = 0 END // cast new, custom spell instead
  PATCH_FOR_EACH op IN 142 139 45 BEGIN
    LPF DELETE_EFFECT INT_VAR header = 1 match_opcode = op END // delete all stun-related effects from lightning header since they're now in the spell
  END
  BUT_ONLY

// melee stun effect has visuals w/o save or MR check
COPY_EXISTING ~staf13.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR header_type = 1 match_opcode = 215 resist_dispel = 1 savingthrow = BIT0 END
  BUT_ONLY

// woodland staff lacks barskin visuals
COPY_EXISTING ~staf14.itm~ ~override~
  FOR (index = 0 ; index < 6 ; ++index) BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 7 target = 1 timing = 2 parameter1 = 23 parameter2 = index END // major, minor, belt, leather, skin, armor
  END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 7 target = 1 timing = 2 parameter1 = 53 parameter2 = 6 END // diff color for hair
  BUT_ONLY

// mage pre-buffs via items should be dispellable; see cdwi212.spl, cdwi311.spl, cdwi408.spl, mage03.itm, magebrac.itm, stonskin.itm
COPY_EXISTING ~stonskin.itm~ ~override~ // ring which grants the stoneskin effect while equipped (used by Mekrath, Rayic Gethras, Terminsel... etc.)
  LPF DELETE_EFFECT END // delete all exidting effects
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 timing = 2 parameter2 = 1 resist_dispel = 3 STR_VAR resource = cdwi408 END
  BUT_ONLY

// sword of balduran is a bastard sword
COPY_EXISTING ~sw1h18.itm~ ~override~
  WRITE_SHORT 0x26 12 // min str
  WRITE_BYTE  0x31 89 // prof: bastard sword
  BUT_ONLY

// celestial fury's blindness affects every creature in the area, lacks school/secondary
COPY_EXISTING ~sw1h51.itm~ ~override~ // celestial fury
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = 1 target = 2 END // make blindness single target
  LPF ALTER_HEADER INT_VAR silent = 1 header = 1 primary = 5 secondary = 11 END // blindness: illusion, disabling
  BUT_ONLY

// demon wraith can drain two levels on hit, but feedback always says one level drained; also fix percentages while we're here
COPY_EXISTING ~telwrai.itm~  ~override~ // <invalid strref -1> needs 1 probabilities adjusted
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 139                          probability2 = 50 END // now fires only 50-99, "one level drained"
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 216 match_probability1 = 100 probability2 = 50 END // now fires only 50-99, one level drain
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 216 match_probability1 = 50 probability1 = 49 parameter1 = 2 END // now fires only 0-49, upped to two levels to compensate for other 216 changing percentages
  LPF CLONE_EFFECT INT_VAR match_opcode = 139 probability1 = 49 probability2 = 0 parameter1 = 40968 STR_VAR insert = last END     // add 'two levels drained' string to match
  BUT_ONLY

// using wands on neutrals should make them hostile
COPY_EXISTING ~wand04.itm~ ~override~ // wand of paralyzation
              ~wand18.itm~ ~override~ // wand of spell striking
              ~wand19.itm~ ~override~ // wand of cursing
  LPF ALTER_HEADER INT_VAR silent = 1 flag_hostile = 1 END // add hostile flag
  BUT_ONLY IF_EXISTS

// wand of apprenti had imbalanced elemental attacks
COPY_EXISTING ~wand15.itm~   ~override~ // wand of apprenti needs 4 probabilities adjusted
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 probability1 =  33 probability2 =  0 STR_VAR match_resource = spwi304 END // fireball on 34%, was 36%
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 probability1 =  66 probability2 = 34 STR_VAR match_resource = spwi503 END // cone of cold on 33%, was 35%
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 probability1 = 100 probability2 = 67 STR_VAR match_resource = spwi308 END // lightning bolt on 33%, was 29%
  BUT_ONLY

// potion of icedust should be self-targeted
COPY_EXISTING ~wand16.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 target = 1 END // target:self
  LPF ALTER_HEADER INT_VAR header = 0 target = 5 END // target: caster
  BUT_ONLY

// wand should disappear when out of charges
COPY_EXISTING ~wand18.itm~ ~override~ // wand of spell striking
  LPF ALTER_HEADER INT_VAR silent = 1 drained = 1 END // item disappears at 0 charges
  BUT_ONLY IF_EXISTS

// heavy crossbow of searing only doing fire damage on half its hits
COPY_EXISTING ~xbow14.itm~   ~override~ // heavy crossbow of searing +1 needs 1 probabilities adjusted
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 12 probability1 = 100 END // fire damage now 100%, was 51%
  BUT_ONLY

/////                                                  \\\\\
///// bulk spell fixes                                 \\\\\
/////                                                  \\\\\

// casting animation
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  sppr412 => 15 // holy power, necromancy > invocation
  sppr415 => 16 // farsight, invocation > divination
  sppr417 =>  9 // lesser restoration, alteration > necromancy
  sppr515 => 12 // repulse undead, alteration > abjuration
  sppr517 => 14 // insect plague, invocation > conjuration
  sppr717 => 14 // creeping doom, invocation > conjuration
  spwi897 => 14 // symbol death (npc), abjuration > conjuration
  spwi898 => 14 // symbol stun (npc), abjuration > conjuration
  spwi899 => 14 // symbol fear (npc), abjuration > conjuration
  spwm101 => 12 // repulse undead (wild surge), alteration > abjuration
  spwm123 => 14 // symbol fear (wild surge), abjuration > conjuration

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x22 fix
      BUT_ONLY IF_EXISTS

END

// primary school
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spin825 =>  0 // drow transformation
  sppr604 =>  2 // conjure animals, enchanter > conjurer
  spin950 =>  8 // flesh to stone (cutscene), abjurer > transmuter
  spin951 =>  2 // flesh to stone (cutscene), illusionist > conjurer
  sppr417 =>  7 // lesser restoration, transmuter > necromancer
  sppr413 =>  1 // negative plane protection, transmuter > abjurer
  spin746 =>  7 // RESTORE_FULL_HEALTH (tutorial), abjurer > necromancer

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x25 fix
      BUT_ONLY

END

// secondary type
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spin694 =>  7 // dragon stoneskin, SPELLPROTECTIONS > COMBATPROTECTIONS (could be taken down by pierce magic but not breach)
  spin823 =>  0 // slayer_change_two (blocked by spell shield)
  spin825 =>  0 // drow transformation

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      WRITE_BYTE 0x27 fix
      BUT_ONLY

END

// casting speed
ACTION_CLEAR_ARRAY cd_bulk_spell_fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bulk_spell_fixes BEGIN

  spwi124 => 0 // nrd
  spwi417 => 0 // enchanted weapon - spells using 2da tables get casting times pushed to sub-spells
  spwi485 => 4 // enchanted weapon: long sword
  spwi486 => 4 // enchanted weapon: short sword
  spwi487 => 4 // enchanted weapon: axe
  spwi488 => 4 // enchanted weapon: mace

END

ACTION_PHP_EACH cd_bulk_spell_fixes AS spell => fix BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      LPF ALTER_HEADER INT_VAR silent = 1 speed = fix END // speed on all headers
      BUT_ONLY

END

/////                                                  \\\\\
///// spell fixes                                      \\\\\
/////                                                  \\\\\

// dispel magic via items can not be blocked; route through spell to allow for op 206 to block if needed - see also cd_item_dispel_magic item array
COPY_EXISTING ~spwi326.spl~ ~override/cditmdm0.spl~ // create item dispel magic spell for 'always dispel' items
  WRITE_LONG  0x08 `0 // blank name
  WRITE_ASCII 0x10 ~~ #8 // blank casting sound
  WRITE_SHORT 0x22 0 // no casting animation
  WRITE_BYTE  0x25 0 // no school
  LPF ALTER_HEADER INT_VAR projectile = 1 END // no projectile
  LPF ALTER_EFFECT INT_VAR match_opcode = 58 parameter1 = 0 parameter2 = 0 END // always dispel

// dispel magic via items can not be blocked; route through spell to allow for op 206 to block if needed - see also cd_item_dispel_magic item array
COPY_EXISTING ~cditmdm0.spl~ ~override/cditmdm1.spl~ // create item dispel magic spell for 'caster/specific level' items
  LPF CD_EXTEND-O-MATIC INT_VAR step_dur = 0 level_cap = 50 step_size = 1 END // now extend to headers 2-50
  FOR (index = 0 ; index < 50 ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 58 parameter1 = (index + 1) parameter2 = 2 END // use specific level equal to header level
  END

// creatures which resist/are immune to staff of power's pseudo-lightning bolt can still get stunned by it; see also gorfirg.itm, gormisti.itm, spellh01.itm, staf12.itm, stalker.itm
COPY_EXISTING ~spwi308.spl~ ~override/cdstaf12.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 power = 3 parameter2 = 55   resist_dispel = 1 duration = 30 END // stun icon
  LPF ADD_SPELL_EFFECT INT_VAR opcode =  45 target = 2 power = 3                   resist_dispel = 1 duration = 30 END // stun itself
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 3 parameter1 = 1280 resist_dispel = 1 timing = 1 END // stunned string

// two durations are incorrect for righteous magic; stacking handled below
COPY_EXISTING ~dgright.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 122 duration = 120 END // portrait icon, self-cast protection runs two seconds long
  BUT_ONLY IF_EXISTS

// projected images shouldn't be able to attack
COPY_EXISTING ~projimag.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 0 END // some effects are dispelleble
  LPF ADD_SPELL_EFFECT INT_VAR opcode =   1 target = 1 parameter2 = 1 duration = 84 insert_point = 0 END // set APR to 0
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 101 target = 1 parameter2 = 1 duration = 84 insert_point = 1 END // prevent further APR changes
  BUT_ONLY

// melee hit probabilities incorrect
COPY_EXISTING ~sareveff.spl~ ~override~ // <invalid strref -1> needs 8 probabilities adjusted - stun on 11%; adjust manually
  LPF ALTER_EFFECT INT_VAR silent = 1 match_probability1 = 10 match_probability2 =  0 probability1 = 9 END // stun on 10% of hits, not 11%
  BUT_ONLY

// called shot effects kast two rounds instead of the listed ten seconds
COPY_EXISTING ~spcl121.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 12 duration = 10 END
  BUT_ONLY

// add miscast magic icon to wizard slayer hits; see spcl132a.eff
COPY_EXISTING ~spcl133.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 STR_VAR match_resource = spcl132 resource = spcl132a END
  BUT_ONLY

// inquisitor dispel magic missing headers
COPY_EXISTING ~spcl231.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 6 END // adds headers 2-6
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 58 parameter1 = (2 * (index + 1)) END // dispel at level x2
  END
  BUT_ONLY

// ranger charm animal has own, special icon
COPY_EXISTING ~spcl311.spl~ ~override~
  WRITE_ASCII 0x3a ~spcl641c~ #8
  LPF ALTER_SPELL_HEADER STR_VAR icon = spcl641b END
  BUT_ONLY

// maze visuals on special snare at wrong level
COPY_EXISTING ~spcl415.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 141 opcode = 215 power = 8 parameter2 = 1 timing = 0 duration = 3 STR_VAR resource = spmaze1 END
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = spmaze1 resource = spmaze2 END
  BUT_ONLY

// blade defensive spin ac errors - spell descript says -1 ac per level; spell delivers -1 per two levels. Fix spell.
COPY_EXISTING ~spcl522.spl~ ~override~
  // it's missing every even-numbered header. For real. And it has a level 21 header for good measure.
  PATCH_FOR_EACH hdr IN 12 14 16 18 20 BEGIN
    LPF DELETE_SPELL_HEADER INT_VAR min_level = hdr header_type = 1 END // delete all headers past level 10
  END
  PATCH_FOR_EACH hdr IN 2 3 5 7 9 BEGIN
    LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = hdr last_missing = hdr END // adds missing headers
  END
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 0 parameter1 = (index + 1) END // corrects ac bonus
  END
  BUT_ONLY
  
// fear immunity from level 15-19 skald bard song only being applied to singer, not party
COPY_EXISTING ~spcl542a.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_target = 1 target = 2 END
  BUT_ONLY

// mismatched saves and power
COPY_EXISTING ~spcl641.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 power = 0 savingthrow = 0 savebonus = 0 END
  BUT_ONLY

// storm shield missing headers
COPY_EXISTING ~spcl721.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 9 END // adds headers 2-9
  FOR (index = 11 ; index < 20 ; index += 2) BEGIN
    LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = index last_missing = index END // adds headers 11, 13, 15, 17, 19
  END
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index duration = ((index + 1) * 6) END // one round/level
  END
  BUT_ONLY

// talos lightning bolt missing headers
COPY_EXISTING ~spcl722.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 5 END // adds headers 2-5
  READ_SHORT 0x68 abil_num
  // level 1 is funky no matter what
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 header = 0 match_savingthrow = BIT0 END // delete 0d6 save damage
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_savingthrow = 0    dicenumber = ((index + 2) / 2) END // no-save damage
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_savingthrow = BIT0 dicenumber = ((index + 1) / 2) END // save damage
  END
  BUT_ONLY

// seeking sword missing headers
COPY_EXISTING ~spcl731.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 10 END // adds headers 2-10
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 header = index duration_high = (index * 6) END // one round/level, ignore 3-second visual
  END
  BUT_ONLY

// boon of lathander missing headers; spcl741d will be missing if TobEx installed
COPY_EXISTING ~spcl741.spl~  ~override~
              ~spcl741d.spl~ ~override~
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 10 END // adds headers 2-10
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 header = index duration_high = (index * 6) END // one round/level
  END
  BUT_ONLY IF_EXISTS

// hold undead missing headers
// most undead already block spnwchrm animation through their charm immunity
// swap to vvc clone; see spwi324.spl, cdunhvis.eff, cdunhvis.vvc
COPY_EXISTING ~spcl742.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 STR_VAR match_resource = "undchvis" resource = ~cdunhvis~ END // change visual via new eff
  LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = 2 last_missing = 5 END // adds headers 2-5
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 185 duration = ((index + 1) * 12) END // adjust hold length
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 header = index match_opcode = 177 duration = ((index + 1) * 12) match_timing = 4 STR_VAR match_resource = endsnd END // adjust end sound timing
  END
  BUT_ONLY

// hardiness, low level headers
COPY_EXISTING ~spcl907.spl~  ~override~ // hardiness hla
              ~spwish12.spl~ ~override~ // hardiness via wish
  FOR (index = 4 ; index < 20 ; index += 2) BEGIN // adds level 4, 6, ... 18 headers
    LPF CD_MISSING_SPELL_HEADERS INT_VAR first_missing = index last_missing = index END
  END
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR silent = 1 check_globals = 0 header_type = 1 header = index duration_high = (index * 6) END // one round/header (every two levels)
  END
  BUT_ONLY IF_EXISTS

// hardiness via hla and via wish should not stack with each other; see also spwish12.spl
COPY_EXISTING ~spcl907.spl~ ~override~ // hardiness (hla)
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spcl907 resource = spwish12 END // clone self-protection
  BUT_ONLY IF_EXISTS

// (greater) evasion shouldn't cross-stack with itself or other version
COPY_EXISTING ~spcl913.spl~ ~override~ // evasion
              ~spcl914.spl~ ~override~ // greater evasion
  PATCH_FOR_EACH spell IN spcl913 spcl914 BEGIN
    PATCH_IF ("%spell%" STRING_COMPARE_CASE "%SOURCE_RES%") BEGIN // if not a self-reference
      LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 opcode = 206 parameter1 = RESOLVE_STR_REF (@219) STR_VAR resource = EVAL ~%spell%~ END
    END
  END
  BUT_ONLY IF_EXISTS

// adding magic resistance icon to enhanced bard song
COPY_EXISTING ~spcl920a.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 match_parameter2 = 40 parameter2 = 63 END // clone bard song icon into MR icon
  BUT_ONLY IF_EXISTS

// wrong icon for avenger chain lightning
COPY_EXISTING ~spdr601.spl~ ~override~
  WRITE_ASCII 0x3a ~spwi615c~ #8
  BUT_ONLY

// Innate Larloch's Minor Drain (SPIN104) plays EFF_M07 with wrong target type (should be 2), timing mode (should be 1) and duration (should be 0). Compare with the Mage variant (SPWI119).
COPY_EXISTING ~spin104a.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 target = 2 timing = 1 duration = 0 STR_VAR match_resource = eff_m07 END // target, timing of sound
  BUT_ONLY

// Innate Horror (SPIN105) plays EFF_E05 with wrong duration (should be 18) or horror effect duration should be 60 like in the fixed Mage variant (SPWI205). There also seems to be a morale break effect which isn't present in the Mage variant; its parameters should be adjusted accordingly.
COPY_EXISTING ~spin105.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 106 END // delete morale break modifier (not present in mage version)
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 duration = 18 END // correct timing of expiry sound
  BUT_ONLY
  
// minsc's berserk - level drain immunity strings expire early
COPY_EXISTING ~spin117.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 30 duration = 120 END
  BUT_ONLY

// Innate Protection from Evil (SPIN121) plays EFF_E04 with wrong target type (should be 2), timing mode (should be 4) and duration (should be 60).
COPY_EXISTING ~spin121.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 target = 2 timing = 4 duration = 60 END // target, timing of expiry sound
  BUT_ONLY

// like LMD and other life-drain spells, need shell spell for proper drain/gain on PSIONIC_LIFE_DRAIN
COPY_EXISTING ~spin545.spl~ ~override/spin545a.spl~ // PSIONIC_LIFE_DRAIN
  WRITE_LONG NAME1 0xffffffff // blanks name
  WRITE_LONG NAME2 0xffffffff
  LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END // removes projectile
  LPF ALTER_EFFECT INT_VAR silent = 1 power = 0 END // power to zero
  LPF ALTER_EFFECT INT_VAR silent = 1 match_resist_dispel = 1 resist_dispel = 3 END // bypass MR
  LPF ALTER_EFFECT INT_VAR silent = 1 match_resist_dispel = 2 resist_dispel = 0 END // bypass MR
  PATCH_FOR_EACH op IN 12 18 BEGIN // change 1d6+4 to fixed d100 percentages so max/current HP boosts always match
    LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = op dicesize = 0 dicenumber = 0                      parameter1 =  5 probability2 =  0 probability1 =  16 END
    LPF CLONE_EFFECT INT_VAR            match_opcode = op dicesize = 0 dicenumber = 0 match_parameter1 = 5 parameter1 =  6 probability2 = 17 probability1 =  33 END
    LPF CLONE_EFFECT INT_VAR            match_opcode = op dicesize = 0 dicenumber = 0 match_parameter1 = 5 parameter1 =  7 probability2 = 34 probability1 =  50 END
    LPF CLONE_EFFECT INT_VAR            match_opcode = op dicesize = 0 dicenumber = 0 match_parameter1 = 5 parameter1 =  8 probability2 = 51 probability1 =  67 END
    LPF CLONE_EFFECT INT_VAR            match_opcode = op dicesize = 0 dicenumber = 0 match_parameter1 = 5 parameter1 =  9 probability2 = 68 probability1 =  83 END
    LPF CLONE_EFFECT INT_VAR            match_opcode = op dicesize = 0 dicenumber = 0 match_parameter1 = 5 parameter1 = 10 probability2 = 84 probability1 = 100 END
  END
  IF_EXISTS

// makes MR-checking "shell" spell that also eliminates self-cast exploit
COPY_EXISTING ~spin545.spl~ ~override~ // innate LMD
  LPF DELETE_EFFECT INT_VAR END // deletes all effects
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 power = 4 parameter2 = 1 timing = 1 resist_dispel = 1 STR_VAR resource = spin545a END // cast secondary spell
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 1 STR_VAR resource = spin545 END // prevents self-casting, make first effect
  IF_EXISTS

// one charm effect save wrong
COPY_EXISTING ~spin553.spl~ ~override~ // NALMISSRA_CHARM
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 savebonus = "-2" END // visuals were -6, but all other charm stuff is -2
  BUT_ONLY IF_EXISTS

// cosmetics don't match save of charm effect
COPY_EXISTING ~spin558.spl~ ~override~ // ERINYES_CHARM
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 savebonus = "-5" END // icon, visuals were -8, but charm itself is -5
  BUT_ONLY IF_EXISTS

// dragon breath attacks hit multiple times - separate into visual & damage spells a la red/green dragon breath
// see also cddrblck.pro, cddrsilv.pro, dragyell.bcs, dragbrow.bcs, dragblac.bcs, draghell.bcs, dragsilv.bcs, shadra01.bcs, test99.bcs
// now create visuals-only spells from originals
COPY_EXISTING ~spin595.spl~ ~override/spin595v.spl~ // yellow dragon scorching sand
              ~spin596.spl~ ~override/spin596v.spl~ // brown dragon acid breath
              ~spin691.spl~ ~override/spin691v.spl~ // black dragon breath
              ~spin832.spl~ ~override/spin832v.spl~ // silver dragon paralyzation
              ~spin833.spl~ ~override/spin833v.spl~ // silver dragon cone of cold
              ~spin893.spl~ ~override/spin893v.spl~ // shadow dragon breath
  LPF DELETE_EFFECT INT_VAR END // delete all effects
  WRITE_LONG 0x08 "-1" // blank name
  BUT_ONLY IF_EXISTS

// update originals to use new, non-repeating projectiles
COPY_EXISTING ~spin691.spl~ ~override~ // black dragon breath
              ~spin893.spl~ ~override~ // hadow dragon breath
  LPF ALTER_SPELL_HEADER INT_VAR projectile = cddrblck END // cddrblck (no visuals, no repeat)
  BUT_ONLY

// now create visuals-only spells from originals
COPY_EXISTING ~spin595.spl~ ~override~ // yellow dragon scorching sand
              ~spin596.spl~ ~override~ // brown dragon acid breath
              ~spin832.spl~ ~override~ // silver dragon paralyzation
              ~spin833.spl~ ~override~ // silver dragon cone of cold
  LPF ALTER_SPELL_HEADER INT_VAR projectile = cddrsilv END // cddrsilv (no visuals, no repeat)
  BUT_ONLY IF_EXISTS

// air form of chromatic demon should be subject to poison; see also cdrngchr.itm
COPY_EXISTING ~spin602.spl~ ~override~ // chromatic demon shift to tanarri
              ~spin603.spl~ ~override~ // chromatic demon shift to ice golem
              ~spin604.spl~ ~override~ // chromatic demon shift to shambling mound
  LPF CLONE_EFFECT INT_VAR match_opcode = 27 opcode = 143 parameter1 = 7 STR_VAR resource = ringdemn END // use ringdemn for all non-air elemental shifts
  LPF CLONE_EFFECT INT_VAR match_opcode = 27 opcode =  11 parameter1 = 0 parameter2 = 0 END              // also cure any poison
  IF_EXISTS

// air form of chromatic demon should be subject to poison; see also cdrngchr.itm
COPY_EXISTING ~spin605.spl~ ~override~ // chromatic demon shift to air elemental
  LPF CLONE_EFFECT INT_VAR match_opcode = 27 opcode = 143 parameter1 = 7 STR_VAR resource = cdrngchr END // use cdrngchr for all air elemental shifts
  IF_EXISTS

// wrong icon, duration for kilthix's web
COPY_EXISTING ~spin683.spl~ ~override~ // kilthix's web ability
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 142 duration = 12 parameter2 = 129 END // change from held to webbed icon, two rounds
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_duration = 21 duration = 12 END // other effects, two rounds
  BUT_ONLY

// getting one too many stoneskins at level 7
COPY_EXISTING ~spin694.spl~ ~override~ // dragon_stone_skin
              ~spwi408.spl~ ~override~ // stoneskin, arcane
  LPF ALTER_EFFECT INT_VAR match_globals = 0 silent = 1 match_opcode = 218 parameter1 = 3 header = 0 END // three skins at level 7
  BUT_ONLY

// cerebus' improved invisibility provides non-detection and lacks sound on one header
COPY_EXISTING ~spin698.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 69 END // delete non-detection
  LPF ADD_SPELL_EFFECT INT_VAR header = 2 opcode = 174 target = 1 timing = 1 resist_dispel = 2 STR_VAR resource = eff_m51 END // add sound to second header
  BUT_ONLY

// great druid title change should retain 'fighter' part fot multi/dual-classes; see also spin722a.eff, spin722b.eff, spin722c.eff
COPY_EXISTING ~spin722.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 290 match_parameter2 = 0 opcode = 177 parameter1 = 11 parameter2 = 5 STR_VAR resource = spin722a END // class name change, druid
  LPF ALTER_EFFECT INT_VAR match_opcode = 290 match_parameter2 = 1 opcode = 177 parameter1 = 11 parameter2 = 5 STR_VAR resource = spin722b END // record screen name change, druid
  LPF CLONE_EFFECT INT_VAR parameter1 = 16 STR_VAR match_resource = spin722b resource = spin722c END                                           // record screen name change, fighter-druid
  BUT_ONLY

// stoneskin effect for trademeet statues can be dispelled
COPY_EXISTING ~spin776.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 2 END
  BUT_ONLY

// sound effect for demilich howl has wrong save
COPY_EXISTING ~spin789.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 savebonus = 0 savingthrow = BIT2 END // other effects have +0 save vs. death
  BUT_ONLY

// can avoid slayer death with immunity to magic energy; we set that resistance to 0 juuuust before the nasty 1500 hp kill shot
COPY_EXISTING ~spin823.spl~ ~override~ // Slayer Change
  LPF CLONE_EFFECT INT_VAR match_opcode = 12 match_parameter1 = 1500 opcode = 31 parameter1 = 0 parameter2 = 1 duration = 43 dicesize = 0 STR_VAR insert = above END // go into effect one second early
  BUT_ONLY

// consistent lay on hands icons
COPY_EXISTING ~spin827.spl~ ~override~ // mazzy version
  LPF ALTER_SPELL_HEADER STR_VAR icon = ~spcl815~ END // casting icon, from CLW icon
  BUT_ONLY

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~spin967.spl~ ~override/cdgasne2.spl~ // spell to turn noble efreeti back from gas
  LPF ALTER_EFFECT INT_VAR match_opcode = 151 STR_VAR resource = genefn01 END // replace self (noble efreeti)

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~spin969.spl~ ~override/cdgasnd2.spl~ // spell to turn noble efreeti back from gas
  LPF ALTER_EFFECT INT_VAR match_opcode = 151 STR_VAR resource = gendjn01 END // replace self (noble djinni)

// summoned efreeti/djinni come back from gaseous form as hostile, noble come back as generics
// see cdgasnd.spl, cdgasnd2.spl, cdgasnd.cre, cdgasnd.bcs, cdgasne.spl, cdgasne2.spl, cdgasne.cre, cdgasne.bcs, djinni01.bcs, djinni04.bcs, efreet01.bcs, efreet04.bcs
COPY_EXISTING ~spin970.spl~ ~override/cdgasne.spl~ // new spell to turn noble efreeti to gas
              ~spin972.spl~ ~override/cdgasnd.spl~ // new spell to turn noble djinni to gas
  LPF ALTER_EFFECT INT_VAR match_opcode = 151 STR_VAR resource = EVAL ~%DEST_RES%~ END // replace self with new gaseous creature

// using 'rock' icon instead of scroll icon
COPY_EXISTING ~sppr111.spl~ ~override~ // armor of faith
  WRITE_ASCII 0x3a ~sppr111c~ #8
  BUT_ONLY

// sounds shouldn't play on successful save
COPY_EXISTING ~sppr310.spl~ ~override~ // miscast magic
              ~sppr986.spl~ ~override~ // miscast magic via trap
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 savingthrow = BIT0 savebonus = "-2" END // same save as other effects
  BUT_ONLY
  
// one bad resist/dispel on shield of the archons (min lev 16, protection from lev 7 spells)
COPY_EXISTING ~sppr701.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 resist_dispel = 2 END
  BUT_ONLY

// mass raise dead not healing resurrectees
COPY_EXISTING ~sppr729.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR power = 7 END // power levels almost all 5 from copied resurrection spell
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sppr729.spl~ ~override/sppr729b.spl~
  WRITE_LONG  0x08 `0
  WRITE_LONG  0x50 `0
  WRITE_ASCII 0x10 ~~ #8
  LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 304 END
  LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 172 END
  PATCH_FOR_EACH op IN 17 50 139 BEGIN
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = op timing = 1 duration = 0 END
  END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 215 timing = 0 parameter2 = 0 END
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~sppr729.spl~ ~override~
  PATCH_FOR_EACH op IN 17 50 139 215 BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = op END
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 3 power = 7 parameter2 = 1 timing = 4 duration = 1 insert_point = 99 STR_VAR resource = sppr729b END
  BUT_ONLY IF_EXISTS
  
// stalker haste should bypass MR
COPY_EXISTING ~spra301.spl~ ~override~ // dire charm
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_resist_dispel = 1 resist_dispel = 3 END // dispel/not bypass to dispel/bypass
  BUT_ONLY

// ranger minor spell deflection uses minor spell turning icons; has extraneous portrait icon
COPY_EXISTING ~spra302.spl~ ~override~
  WRITE_ASCII 0x3a ~spwi318c~ #8 
  LPF ALTER_HEADER INT_VAR silent = 1 STR_VAR icon = spwi318b END
  LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 142 END // already has hardcoded spell deflection icon
  FOR (index = 5 ; index < 8 ; ++index) BEGIN // should protect from spell levels 1-7, not 1-4
    LPF CLONE_EFFECT INT_VAR match_opcode = 201 multi_match = 1 parameter2 = index END
  END
  BUT_ONLY

// beast master version of animal summoning i has incorrect duration
COPY_EXISTING ~spra304.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 177 duration = 180 END
  BUT_ONLY

// Grease (SPWI101) shouldn't have the hostile flag on. This causes the unfortunate battle music loop. No other AoE-stopping spells have this flag on.
COPY_EXISTING ~spwi101.spl~ ~override~
  WRITE_LONG 0x18 (THIS BAND `BIT10) // removes hostile flag
  BUT_ONLY

// spook should require LOS
COPY_EXISTING ~spwi125.spl~ ~override~
  WRITE_BYTE 0x19 (THIS & `BIT3) // remove 'no LOS required' flag
  BUT_ONLY

// ray of enfeeblement message has wrong saving throw
COPY_EXISTING ~spwi221.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 savebonus = 0 END
  BUT_ONLY

// most undead already block spnwchrm animation through their charm immunity
// swap to vvc clone; see spcl742.spl, cdunhvis.eff, cdunhvis.vvc
COPY_EXISTING ~spwi324.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 STR_VAR match_resource = "undchvis" resource = ~cdunhvis~ END // change visual via new eff
  BUT_ONLY

// Mage Remove Curse (SPWI410) plays wrong visual effect (should be SPRMCURS). Compare with the Priest variant (SPPR307).
COPY_EXISTING ~spwi410.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 215 STR_VAR resource = sprmcurs END // from sphealin to sprmcurs - identical visuals, different sound
  BUT_ONLY

// pause can screw up spells using 2da tables, so change target and make casting instant (casting time pushed to sub-spells)
COPY_EXISTING ~spwi417.spl~ ~override~ // enchanted weapon
              ~spwi510.spl~ ~override~ // spell immunity
  LPF ALTER_HEADER INT_VAR silent = 1 target = 7 speed = 0 END

// sunfire still shows (cosmetic) fire hit on caster
COPY_EXISTING ~spwi523.spl~ ~override~
  LPF CLONE_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 30 opcode = 206 power = 0 parameter1 = 0 parameter2 = 0 STR_VAR resource = "spwi523" insert = "first" END // clone 30 into 206, move first
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 30 END // delete fire resistance; no longer needed with new 206
  BUT_ONLY
     
// tenser's transformation not providing the final two bumps to thac0 (levs 19, 20)
COPY_EXISTING spwi603.spl override
  LPF ALTER_EFFECT INT_VAR match_opcode = 54 match_parameter2 = 1 match_duration = 114 parameter1 = 2 END // lev 19
  LPF ALTER_EFFECT INT_VAR match_opcode = 54 match_parameter2 = 1 match_duration = 120 parameter1 = 1 END // lev 20
  BUT_ONLY
  
// improved haste should prevent ranger haste, as it does regular haste
COPY_EXISTING ~spwi613.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 206 STR_VAR match_resource = spwi305 resource = spra301 END // clone one to the other
  BUT_ONLY
  
// sphere of chaos effects should be subject to MR
COPY_EXISTING ~spwi711.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END // damage, disintegrate, heal
  BUT_ONLY

// Time Stop (SPWI909, SPWISH17) plays EFF_M91 with wrong duration (should be set to half of the Time Stop effect duration due to the quirks of opcode 231).
COPY_EXISTING ~spwi909.spl~ ~override~ // time stop (arcane)
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 match_duration = 36 duration = 18 END // time stop only runs half its listed duration
  BUT_ONLY

// improved alacrity expiry sound plays too late
COPY_EXISTING ~spwi921.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 174 match_timing = 4 duration = 12 END // sound should play at end of spell
  BUT_ONLY IF_EXISTS

// missing visuals for many wish effects due to typo
COPY_EXISTING ~spwish01.spl~ ~override~
              ~spwish02.spl~ ~override~
              ~spwish03.spl~ ~override~
              ~spwish04.spl~ ~override~
              ~spwish05.spl~ ~override~
              ~spwish06.spl~ ~override~
              ~spwish08.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 215 STR_VAR match_resource = "inmagich" resource = "icmagich" END // typo
  BUT_ONLY IF_EXISTS

// hardiness via hla and via wish should not stack with each other; see also spcl907.spl
COPY_EXISTING ~spwish12.spl~ ~override~ // hardiness (wish)
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwish12 resource = spcl907 END // clone self-protection
  BUT_ONLY IF_EXISTS

// Time Stop (SPWI909, SPWISH17) plays EFF_M91 with wrong duration (should be set to half of the Time Stop effect duration due to the quirks of opcode 231).
COPY_EXISTING ~spwish17.spl~ ~override~ // time stop (wish)
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 match_duration = 72 duration = 36 END // time stop only runs half its listed duration
  BUT_ONLY
  
// wild surge hold was using special, unblockable hold
COPY_EXISTING ~spwm114.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 185 opcode = 175 END

// lighting for cacofiend via wild surge only plays half the time
COPY_EXISTING ~spwm154.spl~  ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 probability1 = 100 END // op 141 was at 50%
  BUT_ONLY

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

// lazarus is supposed to have all spell scrolls in the game
COPY_EXISTING ~25spell.sto~  ~override~ // arcana archives
              ~25spell2.sto~ ~override~ // arcana archives
  ADD_STORE_ITEM ~SCRL81~ AFTER ~SCRL71~ #1 #0 #0 ~IDENTIFIED~ #2 // Sleep
  ADD_STORE_ITEM ~SCRL73~ AFTER ~SCRL71~ #1 #0 #0 ~IDENTIFIED~ #2 // Protection from Petrification
  BUT_ONLY IF_EXISTS

// standardize on one stoneskin scroll; see also ar0510.are
COPY_EXISTING ~garlena.sto~  ~override~ // temple of helm
              ~sahpr1.sto~   ~override~ // temple of sekolah
              ~suelf10.sto~  ~override~ // temple of rillifane
              ~type1.sto~    ~override~ // merchant
              ~udduer01.sto~ ~override~ // merchant
  LPF ALTER_STORE_ITEM STR_VAR match_item = scrl1v item = scrl2b END
  BUT_ONLY

/////                                                  \\\\\
///// pro fixes                                        \\\\\
/////                                                  \\\\\

// 'trap' projectiles should be more sensitive, go off immediately
COPY_EXISTING ~iceglyp.pro~  ~override~
              ~trapglpn.pro~ ~override~
              ~trapglyp.pro~ ~override~
              ~trapskul.pro~ ~override~
              ~tskullnp.pro~ ~override~
  WRITE_SHORT 0x210 1 // explosion frequency
  BUT_ONLY

// ice storm lasts four rounds, not three
COPY_EXISTING ~icestorm.pro~ ~override~
  WRITE_BYTE 0x216 4 // should last 4 rounds per descript (four repetitions)
  BUT_ONLY

/////                                                  \\\\\
///// vvc fixes                                        \\\\\
/////                                                  \\\\\

// hold undead visuals don't work; see spcl742.spl, spwi324.spl, cdunhvis.eff
COPY_EXISTING ~spnwchrm.vvc~ ~override/cdunhvis.vvc~

/////                                                  \\\\\
///// eff fixes                                        \\\\\
/////                                                  \\\\\

// chill touch shouldn't apply to constructs and/or undead ("This energy attacks the life force of any living creature"); see spwi117a.spl, chillt.itm
COPY_EXISTING ~immsp710.eff~ ~override/spwi117a.eff~
  WRITE_ASCII 0x30 ~spwi117a~ #8

// conjure air elemental lacks 24 HD elemental; see also elairsu2-4.cre, spair1-3.eff
OUTER_FOR (i = 1 ; i < 4 ; ++i) BEGIN
  
  OUTER_SET j = i + 1

  COPY_EXISTING ~spair%i%.eff~ ~override~
    WRITE_ASCIIE 0x30 ~elairsu%j%~

END

COPY_EXISTING ~lycnhit2.eff~ ~override~ // unused, but fixed anyway
  WRITE_LONG 0x1c 122 // change racial target from vampires to lycanthropes, matching rest of series
  BUT_ONLY

// werebane lacks listed +3 thac0 vs lycanthropes since effect's probability is 0
COPY_EXISTING ~lycnhit3.eff~ ~override~
  WRITE_SHORT 0x2c 100 // prob1
  BUT_ONLY

// add miscast magic icon to wizard slayer hits; see spcl133.spl
COPY_EXISTING ~spcl132.eff~ ~override/spcl132a.eff~
  WRITE_LONG 0x10 142 // dsplay icon
  WRITE_LONG 0x20 105 // miscast magic

// great druid title change should retain 'fighter' part fot multi/dual-classes; see also spin722.spl, spin722a.eff, spin722c.eff
COPY_EXISTING ~spin722a.eff~ ~override/spin722b.eff~
  WRITE_LONG 0x1c 2543 // great druid
  WRITE_LONG 0x20 1    // record screen change

// great druid title change should retain 'fighter' part fot multi/dual-classes; see also spin722.spl, spin722a.eff, spin722b.eff
COPY_EXISTING ~spin722b.eff~ ~override/spin722c.eff~ // class name
  SAY 0x1c @259

// conjure lesser fire/earth elemental summoning wrong creature
COPY_EXISTING ~splesfir.eff~ ~override~
              ~splesear.eff~ ~override~
  WRITE_ASCII 0x37 ~1~ // corrected file for conjure lesser earth/fire elemental

// hold undead visuals don't work; see spcl742.spl, spwi324.spl, cdunhvis.vvc
COPY_EXISTING ~undchvis.eff~ ~override/cdunhvis.eff~
  WRITE_ASCII 0x30 ~cdunhvis~ // new visual// new eff for target-limited charm spells; creates charm sparklies via eff
COPY_EXISTING ~dawnvis.eff~ ~override/cdcvis.eff~
  WRITE_LONG  0x18 0          // power
  WRITE_LONG  0x28 4          // duration
  WRITE_ASCII 0x30 ~spnwchrm~ // charm sparklies

// since charm can be targeted via general.ids, related effects need to be routed through EFFs
ACTION_FOR_EACH dur IN 30 60 100 120 300 7200 BEGIN

  // new eff for target-limited charm spells; creates charm icon via eff
  COPY_EXISTING ~cdca30.eff~ ~override/cdca%dur%.eff~
    WRITE_LONG 0x28 dur // duration

END
  
ACTION_FOR_EACH dur IN 30 48 60 BEGIN // now clone into several ones for varying durations

  COPY_EXISTING ~cdca30.eff~ ~override/cdcb%dur%.eff~
    WRITE_LONG 0x20   1 // icon: dire charm
    WRITE_LONG 0x28 dur // duration

END

// new eff for target-limited charm spells; creates eff_e07 expiry sound via eff
ACTION_FOR_EACH dur IN 60 120 7200 BEGIN // now clone into several ones for varying durations

  COPY_EXISTING ~coldhit.eff~ ~override/cdcc%dur%.eff~
    WRITE_ASCII 0x30 ~eff_m22a~ #8 // charm expiry sound
    WRITE_LONG  0x28 dur           // duration

END

// new eff for target-limited charm spells; creates eff_e07 expiry sound via eff
ACTION_FOR_EACH dur IN 30 100 BEGIN // now clone into several ones for varying durations

  COPY_EXISTING ~coldhit.eff~ ~override/cdcd%dur%.eff~
    WRITE_ASCII 0x30 ~eff_e07~ #8 // charm expiry sound
    WRITE_LONG  0x28 dur          // duration

END

COPY_EXISTING ~undhit2.eff~ ~override~ // unused, but fixed anyway
  WRITE_LONG 0x60 2 // change bonus to 2 from 3, as the name implies and matching rest of eff series
  BUT_ONLY

/////                                                  \\\\\
///// bam fixes                                        \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// music fixes                                      \\\\\
/////                                                  \\\\\

/////                                                  \\\\\
///// immunity effects batch patches                   \\\\\
/////                                                  \\\\\

// adds string and portrait icon to stuns; items commented out already have the full suite but were getting flagged since they use an alt stun string
COPY_EXISTING ~carrio.itm~   ~override~ // ghoul hand, 2 new effects from cd_full_stun_arrays
              ~cornugon.itm~ ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~demcor01.itm~ ~override~ // skull, 2 new effects from cd_full_stun_arrays
              ~deva.itm~     ~override~ // mace of disruption +2, 1 new effects from cd_full_stun_arrays
              ~elemhydr.itm~ ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~fampsdat.itm~ ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~ghoulc.itm~   ~override~ // ghoul hand, 2 new effects from cd_full_stun_arrays
              ~goltome4.itm~ ~override~ // attack, 1 new effects from cd_full_stun_arrays
              ~gorcamb.itm~  ~override~ // two-handed sword +2, 1 new effects from cd_full_stun_arrays
              ~gorsnake.itm~ ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~icetrl.itm~   ~override~ // attack, 2 new effects from cd_full_stun_arrays
              ~mepsal.itm~   ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~misc3h.itm~   ~override~ // horn of blasting, 1 new effects from cd_full_stun_arrays
              ~mistwa.itm~   ~override~ // skull, 1 new effects from cd_full_stun_arrays
              ~ravag02.itm~  ~override~ // boneblade +4, 1 new effects from cd_full_stun_arrays
              ~sareveff.spl~ ~override~ // <invalid strref -1>, 1 new effects from cd_full_stun_arrays
              ~slng06.itm~   ~override~ // sling of arvoreen +4, 1 new effects from cd_full_stun_arrays
              ~spcl123.spl~  ~override~ // <invalid strref -1>, 2 new effects from cd_full_stun_arrays
//              ~spdr101.spl~  ~override~ // chromatic orb, 2 new effects from cd_full_stun_arrays
              ~spermel.itm~  ~override~ // spear, 1 new effects from cd_full_stun_arrays
              ~spin543.spl~  ~override~ // inflict pain, 1 new effects from cd_full_stun_arrays
              ~spin727.spl~  ~override~ // psionic blast, 1 new effects from cd_full_stun_arrays
              ~spin832.spl~  ~override~ // silver dragon paralyzation, 1 new effects from cd_full_stun_arrays
              ~spin834.spl~  ~override~ // psionic blast, 1 new effects from cd_full_stun_arrays
              ~spin927.spl~  ~override~ // boiling rain storm, 1 new effects from cd_full_stun_arrays
              ~spin934.spl~  ~override~ // salt crystals, 1 new effects from cd_full_stun_arrays
              ~spin944.spl~  ~override~ // power word, stun, 1 new effects from cd_full_stun_arrays
              ~spin959.spl~  ~override~ // psionic blast, 1 new effects from cd_full_stun_arrays
              ~spin974.spl~  ~override~ // psionic blast, 1 new effects from cd_full_stun_arrays
              ~sppr718.spl~  ~override~ // symbol, stun, 4 new effects from cd_full_stun_arrays
//              ~sppr984.spl~  ~override~ // chromatic orb, 1 new effects from cd_full_stun_arrays
//              ~spwi118.spl~  ~override~ // chromatic orb, 2 new effects from cd_full_stun_arrays
              ~spwi816.spl~  ~override~ // symbol, stun, 3 new effects from cd_full_stun_arrays
              ~spwi959.spl~  ~override~ // power word, stun, 1 new effects from cd_full_stun_arrays
              ~spwm152.spl~  ~override~ // polymorph other, 2 new effects from cd_full_stun_arrays
//              ~staf12.itm~   ~override~ // staff of power, 1 new effects from cd_full_stun_arrays - now handled by cdstaf12.spl
              ~staf13.itm~   ~override~ // staff of thunder and lightning, 1 new effects from cd_full_stun_arrays
              ~sw1h51.itm~   ~override~ // celestial fury, 2 new effects from cd_full_stun_arrays
  LPF cd_apply_batch STR_VAR array_name = cd_full_stun_arrays END // adds string/icon to stun effects
  BUT_ONLY IF_EXISTS
  
COPY_EXISTING ~plyspid.itm~ ~override~ // weapon for polymorphed spider
  LPF cd_apply_batch INT_VAR force_cosmetic = 1 STR_VAR array_name = cd_immunity_web_arrays END // undroppable but player-available via polymorph so needs cosmetic protections
  BUT_ONLY

// alters spell school immunities to cover AoE spells in school
COPY_EXISTING ~spwi590.spl~  ~override~ // immunity: abjuration, 24 new effects from cd_immunity_abjuration
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_abjuration END
  BUT_ONLY

// alters spell school immunities to cover AoE spells in school
COPY_EXISTING ~spwi591.spl~  ~override~ // immunity: conjuration, 84 new effects from cd_immunity_conjuration
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_conjuration END
  BUT_ONLY

// alters spell school immunities to cover AoE spells in school
COPY_EXISTING ~spwi595.spl~  ~override~ // immunity: evocation, 120 new effects from cd_immunity_evocation
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_evocation END
  BUT_ONLY

// alters spell school immunities to cover AoE spells in school
COPY_EXISTING ~spwi596.spl~  ~override~ // immunity: necromancy, 60 new effects from cd_immunity_necromancy
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_necromancy END
  BUT_ONLY

// alters spell school immunities to cover AoE spells in school
COPY_EXISTING ~spwi597.spl~  ~override~ // immunity: alteration, 60 new effects from cd_immunity_alteration
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_alteration END
  BUT_ONLY

// probability fixes - automated
COPY_EXISTING ~aurstaf.itm~  ~override~ // staff of the ram +4 needs 3 probabilities adjusted - wing buffet/sleep at 11%
              ~ax1h15.itm~   ~override~ // axe of the unyielding +5 needs 3 probabilities adjusted - vorpal hit at 11%
              ~balor.itm~    ~override~ // skull needs 3 probabilities adjusted - slay effect at 16%
              ~blakblad.itm~ ~override~ // black blade of disaster needs 7 probabilities adjusted - level drain on 11%
              ~blun14.itm~   ~override~ // flail of ages +3 needs 2 probabilities adjusted - slow at 34%
              ~blun29.itm~   ~override~ // storm star +5 needs 1 probabilities adjusted - chain lightning on 6%
              ~blun30.itm~   ~override~ // flail of ages +5 needs 2 probabilities adjusted - slow at 34%
              ~blun30c.itm~  ~override~ // flail of ages +4 needs 2 probabilities adjusted - alow at 34%
              ~blun30d.itm~  ~override~ // flail of ages +4 needs 2 probabilities adjusted - slow at 34%
              ~bonedag.itm~  ~override~ // mordenkainen's sword needs 3 probabilities adjusted - slow at 26%
              ~bonefd.itm~   ~override~ // skull needs 4 probabilities adjusted - str/dex penalties at 31%
              ~dagg17.itm~   ~override~ // stiletto of demarchess +2 needs 3 probabilities adjusted - hold at 21%
              ~dagg20.itm~   ~override~ // dagger +4, life-stealer needs 2 probabilities adjusted - level drain at 16%
              ~dagg21.itm~   ~override~ // dagger of the star +4 needs 1 probabilities adjusted - invisibility at 6%
              ~demmau01.itm~ ~override~ // skull needs 6 probabilities adjusted - hold at 16%
              ~deva.itm~     ~override~ // mace of disruption +2 needs 2 probabilities adjusted - dispel magic at 26%
              ~devaevil.itm~ ~override~ // silver sword needs 4 probabilities adjusted - dispel magic, vorpal hit at 26%
              ~devmon01.itm~ ~override~ // <invalid strref -1> needs 1 probabilities adjusted - crushing damage at 11%
              ~dogwawp.itm~  ~override~ // attack needs 3 probabilities adjusted - level drain at 21% (slow at 20% correct)
              ~finsol01.itm~ ~override~ // <invalid strref -1> needs 7 probabilities adjusted - slay at 16%, dispel magic at 84%
              ~finsol02.itm~ ~override~ // <invalid strref -1> needs 2 probabilities adjusted - kill target at 26%
              ~gith.itm~     ~override~ // silver sword needs 2 probabilities adjusted - kill target at 26%
              ~gorcamb.itm~  ~override~ // two-handed sword +2 needs 10 probabilities adjusted - stun at 51%, attribute drain at 49% (mr drain ok, but needs adjustment down to match)
              ~gorwom1.itm~  ~override~ // <invalid strref -1> needs 18 probabilities adjusted - charm at 51% (sleep/charm effs fixed elsewhere)
              ~gorwom4.itm~  ~override~ // drow flail +3 needs 5 probabilities adjusted - slow at 51%, blindness at 49%
              ~halb09.itm~   ~override~ // halberd +4: wave needs 1 probabilities adjusted - cold damage on 16% of hits
              ~halb11.itm~   ~override~ // ravager +6 needs 3 probabilities adjusted - vorpal hits at 11%
              ~icetrl.itm~   ~override~ // attack needs 5 probabilities adjusted - dex/stun penalty at 34%
              ~insanity.spl~ ~override~ // insanity gaze needs 3 probabilities adjusted - feeblemind at 51%
              ~marili.itm~   ~override~ // sword of flame +1 needs 16 probabilities adjusted - fire damage at 11%, poison damage at 9%
              ~miscbc.itm~   ~override~ // blackrazor needs 8 probabilities adjusted - level drain etc. on 16% of hits
              ~mistho.itm~   ~override~ // skull needs 5 probabilities adjusted - panic at 31%, drop item at 11%
              ~mistice.itm~  ~override~ // skull needs 4 probabilities adjusted - panic at 31%
              ~mistva.itm~   ~override~ // skull needs 3 probabilities adjusted - level drain at 19%
              ~mistva2.itm~  ~override~ // skull needs 3 probabilities adjusted - level drain at 51%
              ~mound.itm~    ~override~ // <invalid strref -1> needs 10 probabilities adjusted - entangle at 51%
              ~planetar.itm~ ~override~ // silver sword needs 5 probabilities adjusted - vorpal hits/dispel magic at 26%
              ~ravag01.itm~  ~override~ // skull needs 3 probabilities adjusted - wing buffet/sleep at 49%
              ~reaver.itm~   ~override~ // unholy reaver needs 1 probabilities adjusted - dispel magic at 51%
              ~rods05.itm~   ~override~ // rod of terror needs 2 probabilities adjusted - charisma penalty at 21%
              ~sahzom01.itm~ ~override~ // <invalid strref -1> needs 2 probabilities adjusted - disease at 11%
              ~sendai.itm~   ~override~ // drow flail +3 needs 5 probabilities adjusted - poison on 76%, slow on 24%
              ~sharswd.itm~  ~override~ // bastard sword +1 needs 6 probabilities adjusted - poison on 51%, slow on 49
              ~slimed2.itm~  ~override~ // broken shield needs 4 probabilities adjusted - delayed exploding deaths +1%
              ~spauru.spl~   ~override~ // <invalid strref -1> needs 6 probabilities adjusted - 26% fire damage, 24% magic damage
              ~spcl918.spl~  ~override~ // alchemy needs 14 probabilities adjusted - 13% master thievery, 11% rogue potion of frost giant strength
              ~spermel.itm~  ~override~ // spear needs 5 probabilities adjusted - stun 26%, slow 74%
              ~spin106a.spl~ ~override~ // <invalid strref -1> needs 90 probabilities adjusted - lowest hp drain 11%, highest 9%
              ~spin106b.spl~ ~override~ // <invalid strref -1> needs 90 probabilities adjusted - lowest hp drain 11%, highest 9%
              ~spin203.spl~  ~override~ // cloak of fear needs 1 probabilities adjusted - 26% chance to drop item
              ~spin534.spl~  ~override~ // call dark horde needs 1 probabilities adjusted - 61% summon chance
              ~spin675.spl~  ~override~ // wandering horror needs 1 probabilities adjusted - 26% chance to drop item
              ~spin787.spl~  ~override~ // <invalid strref -1> needs 6 probabilities adjusted - 26% acid damage, 24% fire
              ~spogre01.spl~ ~override~ // earthquake needs 1 probabilities adjusted - 3% to summon greater earth elemental
              ~sppr301.spl~  ~override~ // animate dead needs 30 probabilities adjusted - always +1% for one summon, -1% for double
              ~sppr416.spl~  ~override~ // cloak of fear needs 1 probabilities adjusted - 26% to drop item
              ~sppr605.spl~  ~override~ // conjure fire elemental needs 40 probabilities adjusted - 61% for weakest elemental, 4% for strongest
              ~sppr702.spl~  ~override~ // conjure earth elemental needs 28 probabilities adjusted - 61% for weakest elemental, 4% for strongest
              ~sppr720.spl~  ~override~ // earthquake needs 1 probabilities adjusted - 3% of elemental summoning
              ~spwi309.spl~  ~override~ // monster summoning i needs 48 probabilities adjusted - 61% for one monster, 39% for two
              ~spwi314a.spl~ ~override~ // <invalid strref -1> needs 90 probabilities adjusted - lowest hp drain 11%, highest 9%
              ~spwi314b.spl~ ~override~ // <invalid strref -1> needs 90 probabilities adjusted - lowest hp drain 11%, highest 9%
              ~spwi407.spl~  ~override~ // monster summoning ii needs 42 probabilities adjusted - 61% for one monster, 39% for two
              ~spwi423.spl~  ~override~ // spider spawn needs 36 probabilities adjusted - 81% for one spider, 19% for two
              ~spwi501.spl~  ~override~ // animate dead needs 18 probabilities adjusted - always +1% for one skellie, -1% for two (skel warriors at lev 15+ fine)
              ~spwi504.spl~  ~override~ // monster summoning iii needs 36 probabilities adjusted - 61% for one monster, 39% for two
              ~spwi620.spl~  ~override~ // conjure fire elemental needs 40 probabilities adjusted - summon should be 60/35/5 split, instead is 61/35/4
              ~spwi621.spl~  ~override~ // conjure air elemental needs 40 probabilities adjusted - summon should be 60/35/5 split, instead is 61/35/4
              ~spwi622.spl~  ~override~ // conjure earth elemental needs 40 probabilities adjusted - summon should be 60/35/5 split, instead is 61/35/4
              ~spwi623.spl~  ~override~ // carrion summons needs 30 probabilities adjusted - 66% chance to summon one carrion crawler, 34% two
              ~spwi711.spl~  ~override~ // sphere of chaos needs 19 probabilities adjusted - 11% polymorph, 9% haste
              ~spwish14.spl~ ~override~ // create wand needs 14 probabilities adjusted - 13% wand of fear, 11% wand of cursing
              ~spwm130.spl~  ~override~ // repulsion, caster needs 18 probabilities adjusted - 11% spfirepi.vvc, 9% spfleshs.vvc
              ~spwm136.spl~  ~override~ // good berries needs 4 probabilities adjusted +1% chance for horn coral, -1% pearl
              ~staf13.itm~   ~override~ // staff of thunder and lightning needs 4 probabilities adjusted - thunderclap on 11% of hits
              ~staf21.itm~   ~override~ // staff of the ram +4 needs 3 probabilities adjusted - stun/knockback on 11%
              ~staf22.itm~   ~override~ // staff of the ram +6 needs 3 probabilities adjusted - stun/knockbak on 16%
              ~staf23.itm~   ~override~ // serpent shaft needs 2 probabilities adjusted - poison at 51%
              ~sw1h51.itm~   ~override~ // celestial fury needs 1 probabilities adjusted - electrical damage at 6%
              ~sw1h58.itm~   ~override~ // short sword of mask +4 needs 6 probabilities adjusted - entangle at 16%
              ~sw1h59.itm~   ~override~ // short sword of mask +5 needs 12 probabilities adjusted - entangle at 16%
              ~sw1h67.itm~   ~override~ // usuno's blade +4 needs 2 probabilities adjusted - 11% electrical damage
              ~sw1h99.itm~   ~override~ // sword of chaos needs 3 probabilities adjusted - slay at 11%
              ~sw2h07.itm~   ~override~ // harbinger +3 needs 2 probabilities adjusted - fireball at 6%
              ~sw2h15.itm~   ~override~ // silver sword needs 3 probabilities adjusted - vorpal hit at 26%
              ~sw2h17.itm~   ~override~ // gram the sword of grief +5 needs 1 probabilities adjusted - poison at 11%
              ~sw2h18.itm~   ~override~ // gram the sword of grief +5 needs 1 probabilities adjusted - poison at 11%
              ~sw2hdeat.itm~ ~override~ // soul reaver +4 needs 1 probabilities adjusted - level drain at 51%
              ~telslav.itm~  ~override~ // <invalid strref -1> needs 8 probabilities adjusted - level drain 86%, slay 14%
              ~trolltor.itm~ ~override~ // attack needs 4 probabilities adjusted - str penalty 51%, slow 49%
              ~vamt01.itm~   ~override~ // vampiric touch needs 36 probabilities adjusted - +1% for lowest hp drian -1% for highest
              ~vamt06.itm~   ~override~ // vampiric touch needs 36 probabilities adjusted
              ~vamt08.itm~   ~override~ // vampiric touch needs 36 probabilities adjusted
              ~vamt10.itm~   ~override~ // vampiric touch needs 36 probabilities adjusted
              ~vamt12.itm~   ~override~ // vampiric touch needs 36 probabilities adjusted
              ~was2h.itm~    ~override~ // joril's dagger +3 needs 3 probabilities adjusted - confusion at 26%
              ~wastar.itm~   ~override~ // everard's morning star +2 needs 2 probabilities adjusted - spell drain at 51%
              ~zomsea.itm~   ~override~ // short sword  needs 2 probabilities adjusted - disease at 11%
//              ~chalcy2.itm~  ~override~ // short sword of backstabbing  needs 3 probabilities adjusted - fine as is
//              ~misc3a.itm~   ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a1.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a2.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a3.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a4.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a5.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a6.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a7.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3a8.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~misc3aa.itm~  ~override~ // book of infinite spells needs 18 probabilities adjusted - all correct
//              ~ravag02.itm~  ~override~ // boneblade +4 needs 8 probabilities adjusted - all correct
//              ~spcl919.spl~  ~override~ // scribe scrolls needs 16 probabilities adjusted - fine as is
//              ~spin545a.spl~ ~override~ // <invalid strref -1> needs 20 probabilities adjusted - fine as is
//              ~sppr723.spl~  ~override~ // elemental summoning needs 17 probabilities adjusted - fine as is
//              ~sppr724.spl~  ~override~ // greater elemental summoning needs 4 probabilities adjusted - fine as is
//              ~spwi714.spl~  ~override~ // prismatic spray needs 21 probabilities adjusted - fine as is
//              ~spwish13.spl~ ~override~ // alchemy needs 20 probabilities adjusted - fine as is
//              ~wand12.itm~   ~override~ // wand of wonder needs 26 probabilities adjusted - fine as is
  READ_ASCII 0x00 type (3)
  SET min_size = 0
  PATCH_IF ("%type%" STRING_COMPARE_CASE "spl" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x28
    SET global_loop    = 0
    SET fx_type        = 0
    SET min_size       = 0x72
  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "itm" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x38
    SET global_loop    = 1
    SET fx_type        = 0
    SET min_size       = 0x72
/*  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "cre" = 0) BEGIN
    READ_LONG  0x2c4 fx_off ELSE 0
    READ_BYTE 0x33 fx_type ELSE 2
    SET abil_off       = 0
    SET abil_num       = 0
    SET counter_offset = 0x2c8
    SET abil_length    = 0
    SET global_loop    = 1
    SET min_size       = 0x2d4
    SET cosmetic       = 0 */
  END
  PATCH_IF ((SOURCE_SIZE >= min_size) AND (min_size != 0)) BEGIN // min_size must get set by file type detection
    FOR (index = (0 - global_loop) ; index < abil_num ; ++index) BEGIN
      PATCH_IF (index < 0) BEGIN // if loop through globals needed
        SET abil_fx_idx = 0
      END ELSE BEGIN // otherwise normal ability
        SET counter_offset = (abil_off + 0x1e + (abil_length * index))
        READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
      END
      READ_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
      // run one pass purely looking for keys
      FOR (index2 = 0 ; index2 < counter ; ++index2) BEGIN
        PATCH_FOR_EACH offset IN 0x12 0x13 BEGIN
          READ_BYTE  (fx_off + offset + (offset * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) prob
          PATCH_IF (prob > 0) AND (prob < 100) BEGIN
            WRITE_BYTE  (fx_off + offset + (offset * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (prob - 1)
          END
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Optional, but cool                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// keldorn xp                                       \\\\\
/////                                                  \\\\\

BEGIN @6 DESIGNATED 100
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// get xp for keldorn's reconciliation if keldorn is released from the party
COMPILE ~bg2fixpack/dlg/keldorn.d~

/////                                                  \\\\\
///// maze animations                                  \\\\\
/////                                                  \\\\\

BEGIN @7 DESIGNATED 101
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// maze animation, part 1
COPY_EXISTING ~spcl415.spl~ ~override~ // maze (special snare)
              ~spin774.spl~ ~override~ // maze (psionic)
              ~spwi813.spl~ ~override~ // maze (arcane)
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215              STR_VAR match_resource = spmaze2 END
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 215 duration = 5 STR_VAR match_resource = spmaze1 resource = spspmaze END
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 213 duration = 4 timing = 4 END
  BUT_ONLY

// maze animation, part 2
COPY_EXISTING ~spmaze2.vvc~ ~override/spspmaze.vvc~
  WRITE_ASCIIE 0x08 ~%DEST_RES%~ #8
  WRITE_BYTE   0x4c 0x08
  WRITE_BYTE   0x68 0x00
  WRITE_ASCII  0x78 ~EFF_M74~ #8

// change enemy symbol spells to use same visuals as PC symbols
COPY_EXISTING ~iceglyp.pro~ ~override/cdnpcsym.pro~ // clone symbol graphics into ally-friendly projectile
  WRITE_SHORT 0x200 (THIS BOR BIT6) // add ally-friendly flag

ADD_PROJECTILE ~override/cdnpcsym.pro~ // add to projectl.ids

// now assign new projectile to enemy symbol spells
COPY_EXISTING ~spin897.spl~ ~override~ // WIZARD_NPC_SYMBOL_DEATH
              ~spin898.spl~ ~override~ // WIZARD_NPC_SYMBOL_STUN
              ~spin899.spl~ ~override~ // WIZARD_NPC_SYMBOL_FEAR
  LPF ALTER_SPELL_HEADER INT_VAR projectile = cdnpcsym END
    
// define max play length for these animations when called via 215s
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_anim_max_dur BEGIN
  icrmpari => 2
  icstreni => 2
  icwrati  => 4
  spdispma => 3
  spnwchrm => 4
  sppowwrd => 2
  sprotect => 2
  sptrusee => 3
END

// animation fixes
COPY_EXISTING ~bhaal4b.spl~ ~override~
              ~spcl213.spl~ ~override~
              ~spin550.spl~ ~override~
              ~spin553.spl~ ~override~
              ~spin558.spl~ ~override~
              ~spin674.spl~ ~override~
              ~spin675.spl~ ~override~
              ~spin696.spl~ ~override~
              ~spin826.spl~ ~override~
              ~spin891.spl~ ~override~
              ~sppr107.spl~ ~override~
              ~sppr408.spl~ ~override~
              ~spwi113.spl~ ~override~
              ~spwi214.spl~ ~override~
              ~spwi702.spl~ ~override~
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = fx_off ; index < SOURCE_SIZE ; index += 0x30) BEGIN
    READ_SHORT (index       ) opcode
    READ_BYTE  (index + 0x0c) timing
    PATCH_IF ((opcode = 215) AND (timing = 0)) BEGIN
      READ_LONG  (index + 0x0e) duration
      READ_ASCII (index + 0x14) anim
      PATCH_PHP_EACH cd_anim_max_dur AS anim_match => dur BEGIN
        PATCH_IF (("%anim%" STRING_COMPARE_CASE "%anim_match%" = 0) AND (duration > dur)) BEGIN
          WRITE_LONG  (index + 0x0e) dur
        END
      END
    END
  END
  BUT_ONLY IF_EXISTS

// make burning hands AoE instead of single target
COPY_EXISTING ~cspray.pro~   ~override/burnhand.pro~
  WRITE_SHORT 0x206 64 // range (5')
  WRITE_BYTE  0x217  0 // no explosion

ADD_PROJECTILE ~override/burnhand.pro~

COPY_EXISTING ~spwi103.spl~  ~override~
  LPF ALTER_SPELL_HEADER INT_VAR target = 4 projectile = burnhand END // use new projectile, target any point
  BUT_ONLY

// update scroll with AoE targeting
COPY_EXISTING ~scrl68.itm~ ~override~ // burning hands
  LPF ALTER_ITEM_HEADER INT_VAR header = 1 target = 4 range = 5 END // ability header
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 opcode = 148 target = 1 END // spell cast itself
  BUT_ONLY

// Make Errard's Divination spell actually use the Divination casting graphics (aVENGER)
COPY_EXISTING ~spin541.spl~ ~override~ // divination
  WRITE_SHORT 0x22 16 // Casting graphics: 16 (Divination)
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// Cromwell's forging actually takes a day (24 hrs) \\\\\
/////                                                  \\\\\

BEGIN @36 DESIGNATED 102 DEPRECATED @22
SUBCOMPONENT @8
GROUP @2

/////                                                  \\\\\
///// Mixed-use dagger fixes                           \\\\\
/////                                                  \\\\\

BEGIN @9 DESIGNATED 103
GROUP @2

// removes melee abilities from Fire Tooth and Boomerang dagger; change APR to be consistent
// with other throwing daggers; alters base Fire Tooth damage to 1d4 (again for consistency); damage change now rescinded (Wisp)
COPY_EXISTING ~dagg11.itm~ ~override~
              ~dagg12.itm~ ~override~
  LPF DELETE_ITEM_HEADER INT_VAR header_type = 1 END
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 1 parameter1 = 2 parameter2 = 1 END // set APR to 2 instead of +1
  BUT_ONLY

COPY_EXISTING ~tooltip.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(DAGG1[12][ %TAB%]+.+\)15529\([ %TAB%].+\)$~ ~\1-1   \2~ // remove melee from tooltip
  BUT_ONLY

/////                                                  \\\\\
///// Ghrey's Holy Symbol Fixes                        \\\\\
/////                                                  \\\\\

BEGIN @10 DESIGNATED 104
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mel01.cre~ @11 // ToB required
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~0~) OR (GAME_IS ~bg2ee eet~)) @33 // requires the holy symbol exploit removed

// disable holy symbols for Aerie, Anomen, and Viconia in main game scripts
COPY_EXISTING ~baldur.bcs~   ~override~
              ~baldur25.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HasItem("CDHLYSYM",\(Player[1-6]\))~ ~HasItem("CDHLYSYM",\1) !Name("Aerie",\1) !Name("Anomen",\1) !Name("Viconia",\1)~
  END
  BUT_ONLY

// now grant their holy symbols via their own override scripts
EXTEND_BOTTOM ~aerie.bcs~   ~bg2fixpack/baf/et_aerie.baf~
EXTEND_BOTTOM ~aeri25.bcs~  ~bg2fixpack/baf/et_aerie.baf~
EXTEND_BOTTOM ~anomen.bcs~  ~bg2fixpack/baf/et_anom.baf~
EXTEND_BOTTOM ~anom25.bcs~  ~bg2fixpack/baf/et_anom.baf~
EXTEND_BOTTOM ~viconia.bcs~ ~bg2fixpack/baf/et_vicon.baf~
EXTEND_BOTTOM ~vico25.bcs~  ~bg2fixpack/baf/et_vicon.baf~

COPY ~bg2fixpack/bam/d0baer.bam~ ~override~
     ~bg2fixpack/bam/d0shar.bam~ ~override~

// Holy Symbol of Helm Usable by Good Aligned. Poo, but necessary to let Anomen use it.
COPY_EXISTING ~belt13.itm~ ~override~
  WRITE_BYTE 0x1e (THIS BAND `BIT2) // removes good flag
  BUT_ONLY

COPY ~bg2fixpack/itm/j#belt12.itm~ ~override~
  SAY NAME2 @128
  SAY IDENTIFIED_DESC @129

COPY ~bg2fixpack/itm/j#belt14.itm~ ~override~
  SAY NAME2 @130
  SAY IDENTIFIED_DESC @131

/////                                                  \\\\\
///// Jenia will wait until PC hero of trademeet       \\\\\
/////                                                  \\\\\

BEGIN @12 DESIGNATED 105
DEPRECATED @22

// moved into core fixes per Gaider

/////                                                  \\\\\
///// giants have penalties v shorties                 \\\\\
/////                                                  \\\\\

BEGIN @13 DESIGNATED 106
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// giant weapons get THAC0 penalties vs. small people (revised by aVENGER)
COPY_EXISTING ~giafir.itm~   ~override~
              ~giafir2.itm~  ~override~
              ~giafir3.itm~  ~override~
              ~giants01.itm~ ~override~
  PATCH_FOR_EACH eff IN giant1 giant2 giant3 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 timing = 2 parameter1 = 142 parameter2 = 4 STR_VAR resource = EVAL ~%eff%~ END // 3 effs go into effect on race: giant
  END
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// Remove Dual-class Restriction from ranger kits   \\\\\
/////                                                  \\\\\

BEGIN @14 DESIGNATED 107
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

COPY_EXISTING ~dualclas.2da~ ~override~
  SET_2DA_ENTRY 32 2 7 ~1~ // archer (feralan)
  SET_2DA_ENTRY 33 2 7 ~1~ // stalker
  BUT_ONLY

/////                                                  \\\\\
///// Remove Wrath-Evil bonuses                        \\\\\
/////                                                  \\\\\

BEGIN @15 DESIGNATED 108
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// remove bonuses here as the player is already rewarded at the door
COPY_EXISTING ~ar2905.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Global("Player1Wrath","GLOBAL",2)~ ~False()~
 END
 BUT_ONLY

/////                                                  \\\\\
///// summoned demon behavior                          \\\\\
/////                                                  \\\\\

BEGIN @16 DESIGNATED 109
GROUP @2

COMPILE ~bg2fixpack/baf/demglasu.baf~
        ~bg2fixpack/baf/demnabsu.baf~
        ~bg2fixpack/baf/dempitsu.baf~

/////                                                  \\\\\
///// additional script fixes                          \\\\\
/////                                                  \\\\\
                
BEGIN @17 DESIGNATED 110
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// commoners missing scripts to turn hostile if attacked
COPY_EXISTING ~aewimer1.cre~ ~override~
              ~aewimer2.cre~ ~override~
              ~aewimer3.cre~ ~override~
              ~bdgoph01.cre~ ~override~
              ~bdgoph02.cre~ ~override~
              ~brat1.cre~    ~override~
              ~brat2.cre~    ~override~
              ~brat3.cre~    ~override~
              ~ftown1.cre~   ~override~
              ~ftown2.cre~   ~override~
              ~ftown3.cre~   ~override~
              ~ftown4.cre~   ~override~
              ~haquat.cre~   ~override~
              ~maria.cre~    ~override~
              ~mourner5.cre~ ~override~
              ~mourner6.cre~ ~override~
              ~mtown1.cre~   ~override~
              ~mtown2.cre~   ~override~
              ~mtown3.cre~   ~override~
              ~mtown4.cre~   ~override~
              ~murtlen.cre~  ~override~
              ~noblem1.cre~  ~override~
              ~noblem2.cre~  ~override~
              ~noblew1.cre~  ~override~
              ~noblew2.cre~  ~override~
              ~peony.cre~    ~override~
              ~postul1.cre~  ~override~
              ~postul3.cre~  ~override~
              ~postul5.cre~  ~override~
              ~postul6.cre~  ~override~
              ~pwauk2.cre~   ~override~
              ~radeel.cre~   ~override~
              ~scbutler.cre~ ~override~
              ~scqar.cre~    ~override~
              ~scsarles.cre~ ~override~
              ~sethle.cre~   ~override~
              ~trskin02.cre~ ~override~
              ~trtavp05.cre~ ~override~
              ~uhmer02.cre~  ~override~
              ~wellyn.cre~   ~override~
  SET script_runenemy = 0
  SET script_wtrunsgt = 0
  // first, check to see if these are present
  FOR (index = 0 ; index < 5 ; ++index) BEGIN
    READ_ASCII (0x248 + (index * 0x08)) script
    PATCH_IF ("%script%" STRING_COMPARE_CASE "runenemy" = 0) BEGIN
      SET script_runenemy = 1
    END ELSE
    PATCH_IF (("%script%" STRING_COMPARE_CASE "wtrunsgt" = 0) OR
              ("%script%" STRING_COMPARE_CASE "wdrunsgt" = 0)) BEGIN
      SET script_wtrunsgt = 1
    END
  END
  // has one but not the other
  PATCH_IF (script_runenemy + script_wtrunsgt = 1) BEGIN
    FOR (index = 0 ; index < 5 ; ++index) BEGIN
      READ_ASCII (0x248 + (index * 0x08)) script
      PATCH_IF (("%script%" STRING_COMPARE_CASE "none" = 0) OR ("%script%" STRING_COMPARE_CASE "" = 0)) BEGIN
        PATCH_IF (script_runenemy = 0) BEGIN
          WRITE_ASCII (0x248 + (index * 0x08)) runenemy
        END ELSE BEGIN
          WRITE_ASCII (0x248 + (index * 0x08)) wtrunsgt
        END
        SET index = 5 // kills loop
      END
    END
  END
  BUT_ONLY

// create fatigue-free restoration spell for cut59a
COPY_EXISTING ~sppr417.spl~  ~override/cdpr417.spl~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 93 END

// dragons have responses to cloud-based attacks
// was presumed to be zone of sweet air, but already included in wing buffet spell, so ref removed
COPY_EXISTING ~abazdrag.bcs~ ~override~ // abazigal
              ~dragbrow.bcs~ ~override~ // draconis
              ~draggre2.bcs~ ~override~
              ~draggree.bcs~ ~override~
              ~gorsal.bcs~   ~override~ // saladrex
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~~ // in response to death fog/incend cloud, cloudkill
 END
 BUT_ONLY IF_EXISTS

// direct target of breath attacks gets some extra damage; see drgrbrht or RED_DRAGON_HIT (spin693) in other dragon AI scripts
COPY_EXISTING ~abazdrag.bcs~ ~override~
              ~dragblue.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(LastSeenBy(Myself),0)~ ~~
  END
 BUT_ONLY IF_EXISTS

// unknown fourth possible option; efreet seems to be a common alternative to nishruu summoning (plus it's known to cre)
COPY_EXISTING ~amlich02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(Myself,0)~ ~SpellNoDec(Myself,WIZARD_SUMMON_EFREET)~ // other three actions--two summoning spells and move to PC
  END
 BUT_ONLY IF_EXISTS

// two presumably olive slimes were supposed to spawn--no such creatures
COPY_EXISTING ~ar2200.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("jeloli01",\[4422\.3524\],6)~ ~CreateCreature("jelmus01",[4422.3524],6)~ // replace w/ mustard jelly
    REPLACE_TEXTUALLY ~CreateCreature("jeloli01",\[4052\.3574\],6)~ ~CreateCreature("jelgre01",[4052.3574],6)~ // replace w/ green slime
  END
  BUT_ONLY

// blank block for HaveSpell, CastSpell--only one memorized but not scripted is ADHW
COPY_EXISTING ~bheye.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)~
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)~
  END
  BUT_ONLY

// script designed to turn invisible repeatedly; given level and kit, assassination seems a good fit here
COPY_EXISTING ~chalcy02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(Myself,0)~ ~SpellNoDec(Myself,ROGUE_ASSASINATION)~ // Some prep spell
  END
 BUT_ONLY IF_EXISTS

// blank block for HaveSpell, CastSpell in offensive situation--two spells memorized but not scripted: fireball and magic missile
// select MM as there's no range check for fireball safety
COPY_EXISTING ~clone1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(NearestEnemyOf(Myself),0)~
      ~HaveSpell(WIZARD_MAGIC_MISSILE) \1 Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)~
  END
  BUT_ONLY

// final game cutscene
COPY_EXISTING ~cut59a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ApplySpell(\([A-Za-z0-9]+\),0)~
      ~ApplySpellRES("cdpr417",\1)~   // some form of restoration--follows resurrections and full heal
  END
  BUT_ONLY

// cutscene
COPY_EXISTING ~cut67d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell("Dpimo01",0)~ ~ForceSpell("Dpimo01",CUTSCENE_DAMAGE_1)~   // irenicus doing something to Imoen
  END
  BUT_ONLY

// degardan has one messed up script
COPY_EXISTING ~degard2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY // against non-humanoids; use HM even though not memorized since used in similar blocks in other AI scripts
      ~\(!General(LastSeenBy(Myself),HUMANOID)[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~\1 HaveSpell(WIZARD_HOLD_MONSTER) \2 Spell(LastSeenBy(Myself),WIZARD_HOLD_MONSTER)~
    REPLACE_TEXTUALLY // death spell? something effective against multiple enemies-- cone of cold?
      ~\(NumCreatureGT(\[ALLY\],4)[%TAB% %LNL%%MNL%%WNL%]+Allegiance(Myself,ENEMY)[%TAB% %LNL%%MNL%%WNL%]+See(NearestEnemyOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~\1 HaveSpell(WIZARD_DEATH_SPELL) \2 Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)~
    REPLACE_TEXTUALLY // one of several generic attack responses--melf's acid arrow?
      ~SpellNoDec(LastSeenBy(Myself),0)~
      ~SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)~
    REPLACE_TEXTUALLY // anti-mage spell--hold person? do this one last, had problems with matching
      ~HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~HaveSpell(WIZARD_HOLD_PERSON) \1 Spell(LastSeenBy(Myself),WIZARD_HOLD_PERSON)~
  END
  BUT_ONLY

// in between dragon fear and a wing buffet--per other dragon AI, should be lowering specific resistances to breath attack, but nothing to reduce poison resistance
COPY_EXISTING ~draggre2.bcs~ ~override~
              ~draggree.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(NearestEnemyOf(Myself),0)~ ~~
  END
  BUT_ONLY IF_EXISTS

/*
// gauths should be firing off more spells
COPY_EXISTING ~gauth01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ReallyForceSpell(\[PC\],0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("GauthBehavior","LOCALS",1)\)~
      ~ReallyForceSpell([PC],0) \1~ // no idea
    REPLACE_TEXTUALLY
      ~\(!See(LeastDamagedOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+HPGT(Myself,35)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+ReallyForceSpell(\[PC\],BEHOLDER_HOLD_PERSON)[%TAB% %LNL%%MNL%%WNL%]+\)ReallyForceSpell([PC],0)~
      ~\1 ReallyForceSpell([PC],0)~ // no idea
    REPLACE_TEXTUALLY
      ~\([^!]See(LeastDamagedOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+HPGT(Myself,35)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+ReallyForceSpell(\[PC\],BEHOLDER_HOLD_PERSON)[%TAB% %LNL%%MNL%%WNL%]+\)ReallyForceSpell([PC],0)~
      ~\1 ReallyForceSpell([PC],0)~ // no idea
  END
  BUT_ONLY
*/
/*
// missing spell in heal-?-restoration sequence
COPY_EXISTING ~gorgua01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~ApplySpell(Myself,FORCE_DISPEL_MAGIC)~ // between full heal and restore--dispel effects?
  END
  BUT_ONLY
*/

// gphealer has two HaveSpell, Spell blocks with unknown IDS entries--something offensive with a 5' radius
// found near-identical sequence for mage symbol spells in gpmage1
COPY_EXISTING ~gphealer.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~\(IF[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+!HasBounceEffects(LastSeenBy(Myself))[%TAB% %LNL%%MNL%%WNL%]+!Range(LastSeenBy(Myself),5)[%TAB% %LNL%%MNL%%WNL%]+RandomNum(2,1)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+END[%TAB% %LNL%%MNL%%WNL%]+IF[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+!HasBounceEffects(LastSeenBy(Myself))[%TAB% %LNL%%MNL%%WNL%]+!Range(LastSeenBy(Myself),5)[%TAB% %LNL%%MNL%%WNL%]+RandomNum(2,1)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+END\)~
      ~\1 HaveSpell(CLERIC_SYMBOL_DEATH) \2 Spell(LastSeenBy(Myself),CLERIC_SYMBOL_DEATH) \3 HaveSpell(CLERIC_SYMBOL_STUN) \4 Spell(LastSeenBy(Myself),CLERIC_SYMBOL_STUN) \5~ //
  END
  BUT_ONLY IF_EXISTS

// gpmage1 is missing IDS refs in a HaveSpell, Spell block
// gpmage2 has identical block
COPY_EXISTING ~gpmage1.bcs~  ~override~
              ~magehigh.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_DISPEL_MAGIC)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)~ ~Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)~ // ... and action
  END
  BUT_ONLY IF_EXISTS

// missing IDS refs in a HaveSpell, Spell block--offensive/disabling spell
// every mage12 ends with a magic missile block before melee
COPY_EXISTING ~lyros.bcs~   ~override~
              ~mage12b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_MAGIC_MISSILE)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)~ // ... and action
  END
  BUT_ONLY

// missing IDS refs in a HaveSpell, Spell block--offensive/disabling spell
// based on sequencing, chaos is a good guess here
COPY_EXISTING ~mage12e.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_CHAOS)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_CHAOS)~ // ... and action
  END
  BUT_ONLY

// missing IDS refs in a HaveSpell, Spell block for > 6 opponents
// kproen02 has exact same sequencing
COPY_EXISTING ~mage14d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_DEATH_SPELL)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)~ ~Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)~ // ... and action
  END
  BUT_ONLY

// one possible attack with mag missile and fireball--no bounce, indiv target, range > 5
// except for two extra response options, similar to many symbol sequences
COPY_EXISTING ~meliss01.bcs~ ~override~
              ~meliss02.bcs~ ~override~
              ~meliss03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(LastSeenBy(Myself),0)~ ~SpellNoDec(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH)~
  END
  BUT_ONLY IF_EXISTS

// big, high-level spell: only against 75% HP+. similar blocks are very high-level spells--comet, bigby, bone blades
COPY_EXISTING ~meliss03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(LastSeenBy(Myself),0)~ ~ForceSpell(LastSeenBy(Myself),WIZARD_DRAGONS_BREATH)~ // dragon's breath?
  END
  BUT_ONLY IF_EXISTS

// some fire mephit attack
COPY_EXISTING ~mepfir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(NearestEnemyOf(Myself),0)~ ~ForceSpell(NearestEnemyOf(Myself),MEPHIT_FLAME_FAN)~ // attack spell
  END
  BUT_ONLY

// lilarcor sound
COPY_EXISTING ~pipe04.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~PlaySound("MISC_02A")~ ~PlaySound("EFF_P54")~ // Cromwell's forging sound
  END
  BUT_ONLY

// two missing actions in four-action blocks; blocks seem identical
// for fire slamanders; should be offensive fire spell
COPY_EXISTING ~salgrfir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(NearestEnemyOf(Myself),0)~ ~SpellNoDec(NearestEnemyOf(Myself),WIZARD_AGANNAZAR_SCORCHER)~
  END
  BUT_ONLY IF_EXISTS

/*
// viekang's scripts; allegiance check to destroy minhp1
COPY_EXISTING ~sarvie01.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,EVILBUTBLUE)~ // third allegiance check?
  END
  BUT_ONLY
*/

// one of four attack options--others all cleric damage/disabling spells
COPY_EXISTING ~tahazz.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(NearestEnemyOf(Myself),0)~ ~ForceSpell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)~
  END
  BUT_ONLY IF_EXISTS

// demon knights
COPY_EXISTING ~uddeath.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ReallyForceSpell(LastSeenBy(Myself),0)~
      ~ReallyForceSpell(LastSeenBy(Myself),WIZARD_SYMBOL_STUN)~ // one of five attack options--symbol stun?
    REPLACE_TEXTUALLY
      ~ForceSpell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("DeathKnightFireball","LOCALS",1)\)~
      ~ForceSpell(LastSeenBy(Myself),WIZARD_FIREBALL) \1~ // one of two attack options--fireball?
    REPLACE_TEXTUALLY 
      ~ForceSpell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("DeathAttack","LOCALS",1)\)~
      ~ForceSpell(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH) \1~ // one of three attack options--symbol death?
  END
  BUT_ONLY

/////                                                  \\\\\
///// bard song fixes                                  \\\\\
/////                                                  \\\\\

BEGIN @18 DESIGNATED 111
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

COPY ~bg2fixpack/spl/fjbard.spl~   ~override~
     ~bg2fixpack/spl/fjbarda.spl~  ~override~
     ~bg2fixpack/spl/fjbardb.spl~  ~override~
     ~bg2fixpack/spl/fjblade.spl~  ~override~
     ~bg2fixpack/spl/fjbladeb.spl~ ~override~

// apply song to trueclass bards and blades
COPY_EXISTING ~clabba01.2da~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    SET_2DA_ENTRY 3 1 3 ~AP_FJBARD  ~
  END
  BUT_ONLY

COPY_EXISTING ~clabba02.2da~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    SET_2DA_ENTRY 3 1 3 ~AP_FJBLADE ~
  END
  BUT_ONLY

/////                                                  \\\\\
///// wizard slayer ranged attacks                     \\\\\
/////                                                  \\\\\

BEGIN @19 DESIGNATED 112
GROUP @2

COPY_EXISTING ~spcl133.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 248 opcode = 249 END
  BUT_ONLY

// wiz slayer description update
ACTION_IF tob_game THEN BEGIN

  STRING_SET 25203 @145

END ELSE BEGIN

  STRING_SET 25203 @144

END

/////                                                  \\\\\
///// additional alignment fixes                       \\\\\
/////                                                  \\\\\

BEGIN @21 DESIGNATED 113
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// alignment fixes
// read in array of changes from external file to make it easier for players to make their own changes here
INCLUDE ~bg2fixpack/lib/alignment_changes_obc.tpa~

ACTION_PHP_EACH cd_bulk_cre_changes_align AS cre_file => n_align BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x27b n_align
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// free action prevents against stun                \\\\\
/////                                                  \\\\\

BEGIN @23 DESIGNATED 114
GROUP @2

INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches.tpa~ // batch patches, mainly immunity stuff

COPY_EXISTING ~blun30.itm~  ~override~ // foa +5
              ~potn45.itm~  ~override~ // potion of freedom
              ~ring09.itm~  ~override~ // ring of free action
              ~sper12.itm~  ~override~ // ixil's spike +6
              ~sppr403.spl~ ~override~ // spell of free action
  LPF cd_apply_batch INT_VAR bonus_var_1 = 1 STR_VAR array_name = cd_immunity_free_action_arrays END // does basic (lol) free action w/ stun protection
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_stun_arrays END // rounds out stun protection
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// shapeshifter paw fixes                           \\\\\
/////                                                  \\\\\

BEGIN @24 DESIGNATED 115
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS ~tobex_ini/tobexcore.ini~ @29 // This component requires TobEx
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// Add the non-dispellable flag to shapechange weapons if TobEx is detected
// This will prevent the natural weapons of the shapechange forms (i.e. claws and such) from being removed by Dispel Magic and Dead Magic Zones
COPY_EXISTING ~brblp.itm~    ~override~ // Shapeshifts Black Bear
              ~brbrp.itm~    ~override~ // Shapeshifts Brown Bear, Shapeshifts Werewolf, Shapeshifts Greater Werewolf
              ~druear.itm~   ~override~ // earth elemental transformation
              ~drufir.itm~   ~override~ // fire elemental transformation
              ~earthrn.itm~  ~override~ // Shapechange Earth Elemental
              ~firern.itm~   ~override~ // Shapechange Fire Elemental
              ~goliro.itm~   ~override~ // used by iron golems, Shapechange Iron Golem
              ~mindflay.itm~ ~override~ // used by mind flayers, Shapechange Mind Flayer
              ~plyjelly.itm~ ~override~ // jelly from cloak of sewers
              ~plysala.itm~  ~override~ // Fire Salamander (Avenger kit form)
              ~plyspid.itm~  ~override~ // Sword Spider (Avenger kit form)
              ~plytroll.itm~ ~override~ // troll from cloak of sewers
              ~plywyvrn.itm~ ~override~ // Baby Wyvern shape attack
              ~polyrat.itm~  ~override~ // rat from cloak of sewers
              ~shakti1.itm~  ~override~ // short sword +4
              ~slayerwp.itm~ ~override~ // slayer, Slayer Change
              ~squirp.itm~   ~override~ // Sphere of Chaos
              ~trollall.itm~ ~override~ // Shapechange Giant Troll
              ~wolfgr.itm~   ~override~ // Shapechange Greater Wolfwere
              ~wolfm.itm~    ~override~ // Shapeshifts Wolf
              ~cdwolfm.itm~  ~override~ // wolfm clone for cloak of the wolf
  WRITE_LONG 0x18 (THIS | BIT24) // sets bit24
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// no thief bonuses for rangers & bards             \\\\\
/////                                                  \\\\\

BEGIN @28 DESIGNATED 116
GROUP @2

// remove thief base HiS/MS bonii
COPY_EXISTING ~skillrng.2da~ ~override~
  COUNT_2DA_ROWS 2 rows
  SET stealth = 0
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    PATCH_IF (stealth != 99) BEGIN
      READ_2DA_ENTRY index 1 2 stealth
      PATCH_IF (IS_AN_INT stealth) BEGIN
        SET_2DA_ENTRY  index 1 2 (stealth - 7)
      END ELSE BEGIN
        SET stealth = 0
      END
    END ELSE BEGIN
      READ_2DA_ENTRY index 0 2 max_rng_level
    END
  END
  BUT_ONLY

// remove thief base pp bonus
COPY_EXISTING ~skillbrd.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 "level"
    READ_2DA_ENTRY index 1 2 "pp"
    PATCH_IF (IS_AN_INT level) BEGIN
      SET pp_adj = (5 + (5 * level))
      PATCH_IF (pp_adj > 100) BEGIN
        SET pp_adj = 100
      END
      SET_2DA_ENTRY index 1 2 "pp_adj"
    END
  END
  BUT_ONLY

// used to adjust joinable ranger stealth scores here, but engine sets these correctly upon joining so it was unnecessary

// need to adjust joinable bard NPCs
ACTION_FOR_EACH file IN haer10 haer11 haer13 haer15 haer19 _garric _garric2 _garric4
  _garric6 _eldoth _eldoth5 eldoth eldoth5 garric garric2 garric4 garric6 BEGIN

  COPY_EXISTING ~%file%.cre~ ~override~
    READ_BYTE 0x234 level
    PATCH_IF (level < 19) BEGIN
      WRITE_BYTE 0x6a (5 + (5 * level))
    END ELSE BEGIN
      WRITE_BYTE 0x6a 100
    END
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// Cromwell's forging actually takes a day (8 hrs)  \\\\\
/////                                                  \\\\\

BEGIN @37 DESIGNATED 117 DEPRECATED @22
SUBCOMPONENT @8
GROUP @2

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Who loves ya baby? The Fixpack Team, that's who! \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// need to uncomment the following line to get this component at install time
BEGIN ~SUPER SECRET COMPONENT~ DESIGNATED 10000
DEPRECATED ~delete this line to make this component active~

INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions

/*
For some of the common 'I don't like this' requests, I had some free time and decided to code
this for players. See something you don't like? Should just be a few un-comments away.
Want to change something not here? Ask in the 'I *hate* this fix' thread:
http://www.gibberlings3.net/forums/index.php?showtopic=7890

Alignments can be directly edited in the main Fixpack component by altering
bg2fixpack/lib/alignment_changes.tpa with a text editor

OBC alignment changes are in bg2fixpack/lib/alignment_changes_obc.tpa
*/

// removes elven/half-elven sleep/charm immunities
COPY_EXISTING_REGEXP GLOB ~^cdelfcm[0-7]\.eff$~ ~override~
                          ~^cdelfsl[0-3]\.eff$~ ~override~
  WRITE_SHORT 0x2e 100
  BUT_ONLY

// undo monk item usability fixes
COPY_EXISTING ~amul21.itm~   ~override~ // Amulet of power
              ~brac21.itm~   ~override~ // gauntlets of ex specialization
              ~ring22.itm~   ~override~ // ring o' holiness
              ~ring35.itm~   ~override~ // ring of lock picking
              ~ring40.itm~   ~override~ // ring of acuity
              ~scrl8c.itm~   ~override~ // stone to flesh
              ~wa2ring.itm~  ~override~ // mercykiller ring
  WRITE_BYTE 0x21 (THIS BAND  `BIT5) // removes monk flag
  BUT_ONLY IF_EXISTS

// Innate Draw Upon Holy Might is fast casting
COPY_EXISTING ~spin103.spl~ ~override~ // duhm innate
  LPF ALTER_HEADER INT_VAR silent = 1 speed = 2 END // speed on all headers
  BUT_ONLY

COPY_EXISTING ~arow08.itm~ ~override~ // make arrows of fire +2 to hit again
              ~arow09.itm~ ~override~ // inexplicably make arrows of ice +2 to hit
  LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 to_hit = 2 END
  BUT_ONLY

// restore save bonuses to the ring of earth control
COPY_EXISTING ~ring29.itm~ ~override~
  FOR (op = 33 ; op < 38 ; ++op) BEGIN // delete save bonuses
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = op target = 1 parameter1 = 1 timing = 2 END
  END
  BUT_ONLY