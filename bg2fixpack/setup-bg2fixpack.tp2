BACKUP ~bg2fixpack/backup~ // location to store files for uninstall purposes
SUPPORT ~https://www.gibberlings3.net/forum/74-bg2-fixpack-general-discussion/~

ALWAYS

  ACTION_IF !VARIABLE_IS_SET tob_game THEN BEGIN

    OUTER_SET ee_game = 1
    OUTER_SET tob_game = 1
    OUTER_SET tobex_game = 0
  
    ACTION_IF GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ THEN BEGIN
  
      OUTER_SET ee_game = 0
  
      ACTION_IF !FILE_EXISTS_IN_GAME ~mel01.cre~ THEN BEGIN
        OUTER_SET tob_game = 0
      END
  
      ACTION_IF FILE_EXISTS ~tobex_ini/tobexcore.ini~ THEN BEGIN
        OUTER_SET tobex_game = 1
      END

    END

  END

END

VERSION ~v13~

README ~bg2fixpack/languages/%LANGUAGE%/readme-bg2fixpack.html~ ~bg2fixpack/readme-bg2fixpack.html~

LANGUAGE ~English~                                ~english~ ~bg2fixpack/languages/english/setup.tra~
LANGUAGE ~Polski (by yarpen)~                     ~polski~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/polski/setup.tra~
LANGUAGE ~Espanol (by Immortality and Clan DLAN)~ ~spanish~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/spanish/setup.tra~
LANGUAGE ~Deutsch (by Leonardo Watson)~           ~german~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/german/setup.tra~
LANGUAGE ~Francais (by Anomaly)~                  ~french~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/french/setup.tra~
LANGUAGE ~Korean (by Web2Air)~                    ~korean~  ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/korean/setup.tra~
LANGUAGE ~Italian (by Andrea C.)~                 ~italian~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/italian/setup.tra~
LANGUAGE ~Russian (by Fess)~                      ~russian~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/russian/setup.tra~
LANGUAGE ~Japanese (by Taro)~                    ~japanese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/japanese/setup.tra~
LANGUAGE ~Traditional Chinese (by good0593)~     ~tchinese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/tchinese/setup.tra~
LANGUAGE ~Simplified Chinese (by good0593)~      ~schinese~ ~bg2fixpack/languages/english/setup.tra~
                                                            ~bg2fixpack/languages/schinese/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Begin core component                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0 DESIGNATED 0 // Core Fixes
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~druidab.bcs~ @26 // in case BD or Fixpack already installed
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

INCLUDE ~bg2fixpack/lib/core_preamble.tph~           // loads functions, fixes key, preps for other fixes
INCLUDE ~bg2fixpack/lib/core_strings.tph~            // strings which need to be updated
INCLUDE ~bg2fixpack/lib/core_ids.tph~                // ids corrections 
INCLUDE ~bg2fixpack/lib/core_2da.tph~                // 2da corrections 
INCLUDE ~bg2fixpack/lib/core_compile.tph~            // any basic copy/compiles go here
INCLUDE ~bg2fixpack/lib/core_dialogue.tph~           // fixes to dialogue
INCLUDE ~bg2fixpack/lib/core_playerai.tph~           // fixes to player AI scripts
INCLUDE ~bg2fixpack/lib/core_scripts_syntax.tph~     // bulk script fixes
INCLUDE ~bg2fixpack/lib/core_scripts_other.tph~      // bespoke script fixes
INCLUDE ~bg2fixpack/lib/core_area.tph~               // all area fixes
INCLUDE ~bg2fixpack/lib/core_creature_bulk.tph~      // bulk creature fixes (race, class, etc.)
INCLUDE ~bg2fixpack/lib/core_creature_inventory.tph~ // creature inventory fixes
INCLUDE ~bg2fixpack/lib/core_creature_other.tph~     // all other creature fixes
INCLUDE ~bg2fixpack/lib/core_item_bulk.tph~          // bulk item fixes (weight, speed, etc.)
INCLUDE ~bg2fixpack/lib/core_item_usability.tph~     // item usability fixes
INCLUDE ~bg2fixpack/lib/core_item_scroll.tph~        // spell scroll fixes
INCLUDE ~bg2fixpack/lib/core_item_other.tph~         // other item fixes
INCLUDE ~bg2fixpack/lib/core_spell_bulk.tph~         // bulk spell fixes (casting animation, speed. etc.)
INCLUDE ~bg2fixpack/lib/core_spell_other.tph~        // all other spell fixes
INCLUDE ~bg2fixpack/lib/core_store.tph~              // store, bags of holding fixes
INCLUDE ~bg2fixpack/lib/core_other_files.tph~        // fixes for vvc, pro, bam, eff, music
INCLUDE ~bg2fixpack/lib/core_immunity_batches.tph~   // immunity batch fixes for items/creatures/spells
INCLUDE ~bg2fixpack/lib/core_effing_effs.tph~        // fixing effs whichprovide damage and thac0 bonuses vs. X
INCLUDE ~bg2fixpack/lib/core_probability.tph~        // probability fixes for items/creatures/spells

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Game text update                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Game text update light                           \\\\\
/////                                                  \\\\\

BEGIN @1000 DESIGNATED 1000 // GTU Light (by Wisp)
SUBCOMPONENT @1  // Game text update
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE
REQUIRE_PREDICATE ((FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtul.tra~) AND
                   (FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtul.tpa~)) @5

LOAD_TRA ~bg2fixpack/languages/%LANGUAGE%/gtul.tra~
INCLUDE  ~bg2fixpack/languages/%LANGUAGE%/gtul.tpa~

/////                                                  \\\\\
///// Game text update, original Baldurdash            \\\\\
/////                                                  \\\\\

BEGIN @1001 DESIGNATED 1001 // GTU Classic (from Baldurdash, by Kevin Dorner)
SUBCOMPONENT @1  // Game text update
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE
REQUIRE_PREDICATE ((FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtu.tra~) AND
                   (FILE_EXISTS ~bg2fixpack/languages/%LANGUAGE%/gtu.tpa~)) @5

LOAD_TRA ~bg2fixpack/languages/%LANGUAGE%/gtu.tra~
INCLUDE  ~bg2fixpack/languages/%LANGUAGE%/gtu.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// modder pack                                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3 DESIGNATED 2 // modder pack
DEPRECATED @22 //Remove this line if you want the component back

INCLUDE ~bg2fixpack/lib/modder.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BETA Core Fixes                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @20 DESIGNATED 3 // Beta Core Fixes
//REQUIRE_PREDICATE MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~0~ @33 // skip if core isn't installed

INCLUDE ~bg2fixpack/lib/beta_core.tph~  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Optional, but cool                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// keldorn xp                                       \\\\\
/////                                                  \\\\\

BEGIN @6 DESIGNATED 100
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// get xp for keldorn's reconciliation if keldorn is released from the party
COMPILE ~bg2fixpack/dlg/keldorn.d~

/////                                                  \\\\\
///// maze animations                                  \\\\\
/////                                                  \\\\\

BEGIN @7 DESIGNATED 101
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// maze animation, part 1
COPY_EXISTING ~spcl415.spl~ ~override~ // maze (special snare)
              ~spin774.spl~ ~override~ // maze (psionic)
              ~spwi813.spl~ ~override~ // maze (arcane)
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215              STR_VAR match_resource = spmaze2 END
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 215 duration = 5 STR_VAR match_resource = spmaze1 resource = spspmaze END
  LPF ALTER_EFFECT  INT_VAR silent = 1 check_globals = 0 match_opcode = 213 duration = 4 timing = 4 END
  BUT_ONLY

// maze animation, part 2
COPY_EXISTING ~spmaze2.vvc~ ~override/spspmaze.vvc~
  WRITE_ASCIIE 0x08 ~%DEST_RES%~ #8
  WRITE_BYTE   0x4c 0x08
  WRITE_BYTE   0x68 0x00
  WRITE_ASCII  0x78 ~EFF_M74~ #8

// change enemy symbol spells to use same visuals as PC symbols
COPY_EXISTING ~iceglyp.pro~ ~override/cdnpcsym.pro~ // clone symbol graphics into ally-friendly projectile
  WRITE_SHORT 0x200 (THIS BOR BIT6) // add ally-friendly flag

ADD_PROJECTILE ~override/cdnpcsym.pro~ // add to projectl.ids

// now assign new projectile to enemy symbol spells
COPY_EXISTING ~spin897.spl~ ~override~ // WIZARD_NPC_SYMBOL_DEATH
              ~spin898.spl~ ~override~ // WIZARD_NPC_SYMBOL_STUN
              ~spin899.spl~ ~override~ // WIZARD_NPC_SYMBOL_FEAR
  LPF ALTER_SPELL_HEADER INT_VAR projectile = cdnpcsym END
    
// define max play length for these animations when called via 215s
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_anim_max_dur BEGIN
  icrmpari => 2
  icstreni => 2
  icwrati  => 4
  spdispma => 3
  spnwchrm => 4
  sppowwrd => 2
  sprotect => 2
  sptrusee => 3
END

// animation fixes
COPY_EXISTING ~bhaal4b.spl~ ~override~
              ~spcl213.spl~ ~override~
              ~spin550.spl~ ~override~
              ~spin553.spl~ ~override~
              ~spin558.spl~ ~override~
              ~spin674.spl~ ~override~
              ~spin675.spl~ ~override~
              ~spin696.spl~ ~override~
              ~spin826.spl~ ~override~
              ~spin891.spl~ ~override~
              ~sppr107.spl~ ~override~
              ~sppr408.spl~ ~override~
              ~spwi113.spl~ ~override~
              ~spwi214.spl~ ~override~
              ~spwi702.spl~ ~override~
  READ_SHORT 0x68 abil_num
  READ_LONG  0x6a fx_off
  FOR (index = fx_off ; index < SOURCE_SIZE ; index += 0x30) BEGIN
    READ_SHORT (index       ) opcode
    READ_BYTE  (index + 0x0c) timing
    PATCH_IF ((opcode = 215) AND (timing = 0)) BEGIN
      READ_LONG  (index + 0x0e) duration
      READ_ASCII (index + 0x14) anim
      PATCH_PHP_EACH cd_anim_max_dur AS anim_match => dur BEGIN
        PATCH_IF (("%anim%" STRING_COMPARE_CASE "%anim_match%" = 0) AND (duration > dur)) BEGIN
          WRITE_LONG  (index + 0x0e) dur
        END
      END
    END
  END
  BUT_ONLY IF_EXISTS

// make burning hands AoE instead of single target
COPY_EXISTING ~cspray.pro~   ~override/burnhand.pro~
  WRITE_SHORT 0x206 64 // range (5')
  WRITE_BYTE  0x217  0 // no explosion

ADD_PROJECTILE ~override/burnhand.pro~

COPY_EXISTING ~spwi103.spl~  ~override~
  LPF ALTER_SPELL_HEADER INT_VAR target = 4 projectile = burnhand END // use new projectile, target any point
  BUT_ONLY

// update scroll with AoE targeting
COPY_EXISTING ~scrl68.itm~ ~override~ // burning hands
  LPF ALTER_ITEM_HEADER INT_VAR header = 1 target = 4 range = 5 END // ability header
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 146 opcode = 148 target = 1 END // spell cast itself
  BUT_ONLY

// Make Errard's Divination spell actually use the Divination casting graphics (aVENGER)
COPY_EXISTING ~spin541.spl~ ~override~ // divination
  WRITE_SHORT 0x22 16 // Casting graphics: 16 (Divination)
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// Cromwell's forging actually takes a day (24 hrs) \\\\\
/////                                                  \\\\\

BEGIN @36 DESIGNATED 102 DEPRECATED @22
SUBCOMPONENT @8
GROUP @2

/////                                                  \\\\\
///// Mixed-use dagger fixes                           \\\\\
/////                                                  \\\\\

BEGIN @9 DESIGNATED 103
GROUP @2

// removes melee abilities from Fire Tooth and Boomerang dagger; change APR to be consistent
// with other throwing daggers; alters base Fire Tooth damage to 1d4 (again for consistency); damage change now rescinded (Wisp)
COPY_EXISTING ~dagg11.itm~ ~override~
              ~dagg12.itm~ ~override~
  LPF DELETE_ITEM_HEADER INT_VAR header_type = 1 END
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 1 parameter1 = 2 parameter2 = 1 END // set APR to 2 instead of +1
  BUT_ONLY

COPY_EXISTING ~tooltip.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(DAGG1[12][ %TAB%]+.+\)15529\([ %TAB%].+\)$~ ~\1-1   \2~ // remove melee from tooltip
  BUT_ONLY

/////                                                  \\\\\
///// Ghrey's Holy Symbol Fixes                        \\\\\
/////                                                  \\\\\

BEGIN @10 DESIGNATED 104
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mel01.cre~ @11 // ToB required
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~BG2FIXPACK/SETUP-BG2FIXPACK.TP2~ ~0~) OR (GAME_IS ~bg2ee eet~)) @33 // requires the holy symbol exploit removed

// disable holy symbols for Aerie, Anomen, and Viconia in main game scripts
COPY_EXISTING ~baldur.bcs~   ~override~
              ~baldur25.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HasItem("CDHLYSYM",\(Player[1-6]\))~ ~HasItem("CDHLYSYM",\1) !Name("Aerie",\1) !Name("Anomen",\1) !Name("Viconia",\1)~
  END
  BUT_ONLY

// now grant their holy symbols via their own override scripts
EXTEND_BOTTOM ~aerie.bcs~   ~bg2fixpack/baf/et_aerie.baf~
EXTEND_BOTTOM ~aeri25.bcs~  ~bg2fixpack/baf/et_aerie.baf~
EXTEND_BOTTOM ~anomen.bcs~  ~bg2fixpack/baf/et_anom.baf~
EXTEND_BOTTOM ~anom25.bcs~  ~bg2fixpack/baf/et_anom.baf~
EXTEND_BOTTOM ~viconia.bcs~ ~bg2fixpack/baf/et_vicon.baf~
EXTEND_BOTTOM ~vico25.bcs~  ~bg2fixpack/baf/et_vicon.baf~

COPY ~bg2fixpack/bam/d0baer.bam~ ~override~
     ~bg2fixpack/bam/d0shar.bam~ ~override~

// Holy Symbol of Helm Usable by Good Aligned. Poo, but necessary to let Anomen use it.
COPY_EXISTING ~belt13.itm~ ~override~
  WRITE_BYTE 0x1e (THIS BAND `BIT2) // removes good flag
  BUT_ONLY

COPY ~bg2fixpack/itm/j#belt12.itm~ ~override~
  SAY NAME2 @128
  SAY IDENTIFIED_DESC @129

COPY ~bg2fixpack/itm/j#belt14.itm~ ~override~
  SAY NAME2 @130
  SAY IDENTIFIED_DESC @131

/////                                                  \\\\\
///// Jenia will wait until PC hero of trademeet       \\\\\
/////                                                  \\\\\

BEGIN @12 DESIGNATED 105
DEPRECATED @22

// moved into core fixes per Gaider

/////                                                  \\\\\
///// giants have penalties v shorties                 \\\\\
/////                                                  \\\\\

BEGIN @13 DESIGNATED 106
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// giant weapons get THAC0 penalties vs. small people (revised by aVENGER)
COPY_EXISTING ~giafir.itm~   ~override~
              ~giafir2.itm~  ~override~
              ~giafir3.itm~  ~override~
              ~giants01.itm~ ~override~
  PATCH_FOR_EACH eff IN giant1 giant2 giant3 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 timing = 2 parameter1 = 142 parameter2 = 4 STR_VAR resource = EVAL ~%eff%~ END // 3 effs go into effect on race: giant
  END
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// Remove Dual-class Restriction from ranger kits   \\\\\
/////                                                  \\\\\

BEGIN @14 DESIGNATED 107
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

COPY_EXISTING ~dualclas.2da~ ~override~
  SET_2DA_ENTRY 32 2 7 ~1~ // archer (feralan)
  SET_2DA_ENTRY 33 2 7 ~1~ // stalker
  BUT_ONLY

/////                                                  \\\\\
///// Remove Wrath-Evil bonuses                        \\\\\
/////                                                  \\\\\

BEGIN @15 DESIGNATED 108
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// remove bonuses here as the player is already rewarded at the door
COPY_EXISTING ~ar2905.bcs~ ~override~
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~Global("Player1Wrath","GLOBAL",2)~ ~False()~
 END
 BUT_ONLY

/////                                                  \\\\\
///// summoned demon behavior                          \\\\\
/////                                                  \\\\\

BEGIN @16 DESIGNATED 109
GROUP @2

COMPILE ~bg2fixpack/baf/demglasu.baf~
        ~bg2fixpack/baf/demnabsu.baf~
        ~bg2fixpack/baf/dempitsu.baf~

/////                                                  \\\\\
///// additional script fixes                          \\\\\
/////                                                  \\\\\
                
BEGIN @17 DESIGNATED 110
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// commoners missing scripts to turn hostile if attacked
COPY_EXISTING ~aewimer1.cre~ ~override~
              ~aewimer2.cre~ ~override~
              ~aewimer3.cre~ ~override~
              ~bdgoph01.cre~ ~override~
              ~bdgoph02.cre~ ~override~
              ~brat1.cre~    ~override~
              ~brat2.cre~    ~override~
              ~brat3.cre~    ~override~
              ~ftown1.cre~   ~override~
              ~ftown2.cre~   ~override~
              ~ftown3.cre~   ~override~
              ~ftown4.cre~   ~override~
              ~haquat.cre~   ~override~
              ~maria.cre~    ~override~
              ~mourner5.cre~ ~override~
              ~mourner6.cre~ ~override~
              ~mtown1.cre~   ~override~
              ~mtown2.cre~   ~override~
              ~mtown3.cre~   ~override~
              ~mtown4.cre~   ~override~
              ~murtlen.cre~  ~override~
              ~noblem1.cre~  ~override~
              ~noblem2.cre~  ~override~
              ~noblew1.cre~  ~override~
              ~noblew2.cre~  ~override~
              ~peony.cre~    ~override~
              ~postul1.cre~  ~override~
              ~postul3.cre~  ~override~
              ~postul5.cre~  ~override~
              ~postul6.cre~  ~override~
              ~pwauk2.cre~   ~override~
              ~radeel.cre~   ~override~
              ~scbutler.cre~ ~override~
              ~scqar.cre~    ~override~
              ~scsarles.cre~ ~override~
              ~sethle.cre~   ~override~
              ~trskin02.cre~ ~override~
              ~trtavp05.cre~ ~override~
              ~uhmer02.cre~  ~override~
              ~wellyn.cre~   ~override~
  SET script_runenemy = 0
  SET script_wtrunsgt = 0
  // first, check to see if these are present
  FOR (index = 0 ; index < 5 ; ++index) BEGIN
    READ_ASCII (0x248 + (index * 0x08)) script
    PATCH_IF ("%script%" STRING_COMPARE_CASE "runenemy" = 0) BEGIN
      SET script_runenemy = 1
    END ELSE
    PATCH_IF (("%script%" STRING_COMPARE_CASE "wtrunsgt" = 0) OR
              ("%script%" STRING_COMPARE_CASE "wdrunsgt" = 0)) BEGIN
      SET script_wtrunsgt = 1
    END
  END
  // has one but not the other
  PATCH_IF (script_runenemy + script_wtrunsgt = 1) BEGIN
    FOR (index = 0 ; index < 5 ; ++index) BEGIN
      READ_ASCII (0x248 + (index * 0x08)) script
      PATCH_IF (("%script%" STRING_COMPARE_CASE "none" = 0) OR ("%script%" STRING_COMPARE_CASE "" = 0)) BEGIN
        PATCH_IF (script_runenemy = 0) BEGIN
          WRITE_ASCII (0x248 + (index * 0x08)) runenemy
        END ELSE BEGIN
          WRITE_ASCII (0x248 + (index * 0x08)) wtrunsgt
        END
        SET index = 5 // kills loop
      END
    END
  END
  BUT_ONLY

// create fatigue-free restoration spell for cut59a
COPY_EXISTING ~sppr417.spl~  ~override/cdpr417.spl~
  LPF DELETE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 93 END

// dragons have responses to cloud-based attacks
// was presumed to be zone of sweet air, but already included in wing buffet spell, so ref removed
COPY_EXISTING ~abazdrag.bcs~ ~override~ // abazigal
              ~dragbrow.bcs~ ~override~ // draconis
              ~draggre2.bcs~ ~override~
              ~draggree.bcs~ ~override~
              ~gorsal.bcs~   ~override~ // saladrex
 DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~~ // in response to death fog/incend cloud, cloudkill
 END
 BUT_ONLY IF_EXISTS

// direct target of breath attacks gets some extra damage; see drgrbrht or RED_DRAGON_HIT (spin693) in other dragon AI scripts
COPY_EXISTING ~abazdrag.bcs~ ~override~
              ~dragblue.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(LastSeenBy(Myself),0)~ ~~
  END
 BUT_ONLY IF_EXISTS

// unknown fourth possible option; efreet seems to be a common alternative to nishruu summoning (plus it's known to cre)
COPY_EXISTING ~amlich02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(Myself,0)~ ~SpellNoDec(Myself,WIZARD_SUMMON_EFREET)~ // other three actions--two summoning spells and move to PC
  END
 BUT_ONLY IF_EXISTS

// two presumably olive slimes were supposed to spawn--no such creatures
COPY_EXISTING ~ar2200.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("jeloli01",\[4422\.3524\],6)~ ~CreateCreature("jelmus01",[4422.3524],6)~ // replace w/ mustard jelly
    REPLACE_TEXTUALLY ~CreateCreature("jeloli01",\[4052\.3574\],6)~ ~CreateCreature("jelgre01",[4052.3574],6)~ // replace w/ green slime
  END
  BUT_ONLY

// blank block for HaveSpell, CastSpell--only one memorized but not scripted is ADHW
COPY_EXISTING ~bheye.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)~
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)~
  END
  BUT_ONLY

// script designed to turn invisible repeatedly; given level and kit, assassination seems a good fit here
COPY_EXISTING ~chalcy02.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(Myself,0)~ ~SpellNoDec(Myself,ROGUE_ASSASINATION)~ // Some prep spell
  END
 BUT_ONLY IF_EXISTS

// blank block for HaveSpell, CastSpell in offensive situation--two spells memorized but not scripted: fireball and magic missile
// select MM as there's no range check for fireball safety
COPY_EXISTING ~clone1.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(NearestEnemyOf(Myself),0)~
      ~HaveSpell(WIZARD_MAGIC_MISSILE) \1 Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)~
  END
  BUT_ONLY

// final game cutscene
COPY_EXISTING ~cut59a.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ApplySpell(\([A-Za-z0-9]+\),0)~
      ~ApplySpellRES("cdpr417",\1)~   // some form of restoration--follows resurrections and full heal
  END
  BUT_ONLY

// cutscene
COPY_EXISTING ~cut67d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell("Dpimo01",0)~ ~ForceSpell("Dpimo01",CUTSCENE_DAMAGE_1)~   // irenicus doing something to Imoen
  END
  BUT_ONLY

// degardan has one messed up script
COPY_EXISTING ~degard2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY // against non-humanoids; use HM even though not memorized since used in similar blocks in other AI scripts
      ~\(!General(LastSeenBy(Myself),HUMANOID)[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~\1 HaveSpell(WIZARD_HOLD_MONSTER) \2 Spell(LastSeenBy(Myself),WIZARD_HOLD_MONSTER)~
    REPLACE_TEXTUALLY // death spell? something effective against multiple enemies-- cone of cold?
      ~\(NumCreatureGT(\[ALLY\],4)[%TAB% %LNL%%MNL%%WNL%]+Allegiance(Myself,ENEMY)[%TAB% %LNL%%MNL%%WNL%]+See(NearestEnemyOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~\1 HaveSpell(WIZARD_DEATH_SPELL) \2 Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)~
    REPLACE_TEXTUALLY // one of several generic attack responses--melf's acid arrow?
      ~SpellNoDec(LastSeenBy(Myself),0)~
      ~SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)~
    REPLACE_TEXTUALLY // anti-mage spell--hold person? do this one last, had problems with matching
      ~HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)~
      ~HaveSpell(WIZARD_HOLD_PERSON) \1 Spell(LastSeenBy(Myself),WIZARD_HOLD_PERSON)~
  END
  BUT_ONLY

// in between dragon fear and a wing buffet--per other dragon AI, should be lowering specific resistances to breath attack, but nothing to reduce poison resistance
COPY_EXISTING ~draggre2.bcs~ ~override~
              ~draggree.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ReallyForceSpell(NearestEnemyOf(Myself),0)~ ~~
  END
  BUT_ONLY IF_EXISTS

/*
// gauths should be firing off more spells
COPY_EXISTING ~gauth01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ReallyForceSpell(\[PC\],0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("GauthBehavior","LOCALS",1)\)~
      ~ReallyForceSpell([PC],0) \1~ // no idea
    REPLACE_TEXTUALLY
      ~\(!See(LeastDamagedOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+HPGT(Myself,35)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+ReallyForceSpell(\[PC\],BEHOLDER_HOLD_PERSON)[%TAB% %LNL%%MNL%%WNL%]+\)ReallyForceSpell([PC],0)~
      ~\1 ReallyForceSpell([PC],0)~ // no idea
    REPLACE_TEXTUALLY
      ~\([^!]See(LeastDamagedOf(Myself))[%TAB% %LNL%%MNL%%WNL%]+HPGT(Myself,35)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+ReallyForceSpell(\[PC\],BEHOLDER_HOLD_PERSON)[%TAB% %LNL%%MNL%%WNL%]+\)ReallyForceSpell([PC],0)~
      ~\1 ReallyForceSpell([PC],0)~ // no idea
  END
  BUT_ONLY
*/
/*
// missing spell in heal-?-restoration sequence
COPY_EXISTING ~gorgua01.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ApplySpell(Myself,0)~ ~ApplySpell(Myself,FORCE_DISPEL_MAGIC)~ // between full heal and restore--dispel effects?
  END
  BUT_ONLY
*/

// gphealer has two HaveSpell, Spell blocks with unknown IDS entries--something offensive with a 5' radius
// found near-identical sequence for mage symbol spells in gpmage1
COPY_EXISTING ~gphealer.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~\(IF[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+!HasBounceEffects(LastSeenBy(Myself))[%TAB% %LNL%%MNL%%WNL%]+!Range(LastSeenBy(Myself),5)[%TAB% %LNL%%MNL%%WNL%]+RandomNum(2,1)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+END[%TAB% %LNL%%MNL%%WNL%]+IF[%TAB% %LNL%%MNL%%WNL%]+\)HaveSpell(0)\([%TAB% %LNL%%MNL%%WNL%]+!HasBounceEffects(LastSeenBy(Myself))[%TAB% %LNL%%MNL%%WNL%]+!Range(LastSeenBy(Myself),5)[%TAB% %LNL%%MNL%%WNL%]+RandomNum(2,1)[%TAB% %LNL%%MNL%%WNL%]+THEN[%TAB% %LNL%%MNL%%WNL%]+RESPONSE #100[%TAB% %LNL%%MNL%%WNL%]+\)Spell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+END\)~
      ~\1 HaveSpell(CLERIC_SYMBOL_DEATH) \2 Spell(LastSeenBy(Myself),CLERIC_SYMBOL_DEATH) \3 HaveSpell(CLERIC_SYMBOL_STUN) \4 Spell(LastSeenBy(Myself),CLERIC_SYMBOL_STUN) \5~ //
  END
  BUT_ONLY IF_EXISTS

// gpmage1 is missing IDS refs in a HaveSpell, Spell block
// gpmage2 has identical block
COPY_EXISTING ~gpmage1.bcs~  ~override~
              ~magehigh.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_DISPEL_MAGIC)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)~ ~Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)~ // ... and action
  END
  BUT_ONLY IF_EXISTS

// missing IDS refs in a HaveSpell, Spell block--offensive/disabling spell
// every mage12 ends with a magic missile block before melee
COPY_EXISTING ~lyros.bcs~   ~override~
              ~mage12b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_MAGIC_MISSILE)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)~ // ... and action
  END
  BUT_ONLY

// missing IDS refs in a HaveSpell, Spell block--offensive/disabling spell
// based on sequencing, chaos is a good guess here
COPY_EXISTING ~mage12e.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_CHAOS)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_CHAOS)~ // ... and action
  END
  BUT_ONLY

// missing IDS refs in a HaveSpell, Spell block for > 6 opponents
// kproen02 has exact same sequencing
COPY_EXISTING ~mage14d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(WIZARD_DEATH_SPELL)~ // trigger...
    REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),0)~ ~Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)~ // ... and action
  END
  BUT_ONLY

// one possible attack with mag missile and fireball--no bounce, indiv target, range > 5
// except for two extra response options, similar to many symbol sequences
COPY_EXISTING ~meliss01.bcs~ ~override~
              ~meliss02.bcs~ ~override~
              ~meliss03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(LastSeenBy(Myself),0)~ ~SpellNoDec(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH)~
  END
  BUT_ONLY IF_EXISTS

// big, high-level spell: only against 75% HP+. similar blocks are very high-level spells--comet, bigby, bone blades
COPY_EXISTING ~meliss03.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(LastSeenBy(Myself),0)~ ~ForceSpell(LastSeenBy(Myself),WIZARD_DRAGONS_BREATH)~ // dragon's breath?
  END
  BUT_ONLY IF_EXISTS

// some fire mephit attack
COPY_EXISTING ~mepfir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(NearestEnemyOf(Myself),0)~ ~ForceSpell(NearestEnemyOf(Myself),MEPHIT_FLAME_FAN)~ // attack spell
  END
  BUT_ONLY

// lilarcor sound
COPY_EXISTING ~pipe04.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~PlaySound("MISC_02A")~ ~PlaySound("EFF_P54")~ // Cromwell's forging sound
  END
  BUT_ONLY

// two missing actions in four-action blocks; blocks seem identical
// for fire slamanders; should be offensive fire spell
COPY_EXISTING ~salgrfir.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SpellNoDec(NearestEnemyOf(Myself),0)~ ~SpellNoDec(NearestEnemyOf(Myself),WIZARD_AGANNAZAR_SCORCHER)~
  END
  BUT_ONLY IF_EXISTS

/*
// viekang's scripts; allegiance check to destroy minhp1
COPY_EXISTING ~sarvie01.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Allegiance(Myself,0)~ ~Allegiance(Myself,EVILBUTBLUE)~ // third allegiance check?
  END
  BUT_ONLY
*/

// one of four attack options--others all cleric damage/disabling spells
COPY_EXISTING ~tahazz.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ForceSpell(NearestEnemyOf(Myself),0)~ ~ForceSpell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)~
  END
  BUT_ONLY IF_EXISTS

// demon knights
COPY_EXISTING ~uddeath.bcs~  ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ReallyForceSpell(LastSeenBy(Myself),0)~
      ~ReallyForceSpell(LastSeenBy(Myself),WIZARD_SYMBOL_STUN)~ // one of five attack options--symbol stun?
    REPLACE_TEXTUALLY
      ~ForceSpell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("DeathKnightFireball","LOCALS",1)\)~
      ~ForceSpell(LastSeenBy(Myself),WIZARD_FIREBALL) \1~ // one of two attack options--fireball?
    REPLACE_TEXTUALLY 
      ~ForceSpell(LastSeenBy(Myself),0)\([%TAB% %LNL%%MNL%%WNL%]+SetGlobal("DeathAttack","LOCALS",1)\)~
      ~ForceSpell(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH) \1~ // one of three attack options--symbol death?
  END
  BUT_ONLY

/////                                                  \\\\\
///// bard song fixes                                  \\\\\
/////                                                  \\\\\

BEGIN @18 DESIGNATED 111
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

COPY ~bg2fixpack/spl/fjbard.spl~   ~override~
     ~bg2fixpack/spl/fjbarda.spl~  ~override~
     ~bg2fixpack/spl/fjbardb.spl~  ~override~
     ~bg2fixpack/spl/fjblade.spl~  ~override~
     ~bg2fixpack/spl/fjbladeb.spl~ ~override~

// apply song to trueclass bards and blades
COPY_EXISTING ~clabba01.2da~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    SET_2DA_ENTRY 3 1 3 ~AP_FJBARD  ~
  END
  BUT_ONLY

COPY_EXISTING ~clabba02.2da~ ~override~
  PATCH_IF (SOURCE_SIZE > 0) BEGIN
    SET_2DA_ENTRY 3 1 3 ~AP_FJBLADE ~
  END
  BUT_ONLY

/////                                                  \\\\\
///// wizard slayer ranged attacks                     \\\\\
/////                                                  \\\\\

BEGIN @19 DESIGNATED 112
GROUP @2

COPY_EXISTING ~spcl133.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 248 opcode = 249 END
  BUT_ONLY

// wiz slayer description update
ACTION_IF tob_game THEN BEGIN

  STRING_SET 25203 @145

END ELSE BEGIN

  STRING_SET 25203 @144

END

/////                                                  \\\\\
///// additional alignment fixes                       \\\\\
/////                                                  \\\\\

BEGIN @21 DESIGNATED 113
GROUP @2
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// alignment fixes
// read in array of changes from external file to make it easier for players to make their own changes here
INCLUDE ~bg2fixpack/lib/alignment_changes_obc.tpa~

ACTION_PHP_EACH cd_bulk_cre_changes_align AS cre_file => n_align BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_BYTE 0x27b n_align
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// free action prevents against stun                \\\\\
/////                                                  \\\\\

BEGIN @23 DESIGNATED 114
GROUP @2

INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches.tpa~ // batch patches, mainly immunity stuff

COPY_EXISTING ~blun30.itm~  ~override~ // foa +5
              ~potn45.itm~  ~override~ // potion of freedom
              ~ring09.itm~  ~override~ // ring of free action
              ~sper12.itm~  ~override~ // ixil's spike +6
              ~sppr403.spl~ ~override~ // spell of free action
  LPF cd_apply_batch INT_VAR bonus_var_1 = 1 STR_VAR array_name = cd_immunity_free_action_arrays END // does basic (lol) free action w/ stun protection
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_stun_arrays END // rounds out stun protection
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// shapeshifter paw fixes                           \\\\\
/////                                                  \\\\\

BEGIN @24 DESIGNATED 115
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS ~tobex_ini/tobexcore.ini~ @29 // This component requires TobEx
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @27 // not needed for EE

// Add the non-dispellable flag to shapechange weapons if TobEx is detected
// This will prevent the natural weapons of the shapechange forms (i.e. claws and such) from being removed by Dispel Magic and Dead Magic Zones
COPY_EXISTING ~brblp.itm~    ~override~ // Shapeshifts Black Bear
              ~brbrp.itm~    ~override~ // Shapeshifts Brown Bear, Shapeshifts Werewolf, Shapeshifts Greater Werewolf
              ~druear.itm~   ~override~ // earth elemental transformation
              ~drufir.itm~   ~override~ // fire elemental transformation
              ~earthrn.itm~  ~override~ // Shapechange Earth Elemental
              ~firern.itm~   ~override~ // Shapechange Fire Elemental
              ~goliro.itm~   ~override~ // used by iron golems, Shapechange Iron Golem
              ~mindflay.itm~ ~override~ // used by mind flayers, Shapechange Mind Flayer
              ~plyjelly.itm~ ~override~ // jelly from cloak of sewers
              ~plysala.itm~  ~override~ // Fire Salamander (Avenger kit form)
              ~plyspid.itm~  ~override~ // Sword Spider (Avenger kit form)
              ~plytroll.itm~ ~override~ // troll from cloak of sewers
              ~plywyvrn.itm~ ~override~ // Baby Wyvern shape attack
              ~polyrat.itm~  ~override~ // rat from cloak of sewers
              ~shakti1.itm~  ~override~ // short sword +4
              ~slayerwp.itm~ ~override~ // slayer, Slayer Change
              ~squirp.itm~   ~override~ // Sphere of Chaos
              ~trollall.itm~ ~override~ // Shapechange Giant Troll
              ~wolfgr.itm~   ~override~ // Shapechange Greater Wolfwere
              ~wolfm.itm~    ~override~ // Shapeshifts Wolf
              ~cdwolfm.itm~  ~override~ // wolfm clone for cloak of the wolf
  WRITE_LONG 0x18 (THIS | BIT24) // sets bit24
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// no thief bonuses for rangers & bards             \\\\\
/////                                                  \\\\\

BEGIN @28 DESIGNATED 116
GROUP @2

// remove thief base HiS/MS bonii
COPY_EXISTING ~skillrng.2da~ ~override~
  COUNT_2DA_ROWS 2 rows
  SET stealth = 0
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    PATCH_IF (stealth != 99) BEGIN
      READ_2DA_ENTRY index 1 2 stealth
      PATCH_IF (IS_AN_INT stealth) BEGIN
        SET_2DA_ENTRY  index 1 2 (stealth - 7)
      END ELSE BEGIN
        SET stealth = 0
      END
    END ELSE BEGIN
      READ_2DA_ENTRY index 0 2 max_rng_level
    END
  END
  BUT_ONLY

// remove thief base pp bonus
COPY_EXISTING ~skillbrd.2da~ ~override~
  COUNT_2DA_ROWS ~2~ "rows"
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 "level"
    READ_2DA_ENTRY index 1 2 "pp"
    PATCH_IF (IS_AN_INT level) BEGIN
      SET pp_adj = (5 + (5 * level))
      PATCH_IF (pp_adj > 100) BEGIN
        SET pp_adj = 100
      END
      SET_2DA_ENTRY index 1 2 "pp_adj"
    END
  END
  BUT_ONLY

// used to adjust joinable ranger stealth scores here, but engine sets these correctly upon joining so it was unnecessary

// need to adjust joinable bard NPCs
ACTION_FOR_EACH file IN haer10 haer11 haer13 haer15 haer19 _garric _garric2 _garric4
  _garric6 _eldoth _eldoth5 eldoth eldoth5 garric garric2 garric4 garric6 BEGIN

  COPY_EXISTING ~%file%.cre~ ~override~
    READ_BYTE 0x234 level
    PATCH_IF (level < 19) BEGIN
      WRITE_BYTE 0x6a (5 + (5 * level))
    END ELSE BEGIN
      WRITE_BYTE 0x6a 100
    END
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// Cromwell's forging actually takes a day (8 hrs)  \\\\\
/////                                                  \\\\\

BEGIN @37 DESIGNATED 117 DEPRECATED @22
SUBCOMPONENT @8
GROUP @2

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Who loves ya baby? The Fixpack Team, that's who! \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// need to uncomment the following line to get this component at install time
BEGIN ~SUPER SECRET COMPONENT~ DESIGNATED 10000
DEPRECATED ~delete this line to make this component active~

INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions

/*
For some of the common 'I don't like this' requests, I had some free time and decided to code
this for players. See something you don't like? Should just be a few un-comments away.
Want to change something not here? Ask in the 'I *hate* this fix' thread:
http://www.gibberlings3.net/forums/index.php?showtopic=7890

Alignments can be directly edited in the main Fixpack component by altering
bg2fixpack/lib/alignment_changes.tpa with a text editor

OBC alignment changes are in bg2fixpack/lib/alignment_changes_obc.tpa
*/

// removes elven/half-elven sleep/charm immunities
COPY_EXISTING_REGEXP GLOB ~^cdelfcm[0-7]\.eff$~ ~override~
                          ~^cdelfsl[0-3]\.eff$~ ~override~
  WRITE_SHORT 0x2e 100
  BUT_ONLY

// undo monk item usability fixes
COPY_EXISTING ~amul21.itm~   ~override~ // Amulet of power
              ~brac21.itm~   ~override~ // gauntlets of ex specialization
              ~ring22.itm~   ~override~ // ring o' holiness
              ~ring35.itm~   ~override~ // ring of lock picking
              ~ring40.itm~   ~override~ // ring of acuity
              ~scrl8c.itm~   ~override~ // stone to flesh
              ~wa2ring.itm~  ~override~ // mercykiller ring
  WRITE_BYTE 0x21 (THIS BAND  `BIT5) // removes monk flag
  BUT_ONLY IF_EXISTS

// Innate Draw Upon Holy Might is fast casting
COPY_EXISTING ~spin103.spl~ ~override~ // duhm innate
  LPF ALTER_HEADER INT_VAR silent = 1 speed = 2 END // speed on all headers
  BUT_ONLY

COPY_EXISTING ~arow08.itm~ ~override~ // make arrows of fire +2 to hit again
              ~arow09.itm~ ~override~ // inexplicably make arrows of ice +2 to hit
  LPF ALTER_HEADER INT_VAR silent = 1 match_location = 1 to_hit = 2 END
  BUT_ONLY

// restore save bonuses to the ring of earth control
COPY_EXISTING ~ring29.itm~ ~override~
  FOR (op = 33 ; op < 38 ; ++op) BEGIN // delete save bonuses
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = op target = 1 parameter1 = 1 timing = 2 END
  END
  BUT_ONLY
