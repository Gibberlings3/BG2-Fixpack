INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions
INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches.tpa~ // batch patches, mainly immunity stuff

/////                                                  \\\\\
///// mass compile/copy actions actions                \\\\\
/////                                                  \\\\\

COMPILE ~bg2fixpack/dlg/beta_soa-dlg.d~ // dialogue fixes

// list these individually for reference instead of a bulk copy
COPY ~bg2fixpack/copy/spin823a.spl~ ~override~ // slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823.spl
     ~bg2fixpack/copy/spin823b.spl~ ~override~ // slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823.spl
     ~bg2fixpack/copy/spin823c.spl~ ~override~ // slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823.spl
     ~bg2fixpack/copy/spin823d.spl~ ~override~ // slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823.spl
     ~bg2fixpack/copy/spin823e.spl~ ~override~ // slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823.spl

/////                                                  \\\\\
///// script fixes                                     \\\\\
/////                                                  \\\\\

// ranger stronghold fails if you're in Umar Hills when Delon spawn timer expires; see also delon.dlg
COPY_EXISTING ~ar1100.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(NumDeadLT("rogron",6)\)~ ~\1 Global("CDDelonSpoke","GLOBAL",1)~
  END
  BUT_ONLY  

// love talk sets trigger for jaheira's bandits to spawn, but shouldn't occur if romance ends before timer is up
COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("JaheiraBanditPlot","GLOBAL",1)\)~ ~\1 OR(2) Global("JaheiraRomanceActive","GLOBAL",1) Global("JaheiraRomanceActive","GLOBAL",2)~
  END
  BUT_ONLY  
ACTION_IF tob_game BEGIN

  // siege camp reinforcements checking wrong blocks
  COPY_EXISTING ~ar5203.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~GlobalLT("EXTRACOUNT2","MYAREA",1)[ %TAB%%LNL%%MNL%%WNL%]+\(ActionListEmpty()[ %TAB%%LNL%%MNL%%WNL%]+InActiveArea(Myself)[ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE #30[ %TAB%%LNL%%MNL%%WNL%]+IncrementGlobal("MaxSpawn","AR5203",1)[ %TAB%%LNL%%MNL%%WNL%]+CreateCreatureObjectOffScreen("YSSOLD04",Player1,0,0,0)[ %TAB%%LNL%%MNL%%WNL%]+IncrementGlobal("EXTRACOUNT3","MYAREA",1)\)~
                        ~GlobalLT("EXTRACOUNT3","MYAREA",1) \1~
    END
    BUT_ONLY  

END  

/////                                                  \\\\\
///// other item file fixes                            \\\\\
/////                                                  \\\\\

// protection from magic scrolls not dispelling all buffs
COPY_EXISTING ~scrl07.itm~ ~override~ // protection from magic
  FOR (index = 0 ; index < 11 ; ++index) BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 220 parameter1 = 99 parameter2 = index target = 2 timing = 1 END // remove school protections: index 
  END  
  FOR (index = 0 ; index < 14 ; ++index) BEGIN // only 10 spell schools, but 13 secondary types
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 221 parameter1 = 99 parameter2 = index target = 2 timing = 1 END // remove school protections: index 
  END  

// staves of air/earth/water force saving throws on non-elementals; see also staf15[abc].eff
COPY_EXISTING ~staf15.itm~ ~override~ // staff of air +2
              ~staf16.itm~ ~override~ // staff of earth +2
              ~staf17.itm~ ~override~ // staff of fire +2
  LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 174 STR_VAR match_resource = eff_m36b END // delete
  LPF ALTER_EFFECT  INT_VAR check_globals = 0 match_opcode = 55 opcode = 177 savingthrow = 0 STR_VAR resource = staf15a END // change slay into eff call, remove save
  LPF CLONE_EFFECT  INT_VAR check_globals = 0 match_opcode = 177 STR_VAR match_resource = staf15a resource = staf15b insert = above END    // clone slay into second eff for sound
  LPF CLONE_EFFECT  INT_VAR check_globals = 0 match_opcode = 177 STR_VAR match_resource = staf15a resource = staf15c insert = above END    // clone slay into third eff for message

// slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also spin718.spl, spin823.spl, spin823[abcde].spl
COPY_EXISTING ~slayerwp.itm~ ~override/slayerw3.itm~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = slayer END // atach polymorph to weapon
  PATCH_FOR_EACH p2 IN 5 128 39 217 24 BEGIN // charm, confusion, sleep, panic 
    LPF CLONE_EFFECT INT_VAR opcode = 101 match_parameter2 = 45 parameter2 = p2 END // clone stun immunity into new protections
  END
  // move this batch to immunity batches when moved to core; remove spin823.spl
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_sleep_arrays END 
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_confusion_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_charm_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_fear_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_fear_removal_arrays END
  
/////                                                  \\\\\
///// other spell fixes                                \\\\\
/////                                                  \\\\\

// slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin823.spl, spin823[abcde].spl
COPY_EXISTING ~spin718.spl~ ~override~
  LPF ALTER_EFFECT  INT_VAR match_opcode = 31 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR resource = spin823a END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 206 STR_VAR match_resource = spin823a resource = spin823b END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 206 STR_VAR match_resource = spin823a resource = spin823c END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 206 STR_VAR match_resource = spin823a resource = spin823d END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 206 STR_VAR match_resource = spin823a resource = spin823e END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 112 STR_VAR match_resource = slayerwp resource = slayerw3 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 135 STR_VAR insert = last END // copy polymorph to last effect
  LPF DELETE_EFFECT INT_VAR match_opcode = 135 multi_match = 1 END // delete original polymorph effect

// slayer change issues: unnecessary MDR when shifting back; attach abilities to paw not spell - see also slayerw3.itm, spin718.spl, spin823[abcde].spl
COPY_EXISTING ~spin823.spl~ ~override~ // leave 111 (create weapon), 171 (give innate ability), 172 (remove spell), 174 (play sound), 215 (visual effects), 206 (protects against further shapeshifting)
  PATCH_FOR_EACH op IN 12 267 142 101 296 31 106 135 169 161 240 23 BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = op END
  END  
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR match_resource = slayerwp resource = slayerw3 END // change to new weapon
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 opcode = 146 parameter1 = 0 parameter2 = 1 timing = 4 duration = 20 STR_VAR resource = spin823a END // damage now comes in delayed castings
  LPF CLONE_EFFECT INT_VAR match_opcode = 146 duration = 26 STR_VAR match_resource = spin823a resource = spin823b END
  LPF CLONE_EFFECT INT_VAR match_opcode = 146 duration = 32 STR_VAR match_resource = spin823a resource = spin823c END
  LPF CLONE_EFFECT INT_VAR match_opcode = 146 duration = 38 STR_VAR match_resource = spin823a resource = spin823d END
  LPF CLONE_EFFECT INT_VAR match_opcode = 146 duration = 44 STR_VAR match_resource = spin823a resource = spin823e END

// don't play berserk damage warnings on non-party memebers; see also spin117[ab].eff
COPY_EXISTING ~spcl321.spl~  ~override~ // berserker enrage
              ~spcl321d.spl~ ~override~ // berserker enrage (cooldown)
              ~spin117.spl~  ~override~ // minsc's berserk (also used by, randomly, Gorf)
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 139 match_parameter1 = 20723 opcode = 177 parameter1 = 30 parameter2 = 2 STR_VAR resource = spin117a END // only for GOODCUTOFF
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 139 match_parameter1 = 20726 opcode = 177 parameter1 = 30 parameter2 = 2 STR_VAR resource = spin117b END // only for GOODCUTOFF

/////                                                  \\\\\
///// eff fixes                                        \\\\\
/////                                                  \\\\\

// staves of air/earth/water force saving throws on non-elementals; see also staf15-17.itm~
COPY_EXISTING ~slaysum.eff~ ~override/staf15a.eff~
  WRITE_LONG  0x28 0 // duration
  WRITE_LONG  0x38 0 // dice num
  WRITE_LONG  0x3c 0 // dice thrown
  WRITE_LONG  0x40 BIT0 // add save vs. spell
  WRITE_LONG  0x58 0 // min level
  WRITE_LONG  0x5c 0 // max level
  WRITE_LONG  0x60 0 // param3

// staves of air/earth/water force saving throws on non-elementals; see also staf15-17.itm~
COPY_EXISTING ~staf15a.eff~ ~override/staf15b.eff~
  WRITE_LONG  0x10 174 // play sound
  WRITE_ASCII 0x30 eff_m36b #8 // sound file

// staves of air/earth/water force saving throws on non-elementals; see also staf15-17.itm~
COPY_EXISTING ~staf15a.eff~ ~override/staf15c.eff~
  WRITE_LONG  0x10 139 // display string
  SAY 0x1c @264

// don't play berserk damage warnings on non-party memebers; see also spcl321.spl spcl321d.spl, spin117.spl
COPY_EXISTING ~mesdie.eff~ ~override/spin117a.eff~
  WRITE_LONG 0x10 139   // display string
  WRITE_LONG 0x1c 20723 // When the berserk state ends, the character will take 15 damage.
  WRITE_LONG 0x40 0     // remove save
  WRITE_LONG 0x44 0     // remove save penalty
  WRITE_LONG 0x60 0     // param3
  
COPY_EXISTING ~spin117a.eff~ ~override/spin117b.eff~
  WRITE_LONG 0x1c 20726 // The berserk state has ended...
  
  